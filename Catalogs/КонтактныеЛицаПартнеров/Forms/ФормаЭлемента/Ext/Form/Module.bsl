
#Область ОбработчикиСобытийФормы

// #wortmann {
// #4.2.03
// Добавляет в обработчик события вызов создания полей контактного лица, устанавливает доступность групп доступа
// Журавлев Чугуев 2022/07/12
// 
// Параметры:
// Отказ				- Булево
// СтандартнаяОбработка	- Булево
&НаСервере
Процедура гф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	гф_СоздатьРеквизитыКонтактногоЛица();

	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		Элементы.гф_ГруппыДоступа.Доступность = РольДоступна("гф_РедактированиеКонтактныхЛицПартнеров");
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #4.2.03
// Добавляет в обработчик события вызовы создания и заполнения полей контактного лица
// Журавлев Чугуев 2022/07/12
// 
// Параметры:
// ТекущийОбъект - СправочникОбъект.КонтактныеЛица
&НаСервере
Процедура гф_ПриЧтенииНаСервереПосле(ТекущийОбъект)
	
	гф_СоздатьРеквизитыКонтактногоЛица();
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #4.2.03
// Добавляет на форму поля контактного лица, если еще не созданы
// Журавлев Чугуев 2022/07/12
&НаСервере
Процедура гф_СоздатьРеквизитыКонтактногоЛица()
	
	Если Элементы.Найти("гф_ГруппыДоступа") = Неопределено Тогда
		
		ТипПолеФормы = Тип("ПолеФормы");
		ТипТаблицаФормы	= Тип("ТаблицаФормы");
		
		НовоеПоле = Элементы.Добавить("гф_ГруппыДоступа", ТипТаблицаФормы,
										Элементы.ГруппаОсновныеСведения);
		
		НовоеПоле.Видимость				= Истина;
		НовоеПоле.ПутьКДанным			= "Объект.гф_ГруппыДоступа";
		НовоеПоле.Отображение			= ОтображениеТаблицы.Список;
		НовоеПоле.ПоложениеЗаголовка	= ПоложениеЗаголовкаЭлементаФормы.Лево;
		
		НовоеПоле = Элементы.Добавить("гф_ГруппыДоступа_ГруппаДоступа", ТипПолеФормы,
										Элементы.гф_ГруппыДоступа);
		
		НовоеПоле.Вид			= ВидПоляФормы.ПолеВвода;
		НовоеПоле.Видимость		= Истина;
		НовоеПоле.ПутьКДанным	= "Объект.гф_ГруппыДоступа.ГруппаДоступа";
		
		//НовоеПоле.УстановитьДействие("НачалоВыбора", Новый Действие("гф_ГруппыДоступа_ГруппаДоступаНачалоВыбора"));
		
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

&НаКлиенте
Процедура гф_Команда1После(Команда)
	
	ГруппаДоступа = ПолучитьГруппуДоступаИзНастроек();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ГруппаДоступа", ГруппаДоступа);
	
	ОткрытьФорму("Справочник.ГруппыДоступа.Форма.гф_ФормаВыбора", ПараметрыФормы, ЭтаФорма, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьГруппуДоступаИзНастроек()
	
	НастройкаГруппаДоступа = ПланыВидовХарактеристик.гф_ГлобальныеЗначения.НайтиПоРеквизиту("Ключ", "ГруппаДоступаДляКонтрагентов");
	
	Если НЕ НастройкаГруппаДоступа.Пустая() Тогда
		Возврат НастройкаГруппаДоступа.Значение;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти


