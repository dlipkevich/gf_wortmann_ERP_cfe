
&После("ОбработкаПолученияДанныхВыбора")
Процедура гф_ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаДоступа = Константы.гф_ГруппыДоступаДляКонтрагентов.Получить();
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГруппаДоступа", ГруппаДоступа);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактныеЛицаПартнеров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров.гф_ГруппыДоступа КАК ГруппыДоступа
	|		ПО (ГруппыДоступа.Ссылка = КонтактныеЛицаПартнеров.Ссылка)
	|ГДЕ
	|	ГруппыДоступа.ГруппаДоступа В ИЕРАРХИИ(&ГруппаДоступа)
	|	И &ПараметрыОтбора
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппыДоступа.Ссылка.Наименование";
	
	УсловияОтбора = Новый Массив;
	Для Каждого ЭлементОтбора Из Параметры.Отбор Цикл
		ИмяПараметра = "Отбор" + ЭлементОтбора.Ключ;
		Запрос.УстановитьПараметр(ИмяПараметра, ЭлементОтбора.Значение);
		Если ТипЗнч(ЭлементОтбора.Значение) = Тип("ФиксированныйМассив") Тогда
			УсловияОтбора.Добавить(СтрШаблон("КонтактныеЛицаПартнеров.%1 В (&%2)", ЭлементОтбора.Ключ, ИмяПараметра));
		Иначе
			УсловияОтбора.Добавить(СтрШаблон("КонтактныеЛицаПартнеров.%1 = &%2", ЭлементОтбора.Ключ, ИмяПараметра));
		КонецЕсли; 
	КонецЦикла;
	Если УсловияОтбора.Количество() = 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПараметрыОтбора", "ИСТИНА");
	Иначе	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПараметрыОтбора", СтрСоединить(УсловияОтбора, " И "));
	КонецЕсли;	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;

КонецПроцедуры
