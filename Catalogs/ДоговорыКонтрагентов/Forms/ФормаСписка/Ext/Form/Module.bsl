#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура гф_ПриОткрытииПосле(Отказ)
	// #wortmann {
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ed130ce78add5e
	// отображение реквизита гф_ОсновнойДоговорКонтрагента
	// Галфинд Домнышева 2022/08/04
	Если ВладелецФормы.ИмяФормы = "Справочник.Партнеры.Форма.ФормаЭлементаРеквизитыКонтрагента" Тогда
		ДобавитьРеквизитОсновнойКонтрагент(ВладелецФормы.ГоловнойКонтрагент);
	КонецЕсли;
	// } #wortmann
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции 

// #wortmann {
// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ed130ce78add5e
// добавление реквизита гф_ОсновнойДоговорКонтрагента на форму
// Галфинд Домнышева 2022/08/04
// Параметры:
// Контрагент - СправочникСсылка.Контрагенты - Контрагент по которому находим основной договор
&НаСервере
Процедура ДобавитьРеквизитОсновнойКонтрагент(Контрагент)
	
	ОписаниеДоговорыКонтрагентов = Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов");
	
	ДобавляемыеРеквизиты = Новый Массив;
	НовыйРеквизит = Новый РеквизитФормы("гф_ОсновнойДоговорКонтрагента", ОписаниеДоговорыКонтрагентов);
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	ТипПолеФормы = Тип("ПолеФормы");
	НовыйЭлемент = Элементы.Вставить("гф_ОсновнойДоговорКонтрагента", ТипПолеФормы, Элементы.БыстрыеОтборы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = "гф_ОсновнойДоговорКонтрагента";
	НовыйЭлемент.Заголовок = "Основной договор контрагента";
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	НовыйЭлемент.ТолькоПросмотр = Истина;
	
	гф_ОсновнойДоговорКонтрагента = ПолучитьОсновнойДоговор(Контрагент);
КонецПроцедуры// } #wortmann

// #wortmann {
// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ed130ce78add5e
// заполняем реквизит гф_ОсновнойДоговорКонтрагента на форме
// Галфинд Домнышева 2022/08/04
// Параметры:
// Контрагент - СправочникСсылка.Контрагенты - Контрагент по которому находим основной договор
// Возвращаемое значение: 
//  - СправочникСсылка.ДоговорыКонтрагентов - ссылка основной договор Контрагента.
//  - Неопределено - если основной договор не назначен.
&НаСервере
Функция ПолучитьОсновнойДоговор(Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.гф_ОсновнойДоговорКонтрагента КАК гф_ОсновнойДоговорКонтрагента
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка.Следующий();
		Возврат Выборка.гф_ОсновнойДоговорКонтрагента;
	Иначе 
		Возврат Неопределено;
	КонецЕсли;

КонецФункции// } #wortmann

#КонецОбласти
