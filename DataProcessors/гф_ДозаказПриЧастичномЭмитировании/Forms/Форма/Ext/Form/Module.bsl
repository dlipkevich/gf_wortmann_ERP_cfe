
&НаСервере
Процедура СоздатьДокументыНаСервере()
	Объект.СозданныеДокументы.Очистить();
	тзЗаказыПоОснованиям =Объект.ИсправляемыеДокументы.Выгрузить(); 
	мОтмеченных = тзЗаказыПоОснованиям.НайтиСтроки(Новый Структура("Пометка", Истина));
	тзОтмеченныхЗаказов = тзЗаказыПоОснованиям.Скопировать(мОтмеченных);
	мЗаказов = тзОтмеченныхЗаказов.ВыгрузитьКолонку("ЗаказНаЭмиссию");
	ТекстЗапроса = ТекстЗапросаДанныхЭмиссии(мЗаказов);
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивЗаказов", мЗаказов);
	ПакетРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	тзТовары = ПакетРезультатов[1].Выгрузить();
	тзОснования = ПакетРезультатов[3].Выгрузить();
	тзЗаказыПоОснованиям = ПакетРезультатов[2].Выгрузить();

	
	тзСозданныеДокументы = Новый ТаблицаЗначений;
	тзСозданныеДокументы.Колонки.Добавить("ЗаказНаЭмиссию", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ"));
	УспешноеВыполнение = Истина;

	ИтоговыйМассивДляОбработки = тзОснования.ВыгрузитьКолонку("ДокументОснование");
	
	Если ИтоговыйМассивДляОбработки.ВГраница() = -1 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Эл Из ИтоговыйМассивДляОбработки Цикл
		УспешнаяОбработкаПоПоставке = Истина;
		// Для каждого учетного документа создаются документы Заказов на эмиссию кодов маркировки
		// с учетом того, чтобы в каждом документе эмиссии было не более 10 строк.
		// например: в поступлении 300 строк - создается 30 заказов на эмиссию.
		
		мМассивовСтрок = Новый Массив;
		
		СтрукПоиска = Новый Структура("ДокументОснование", Эл);
		мСтрокТоваровДляЗаказа = тзТовары.НайтиСтроки(СтрукПоиска);
		Если мСтрокТоваровДляЗаказа.ВГраница() = -1 Тогда
			Продолжить;	
		КонецЕсли;
		тчТовары = тзТовары.Скопировать(мСтрокТоваровДляЗаказа);
		тчТовары.Свернуть("Номенклатура, Характеристика", "Количество, КоличествоУпаковок");
		
		мСтрокДляОдногоДокумента = Новый Массив;
		
		Сч = 1;
		Обработано = 0;
		КоличествоСтрок = тчТовары.Количество();
		Для каждого стрТч Из тчТовары Цикл
			Обработано = Обработано + 1;
			ОбобенностьУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(стрТч["Номенклатура"], "ОсобенностьУчета");
			Если ОбобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция Тогда
				Если Сч <= Мин(10, КоличествоСтрок) Тогда
					мСтрокДляОдногоДокумента.Добавить(стрТч);
				Иначе
					Сч = 1;
					мМассивовСтрок.Добавить(мСтрокДляОдногоДокумента);
					мСтрокДляОдногоДокумента = Новый Массив;
					мСтрокДляОдногоДокумента.Добавить(стрТч);
				КонецЕсли;
				Сч = Сч + 1;
			КонецЕсли;
			
		КонецЦикла;
		Если Обработано = КоличествоСтрок И мСтрокДляОдногоДокумента.ВГраница() > -1 Тогда
			мМассивовСтрок.Добавить(мСтрокДляОдногоДокумента);
		КонецЕсли;

		Для каждого ЭлМассив Из мМассивовСтрок Цикл
			
			тзТоварыДляОбработки = тчТовары.Скопировать(ЭлМассив);
			ДопПараметры = Новый Структура();
				Если ТипЗнч(Эл) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
					ИмяНабора = "ВозвращаемыеТовары";
				Иначе
					ИмяНабора = "Товары";
				КонецЕсли;
			ДопПараметры.Вставить(ИмяНабора, тзТоварыДляОбработки);
			ДопПараметры.Вставить("НеПроверятьНаличиеЗаказаНаЭмиссию", Истина);
			ДопПараметры.Вставить("ПринудительноЗаполнятьОтветственного", Истина);
			
			Если Отладка Тогда
				НачатьТранзакцию();
			КонецЕсли;
			
			Результат = гф_ЭмиссияКодовМаркировкиВызовСервера.гф_СоздатьЗаказНаЭмиссиюКодовМаркировки(Эл, ДопПараметры);

			Если Не (ЗначениеЗаполнено(Результат["ЗаказНаЭмиссиюКодовМаркировкиСУЗ"]) 
				И Результат["ЗаказКодовВозможен"]) Тогда
				УспешноеВыполнение = Ложь;
				УспешнаяОбработкаПоПоставке = Ложь;
			Иначе
				нсСозданныеЗаказы = тзСозданныеДокументы.Добавить();
				нсСозданныеЗаказы["ЗаказНаЭмиссию"] = Результат["ЗаказНаЭмиссиюКодовМаркировкиСУЗ"];
			КонецЕсли;
			
			
			Если Отладка Тогда
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();	
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если УспешнаяОбработкаПоПоставке Тогда
			
			мЗаказовПоОснованию = тзЗаказыПоОснованиям.НайтиСтроки(Новый Структура("ДокументОснование" , Эл));
			
			Для каждого СтрЗаказПоОснованию Из мЗаказовПоОснованию Цикл
				обЗаказЭмиссии = СтрЗаказПоОснованию["ЗаказНаЭмиссию"].ПолучитьОбъект();
				мУдаляемыхGTIN = Новый Массив;
				Для каждого стрТч Из обЗаказЭмиссии.Товары Цикл
					Если стрТч["GTIN"] = "" Тогда
						мУдаляемыхGTIN.Добавить(стрТч);	
					КонецЕсли;
				КонецЦикла;
				
				Для каждого элУдаляемыхGTIN Из мУдаляемыхGTIN Цикл
					обЗаказЭмиссии.Товары.Удалить(элУдаляемыхGTIN);
				КонецЦикла;
				Если Отладка Тогда
					НачатьТранзакцию();
				КонецЕсли;	
				обЗаказЭмиссии.Записать(?(обЗаказЭмиссии.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
				
				Если Отладка Тогда
					Если ТранзакцияАктивна() Тогда
						ОтменитьТранзакцию();
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;	
		
		КонецЕсли;	
	КонецЦикла;

	
	Если УспешноеВыполнение Тогда
		РезультатОбработки = Истина;
	Иначе
		РезультатОбработки = Ложь;
	КонецЕсли;
	
	Объект.СозданныеДокументы.Загрузить(тзСозданныеДокументы);
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьНаСервере()
	Объект.ИсправляемыеДокументы.Очистить();
	Объект.СозданныеДокументы.Очистить();
	ТекстЗапроса = ТекстЗапросаДанныхЭмиссии();
	Запрос = Новый Запрос(ТекстЗапроса);
	ПакетРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	тзЗаказыПоОснованиям = ПакетРезультатов[2].Выгрузить();
	Объект.ИсправляемыеДокументы.Загрузить(тзЗаказыПоОснованиям);
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	СоздатьДокументыНаСервере();
КонецПроцедуры

Функция ТекстЗапросаДанныхЭмиссии(МассивЗаказов = Неопределено)
	#Область текст_запроса
	
	Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.Ссылка КАК ЗаказНаЭмиссию,
	|	Таблица.Проведен КАК Проведен,
	|	Таблица.ПометкаУдаления КАК ПометкаУдаления,
	|	Таблица.Организация КАК Организация,
	|	Таблица.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ вт_Заказы
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовИСМП КАК СтатусыДокументовИСМП
	|		ПО (СтатусыДокументовИСМП.Документ = Таблица.Ссылка)
	|ГДЕ
	|	НЕ Таблица.ПометкаУдаления
	|	И СтатусыДокументовИСМП.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиЗаказовНаЭмиссиюКодовМаркировкиИСМП.СУЗКодыМаркировкиЭмитированыЧастично)
	|	И &УсловиеОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Заказы.ДокументОснование КАК ДокументОснование,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Номенклатура КАК Номенклатура,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Характеристика КАК Характеристика,
	|	СУММА(ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Количество) КАК Количество,
	|	СУММА(ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ вт_товары
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Заказы КАК вт_Заказы
	|		ПО ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = вт_Заказы.ЗаказНаЭмиссию
	|ГДЕ
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN = """"
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Заказы.ДокументОснование,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Номенклатура,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 10000
	|	ИСТИНА КАК Пометка,
	|	вт_Заказы.ЗаказНаЭмиссию КАК ЗаказНаЭмиссию,
	|	вт_Заказы.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ вт_ЗаказыПоОснованиям
	|ИЗ
	|	вт_Заказы КАК вт_Заказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары
	|		ПО (ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.Ссылка = вт_Заказы.ЗаказНаЭмиссию)
	|ГДЕ
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗТовары.GTIN = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	вт_ЗаказыПоОснованиям.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ вт_Основания
	|ИЗ
	|	вт_ЗаказыПоОснованиям КАК вт_ЗаказыПоОснованиям";
	#КонецОбласти	
	
	Если МассивЗаказов <> Неопределено Тогда
		ТестЗапроса = СтрЗаменить(Текст, "&УсловиеОтбора", " Таблица.Ссылка В (&МассивЗаказов) ");
	Иначе
		ТестЗапроса = СтрЗаменить(Текст, "&УсловиеОтбора", " ИСТИНА ");
	КонецЕсли;
	
	Возврат ТестЗапроса;
	
КонецФункции 


&НаКлиенте
Процедура УстановитьПометку(Команда)
	Для каждого стрТч Из Объект.ИсправляемыеДокументы Цикл
		стрТЧ["Пометка"] = Истина;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура СнятьПометку(Команда)
	Для каждого стрТч Из Объект.ИсправляемыеДокументы Цикл
		стрТЧ["Пометка"] = Ложь;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ИнвертироватьПометку(Команда)
	Для каждого стрТч Из Объект.ИсправляемыеДокументы Цикл
		стрТЧ["Пометка"] = Не стрТЧ["Пометка"];
	КонецЦикла;
КонецПроцедуры

