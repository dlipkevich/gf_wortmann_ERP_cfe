
&НаКлиенте
Процедура ЗагрузитьИзEcxel(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВыборGTIN", ВыборGTIN);
	Оповещение = Новый ОписаниеОповещения("ВыборВСписокЗавершение", ЭтотОбъект);	
	
	ОткрытьФорму("Обработка.гф_УстановкаСтатусаПолученияGTIN.Форма.ФормаЗагрузки", СтруктураПараметров, ЭтотОбъект, , , ,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ВыборВСписокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И
		
		Результат.Действие = "ЗаполнениеСпискаОтбора" Тогда
			
			Список.Очистить();
			Для каждого Значение Из Результат.МассивДанных Цикл
				
				Список.Добавить(Значение);
			КонецЦикла;
		    Элементы.Список.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыборGTIN = 1;
		
КонецПроцедуры 

&НаСервере
Функция НаборСвойств()
	
	НаборыСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(Справочники.Номенклатура.ПустаяСсылка());
	ДоступныеНаборыСвойств = Новый Массив;

	Для каждого Строка Из НаборыСвойств Цикл
		ДоступныеНаборыСвойств.Добавить(Строка.Набор);
	КонецЦикла;
	Возврат ДоступныеНаборыСвойств[0]; 
	
КонецФункции

&НаКлиенте
Процедура СвойствоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказатьДополнительныеСведения"); 
	ПараметрыФормы.Вставить("Набор", НаборСвойств());
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
    Оповещение = Новый ОписаниеОповещения("ВыборСвойстваЗавершение", ЭтотОбъект);

	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , , ,
		Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыборСвойстваЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Свойство = Результат;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ВидНоменклатурыОбувь = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Обувь");
	
	Если ВидНоменклатуры.Количество() = 0 Тогда
		СписокВидовНоменклатуры = Новый Массив;
		СписокВидовНоменклатуры.Добавить(ВидНоменклатурыОбувь);
	Иначе	
		СписокВидовНоменклатуры = ВидНоменклатуры.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументЗагрузки) Тогда
		
		ТкстСоединения = Символы.НПП + "ИЗ
		|	ВТ_НоменклатураОсн КАК ВТ_НоменклатураОсн
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_НоменклатураДоп КАК ВТ_НоменклатураДоп
		|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_НоменклатураШтрих КАК ВТ_НоменклатураШтрих
		|				ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ПТУ КАК ВТ_ПТУ
		|				ПО ВТ_НоменклатураШтрих.Номенклатура = ВТ_ПТУ.Номенклатура
		|			ПО ВТ_НоменклатураДоп.Объект = ВТ_НоменклатураШтрих.Номенклатура
		|		ПО ВТ_НоменклатураОсн.Ссылка = ВТ_НоменклатураДоп.Объект" + Символы.НПП;
	Иначе
		 ТкстСоединения = Символы.НПП + "ИЗ
		|	ВТ_ПТУ КАК ВТ_ПТУ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НоменклатураОсн КАК ВТ_НоменклатураОсн
		|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_НоменклатураДоп КАК ВТ_НоменклатураДоп
		|				ПОЛНОЕ СОЕДИНЕНИЕ ВТ_НоменклатураШтрих КАК ВТ_НоменклатураШтрих
		|				ПО ВТ_НоменклатураДоп.Объект = ВТ_НоменклатураШтрих.Номенклатура
		|			ПО ВТ_НоменклатураОсн.Ссылка = ВТ_НоменклатураДоп.Объект
		|		ПО ВТ_ПТУ.Номенклатура = ВТ_НоменклатураОсн.Ссылка" + Символы.НПП;

	КонецЕсли;		
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_НоменклатураОсн
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ОтборПоВидуНоменклатуры
		|				ТОГДА Номенклатура.ВидНоменклатуры В (&ВидНоменклатуры)
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Сезон <> ЗНАЧЕНИЕ(Справочник.КоллекцииНоменклатуры.ПустаяСсылка)
		|				ТОГДА Номенклатура.КоллекцияНоменклатуры = &Сезон
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &КодТНВЭД <> ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
		|				ТОГДА Номенклатура.КодТНВЭД = &КодТНВЭД
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Артикул <> """"
		|				ТОГДА Номенклатура.Артикул = &Артикул
		|			ИНАЧЕ ВЫБОР
		|					КОГДА &ОтборПоСпискуАртикул
		|						ТОГДА Номенклатура.Артикул В (&Список)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Объект КАК Объект
		|ПОМЕСТИТЬ ВТ_НоменклатураДоп
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Свойство <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка)
		|				ТОГДА ДополнительныеСведения.Свойство = &Свойство
		|						И ДополнительныеСведения.Значение = &Значение
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_НоменклатураШтрих
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Штрихкод <> """"
		|				ТОГДА ШтрихкодыНоменклатуры.Штрихкод = &Штрихкод
		|			ИНАЧЕ ВЫБОР
		|					КОГДА &ОтборПоСпискуGTIN
		|						ТОГДА ШтрихкодыНоменклатуры.Штрихкод В (&Список)
		|				КОНЕЦ
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_ПТУ
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ПТУ <> ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
		|				ТОГДА ПриобретениеТоваровУслугТовары.Ссылка = &ПТУ
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВТ_НоменклатураОсн.Ссылка <> ЗНАЧЕНИЕ(Справочник.Номенклатура.пустаяСсылка)
		|			ТОГДА ВТ_НоменклатураОсн.Ссылка
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВТ_НоменклатураДоп.Объект <> ЗНАЧЕНИЕ(Справочник.Номенклатура.пустаяСсылка)
		|					ТОГДА ВТ_НоменклатураДоп.Объект
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВТ_НоменклатураШтрих.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.пустаяСсылка)
		|							ТОГДА ВТ_НоменклатураШтрих.Номенклатура
		|						ИНАЧЕ ВТ_ПТУ.Номенклатура
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_НоменклатураВся"
		+ ТкстСоединения +
		";
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НоменклатураВся.Номенклатура КАК Номенклатура,
		|	СоответствиеОрганизации.Организация КАК Организация
		|ПОМЕСТИТЬ ВТ_НоменклатураОрганизация
		|ИЗ
		|	ВТ_НоменклатураВся КАК ВТ_НоменклатураВся
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гф_СоответствиеОрганизацииГруппеДступаИСписка КАК СоответствиеОрганизации
		|		ПО ВТ_НоменклатураВся.Номенклатура.ГруппаДоступа = СоответствиеОрганизации.ГруппаДоступа
		|			И ВТ_НоменклатураВся.Номенклатура.Родитель = СоответствиеОрганизации.ГруппаСписка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	гф_СтатусыGTINСрезПоследних.Статус КАК Статус,
		|	гф_СтатусыGTINСрезПоследних.GTIN КАК GTIN
		|ПОМЕСТИТЬ ВТ_СтатусыGTIN
		|ИЗ
		|	РегистрСведений.гф_СтатусыGTIN.СрезПоследних КАК гф_СтатусыGTINСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_НоменклатураОрганизация.Номенклатура КАК Номенклатура,
		|	ВТ_НоменклатураОрганизация.Номенклатура.Артикул КАК Артикул,
		|	ВТ_НоменклатураОрганизация.Организация КАК Организация,
		|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
		|	ВТ_СтатусыGTIN.Статус КАК Статус
		|ИЗ
		|	ВТ_НоменклатураОрганизация КАК ВТ_НоменклатураОрганизация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусыGTIN КАК ВТ_СтатусыGTIN
		|			ПО ШтрихкодыНоменклатуры.Штрихкод = ВТ_СтатусыGTIN.GTIN
		|		ПО ВТ_НоменклатураОрганизация.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Штрихкод <> """"
		|				ТОГДА ШтрихкодыНоменклатуры.Штрихкод = &Штрихкод
		|			ИНАЧЕ ВЫБОР
		|					КОГДА &ОтборПоСпискуGTIN
		|						ТОГДА ШтрихкодыНоменклатуры.Штрихкод В (&Список)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &СтатусНК <> ЗНАЧЕНИЕ(Перечисление.гф_СостоянияСтатусовGTIN.ПустаяСсылка)
		|				ТОГДА ШтрихкодыНоменклатуры.гф_СостояниеВыгрузкиНоменклатуры = &СтатусНК
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ШтрихкодыНоменклатуры.Штрихкод <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	Артикул";
	 	
	Запрос.УстановитьПараметр("Артикул", Артикул);
	Запрос.УстановитьПараметр("Значение", ЗначениеСвойства);
	Запрос.УстановитьПараметр("КодТНВЭД", КодТНВЭД);
	Запрос.УстановитьПараметр("ПТУ", ДокументЗагрузки);
	Запрос.УстановитьПараметр("Свойство", Свойство);
	Запрос.УстановитьПараметр("Сезон", Сезон);
	Запрос.УстановитьПараметр("СтатусНК", СтатусНК);
    Запрос.УстановитьПараметр("Список", Список);
	Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
	Запрос.УстановитьПараметр("ВидНоменклатуры", СписокВидовНоменклатуры); 
	Запрос.УстановитьПараметр("ОтборПоВидуНоменклатуры", Истина);

	Если Список.Количество() = 0 Тогда
		Запрос.УстановитьПараметр("ОтборПоСпискуGTIN", Ложь);
		Запрос.УстановитьПараметр("ОтборПоСпискуАртикул", Ложь);
	Иначе 
		Запрос.УстановитьПараметр("ОтборПоСпискуGTIN", ?(ВыборGTIN = "1", Истина, Ложь));
		Запрос.УстановитьПараметр("ОтборПоСпискуАртикул", ?(ВыборGTIN = "2", Истина, Ложь));
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Объект.Товары.Очистить();
	
	Объект.Товары.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусGTINНаСервере()
	
	СтатсусПолучен = Перечисления.гф_СтатусыGTIN_В_НК.GTINПолучен;
	ДатаСтатуса = ТекущаяДатаСеанса();
	
	Для каждого Строка Из Объект.Товары Цикл
		
		НоваяЗапись = РегистрыСведений.гф_СтатусыGTIN.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период = ДатаСтатуса;
		НоваяЗапись.GTIN = Строка.Штрихкод;
		НоваяЗапись.Номенклатура = Строка.Номенклатура;
		НоваяЗапись.Характеристика = Строка.Характеристика;
		НоваяЗапись.Организация = Строка.Организация;
		
		Если Строка.СтатусGTIN <> СтатсусПолучен Тогда
			НоваяЗапись.Статус = СтатсусПолучен;		
			НоваяЗапись.Записать();
		КонецЕсли; 
	
	КонецЦикла;	
	
	ОбновитьТЧТовары();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусGTIN(Команда)
	
	//Если Не ЗначениеЗаполнено(Организация) Тогда
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнено значение Организации необходимое для установки статуса GTIN");
	//	Возврат;
	//КонецЕсли;
	ТекстВопроса = НСтр("ru='Будет установлен статус GTIN получен. Продолжить?'");
	ДопПараметры = Новый Структура() ;
	Оповещение = Новый ОписаниеОповещения("гф_ВопросУстановитьСтатусЗавершение", ЭтотОбъект, ДопПараметры);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);

КонецПроцедуры

&НаКлиенте
Процедура гф_ВопросУстановитьСтатусЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьСтатусGTINНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьТЧТовары()

	МассивШтрихкодов = Объект.Товары.Выгрузить(,"Штрихкод");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гф_СтатусыGTINСрезПоследних.Номенклатура КАК Номенклатура,
		|	гф_СтатусыGTINСрезПоследних.Номенклатура.Артикул КАК Артикул,
		|	гф_СтатусыGTINСрезПоследних.Характеристика КАК Характеристика,
		|	гф_СтатусыGTINСрезПоследних.GTIN КАК Штрихкод,
		|	гф_СтатусыGTINСрезПоследних.Статус КАК СтатусGTIN
		|ИЗ
		|	РегистрСведений.гф_СтатусыGTIN.СрезПоследних КАК гф_СтатусыGTINСрезПоследних
		|ГДЕ
		|	гф_СтатусыGTINСрезПоследних.GTIN В(&МассивШтрихкодов)";
	
	Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Объект.Товары.Очистить();

	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры
