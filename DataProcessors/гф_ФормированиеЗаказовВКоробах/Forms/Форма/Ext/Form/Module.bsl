
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	ИспользованиеОтбораПоПериоду = ЗначениеЗаполнено(ОтборПоДатеЗаказов.ДатаОкончания)
	ИЛИ ЗначениеЗаполнено(ОтборПоДатеЗаказов.ДатаНачала);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ДинСписок, 
	"НачалоПериода", 
	ОтборПоДатеЗаказов.ДатаНачала, 
	ИспользованиеОтбораПоПериоду);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
	ДинСписок, 
	"КонецПериода", 
	?(Не ЗначениеЗаполнено(ОтборПоДатеЗаказов.ДатаОкончания),
	КонецДня(Дата('29991231')), КонецДня(ОтборПоДатеЗаказов.ДатаОкончания)), 
	ИспользованиеОтбораПоПериоду);
		
	СхемаКомпоновкиДанных = Элементы.ДинСписок.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
    НастройкиКомпоновкиДанных = Элементы.ДинСписок.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
    
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
	НастройкиКомпоновкиДанных, , ,
	Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
    
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
    
    тз = Новый ТаблицаЗначений;
    
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    ПроцессорВывода.УстановитьОбъект(тз);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);

	тз.Сортировать("Организация, Поставщик, НаименованиеВарианта");
	
	Объект.ЗаказыКлиентов.Загрузить(тз);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПометки(Признак = Неопределено)
	
	Для каждого ЭлКоллекции Из Объект.ЗаказыКлиентов Цикл
		Если Признак = Неопределено Тогда
			ЭлКоллекции["Отметка"] = Не ЭлКоллекции["Отметка"];
		Иначе
			ЭлКоллекции["Отметка"] = Признак;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометку(Команда) 
	УстановитьЗначениеПометки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометку(Команда)
	УстановитьЗначениеПометки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИнвертироватьПометку(Команда)
	УстановитьЗначениеПометки();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//ОбновитьСписокНаСервере();
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаВыбратьОстаткиДляВариантовДляОбработки()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	т.Организация КАК Организация,
	|	т.ВариантКомплектации КАК ВариантКомплектации,
	|	т.Поставщик КАК Поставщик,
	|	т.Склад КАК Склад,
	|	т.ЗаказКлиента КАК ЗаказКлиента,
	|	т.Количество КАК Количество,
	|	т.Отметка КАК Отметка
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&тз КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тз.Организация КАК Организация,
	|	тз.ВариантКомплектации КАК ВариантКомплектации,
	|	тз.Поставщик КАК Поставщик,
	|	тз.Склад КАК Склад,
	|	тз.ЗаказКлиента КАК ЗаказКлиента,
	|	тз.Количество КАК Количество,
	|	ИСТИНА КАК Отметка
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	вт КАК тз
	|ГДЕ
	|	тз.Отметка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.Организация КАК Организация,
	|	ИсходныеДанные.Поставщик КАК Поставщик,
	|	ИсходныеДанные.Склад КАК Склад,
	|	ИсходныеДанные.ВариантКомплектации КАК ВариантКомплектации,
	|	СУММА(ИсходныеДанные.Количество) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиПредварительно
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Организация,
	|	ИсходныеДанные.ВариантКомплектации,
	|	ИсходныеДанные.Поставщик,
	|	ИсходныеДанные.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Шапка.Организация,
	|	Шапка.Контрагент,
	|	Шапка.Склад,
	|	ПродукцияВКоробах.ВариантКомплектации,
	|	СУММА(ВЫБОР
	|			КОГДА ПродукцияВКоробах.КоличествоКоробов = 0
	|				ТОГДА -1
	|			ИНАЧЕ -ПродукцияВКоробах.КоличествоКоробов
	|		КОНЕЦ)
	|ИЗ
	|	Документ.ЗаказПоставщику.гф_ПродукцияВКоробах КАК ПродукцияВКоробах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК Шапка
	|		ПО ПродукцияВКоробах.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Шапка.Проведен
	|	И Шапка.Организация В
	|			(ВЫБРАТЬ
	|				т.Организация
	|			ИЗ
	|				ИсходныеДанные КАК т)
	|	И Шапка.Контрагент В
	|			(ВЫБРАТЬ
	|				т1.Поставщик
	|			ИЗ
	|				ИсходныеДанные КАК т1)
	|	И ПродукцияВКоробах.ВариантКомплектации В
	|			(ВЫБРАТЬ
	|				т2.ВариантКомплектации
	|			ИЗ
	|				ИсходныеДанные КАК т2)
	|	И Шапка.Склад В
	|			(ВЫБРАТЬ
	|				т3.Склад
	|			ИЗ
	|				ИсходныеДанные КАК т3)
	|
	|СГРУППИРОВАТЬ ПО
	|	Шапка.Организация,
	|	Шапка.Контрагент,
	|	ПродукцияВКоробах.ВариантКомплектации,
	|	Шапка.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПредварительно.Организация КАК Организация,
	|	ОстаткиПредварительно.Поставщик КАК Поставщик,
	|	ОстаткиПредварительно.Склад КАК Склад,
	|	ОстаткиПредварительно.ВариантКомплектации КАК ВариантКомплектации,
	|	СУММА(ОстаткиПредварительно.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ остатки
	|ИЗ
	|	ОстаткиПредварительно КАК ОстаткиПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПредварительно.Организация,
	|	ОстаткиПредварительно.Поставщик,
	|	ОстаткиПредварительно.Склад,
	|	ОстаткиПредварительно.ВариантКомплектации
	|
	|ИМЕЮЩИЕ
	|	НЕ СУММА(ОстаткиПредварительно.КоличествоОстаток) = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказПоставщику.Ссылка КАК Ссылка,
	|	ЗаказПоставщику.Статус КАК Статус,
	|	ЗаказПоставщику.Контрагент КАК Контрагент,
	|	ЗаказПоставщику.Организация КАК Организация,
	|	ЗаказПоставщику.Склад КАК Склад
	|ПОМЕСТИТЬ ЗаказыПоОстаткам
	|ИЗ
	|	остатки КАК остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|		ПО (ЗаказПоставщику.Проведен)
	|			И (ЗаказПоставщику.Организация = остатки.Организация)
	|			И (ЗаказПоставщику.Контрагент = остатки.Поставщик)
	|			И (ЗаказПоставщику.Склад = остатки.Склад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Заказы.Ссылка КАК Ссылка,
	|	Заказы.Статус КАК Статус,
	|	Заказы.Контрагент КАК Контрагент,
	|	Заказы.Организация КАК Организация,
	|	Заказы.Склад КАК Склад,
	|	ПродукцияВКоробах.ВариантКомплектации КАК ВариантКомплектации,
	|	ПродукцияВКоробах.КоличествоКоробов КАК КоличествоКоробов
	|ПОМЕСТИТЬ ВариантыВЗаказахПоОстаткам
	|ИЗ
	|	ЗаказыПоОстаткам КАК Заказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.гф_ПродукцияВКоробах КАК ПродукцияВКоробах
	|		ПО Заказы.Ссылка = ПродукцияВКоробах.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	остатки.Организация КАК Организация,
	|	остатки.Поставщик КАК Поставщик,
	|	остатки.Склад КАК Склад,
	|	остатки.ВариантКомплектации КАК ВариантКомплектации,
	|	остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ЗаказыКСозданиюПредварительно
	|ИЗ
	|	остатки КАК остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВариантыВЗаказахПоОстаткам КАК ВариантыВЗаказахПоОстаткам
	|		ПО остатки.Организация = ВариантыВЗаказахПоОстаткам.Организация
	|			И остатки.Поставщик = ВариантыВЗаказахПоОстаткам.Контрагент
	|			И остатки.ВариантКомплектации = ВариантыВЗаказахПоОстаткам.ВариантКомплектации
	|ГДЕ
	|	ВариантыВЗаказахПоОстаткам.Ссылка ЕСТЬ NULL
	|	И остатки.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000000
	|	остатки.Организация КАК Организация,
	|	остатки.Поставщик КАК Поставщик,
	|	остатки.Склад КАК Склад,
	|	остатки.ВариантКомплектации КАК ВариантКомплектации,
	|	ВариантыВЗаказахПоОстаткам.Ссылка КАК ЗаказПоставщику,
	|	ВариантыВЗаказахПоОстаткам.Ссылка.Дата КАК ДатаЗаказаПоставщику,
	|	ВариантыВЗаказахПоОстаткам.Статус КАК СтатусЗаказаПоставщику,
	|	ВЫБОР
	|		КОГДА ВариантыВЗаказахПоОстаткам.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.НеСогласован)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК ПорядокПоСтатусу,
	|	ЕСТЬNULL(ВариантыВЗаказахПоОстаткам.КоличествоКоробов, 0) КАК КоличествоКоробовЗаказа,
	|	остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ЗаказыКИзменениюПредварительно
	|ИЗ
	|	остатки КАК остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВариантыВЗаказахПоОстаткам КАК ВариантыВЗаказахПоОстаткам
	|		ПО остатки.Организация = ВариантыВЗаказахПоОстаткам.Организация
	|			И остатки.Поставщик = ВариантыВЗаказахПоОстаткам.Контрагент
	|			И остатки.ВариантКомплектации = ВариантыВЗаказахПоОстаткам.ВариантКомплектации
	|ГДЕ
	|	ВариантыВЗаказахПоОстаткам.Ссылка ЕСТЬ НЕ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокПоСтатусу,
	|	ДатаЗаказаПоставщику,
	|	ЗаказПоставщику
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКИзменениюПредварительно.Организация КАК Организация,
	|	ЗаказыКИзменениюПредварительно.Поставщик КАК Поставщик,
	|	ЗаказыКИзменениюПредварительно.Склад КАК Склад,
	|	ЗаказыКИзменениюПредварительно.ВариантКомплектации КАК ВариантКомплектации,
	|	МАКСИМУМ(ЗаказыКИзменениюПредварительно.ЗаказПоставщику) КАК ЗаказПоставщику
	|ПОМЕСТИТЬ ЗаказыКИзменениюПредварительно_2
	|ИЗ
	|	ЗаказыКИзменениюПредварительно КАК ЗаказыКИзменениюПредварительно
	|ГДЕ
	|	ЗаказыКИзменениюПредварительно.ПорядокПоСтатусу = 1
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыКИзменениюПредварительно.Организация,
	|	ЗаказыКИзменениюПредварительно.Поставщик,
	|	ЗаказыКИзменениюПредварительно.Склад,
	|	ЗаказыКИзменениюПредварительно.ВариантКомплектации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКИзменениюПредварительно.Организация КАК Организация,
	|	ЗаказыКИзменениюПредварительно.Поставщик КАК Поставщик,
	|	ЗаказыКИзменениюПредварительно.Склад КАК Склад,
	|	ЗаказыКИзменениюПредварительно.ВариантКомплектации КАК ВариантКомплектации,
	|	МИНИМУМ(ЗаказыКИзменениюПредварительно.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ЗаказыКСозданиюПредварительно_2
	|ИЗ
	|	ЗаказыКИзменениюПредварительно КАК ЗаказыКИзменениюПредварительно
	|ГДЕ
	|	ЗаказыКИзменениюПредварительно.ПорядокПоСтатусу = 2
	|	И ЗаказыКИзменениюПредварительно.КоличествоОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыКИзменениюПредварительно.Организация,
	|	ЗаказыКИзменениюПредварительно.Поставщик,
	|	ЗаказыКИзменениюПредварительно.Склад,
	|	ЗаказыКИзменениюПредварительно.ВариантКомплектации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	т1.Организация КАК Организация,
	|	т1.Поставщик КАК Поставщик,
	|	т1.Склад КАК Склад,
	|	т1.ВариантКомплектации КАК ВариантКомплектации,
	|	т1.ЗаказПоставщику КАК ЗаказПоставщику,
	|	т1.КоличествоКоробовЗаказа КАК КоличествоКоробовЗаказа,
	|	т1.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ЗаказыКИзменению
	|ИЗ
	|	ЗаказыКИзменениюПредварительно КАК т1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыКИзменениюПредварительно_2 КАК т2
	|		ПО т1.Организация = т2.Организация
	|			И т1.Поставщик = т2.Поставщик
	|			И т1.Склад = т2.Склад
	|			И т1.ВариантКомплектации = т2.ВариантКомплектации
	|			И т1.ЗаказПоставщику = т2.ЗаказПоставщику
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКСозданиюПредварительно.Организация КАК Организация,
	|	ЗаказыКСозданиюПредварительно.Поставщик КАК Поставщик,
	|	ЗаказыКСозданиюПредварительно.Склад КАК Склад,
	|	ЗаказыКСозданиюПредварительно.ВариантКомплектации КАК ВариантКомплектации,
	|	ЗаказыКСозданиюПредварительно.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ЗаказыКСозданию
	|ИЗ
	|	ЗаказыКСозданиюПредварительно КАК ЗаказыКСозданиюПредварительно
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыКСозданиюПредварительно_2.Организация,
	|	ЗаказыКСозданиюПредварительно_2.Поставщик,
	|	ЗаказыКСозданиюПредварительно_2.Склад,
	|	ЗаказыКСозданиюПредварительно_2.ВариантКомплектации,
	|	ЗаказыКСозданиюПредварительно_2.КоличествоОстаток
	|ИЗ
	|	ЗаказыКСозданиюПредварительно_2 КАК ЗаказыКСозданиюПредварительно_2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.Организация КАК Организация,
	|	Шапка.Контрагент КАК Контрагент,
	|	Шапка.Склад КАК Склад,
	|	ПродукцияВКоробах.ВариантКомплектации КАК ВариантКомплектации,
	|	СУММА(ВЫБОР
	|			КОГДА ПродукцияВКоробах.КоличествоКоробов = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПродукцияВКоробах.КоличествоКоробов
	|		КОНЕЦ) КАК КоличествоКоробов,
	|	Шапка.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗаказыДляОстатковПротокол
	|ИЗ
	|	Документ.ЗаказПоставщику.гф_ПродукцияВКоробах КАК ПродукцияВКоробах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК Шапка
	|		ПО (ПродукцияВКоробах.Ссылка = Шапка.Ссылка)
	|ГДЕ
	|	Шапка.Проведен
	|	И Шапка.Организация В
	|			(ВЫБРАТЬ
	|				т.Организация
	|			ИЗ
	|				ИсходныеДанные КАК т)
	|	И Шапка.Контрагент В
	|			(ВЫБРАТЬ
	|				т1.Поставщик
	|			ИЗ
	|				ИсходныеДанные КАК т1)
	|	И ПродукцияВКоробах.ВариантКомплектации В
	|			(ВЫБРАТЬ
	|				т2.ВариантКомплектации
	|			ИЗ
	|				ИсходныеДанные КАК т2)
	|	И Шапка.Склад В
	|			(ВЫБРАТЬ
	|				т3.Склад
	|			ИЗ
	|				ИсходныеДанные КАК т3)
	|
	|СГРУППИРОВАТЬ ПО
	|	Шапка.Организация,
	|	Шапка.Контрагент,
	|	ПродукцияВКоробах.ВариантКомплектации,
	|	Шапка.Склад,
	|	Шапка.Ссылка";
	
	Возврат ТекстЗапроса;
КонецФункции 

&НаСервере
Функция ВыбратьОстаткиДляВариантовДляОбработки()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = ТекстЗапросаВыбратьОстаткиДляВариантовДляОбработки();
	
	тз = Объект.ЗаказыКлиентов.Выгрузить();
	Запрос.УстановитьПараметр("тз", тз);
	УстановитьПривилегированныйРежим(Истина);
	ПакетРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	Возврат ПакетРезультатов;
	
КонецФункции

&НаСервере
Процедура ВыполнитьАнализПотребностей(ПакетРезультатов)
	Протокол.Очистить();
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Макет = ОбработкаОбъект.ПолучитьМакет("Макет");

	Если РежимОтладки Тогда
		ОбластьИнформация = Макет.ПолучитьОбласть("ИнформацияРежим");
		Инфо = "Установлен режим предварительного расчета. Данные в базе не изменены.";
		ОбластьИнформация.Параметры["ИнформацияОРежиме"] = Инфо;
		Протокол.Вывести(ОбластьИнформация);
		Протокол.ФиксацияСверху = 2;
	КонецЕсли;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	Протокол.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	т.ВариантКомплектации КАК ВариантКомплектации,
	|	т.Поставщик КАК Поставщик,
	|	т.Склад КАК Склад,
	|	т.ЗаказКлиента КАК ЗаказКлиента,
	|	т.Количество КАК Количество
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&тз КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.КоллекцияНоменклатуры.Наименование КАК Сезон, 
	|	тз.Поставщик КАК Поставщик,
	|	тз.ВариантКомплектации КАК ВариантКомплектации,
	|	тз.ЗаказКлиента КАК ЗаказКлиента,
	|	докЗаказКлиента.Контрагент КАК Клиент,
	|	тз.Количество КАК Количество
	|ИЗ
	|	вт КАК тз
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|			ПО (ВариантыКомплектацииНоменклатуры.Владелец = Номенклатура.Ссылка)
	|		ПО (тз.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК докЗаказКлиента
	|		ПО (тз.ЗаказКлиента = докЗаказКлиента.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сезон,
	|	Поставщик,
	|	Клиент
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	Сезон,
	|	Поставщик,
	|	ВариантКомплектации";
	Запрос.УстановитьПараметр("тз", ПакетРезультатов[1]);
	ТаблицаОстатковЗаказано = ПакетРезультатов[12].Выгрузить();
	Результат = Запрос.Выполнить();
	
	ВыборкаСезон = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСезон.Следующий() Цикл
		ОбластьСезон = Макет.ПолучитьОбласть("Сезон");
		ОбластьСезон.Параметры["Сезон"] = ВыборкаСезон["Сезон"];
		Протокол.Вывести(ОбластьСезон);
		ВыборкаПоставщик = ВыборкаСезон.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоставщик.Следующий() Цикл
			ВыборкаВариант = ВыборкаПоставщик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ВывестиИтогиПоВарианту(ВыборкаВариант, ТаблицаОстатковЗаказано, Макет);
		КонецЦикла;
	КонецЦикла;
	
	Если Не РежимОтладки Тогда
		ОбластьИнформация = Макет.ПолучитьОбласть("ИнформацияРежим");
		Инфо = "Произведены следующие изменения данных информационной базы:";
		ОбластьИнформация.Параметры["ИнформацияОРежиме"] = Инфо;
		Протокол.Вывести(ОбластьИнформация);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВывестиИтогиПоВарианту(ВыборкаВариант, ТаблицаОстатковЗаказано, Макет)
	Пока ВыборкаВариант.Следующий()  Цикл
		ЗаказаноПоВарианту = 0;
		СтруктураПоискаОстаткиЗаказано = Новый Структура("Контрагент, ВариантКомплектации",
		ВыборкаВариант["Поставщик"], ВыборкаВариант["ВариантКомплектации"]);
		мСтрокЗаказано = ТаблицаОстатковЗаказано.НайтиСтроки(СтруктураПоискаОстаткиЗаказано);
		Для каждого стрЗаказано Из мСтрокЗаказано Цикл
			ЗаказаноПоВарианту = ЗаказаноПоВарианту + стрЗаказано["КоличествоКоробов"];
		КонецЦикла;
		
		ОбластьИтогиПоВарианту = Макет.ПолучитьОбласть("ИтогиПоВарианту");
		ОбластьИтогиПоВарианту.Параметры["ВариантКомплектации"] = ВыборкаВариант["ВариантКомплектации"];
		ОбластьИтогиПоВарианту.Параметры["Поставщик"] = ВыборкаВариант["Поставщик"];
		ОбластьИтогиПоВарианту.Параметры["ПотребностьИТОГО"] = ВыборкаВариант["Количество"];
		ОбластьИтогиПоВарианту.Параметры["РазмещеноИТОГО"] = ЗаказаноПоВарианту;
		ОбластьИтогиПоВарианту.Параметры["ОбеспечитьИТОГО"] = Макс(ВыборкаВариант["Количество"] - ЗаказаноПоВарианту, 0);
		Протокол.Вывести(ОбластьИтогиПоВарианту);
		
		ВыборкаДетали = ВыборкаВариант.Выбрать();
		
		Пока ВыборкаДетали.Следующий() Цикл
			ОбластьЗаказ = Макет.ПолучитьОбласть("СтрокаЗаказы");
			ОбластьЗаказ.Параметры["ЗаказКлиента"] = ВыборкаДетали["ЗаказКлиента"];
			ОбластьЗаказ.Параметры["Клиент"] = ВыборкаДетали["Клиент"];
			ОбластьЗаказ.Параметры["Потребность"] = ВыборкаДетали["Количество"];
			Протокол.Вывести(ОбластьЗаказ);
		КонецЦикла;
		Для каждого стрЗаказано Из мСтрокЗаказано Цикл
			ОбластьЗаказ = Макет.ПолучитьОбласть("СтрокаЗаказы");
			ОбластьЗаказ.Параметры["Размещено"] = стрЗаказано["КоличествоКоробов"];
			ОбластьЗаказ.Параметры["ЗаказПоставщику"] = стрЗаказано["Ссылка"];
			Протокол.Вывести(ОбластьЗаказ);
		КонецЦикла;
		Протокол.Вывести(Макет.ПолучитьОбласть("Пустая"));
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаказыПоставщикуНаСервере()
	ПакетРезультатов = ВыбратьОстаткиДляВариантовДляОбработки();
	
	// 0. Сформировать протокол распределения потребностей
	ВыполнитьАнализПотребностей(ПакетРезультатов);

	РезультатОстаткиКСозданию = ПакетРезультатов[11];
	РезультатОстаткиКИзменению = ПакетРезультатов[7];
	
	Если РезультатОстаткиКСозданию.Пустой() И РезультатОстаткиКИзменению.Пустой() Тогда
		Возврат;	
	КонецЕсли;
	// 1. Создание новых заказов поставщику
	Если Не РезультатОстаткиКСозданию.Пустой() Тогда
		СоздатьЗаказыПоставщику(РезультатОстаткиКСозданию);
	КонецЕсли;
	
	// 2. Изменение существующих заказов поставщику если статус заказа "НеСогласован"
	// или создание новых заказов поставщику, если любой другой статус
	Если Не РезультатОстаткиКИзменению.Пустой() Тогда
		ИзменитьЗаказыПоставщику(РезультатОстаткиКИзменению);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ИзменитьЗаказыПоставщику(РезультатЗапроса)
	
	ТаблицаДляИзмененияПолная = РезультатЗапроса.Выгрузить();
	ТаблицаСвернутоПоРеквизитамЗаказа = ТаблицаДляИзмененияПолная.Скопировать();
	СтрокаСвертки = "Организация, Поставщик, Склад";
	ТаблицаСвернутоПоРеквизитамЗаказа.Свернуть(СтрокаСвертки);
	СтруктураРеквизитов = Новый Структура(СтрокаСвертки);
	Для каждого стрТз Из ТаблицаСвернутоПоРеквизитамЗаказа Цикл
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, стрТз);
		мСтрок = ТаблицаДляИзмененияПолная.НайтиСтроки(СтруктураРеквизитов);
		ТаблицаПоПоставщикуСкладу = ТаблицаДляИзмененияПолная.Скопировать(мСтрок);
		
		// 1. Изменение заказов
		РезультатЗапросаДанныхДляИзмененияЗаказов =
		ПолучитьРезультатыДляИзмененияЗаказовПоставщику(ТаблицаПоПоставщикуСкладу);
		ВыборкаИтогиЗаказИзменение = 
		РезультатЗапросаДанныхДляИзмененияЗаказов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаИтогиЗаказИзменение.Следующий() Цикл
			обЗаказ = ВыборкаИтогиЗаказИзменение["ЗаказПоставщику"].ПолучитьОбъект();
			Если обЗаказ <> Неопределено Тогда
				ПродукцияВКоробах = обЗаказ["гф_ПродукцияВКоробах"].Выгрузить();
				ПродукцияВКоробах.Колонки.Добавить("КоличествоКоробов1",
				Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
				Для каждого стрТЗ Из ПродукцияВКоробах Цикл
					стрТЗ["КоличествоКоробов1"] = стрТЗ["КоличествоКоробов"];
				КонецЦикла;
				ВыборкаИтогиВариант = ВыборкаИтогиЗаказИзменение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаИтогиВариант.Следующий() Цикл
					ВыборкаДетали = ВыборкаИтогиВариант.Выбрать();
					Пока ВыборкаДетали.Следующий() Цикл
						нс = ПродукцияВКоробах.Добавить();
						нс["ВариантКомплектации"] = ВыборкаДетали["ВариантКомплектации"];
						нс["КоличествоКоробов1"] = ВыборкаДетали["КоличествоОстаток"];
					КонецЦикла;
				КонецЦикла;
				
				ПродукцияВКоробах.Свернуть("ВариантКомплектации", "КоличествоКоробов, КоличествоКоробов1");
				Для каждого стрТЗ Из ПродукцияВКоробах Цикл
					стрТЗ["КоличествоКоробов"] = стрТЗ["КоличествоКоробов1"];
				КонецЦикла;
				ПродукцияВКоробах.Колонки.Удалить("КоличествоКоробов1");
				мУдаляемыхСтрок = Новый Массив; 
				Для каждого стрТЗ Из ПродукцияВКоробах Цикл
					Если стрТЗ["КоличествоКоробов"] <= 0 Тогда
						мУдаляемыхСтрок.Добавить(стрТЗ);
					КонецЕсли;
				КонецЦикла;
				Для каждого Эл Из мУдаляемыхСтрок Цикл
					ПродукцияВКоробах.Удалить(Эл);
				КонецЦикла;
				
				ПродукцияВКоробах.Сортировать("КоличествоКоробов");
				обЗаказ["гф_ПродукцияВКоробах"].Загрузить(ПродукцияВКоробах);
				обЗаказ.гф_ПересчитатьТЧТоварыНаОсновнииКоробов();
				ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
				Макет = ОбработкаОбъект.ПолучитьМакет("Макет");
				ОбластьОперация = Макет.ПолучитьОбласть("СтрокаОписаниеИзменений");
				
				НачатьТранзакцию();
				Попытка
					Если обЗаказ.Проведен Тогда
						РежимЗаписи = РежимЗаписиДокумента.Проведение;
					Иначе
						РежимЗаписи = РежимЗаписиДокумента.Запись;
					КонецЕсли;
					обЗаказ.Записать(РежимЗаписи);
					Если Не РежимОтладки Тогда
						ОбластьОперация.Параметры["ОперацияСДанными"] = "Изменен документ:";
						ОбластьОперация.Параметры["Заказ"] = обЗаказ.Ссылка;
						Протокол.Вывести(ОбластьОперация);
					КонецЕсли;
				Исключение
					ОтменитьТранзакцию();
					Если обЗаказ.Проведен Тогда
						НачатьТранзакцию();
						Попытка
							обЗаказ.Проведен = Ложь;
							обЗаказ.Записать(РежимЗаписиДокумента.Запись);
							Если Не РежимОтладки Тогда
								ОбластьОперация.Параметры["ОперацияСДанными"] = "Изменен документ (с отменой проведения):";
								ОбластьОперация.Параметры["Заказ"] = обЗаказ.Ссылка;
								Протокол.Вывести(ОбластьОперация);
							КонецЕсли;
						Исключение
							ОтменитьТранзакцию();
						КонецПопытки;
					КонецЕсли;
				КонецПопытки;
				
				Если РежимОтладки Тогда
					Если ТранзакцияАктивна() Тогда
						ОтменитьТранзакцию();
					КонецЕсли;
				Иначе
					Если ТранзакцияАктивна() Тогда
						ЗафиксироватьТранзакцию();
					КонецЕсли;	
				КонецЕсли;
				
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРезультатыДляСозданияНовыхЗаказовЕслиЗаказЗаблокирован(тз)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("тз", тз);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	т.Организация КАК Организация,
	|	т.Поставщик КАК Поставщик,
	|	т.Склад КАК Склад,
	|	т.ВариантКомплектации КАК ВариантКомплектации,
	|	т.ЗаказПоставщику КАК ЗаказПоставщику,
	|	т.ДатаЗаказаПоставщику КАК ДатаЗаказаПоставщику,
	|	т.СтатусЗаказаПоставщику КАК СтатусЗаказаПоставщику,
	|	т.ПорядокПоСтатусу КАК ПорядокПоСтатусу,
	|	т.КоличествоКоробовЗаказа КАК КоличествоКоробовЗаказа,
	|	т.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	&тз КАК т
	|ГДЕ
	|	НЕ т.СтатусЗаказаПоставщику = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.НеСогласован)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Организация КАК Организация,
	|	Данные.Поставщик КАК Поставщик,
	|	Данные.Склад КАК Склад,
	|	Данные.ВариантКомплектации КАК ВариантКомплектации,
	|	МИНИМУМ(Данные.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	Данные КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Организация,
	|	Данные.Поставщик,
	|	Данные.Склад,
	|	Данные.ВариантКомплектации
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(Данные.КоличествоОстаток) > 0
	|ИТОГИ ПО
	|	Организация,
	|	Поставщик,
	|	Склад";
	
	Результат = Запрос.Выполнить();
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьРезультатыДляИзмененияЗаказовПоставщику(тз)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("тз", тз);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	т.Организация КАК Организация,
	|	т.Поставщик КАК Поставщик,
	|	т.Склад КАК Склад,
	|	т.ВариантКомплектации КАК ВариантКомплектации,
	|	т.ЗаказПоставщику КАК ЗаказПоставщику,
	|	т.ДатаЗаказаПоставщику КАК ДатаЗаказаПоставщику,
	|	т.СтатусЗаказаПоставщику КАК СтатусЗаказаПоставщику,
	|	т.ПорядокПоСтатусу КАК ПорядокПоСтатусу,
	|	т.КоличествоКоробовЗаказа КАК КоличествоКоробовЗаказа,
	|	т.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	&тз КАК т
	|ГДЕ
	|	т.СтатусЗаказаПоставщику = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.НеСогласован)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Организация КАК Организация,
	|	Данные.Поставщик КАК Поставщик,
	|	Данные.Склад КАК Склад,
	|	Данные.ВариантКомплектации КАК ВариантКомплектации,
	|	МИНИМУМ(Данные.ДатаЗаказаПоставщику) КАК ДатаЗаказаПоставщику
	|ПОМЕСТИТЬ ДанныеСвернутоПоДатеЗаказа
	|ИЗ
	|	Данные КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Организация,
	|	Данные.Поставщик,
	|	Данные.Склад,
	|	Данные.ВариантКомплектации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Организация КАК Организация,
	|	Данные.Поставщик КАК Поставщик,
	|	Данные.Склад КАК Склад,
	|	Данные.ВариантКомплектации КАК ВариантКомплектации,
	|	Данные.ДатаЗаказаПоставщику КАК ДатаЗаказаПоставщику,
	|	МИНИМУМ(Данные.ЗаказПоставщику) КАК ЗаказПоставщику
	|ПОМЕСТИТЬ ДанныеСвернутоСсылкеЗаказа
	|ИЗ
	|	Данные КАК Данные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеСвернутоПоДатеЗаказа КАК ДанныеСвернутоПоДатеЗаказа
	|		ПО Данные.Организация = ДанныеСвернутоПоДатеЗаказа.Организация
	|			И Данные.Поставщик = ДанныеСвернутоПоДатеЗаказа.Поставщик
	|			И Данные.Склад = ДанныеСвернутоПоДатеЗаказа.Склад
	|			И Данные.ВариантКомплектации = ДанныеСвернутоПоДатеЗаказа.ВариантКомплектации
	|			И Данные.ДатаЗаказаПоставщику = ДанныеСвернутоПоДатеЗаказа.ДатаЗаказаПоставщику
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Организация,
	|	Данные.Поставщик,
	|	Данные.Склад,
	|	Данные.ВариантКомплектации,
	|	Данные.ДатаЗаказаПоставщику
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	Данные.ЗаказПоставщику КАК ЗаказПоставщику,
	|	Данные.ВариантКомплектации КАК ВариантКомплектации,
	|	Данные.КоличествоКоробовЗаказа КАК КоличествоКоробовЗаказа,
	|	Данные.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	Данные КАК Данные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеСвернутоСсылкеЗаказа КАК ДанныеСвернутоСсылкеЗаказа
	|		ПО (Данные.ЗаказПоставщику = ДанныеСвернутоСсылкеЗаказа.ЗаказПоставщику)
	|			И (Данные.ВариантКомплектации = ДанныеСвернутоСсылкеЗаказа.ВариантКомплектации)
	|			И (Данные.ДатаЗаказаПоставщику = ДанныеСвернутоСсылкеЗаказа.ДатаЗаказаПоставщику)
	|ИТОГИ
	|	Сумма(КоличествоКоробовЗаказа),
	|	Максимум(КоличествоОстаток)
	|ПО
	|	ЗаказПоставщику,
	|	ВариантКомплектации";
	Результат = Запрос.Выполнить();
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СоздатьЗаказыПоставщику(РезультатЗапроса)
	ТаблицаДанных = Объект.ЗаказыКлиентов.Выгрузить();
	ПолнаяТаблицаКСозданию = РезультатЗапроса.Выгрузить();
	
	ТаблицаДанныхЗаказов = ПолнаяТаблицаКСозданию.Скопировать();
	ТаблицаДанныхЗаказов.Свернуть("Организация, Поставщик, Склад");
	
	ДатаЗаказа = НачалоДня(ТекущаяДатаСеанса());
	Для каждого стрТз Из ТаблицаДанныхЗаказов Цикл
		СтруктураПоиска = Новый Структура("Организация, Поставщик, Склад, Отметка");
		СтруктураПоиска["Отметка"] = Истина;
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, стрТз);
		мСтрок = ТаблицаДанных.НайтиСтроки(СтруктураПоиска);
		ТаблицаПоОтбору = ТаблицаДанных.Скопировать(мСтрок);
		мЗаказов = ТаблицаПоОтбору.ВыгрузитьКолонку("ЗаказКлиента");
		мУникальныхЗаказов = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(мУникальныхЗаказов, мЗаказов, Истина);
		
		Основание = ТаблицаПоОтбору[0].ЗаказКлиента;
		
		ДанныеЗаполнения = СтруктураДокументаОснованияНаСервере(Основание, Истина);
		УстановитьПривилегированныйРежим(Истина);
		ЗаказПоставщику = Документы.ЗаказПоставщику.СоздатьДокумент();
		ЗаказПоставщику.Дата = ДатаЗаказа;
		ЗаказПоставщику.Партнер = СтруктураПоиска["Поставщик"].Партнер;
		ЗаказПоставщику.Заполнить(ДанныеЗаполнения);
		
		// vvv Галфинд \ Sakovich 07.04.2023
		// Если мУникальныхЗаказов.ВГраница() = 0 Тогда
		// 	ЗаказПоставщику.ДокументОснование = мУникальныхЗаказов[0];
		// Иначе
		// 	ЗаказПоставщику.ДокументОснование = "";
		// КонецЕсли;
		ЗаказПоставщику.ДокументОснование = "";
		// ^^^ Галфинд \ Sakovich 07.04.2023 
		
		// vvv Галфинд \ Sakovich 19.01.2023
		// e1cib/data/Задача.ЗадачаИсполнителя?ref=8128bcee7bda45d711ed81d995d24d7d
		ЗаказПоставщику.ЦенаВключаетНДС = Истина;
		// ^^^ Галфинд \ Sakovich 19.01.2023 
		
		ЗаказПоставщику.Товары.Очистить();
		СтруктураПоиска.Удалить("Отметка");
		мСтрокВарианты = ПолнаяТаблицаКСозданию.НайтиСтроки(СтруктураПоиска);
		ДанныеПоКоробам = ПолнаяТаблицаКСозданию.Скопировать(мСтрокВарианты);
		ДанныеПоКоробам.Свернуть("ВариантКомплектации", "КоличествоОстаток");
		ДанныеПоКоробам.Сортировать("КоличествоОстаток");
		Для каждого стрТз Из ДанныеПоКоробам Цикл
			нс = ЗаказПоставщику.гф_ПродукцияВКоробах.Добавить();
			нс["ВариантКомплектации"] = стрТз["ВариантКомплектации"];
			нс["КоличествоКоробов"] = стрТз["КоличествоОстаток"];
		КонецЦикла;
		ЗаказПоставщику.гф_ПересчитатьТЧТоварыНаОсновнииКоробов();
		// vvv Галфинд \ Sakovich 05.04.2023
		ЗаказПоставщику.Статус = Перечисления.СтатусыЗаказовПоставщикам.НеСогласован;
		// ^^^ Галфинд \ Sakovich 05.04.2023 
		
		ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
		Макет = ОбработкаОбъект.ПолучитьМакет("Макет");
		ОбластьОперация = Макет.ПолучитьОбласть("СтрокаОписаниеИзменений");
		НачатьТранзакцию();
		Попытка
			ЗаказПоставщику.Записать(РежимЗаписиДокумента.Проведение);
			Если Не РежимОтладки Тогда
				ОбластьОперация.Параметры["ОперацияСДанными"] = "Создан и проведен документ:";
				ОбластьОперация.Параметры["Заказ"] = ЗаказПоставщику.Ссылка;
				Протокол.Вывести(ОбластьОперация);
			КонецЕсли;
		Исключение
			ОтменитьТранзакцию();
			НачатьТранзакцию();
			Попытка
				ЗаказПоставщику.Записать(РежимЗаписиДокумента.Запись);
				Если Не РежимОтладки Тогда
					ОбластьОперация.Параметры["ОперацияСДанными"] = "Создан документ (без проведения):";
					ОбластьОперация.Параметры["Заказ"] = ЗаказПоставщику.Ссылка;
					Протокол.Вывести(ОбластьОперация);
			КонецЕсли;	
			Исключение
				ОтменитьТранзакцию();
			КонецПопытки;
		КонецПопытки;
		
		Если РежимОтладки Тогда
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
		Иначе
			Если ТранзакцияАктивна() Тогда
				ЗафиксироватьТранзакцию();
			КонецЕсли;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Функция СтруктураДокументаОснованияНаСервере(ДокументОснование, ИспользованиеСкладов)
	ЕстьРеквизитШапкиСклад = ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", ДокументОснование.Метаданные());
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Организация", "Организация");
	СтруктураПолей.Вставить("Подразделение", "Подразделение");
	СтруктураПолей.Вставить("ДокументОснование", "Ссылка");
	СтруктураПолей.Вставить("НаправлениеДеятельности");
	
	Если ЕстьРеквизитШапкиСклад Тогда
		СтруктураПолей.Вставить("Склад", "Склад");
		СтруктураПолей.Вставить("Сделка", "Сделка");
		СтруктураПолей.Вставить("СкладЭтоГруппа", "Склад.ЭтоГруппа");
		СтруктураПолей.Вставить("Приоритет", "Приоритет");
	КонецЕсли;
	
	РеквизитыЗаполнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, СтруктураПолей);
	СтрокаПолей = "Организация, ДокументОснование, Подразделение, Склад, Сделка, Приоритет, НаправлениеДеятельности";
		
	РеквизитыОснования = Новый Структура(СтрокаПолей);
	ЗаполнитьЗначенияСвойств(РеквизитыОснования, РеквизитыЗаполнения, СтрокаПолей);

	Возврат РеквизитыОснования;

КонецФункции 

&НаКлиенте
Процедура СформироватьЗаказыПоставщику(Команда)
	СформироватьЗаказыПоставщикуНаСервере();
	// vvv Галфинд \ Sakovich 19.12.2023
	Если Протокол.ВысотаТаблицы > 0 Тогда
		// открываем страницу протокола
		Элементы.Группа1.ТекущаяСтраница = Элементы.Группа3;
	КонецЕсли;
	// ^^^ Галфинд \ Sakovich 19.12.2023
КонецПроцедуры
