
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФайлИнформацияПоддержкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработчикОповещения = Новый ОписаниеОповещения("ФайлИнформацияПоддержкиНачалоВыбораЗавершение", ЭтотОбъект);
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.Фильтр = "Файлы JSON (*.json)|*.json";
	ВыборФайла.Показать(ОбработчикОповещения);

КонецПроцедуры

&НаКлиенте
Процедура Декорация2Нажатие(Элемент)
	
	ПерейтиПоНавигационнойСсылке(Элемент.Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗапуститьПриложение(Элемент.Заголовок);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьОбъекты(Команда)
	
	Результат = Новый Массив;
	
	Для Каждого СтрокаДанных Из ДанныеПоддержки Цикл
		ПолноеИмяОбъекта = СтрШаблон("%1.%2", СтрокаДанных.Класс, СтрокаДанных.Объект); 
		Результат.Добавить(ПолноеИмяОбъекта);	
	КонецЦикла;
	
	Закрыть(Результат);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ФайлИнформацияПоддержкиНачалоВыбораЗавершение(ВыбранныеФайлы, ТекущиеДанные) Экспорт
	
	Если Ложь
		Или ВыбранныеФайлы = Неопределено
		Или ВыбранныеФайлы.Количество() = 0 
	Тогда		
		Возврат;
	КонецЕсли;
	
	ФайлИнформацияПоддержки = ВыбранныеФайлы[0];
			
	ЗагрузитьДанныеПоддержки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеПоддержки()
	
	ДанныеПоддержки.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ФайлИнформацияПоддержки) Тогда
		Возврат;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьФайл(ФайлИнформацияПоддержки);
	
	Соответствие = ПрочитатьJSON(ЧтениеJSON, Истина);
	
	Для Каждого СтрокаПоддержки Из Соответствие Цикл
		
		Для Каждого СтрокаОбъекта Из СтрокаПоддержки.Значение Цикл
			
			НоваяСтрока			= ДанныеПоддержки.Добавить();
			НоваяСтрока.Класс	= СтрокаПоддержки.Ключ;
			НоваяСтрока.Объект	= СтрокаОбъекта;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЕстьИзмененияПоддержки(СтрокаОбъекта, КлассМетаданных, ОбъектМетаданных)
	
	Отбор			= Новый Структура("Класс, Объект", КлассМетаданных, ОбъектМетаданных);
	СтрокиПоддержки = ДанныеПоддержки.НайтиСтроки(Отбор);
		
	Возврат ЗначениеЗаполнено(СтрокиПоддержки);
	
КонецФункции

#КонецОбласти
