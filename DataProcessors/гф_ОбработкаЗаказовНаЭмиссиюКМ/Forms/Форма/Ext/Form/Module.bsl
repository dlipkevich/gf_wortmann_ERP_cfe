#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ВладелецФормы = Неопределено Тогда
		Отказ = Истина;
		ПоказатьПредупреждение( , 
		"Не предусмотрено независимое открытие формы обработки!", 
		10, 
		"Обработка заказов на эмиссию кодов маркировки");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбработатьВыделенные(Команда)
	
	ВходящиеДанные = Новый Массив;	
	Для каждого Эл Из Элементы.Список.ВыделенныеСтроки Цикл
		СтруктураПроверки = ПолучитьСтруктуруПроверки();
		гф_ЭмиссияКодовМаркировкиВызовСервера.ЗаполнитьДальнейшиеДействияЭмиссии(Эл, СтруктураПроверки);
		
		Если СтруктураПроверки["ЗаказКодовВозможен"] Тогда
			
			ПараметрыОбработкиДокументов = ИнтеграцияИСМПСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
			ПараметрыОбработкиДокументов.Ссылка = Эл;
			Организация = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Эл, "Организация");
			ПараметрыОбработкиДокументов.Организация = Организация;
			ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение(
			"Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ЗапроситеКодыМаркировки");
			ВходящиеДанные.Добавить(ПараметрыОбработкиДокументов);
		КонецЕсли;
	КонецЦикла;
	
	// см. ИнтеграцияИСМПКлиент.ПодготовитьКПередаче()
	гф_ПодготовитьКПередаче(
	ЭтотОбъект,
	ЭтотОбъект.УникальныйИдентификатор,
	ВходящиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура гф_ПодготовитьКПередаче(
	Форма, 
	Идентификатор, 
	ВходящиеДанные, 
	ОповещениеПриЗавершении = Неопределено)
	
	РезультатОбмена = ИнтеграцияИСМПВызовСервера.ПодготовитьКПередаче(
		ВходящиеДанные,
		Идентификатор);
	
	ИнтеграцияИСМПСлужебныйКлиент.ОбработатьРезультатОбмена(
		РезультатОбмена, Форма, Неопределено, ОповещениеПриЗавершении);

КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруПроверки()
	СтруктураВозврата = Новый Структура(
	"Статус,"
	+ "ДальнейшееДействие,"
	+ "ЗаказКодовВозможен");
	Возврат СтруктураВозврата;
КонецФункции

#КонецОбласти