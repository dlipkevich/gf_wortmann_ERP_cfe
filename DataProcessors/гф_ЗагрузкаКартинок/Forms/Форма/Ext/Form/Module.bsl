
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзКаталога(Команда)
	// #wortmann { 
	// Процедура помещения файлов из выбранного каталога
	// Галфинд_Домнышева 2022/11/20
	МассивФайлов = НайтиФайлы(Объект.Каталог, "*.*");

	ОписанияПередаваемыхФайлов = Новый Массив;
	
	Для каждого Элемент из МассивФайлов Цикл		 
	Описание = Новый ОписаниеПередаваемогоФайла;
	Описание.Имя = Элемент.ПолноеИмя;
	ОписанияПередаваемыхФайлов.Добавить(Описание);
	КонецЦикла;

	ОписаниеОЗавершении = Новый ОписаниеОповещения("ОбработатьВыборВнешнегоФайла", ЭтотОбъект);
	НачатьПомещениеФайловНаСервер(ОписаниеОЗавершении, , ,ОписанияПередаваемыхФайлов, ЭтотОбъект.УникальныйИдентификатор);
	// } #wortmann
КонецПроцедуры 

&НаКлиенте
Процедура ЗагрузитьSFTP(Команда)
	// #wortmann { 
	// Процедура вызывает серверную для загрузки из каталога на SFTP
	// Галфинд_Домнышева 2022/11/20
	ЗагрузитьSFTPНаСервере();
	// } #wortmann
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КаталогПриИзменении(Элемент)
	// #wortmann { 
	// Добавляет к строке "Каталог" при отсутствии крайний слэш
	// Галфинд_Домнышева 2022/11/20
	Объект.Каталог = Объект.Каталог + ?(прав(Объект.Каталог,1) = "\", "", "\");
	// } #wortmann
КонецПроцедуры

&НаКлиенте
Процедура КаталогНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// #wortmann { 
	// Открытие диалога выбора папки при ручном выборе КаталогаЗагрузки
	// Галфинд_Домнышева 2022/11/20
	СтандартнаяОбработка = Ложь;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогОткрытияФайла.Каталог = Объект.Каталог;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Укажите каталог для сохранения данных";
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		Каталог = ДиалогОткрытияФайла.Каталог; 
	КонецЕсли; 
	
	Объект.Каталог = Каталог + ?(прав(Каталог,1) = "\", "", "\");
	// } #wortmann
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// #wortmann { 
// Формирует Массив структур с данными помещенных файлов для загрузки
// и вызывает процедуру ЗагрузитьИзКаталогаНаСервере() 
// Галфинд_Домнышева 2022/11/20
//
// Параметры:
// ПомещенныеФайлы - значение результата, переданное вторым параметром при вызове метода,
// ДополнительныеПараметры - значение, которое было указано при создании объекта оповещения.
//
&НаКлиенте
Процедура ОбработатьВыборВнешнегоФайла(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДанныеДляЗагрузки = Новый Массив;
	
	Для каждого Элемент из ПомещенныеФайлы Цикл
		Элт =Новый Структура("Адрес, Имя, ПолноеИмя, ВремяИзменения, ИмяБезРасширения, Расширение, Размер, Путь");
		Элт.Адрес = Элемент.Адрес;
		Элт.Имя = Элемент.СсылкаНафайл.Файл.Имя;
		Элт.ПолноеИмя = Элемент.СсылкаНафайл.Файл.ПолноеИмя;
		Элт.ВремяИзменения = Элемент.СсылкаНафайл.ПолучитьУниверсальноеВремяИзменения();
        Элт.ИмяБезРасширения = Элемент.СсылкаНафайл.Файл.ИмяБезРасширения;
		Элт.Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(Элемент.СсылкаНафайл.Файл.Расширение);
	
		Элт.Размер = Элемент.СсылкаНафайл.Файл.Размер(); 
		Элт.Путь = Элемент.СсылкаНафайл.Файл.Путь;
		ДанныеДляЗагрузки.Добавить(Элт);
	КонецЦикла;
	
	ЗагрузитьИзКаталогаНаСервере(ДанныеДляЗагрузки);
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Вызывает процедуру ЗагрузитьКартинкиИзКаталога() 
// Галфинд_Домнышева 2022/11/20
//
// Параметры:
// МассивФайлов - Массив структур с данными помещенных файлов для загрузки
&НаСервере
Процедура ЗагрузитьИзКаталогаНаСервере(МассивФайлов)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьКартинкиИзКаталога(МассивФайлов);
КонецПроцедуры// } #wortmann

// #wortmann { 
// Процедура вызывает модуль объекта для загрузки из каталога на SFTP
// Галфинд_Домнышева 2022/11/20
&НаСервере
Процедура ЗагрузитьSFTPНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьКартинкиSFTPСервера();
КонецПроцедуры// } #wortmann

&НаСервере
Процедура ЗагрузитьFTPНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузкаКартинокНоменклатурыFTPСервера();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьFTP(Команда)
	ЗагрузитьFTPНаСервере();
КонецПроцедуры

#КонецОбласти
