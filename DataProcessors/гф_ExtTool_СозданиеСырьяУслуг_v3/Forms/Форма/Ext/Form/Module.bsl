&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Название колонок
	
	Колонка1 = ТабДок.Область("R1C1");
	Колонка1.Текст = "Артикул";
	Колонка1.ШиринаКолонки = 50;
	Колонка2 = ТабДок.Область("R1C2");
	Колонка2.Текст = "Сезон";
	Колонка2.ШиринаКолонки = 50;
	Ряд1 = ТабДок.Область("R1");
	Ряд1.Шрифт = Новый Шрифт(, 10, Истина);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьУслугиСырьеНаСервере()
	
	Строка = 2;
	
	Сообщить("Начало работы:" + ТекущаяДатаСеанса());
	ВсегоОбработаноСтрок = 0;
	
	// Начало ========== Поиск Номенклатуры
	Пока Строка <= ТабДок.ВысотаТаблицы Цикл
		нОбласть = ТабДок.Область("R" + Формат(Строка, "ЧГ=") + "C1");
		ОбластьСезон = ТабДок.Область("R" + Формат(Строка, "ЧГ=") + "C2");
		Если Не ЗначениеЗаполнено(СокрЛП(нОбласть.Текст)) Тогда
			Строка = Строка + 1;
			Продолжить;
		КонецЕсли;
		// Реализовал поиск Номенклатуры по Артикулу + Сезону
		НайденаяНоменклатура = НайтиНоменклатуру(нОбласть.Текст, ОбластьСезон.Текст);
		Если НайденаяНоменклатура = Справочники.Номенклатура.ПустаяСсылка() Тогда
			Сообщить("Строка:" + Строка + " Не найдена номенклатура с артикулом:" + нОбласть.Текст + " и Сезоном:" + ОбластьСезон.Текст);
			Строка = Строка + 1;
			Продолжить;
		КонецЕсли;
		
		// Галфинд СадомцевСА 28.04.2023 Исправил ошибку "нарушение прав доступа". Заменил "поиск по реквизиту" запросом
		//НайденоеСырье = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", нОбласть.Текст + " Сырье");
		НайденоеСырье = НайтиНоменклатуруЗапросом(нОбласть.Текст + " Сырье");
		Если НайденоеСырье = Справочники.Номенклатура.ПустаяСсылка() Тогда
			НайденоеСырье = СоздатьНоменклатуру(Строка, "Сырье");
		КонецЕсли;
		
		// Галфинд СадомцевСА 28.04.2023 Исправил ошибку "нарушение прав доступа". Заменил "поиск по реквизиту" запросом
		//НайденаяУслуга = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", нОбласть.Текст + " Услуга");
		НайденаяУслуга = НайтиНоменклатуруЗапросом(нОбласть.Текст + " Услуга");
		Если НайденаяУслуга = Справочники.Номенклатура.ПустаяСсылка() Тогда
			НайденаяУслуга = СоздатьНоменклатуру(Строка, "Услуга");
		КонецЕсли;
		
		Строка = Строка + 1;
		
		// В номенклатуре "головного" артикула заполнять значения соответствующих доп.реквизитов "Сырье для производства"и "Услуга для производства" (соответствие по артикулу+сезон(если заполнен)).
		ЗаполнитьДопРеквизиты(НайденаяНоменклатура, НайденоеСырье, НайденаяУслуга);
		ВсегоОбработаноСтрок = ВсегоОбработаноСтрок + 1;
	КонецЦикла;
	// Конец ========== Поиск Номенклатуры
	
	Сообщить("Окончание работы:" + ТекущаяДатаСеанса());
	Сообщить("ВсегоОбработаноСтрок:" + ВсегоОбработаноСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьУслугиСырье(Команда)
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	СоздатьУслугиСырьеНаСервере();
КонецПроцедуры

Функция СоздатьНоменклатуру(Строка, ОсобенностьУчета);
	
	Номенклатура = Справочники.Номенклатура.СоздатьЭлемент();
	
	Если ОсобенностьУчета = "Сырье" Тогда
		Номенклатура.Артикул = ТабДок.Область("R" + Формат(Строка, "ЧГ=") + "C1").Текст + " Сырье";
		Номенклатура.Наименование = Номенклатура.Артикул;
		Номенклатура.НаименованиеПолное = Номенклатура.Артикул;
		Номенклатура.ВидНоменклатуры = Объект.сВидНоменклатуры;
		Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
		Номенклатура.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета;
		Номенклатура.ИспользованиеХарактеристик = Объект.сВидНоменклатуры.ИспользованиеХарактеристик;
		Номенклатура.ЕдиницаИзмерения = Объект.сВидНоменклатуры.ЕдиницаИзмерения;
		Номенклатура.СтавкаНДС = Объект.сВидНоменклатуры.СтавкаНДС;
		Номенклатура.ГруппаДоступа = Объект.сВидНоменклатуры.ГруппаДоступа;
		
	ИначеЕсли ОсобенностьУчета = "Услуга" Тогда
		Номенклатура.Артикул = ТабДок.Область("R" + Формат(Строка, "ЧГ=") + "C1").Текст + " Услуга";
		Номенклатура.Наименование = Номенклатура.Артикул;
		Номенклатура.НаименованиеПолное = Номенклатура.Артикул;
		Номенклатура.ВидНоменклатуры = Объект.уВидНоменклатуры;
		Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа;
		Номенклатура.ИспользованиеХарактеристик = Объект.уВидНоменклатуры.ИспользованиеХарактеристик;
		Номенклатура.ЕдиницаИзмерения = Объект.уВидНоменклатуры.ЕдиницаИзмерения;
		Номенклатура.СтавкаНДС = Объект.уВидНоменклатуры.СтавкаНДС;
		Номенклатура.ГруппаДоступа = Объект.уВидНоменклатуры.ГруппаДоступа;
		
	КонецЕсли;
	Номенклатура.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг;
	Номенклатура.Качество = Перечисления.ГрадацииКачества.Новый;
	// Галфинд СадомцевСА 28.04.2023 Реализовал заполнение "родителя" при создании Номенклатуры (Сырье/Услуга)
	// глобальным значением "Группа номенклатуры для сырья/услуг РЗ"
	ГруппаНоменклатуры = _омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ГруппаНоменклатурыСырьеУслугиРЗ");
	Если ЗначениеЗаполнено(ГруппаНоменклатуры) Тогда
		Номенклатура.Родитель = ГруппаНоменклатуры;
	КонецЕсли;
	
	Если Не флТест Тогда
		Номенклатура.Записать();
		Сообщить("Создана новая номенклатура. Артикул: " + Номенклатура.Артикул + " Наименование: " + Номенклатура.Наименование);
		Возврат Номенклатура.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.Номенклатура.ПустаяСсылка();
	
КонецФункции

// В номенклатуре "головного" артикула заполнять значения соответствующих доп.реквизитов "Сырье для производства"и "Услуга для производства" (соответствие по артикулу+сезон(если заполнен)).
&НаСервереБезКонтекста
Процедура ЗаполнитьДопРеквизиты(НайденаяНоменклатура, НайденоеСырье, НайденаяУслуга)
	
	ДопРеквизитСырьеДляПроизводства = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("ИдентификаторДляФормул", "гф_НоменклатураСырьеДляПроизводства");
	ДопРеквизитУслугаДляПроизводства = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("ИдентификаторДляФормул", "гф_НоменклатураУслугаДляПроизводства");
	
	флЗаписать = Ложь;
	ОбъектНоменклатура = НайденаяНоменклатура.ПолучитьОбъект();
	
	Если ЗначениеЗаполнено(ДопРеквизитСырьеДляПроизводства) Тогда
		СтрокаТЧ = ОбъектНоменклатура.ДополнительныеРеквизиты.Найти(ДопРеквизитСырьеДляПроизводства, "Свойство");
		Если СтрокаТЧ = Неопределено Тогда
			СтрокаТЧ = ОбъектНоменклатура.ДополнительныеРеквизиты.Добавить();
			СтрокаТЧ.Свойство = ДопРеквизитСырьеДляПроизводства;
			СтрокаТЧ.Значение = НайденоеСырье;
			флЗаписать = Истина;
		Иначе
			Если СтрокаТЧ.Значение <> НайденоеСырье Тогда
				СтрокаТЧ.Значение = НайденоеСырье;
				флЗаписать = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДопРеквизитУслугаДляПроизводства) Тогда
		СтрокаТЧ = ОбъектНоменклатура.ДополнительныеРеквизиты.Найти(ДопРеквизитУслугаДляПроизводства, "Свойство");
		Если СтрокаТЧ = Неопределено Тогда
			СтрокаТЧ = ОбъектНоменклатура.ДополнительныеРеквизиты.Добавить();
			СтрокаТЧ.Свойство = ДопРеквизитУслугаДляПроизводства;
			СтрокаТЧ.Значение = НайденаяУслуга;
			флЗаписать = Истина;
		Иначе
			Если СтрокаТЧ.Значение <> НайденаяУслуга Тогда
				СтрокаТЧ.Значение = НайденаяУслуга;
				флЗаписать = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если флЗаписать = Истина Тогда
		ОбъектНоменклатура.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиНоменклатуру(Артикул, СезонКод)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ПометкаУдаления
	|	И Номенклатура.Артикул = &Артикул";
	Запрос.УстановитьПараметр("Артикул", Артикул);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	// здесь имеются несколько Номенклатур с одинаковым Артикулом - найдем Номенклатуру по "Артикулу + Сезону"
	// сначала найдем Сезон по Коду
	Сезон = Справочники.КоллекцииНоменклатуры.ПустаяСсылка();
	Если ЗначениеЗаполнено(СокрЛП(СезонКод)) Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КоллекцииНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КоллекцииНоменклатуры КАК КоллекцииНоменклатуры
		|ГДЕ
		|	НЕ КоллекцииНоменклатуры.ПометкаУдаления
		|	И КоллекцииНоменклатуры.Код = &Код";
		Запрос.УстановитьПараметр("Код", СокрЛП(СезонКод));
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			Сезон = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	// здесь нашли Сезон - теперь найдем Номенклатуру
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ПометкаУдаления
	|	И Номенклатура.Артикул = &Артикул
	|	И Номенклатура.КоллекцияНоменклатуры = &КоллекцияНоменклатуры";
	Запрос.УстановитьПараметр("КоллекцияНоменклатуры", Сезон);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.Номенклатура.ПустаяСсылка();
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиНоменклатуруЗапросом(Артикул)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ПометкаУдаления
	|	И Номенклатура.Артикул = &Артикул";
	Запрос.УстановитьПараметр("Артикул", Артикул);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	
КонецФункции

