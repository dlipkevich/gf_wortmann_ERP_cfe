#Область Основные

&НаКлиенте
Процедура ТолькоБезПеревода(Команда)
	
	ТолькоБезПеревода = Не ТолькоБезПеревода;
	Элементы.ФормаТолькоБезПеревода.Пометка = ТолькоБезПеревода;
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	Заголовок = Заголовок + " (" + ЯзыкПеревода + ")";
	Элементы.ФормаТолькоБезПеревода.Пометка = ТолькоБезПеревода;
	
	ОбновитьДанные();
	
КонецПроцедуры 

&НаСервере
Процедура ОбновитьДанные()
	
	ТаблицаОбъекты.Очистить();
	
	Для Каждого СтрокаОбъекты Из Объект.Объекты Цикл
		
		Если ТолькоБезПеревода И Не ПустаяСтрока(СтрокаОбъекты.Перевод) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТаблицыОбъекты = ТаблицаОбъекты.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыОбъекты, СтрокаОбъекты);
		
	КонецЦикла;
	
	ТаблицаОбъекты.Сортировать("ТипОбъекта");
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивНоменклатур = Параметры.МассивНоменклатур;
	ЯзыкПеревода = Параметры.ЯзыкПеревода;
	ТолькоБезПеревода = Параметры.ТолькоБезПеревода;
	
	СвойстваДляПеревода = Новый Массив;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гф_ГлобальныеЗначенияСписок.Значение.Ссылка КАК Значение
		|ИЗ
		|	ПланВидовХарактеристик.гф_ГлобальныеЗначения.Список КАК гф_ГлобальныеЗначенияСписок
		|ГДЕ
		|	гф_ГлобальныеЗначенияСписок.Ссылка.Ключ = &Ключ";
	
	Запрос.УстановитьПараметр("Ключ", "гф_ГлобальныеЗначенияСвойстваСПереводом");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	выборка = РезультатЗапроса.Выбрать();
	
	Пока выборка.Следующий() Цикл
		СвойстваДляПеревода.Добавить(выборка.Значение);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Объект В(&мНоменклатур)
		|	И ДополнительныеСведения.Свойство В(&мСвойств)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Значение КАК Значение
		|ИЗ
		|	ВТ КАК ВТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Значение";
	
	Запрос.УстановитьПараметр("мСвойств", СвойстваДляПеревода);
	Запрос.УстановитьПараметр("мНоменклатур", МассивНоменклатур);
	
	ОбъектыПеревода = Запрос.Выполнить().Выгрузить();	
	
	Для Каждого СтрОбъект Из ОбъектыПеревода Цикл
				
		ОбъектБокс = ПолучитьСлужебныйКонтейнер(СтрОбъект.Значение);
		
		СтрокаОбъекты = Объект.Объекты.Добавить();
		СтрокаОбъекты.Объект = ОбъектБокс.ИсходныйОбъект;
		СтрокаОбъекты.Наименование = НазваниеОбъекта(ОбъектБокс);
		
		Если Не ОбъектБокс.ЭтоСтрока Тогда
			
			СтрокаОбъекты.ТипОбъекта = ОбъектБокс.Объект.Метаданные().ПолноеИмя();
			
		Иначе
			
			СтрокаОбъекты.ТипОбъекта = "Строка";
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		| Объект,
		|	Значение
		|ИЗ РегистрСведений.гф_ПереводЗначенийРеквизитовИСвойств
		|ГДЕ Объект В (&Объекты) И Язык = &Язык";
	
	Запрос.УстановитьПараметр("Объекты", ОбъектыПеревода);
	Запрос.УстановитьПараметр("Язык", ЯзыкПеревода);

	ТаблицаПереводов = Запрос.Выполнить().Выгрузить();
	
	// проверка, все ли переводы есть
	Для Каждого СтрокаОбъекты Из Объект.Объекты Цикл
		
		СтрокаТаблицыПереводов = ТаблицаПереводов.Найти(СтрокаОбъекты.Объект, "Объект");
		
		Если СтрокаТаблицыПереводов <> Неопределено Тогда
			
			СтрокаОбъекты.Перевод = СтрокаТаблицыПереводов.Значение;				
						
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСлужебныйКонтейнер(ПараметрОбъект) Экспорт
	
	СтрокаОбъект = ?(ПараметрОбъект = Неопределено, "", ПараметрОбъект);
	
	ЭтоСтрока = (ТипЗнч(СтрокаОбъект) = Тип("Строка"));
	
	ЭтоДокумент = ?(ЭтоСтрока, Ложь, Лев(СтрокаОбъект.Метаданные().ПолноеИмя(), 8) = "Документ");
	
	ТекСтруктура = Новый Структура;
	ТекСтруктура.Вставить("ИсходныйОбъект", СтрокаОбъект);
	ТекСтруктура.Вставить("Объект", ?(ЭтоСтрока, НРег(СтрокаОбъект), СтрокаОбъект));
	ТекСтруктура.Вставить("ЭтоСтрока", ЭтоСтрока);
	ТекСтруктура.Вставить("ЭтоДокумент", ЭтоДокумент);
	
	Возврат ТекСтруктура;
	
КонецФункции

&НаСервере
Функция НазваниеОбъекта(ОбъектБокс) Экспорт
	
	Если ОбъектБокс.ЭтоСтрока Тогда
		
		Возврат ОбъектБокс.ИсходныйОбъект;
		
	Иначе
		
		Попытка
			
			Возврат ОбъектБокс.Объект.Наименование;
			
		Исключение
			
			Возврат ?(ОбъектБокс.Объект = Неопределено, "", Строка(ОбъектБокс.Объект.Ссылка));
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбъектыПереводПриИзменении(Элемент)
	
	ТекДанные = Элементы.ТабличноеПолеНепереведенныеОбъекты.ТекущиеДанные;

	ОбъектыПереводПриИзмененииНаСервере(ТекДанные.Объект, ТекДанные.Перевод);
	
КонецПроцедуры

&НаСервере
Процедура ОбъектыПереводПриИзмененииНаСервере(СтрокаОбъект, Перевод)
	
	Если Не ПустаяСтрока(Перевод) Тогда
		
		МЗ = РегистрыСведений.гф_ПереводЗначенийРеквизитовИСвойств.СоздатьМенеджерЗаписи();
		МЗ.Объект = СтрокаОбъект;
		МЗ.Язык = ЯзыкПеревода;
		МЗ.Значение = Перевод;
		
		Попытка
			
			МЗ.Записать();
			
			Отбор = Новый Структура;
			Отбор.Вставить("Объект", СтрокаОбъект);
			мОбъектов = Объект.Объекты.НайтиСтроки(Отбор);
			Для каждого Стр  Из мОбъектов Цикл
			
				Стр.Перевод = Перевод;		
			
			КонецЦикла;
			
		Исключение
			
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
			
		КонецПопытки;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти