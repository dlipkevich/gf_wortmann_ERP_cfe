#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ТаблицаЗначений") Тогда
		Отказ = Истина;
	Конецесли;
	ПерваяЗагрузка = Параметры.ПерваяЗагрузка;
	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметры.ТаблицаЗначений); 
	МассивТипаВыбора = Новый Массив;
	МассивТипаВыбора.Добавить(Тип("ТаблицаЗначений"));
	ОписаниеТипаВыбора = Новый ОписаниеТипов(МассивТипаВыбора);
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ТаблицаДанных", ОписаниеТипаВыбора, "", "ТЗН")); 
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ = ТаблицаОбработки.Скопировать();
	
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения,"ТаблицаДанных"));
		
	КонецЦикла; 
	
	ИзменитьРеквизиты(МассивРеквизитов);      
	ТаблицаПолейВыбора = Элементы.Добавить("ТЗН", Тип("ТаблицаФормы"));
	ТаблицаПолейВыбора.ПутьКДанным = "ТаблицаДанных";
	ТаблицаПолейВыбора.Отображение = ОтображениеТаблицы.Список;
	ТаблицаПолейВыбора.ИзменятьСоставСтрок = Ложь;
	
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		
		НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), ТаблицаПолейВыбора);       
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = "ТаблицаДанных." + Колонка.Имя;
		Если Колонка.Имя = "Флаг" Тогда
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;	
		КонецЕсли;
	КонецЦикла;			
		
	Если Параметры.Свойство("РеквизитыДляУсловногоОформления") Тогда
		
		Если Параметры.РеквизитыДляУсловногоОформления.Количество()>0 Тогда
			Для каждого Эл Из Параметры.РеквизитыДляУсловногоОформления Цикл
				Если ПерваяЗагрузка Тогда
				Строка = ТЗ.Найти(Эл.Значение, Эл.Ключ);
				Строка.Флаг = Ложь; 
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю( "В ИБ не найдено значение """ + Эл.Значение 
					+ """ по колонке " + Эл.Ключ + " в строке с артикулом " + Эл.Артикул); 
			КонецЦикла;
		КонецЕсли;
		
	Конецесли;
	Если Параметры.Свойство("РеквизитыДляУсловногоОформленияВариантов") Тогда
		
		Если Параметры.РеквизитыДляУсловногоОформленияВариантов.СтруктурыПоНайденнымСсылкам.Количество()>0 Тогда
			Для каждого Эл Из Параметры.РеквизитыДляУсловногоОформленияВариантов.СтруктурыПоНайденнымСсылкам Цикл
				Если ПерваяЗагрузка Тогда
					Строка = ТЗ.Найти(Эл.Значение, Эл.Ключ);
					Строка.Флаг = Ложь; 
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю( "Вариант """ + Эл.Значение + """ использовался в документах" +
				" информационной базы, если вы хотите внести в него изменения отметьте в колонке ""Флаг"" свой выбор");	
			КонецЦикла;
		КонецЕсли;
		Если Параметры.РеквизитыДляУсловногоОформленияВариантов.СтруктурыНеНайденныхЭлементов.Количество()>0 Тогда
			Для каждого Эл Из Параметры.РеквизитыДляУсловногоОформленияВариантов.СтруктурыНеНайденныхЭлементов Цикл
				Если ПерваяЗагрузка Тогда
					Строка = ТЗ.Найти(Эл.Значение, Эл.Ключ);
					Строка.Флаг = Ложь; 
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю( "В ИБ не найдено значение """ + Эл.Значение 
				+ """ по колонке " + Эл.Ключ + " в строке с артикулом " + Эл.Артикул); 
			КонецЦикла;
		КонецЕсли;

	Конецесли;
               
	ЗначениеВРеквизитФормы(ТЗ, "ТаблицаДанных");
	                                                       
КонецПроцедуры                        

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ПродолжитьЗагрузку(Команда)
	
	ДопПараметры = Новый Структура;
	сзТипы = Новый СписокЗначений();
	сзТипы.Добавить(0, "Загрузить только новые");
	сзТипы.Добавить(1, "Загрузить Новые и Обновить Найденные");
	ооВыборЗначенияТипа = Новый ОписаниеОповещения("ВыборЗначенияЗагрузки", ЭтотОбъект, ДопПараметры);
	сзТипы.ПоказатьВыборЭлемента(
	ооВыборЗначенияТипа,	//Вызов процедуры оповещения
	"Выберите тип информации",	//Заголовок диалогового окна выбора типа
	сзТипы[1]				//Первоначально позиционирование на значение выбора
	);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьМетки(Команда) 
		
	Если Команда = ЭтотОбъект.Команды.ОтметитьВсе Тогда
		
		Флаг = Истина;
		
	ИначеЕсли Команда = ЭтотОбъект.Команды.СнятьВсеОтметки Тогда
		
		Флаг = Ложь;
		
	Иначе       
		
		Возврат;
		
	КонецЕсли;			
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		
		СтрокаТаблицы.Флаг = Флаг;
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборЗначенияЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = ЛОЖЬ;
	Если Результат = Неопределено Тогда Возврат КонецЕсли;
	
  	СтруктураЗначений = Новый Структура;
	СтруктураЗначений.Вставить("ЗначениеЗагрузки", Результат);
    СтруктураЗначений.Вставить("АдресТаблицы", ПолучитьАдрес());
	СтруктураЗначений.Вставить("ПерваяЗагрузка", ПерваяЗагрузка);
	
	Закрыть(СтруктураЗначений);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдрес()
	
	ТЗ = РеквизитФормыВЗначение("ТаблицаДанных", Тип("ТаблицаЗначений"));
	Отбор = Новый Структура;
	Отбор.Вставить("Флаг", Истина);
	
	ТЗДляЗагрузки = ТЗ.Скопировать(Отбор);
	
	Возврат ПоместитьВоВременноеХранилище(ТЗДляЗагрузки, Новый УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

