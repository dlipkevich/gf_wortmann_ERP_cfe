#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда) 
	// #wortmann { 
	// Процедура помещения файлов из выбранного каталога
	// Галфинд_Домнышева 2022/09/19	
	МассивПапокЗагрузки = Новый Массив;
	Если Не ЗначениеЗаполнено(Объект.КаталогЗагрузки) Тогда
		МассивПапокЗагрузки = 
							_омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначениеМассив("гф_ГлобальныеЗначенияI5Incoming");
	Иначе
		МассивПапокЗагрузки.Добавить(Объект.КаталогЗагрузки);
	КонецЕсли;
	
	Для каждого Папка Из МассивПапокЗагрузки Цикл 
		
		Организация = ОрганизацияПоПапке(Папка);	
		МассивФайлов = НайтиФайлы(Папка, "*.xml");		
		ОписанияПередаваемыхФайлов = Новый Массив;
		
		Если МассивФайлов.Количество() <> 0 Тогда
			
			Для каждого Элемент Из МассивФайлов Цикл		 
				Описание = Новый ОписаниеПередаваемогоФайла;
				Описание.Имя = Элемент.ПолноеИмя; 
				ОписанияПередаваемыхФайлов.Добавить(Описание);
			КонецЦикла;
			
			ДополнительныеПараметры = Новый Структура("Организация", Организация);
			
			ОписаниеОЗавершении = Новый ОписаниеОповещения("ОбработатьВыборВнешнегоФайла", ЭтотОбъект, ДополнительныеПараметры);
			НачатьПомещениеФайловНаСервер(ОписаниеОЗавершении, , , ОписанияПередаваемыхФайлов, 
											ЭтотОбъект.УникальныйИдентификатор);
			
		КонецЕсли;
	КонецЦикла;
	// } #wortmann
КонецПроцедуры  

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КаталогЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// #wortmann { 
	// Открытие диалога выбора папки при ручном выборе КаталогаЗагрузки
	// Галфинд_Домнышева 2022/09/19
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога; 
	Диалог = Новый ДиалогВыбораФайла(Режим); 
	Диалог.Каталог = ""; 
	Диалог.МножественныйВыбор = Ложь; 
	Диалог.Заголовок = "Выберите каталог"; 
	Если Диалог.Выбрать() Тогда 
		Объект.КаталогЗагрузки = Диалог.Каталог;
	КонецЕсли;
	// } #wortmann
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
// #wortmann { 
// Производит вызов процедуры для загрузки файлов из i5 и возвращает массив загруженных файлов. 
// Галфинд_Домнышева 2022/09/19
// 
// Параметры:
//	ДанныеДляЗагрузки - Массив - массив структур с данными загружаемых файлов.
//
// Возвращаемое значение:
//	ФайлыВАрхив - Массив - массив состоящий из значений ПолногоИмениФайла.
&НаСервере
Функция ЗагрузитьНаСервере(ДанныеДляЗагрузки)	
	
	ФайлыВАрхив = Новый Массив;
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ФайлыВАрхив = ДокументОбъект.ВыполнитьОбменДанными(ДанныеДляЗагрузки);
	Возврат  ФайлыВАрхив;
	
КонецФункции// } #wortmann

// #wortmann { 
// Формирует Массив структур с данными помещенных файлов и при получении
// массива архивируемых файлов вызывает процедуру ЗаписатьВАрхивУдалитьИзКаталога() 
// Галфинд_Домнышева 2022/09/19
//
// Параметры:
// ПомещенныеФайлы - значение результата, переданное вторым параметром при вызове метода,
// ДополнительныеПараметры - значение, которое было указано при создании объекта оповещения.
//
&НаКлиенте
Процедура ОбработатьВыборВнешнегоФайла(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	ФайлыВАрхив = Новый Массив;
	
	ДанныеДляЗагрузки = Новый Массив;
	Для каждого Элемент Из ПомещенныеФайлы Цикл
		НовыйЭлемент = Новый Структура("Адрес, ИмяФайла, ПолноеИмяФайла");
		НовыйЭлемент.Адрес = Элемент.Адрес;
		НовыйЭлемент.ИмяФайла = Элемент.СсылкаНафайл.Файл.Имя;
		НовыйЭлемент.ПолноеИмяФайла = Элемент.СсылкаНафайл.Файл.ПолноеИмя;
		ДанныеДляЗагрузки.Добавить(НовыйЭлемент);
	КонецЦикла;
	
	ФайлыВАрхив = ЗагрузитьНаСервере(ДанныеДляЗагрузки);
	Если ФайлыВАрхив.Количество() > 0 Тогда
		ЗаписатьВАрхивУдалитьИзКаталога(ФайлыВАрхив, ДополнительныеПараметры.Организация);
	КонецЕсли;	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Записывает в архив массив загруженных файлов и перемещает их в каталог гф_ГлобальныеЗначенияI5Readed 
// Галфинд_Домнышева 2022/09/19
//
// Параметры:
// ФайлыВАрхив - Массив - массив файлов для помещения в архив
// Организация - СправочникСсылка.Организации - Организация по которой идет загрузка документов
&НаКлиенте
Процедура ЗаписатьВАрхивУдалитьИзКаталога(ФайлыВАрхив, Организация) 
	
	ДатаАрхивации = ОбщегоНазначенияКлиент.ДатаСеанса();
	ДатаАрхивации = СтрЗаменить(ДатаАрхивации, ":", "_");
	КудаКопируем = ПапкаПоОрганизации(Организация);
	ИмяЗИПАрхива = "Архив" + ДатаАрхивации;
	ПолноеИмяАрхива = КудаКопируем + "\" + ИмяЗИПАрхива + ".zip";
	
	МетодСжатия = МетодСжатияZIP.Сжатие;
	УровеньСжатия = УровеньСжатияZIP.Оптимальный;
	МетодШифрования = МетодШифрованияZIP.Zip20; 
	// Создадим объект записи ZIP-архива
	ЗаписьЗИП = Новый ЗаписьZipФайла(ПолноеИмяАрхива, ,
	"" + МетодСжатия + Символы.ПС + УровеньСжатия + Символы.ПС + МетодШифрования,
	МетодСжатия,
	УровеньСжатия,
	МетодШифрования);
	Для каждого Файл Из ФайлыВАрхив Цикл
		// Добавим необходимые файлы в архив
		ЗаписьЗИП.Добавить(Файл, РежимСохраненияПутейZIP.НеСохранятьПути);
	КонецЦикла;
	// Запишем архив на диск
	ЗаписьЗИП.Записать();
	
	Для каждого Файл Из ФайлыВАрхив Цикл
		УдалитьФайлы(Файл);
	КонецЦикла;
	
КонецПроцедуры// } #wortmann  

// #wortmann { 
// Находит соответствие организации по названию папки с файлами для загрузки 
// Галфинд_Домнышева 2022/11/28
// 
// Параметры:
//	ПолноеИмя - Строка - полное имя Файла или путь папки с файлами.
//
// Возвращаемое значение:
//	СправочникСсылка.Организации - Ссылка на Организацию если соответствие найдено,
//	Неопределено - если соответствие не найдено
&НаСервере
Функция ОрганизацияПоПапке(ПолноеИмя)
	
	Если СтрНайти(ПолноеИмя, "WORTMANN") Тогда
		Наименование = "ВОРТМАНН ВОСТОК ООО";
	ИначеЕсли СтрНайти(ПолноеИмя, "CAPRICE") Тогда
		Наименование = "КАПРИС ВОСТОК ООО";
	ИначеЕсли СтрНайти(ПолноеИмя, "VENDEL") Тогда
		Наименование = "ВЕНДЕЛЬ ВОСТОК ООО";
	ИначеЕсли СтрНайти(ПолноеИмя, "SHOE") Тогда
		Наименование = "ШУ.КОМ ВОСТОК ООО";
	ИначеЕсли СтрНайти(ПолноеИмя, "JANA") Тогда
		Наименование = "ЯНА ШУЗ ВОСТОК ООО";
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не РезультатЗапроса.Пустой() Тогда
	Выборка.Следующий(); 
    Возврат Выборка.Ссылка;
	КонецЕсли;

КонецФункции

// #wortmann { 
// Находит соответствие папки с файлами для загруженных файлов по названию организации 
// Галфинд_Домнышева 2022/11/28
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация по которой идет загрузка документов.
//
// Возвращаемое значение:
//	Папка - Строка - путь папки с файлами.
//	Неопределено - если соответствие не найдено
&НаСервере
Функция ПапкаПоОрганизации(Организация)
	
	МассивПапокЗагруженных = 
							_омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначениеМассив("гф_ГлобальныеЗначенияI5Readed");
	
	Если Организация.Наименование = "ВОРТМАНН ВОСТОК ООО" Тогда
		Имя = "WORTMANN";
	ИначеЕсли Организация.Наименование = "КАПРИС ВОСТОК ООО" Тогда
		Имя = "CAPRICE"; 
	ИначеЕсли Организация.Наименование = "ВЕНДЕЛЬ ВОСТОК ООО" Тогда
		Имя = "VENDEL";
	ИначеЕсли Организация.Наименование = "ШУ.КОМ ВОСТОК ООО" Тогда
		Имя = "SHOE";
	ИначеЕсли Организация.Наименование = "ЯНА ШУЗ ВОСТОК ООО" Тогда
		Имя = "JANA";
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
	Для каждого Папка Из МассивПапокЗагруженных Цикл
		
		Если СтрНайти(Папка, Имя) Тогда
			Возврат Папка;
			
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

#КонецОбласти