
&НаСервере
Процедура ПроверитьНомераГТДНаСервере()
	
	Элементы.Отличия.Видимость = Ложь;
	Элементы.Отличия.Доступность = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПриобретениеТоваровУслуггф_ПродукцияВКоробах.IDКороба КАК IDКороба,
		|	ПриобретениеТоваровУслуггф_ПродукцияВКоробах.IDКороба.Код КАК IDКоробаКод
		|ПОМЕСТИТЬ УЛ
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.гф_ПродукцияВКоробах КАК ПриобретениеТоваровУслуггф_ПродукцияВКоробах
		|ГДЕ
		|	ПриобретениеТоваровУслуггф_ПродукцияВКоробах.Ссылка = &Ссылка
		|	И ПриобретениеТоваровУслуггф_ПродукцияВКоробах.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыОрганизаций.Номенклатура КАК Номенклатура,
		|	ТоварыОрганизаций.Характеристика КАК Характеристика,
		|	ТоварыОрганизаций.НомерГТД КАК НомерГТД
		|ПОМЕСТИТЬ ТоварыОрганизации
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ТаможеннаяДекларацияИмпорт)) = &Регистратор
		|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ТаможеннаяДекларацияИмпорт).Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка КАК Агрегат,
		|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод КАК Штрихкод,
		|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод.Номенклатура КАК Номенклатура,
		|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод.Характеристика КАК Характеристика,
		|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод.гф_НомерГТД КАК НомерГТДШтрихкода,
		|	ТоварыОрганизации.НомерГТД КАК НомерГТД
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыОрганизации КАК ТоварыОрганизации
		|		ПО ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод.Номенклатура = ТоварыОрганизации.Номенклатура
		|			И ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод.Характеристика = ТоварыОрганизации.Характеристика
		|ГДЕ
		|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка.ЗначениеШтрихкода В
		|			(ВЫБРАТЬ
		|				УЛ.IDКоробаКод
		|			ИЗ
		|				УЛ КАК УЛ)
		|	И ТоварыОрганизации.НомерГТД <> ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод.гф_НомерГТД";
	
	Запрос.УстановитьПараметр("Регистратор", Объект.ГТД);
	Запрос.УстановитьПараметр("Ссылка", Объект.ПТУ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Элементы.Отличия.Видимость = Истина;
		Элементы.Отличия.Доступность = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Отличия.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЦикла;
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Номера ГТД в КМ заполнены корректно.");
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНомераГТД(Команда)
	ПроверитьНомераГТДНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИзменитьНомераГТДНаСервере()
	
	Для каждого Стр из Отличия Цикл
		
		ШтрихкодОбъект = Стр.Штрихкод.ПолучитьОбъект();
		ШтрихкодОбъект.гф_НомерГТД = Стр.НомерГТД;
		ШтрихкодОбъект.ОбменДанными.Загрузка = Истина;
		
		НачатьТранзакцию();
		Попытка 
			ШтрихкодОбъект.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			Сообщить(ОписаниеОшибки());
		КонецПопытки;   
		
	КонецЦикла; 
	
	ТЗАгрегаты = Отличия.Выгрузить(, "Агрегат, НомерГТД"); 
	ТЗАгрегаты.Свернуть("Агрегат, НомерГТД");
	
	Для каждого Стр из ТЗАгрегаты Цикл
		
		Если Стр.Агрегат.гф_НомерГТД = Стр.НомерГТД Тогда
			Продолжить;
		КонецЕсли;
		
		ШтрихкодОбъект = Стр.Агрегат.ПолучитьОбъект();
		ШтрихкодОбъект.гф_НомерГТД = Стр.НомерГТД;
		ШтрихкодОбъект.ОбменДанными.Загрузка = Истина;
		
		НачатьТранзакцию();
		Попытка 
			ШтрихкодОбъект.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			Сообщить(ОписаниеОшибки());
		КонецПопытки;   
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНомераГТД(Команда)
	ИзменитьНомераГТДНаСервере();
КонецПроцедуры
