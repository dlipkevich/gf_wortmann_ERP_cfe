
&НаСервере
Процедура гф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	ТекстЗапроса = СписокДокументыПродажи.ТекстЗапроса;
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ЗапросВыбора = СхемаЗапроса.ПакетЗапросов[0];
	ОператорыЗапроса = ЗапросВыбора.Операторы[0];
	ИсточникОсновной = ОператорыЗапроса.Источники[0];
	ПсевдонимОсновнойТаблицы = ИсточникОсновной.Источник.Псевдоним;
	
	ШаблонСоединения = "%1.Ссылка = НомераРасходныхОрдеров.Ссылка";
	УсловиеСоединения = СтрШаблон(ШаблонСоединения, ПсевдонимОсновнойТаблицы);
	
	ИсточникНомераРО = ОператорыЗапроса.Источники.Добавить(Тип("ВложенныйЗапросСхемыЗапроса"), "НомераРасходныхОрдеров");
	ИсточникНомераРО.Источник.Запрос.УстановитьТекстЗапроса(
	"ВЫБРАТЬ
	|		РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|		РеализацияТоваровУслуг.гф_РасходныйОрдер.Номер КАК НомерРасходногоОрдера
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СчетФактураВыданныйДокументыОснования.Ссылка,
	|			МАКСИМУМ(ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг).гф_РасходныйОрдер.Номер)
	|		ИЗ
	|			Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СчетФактураВыданныйДокументыОснования.Ссылка");	
	ИсточникНомераРО.Соединения.Очистить();
	ИсточникНомераРО.Соединения.Добавить(ПсевдонимОсновнойТаблицы, УсловиеСоединения);
	ИсточникНомераРО.Соединения[0].ТипСоединения = ТипСоединенияСхемыЗапроса.ПравоеВнешнее;
	ИсточникНомераРО.Соединения[0].Условие = Новый ВыражениеСхемыЗапроса(УсловиеСоединения);
	ОператорыЗапроса.ВыбираемыеПоля.Добавить("ЕСТЬNULL(НомераРасходныхОрдеров.НомерРасходногоОрдера, """")");
	КолонкаНомерРасходногоОрдера = ЗапросВыбора.Колонки.Получить(ЗапросВыбора.Колонки.Количество() - 1);
	КолонкаНомерРасходногоОрдера.Псевдоним = "НомерРасходногоОрдера";
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	СписокДокументыПродажи.ТекстЗапроса = ТекстЗапроса;
	
 	ТипПолеФормы = Тип("ПолеФормы");

	НовоеПоле = Элементы.Добавить("НомерРасходногоОрдера", ТипПолеФормы, Элементы.СписокДокументыПродажи);											
	НовоеПоле.Вид			= ВидПоляФормы.ПолеВвода;
	НовоеПоле.Видимость		= Истина;
	НовоеПоле.ПутьКДанным	= "СписокДокументыПродажи.НомерРасходногоОрдера";
	
КонецПроцедуры
