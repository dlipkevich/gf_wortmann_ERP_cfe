#Область ОбработчикиКомандФормы

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Отмечает все строки
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаОтметитьВсе(Команда)
	
	Для Каждого Строка Из Объект.Спецификации Цикл
		
		Строка.Флаг = Истина;	
		
	КонецЦикла;
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Снимает все отметки
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаСнятьВсеОтметки(Команда)
	
	Для Каждого Строка Из Объект.Спецификации Цикл
		
		Строка.Флаг = Ложь;	
		
	КонецЦикла;
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Заполняет таблицы спецификаций и заказов в соответствии с 
// выбранными на форме параметрами
//
&НаСервере
Процедура КомандаЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказКлиента.Контрагент КАК Контрагент, 
	|	ЗаказКлиента.Договор КАК Договор, 
	|	ЗаказКлиента.Ссылка КАК Заказ
	|ПОМЕСТИТЬ ВТ_Заказы
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ЗаказКлиента.Договор = ДоговорыКонтрагентов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов.ДополнительныеРеквизиты КАК ДоговорыКонтрагентовСвойствоДепозит
	|			ПО (ДоговорыКонтрагентовСвойствоДепозит.Ссылка = ДоговорыКонтрагентов.Ссылка)
	|				И (ДоговорыКонтрагентовСвойствоДепозит.Свойство = &СвойствоДепозит)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов.ДополнительныеРеквизиты КАК ДоговорыКонтрагентовСвойствоПредоплата
	|			ПО (ДоговорыКонтрагентовСвойствоПредоплата.Ссылка = ДоговорыКонтрагентов.Ссылка)
	|				И (ДоговорыКонтрагентовСвойствоПредоплата.Свойство = &СвойствоПредоплата)
	|		ПО ЗаказКлиентаТовары.Ссылка = ЗаказКлиента.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураЗаказа
	|		ПО ЗаказКлиентаТовары.Номенклатура = НоменклатураЗаказа.Ссылка
	|ГДЕ
	|	ЗаказКлиента.Организация = &Организация      
	|	И (ЗаказКлиента.гф_СтатусРаботыСЗаказомИ5 = ЗНАЧЕНИЕ(Справочник.гф_СтатусРаботыСЗаказомИ5.Подтвержден)
	|			ИЛИ ЗаказКлиента.гф_СтатусРаботыСЗаказомИ5 = ЗНАЧЕНИЕ(Справочник.гф_СтатусРаботыСЗаказомИ5.ВыпущенаСпецификация))
	|	И (&Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ИЛИ ЗаказКлиента.Склад = &Склад)
	|	И (ДоговорыКонтрагентов.гф_Сезон = &Сезон
	|			ИЛИ &Сезон = ЗНАЧЕНИЕ(Справочник.КоллекцииНоменклатуры.ПустаяСсылка))
	|	И ЗаказКлиента.Проведен
	|	И (&ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|			ИЛИ НоменклатураЗаказа.ВидНоменклатуры = &ВидНоменклатуры)
	|	И НЕ ДоговорыКонтрагентовСвойствоДепозит.Значение ЕСТЬ NULL
	|	И ДоговорыКонтрагентовСвойствоДепозит.Значение <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И НЕ ДоговорыКонтрагентовСвойствоПредоплата.Значение ЕСТЬ NULL
	|	И ДоговорыКонтрагентовСвойствоПредоплата.Значение <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказы.Контрагент КАК Контрагент, 
	|	ВТ_Заказы.Договор КАК Договор, 
	|	гф_СпецификацияПокупателяЗаказыКлиентов.Ссылка КАК Ссылка, 
	|	гф_СпецификацияПокупателя.Дата КАК Дата
	|ПОМЕСТИТЬ ВТ_Спецификации_Дата
	|ИЗ
	|	ВТ_Заказы КАК ВТ_Заказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гф_СпецификацияПокупателя.ЗаказыКлиентов КАК гф_СпецификацияПокупателяЗаказыКлиентов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.гф_СпецификацияПокупателя КАК гф_СпецификацияПокупателя
	|			ПО гф_СпецификацияПокупателяЗаказыКлиентов.Ссылка = гф_СпецификацияПокупателя.Ссылка
	|		ПО ВТ_Заказы.Заказ = гф_СпецификацияПокупателяЗаказыКлиентов.ЗаказКлиента
	|ГДЕ
	|	НЕ гф_СпецификацияПокупателя.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Подзапрос.Контрагент КАК Контрагент, 
	|	Подзапрос.Договор КАК Договор, 
	|	МАКСИМУМ(ВТ_Спецификации_Дата.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Спецификации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_Спецификации_Дата.Контрагент КАК Контрагент, 
	|		ВТ_Спецификации_Дата.Договор КАК Договор, 
	|		МАКСИМУМ(ВТ_Спецификации_Дата.Дата) КАК Дата
	|	ИЗ
	|		ВТ_Спецификации_Дата КАК ВТ_Спецификации_Дата
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_Спецификации_Дата.Контрагент, 
	|		ВТ_Спецификации_Дата.Договор) КАК Подзапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Спецификации_Дата КАК ВТ_Спецификации_Дата
	|		ПО Подзапрос.Контрагент = ВТ_Спецификации_Дата.Контрагент
	|			И Подзапрос.Договор = ВТ_Спецификации_Дата.Договор
	|			И Подзапрос.Дата = ВТ_Спецификации_Дата.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	Подзапрос.Контрагент, 
	|	Подзапрос.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказы.Контрагент КАК Контрагент, 
	|	ВТ_Заказы.Договор КАК ДоговорКонтрагента, 
	|	ВТ_Заказы.Заказ КАК ЗаказКлиента
	|ИЗ
	|	ВТ_Заказы КАК ВТ_Заказы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Флаг, 
	|	ВТ_Спецификации.Контрагент КАК Контрагент, 
	|	ВТ_Спецификации.Договор КАК ДоговорКонтрагента, 
	|	ВТ_Спецификации.Ссылка КАК СпецификацияПокупателя, 
	|	гф_СпецификацияПокупателя.Проведен КАК Проведен, 
	|	гф_СпецификацияПокупателя.ПометкаУдаления КАК ПометкаУдаления, 
	|	ВЫБОР
	|		КОГДА гф_СпецификацияПокупателя.Проведен
	|			ТОГДА 1
	|		КОГДА гф_СпецификацияПокупателя.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИндексКартинки
	|ИЗ
	|	ВТ_Спецификации КАК ВТ_Спецификации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гф_СпецификацияПокупателя КАК гф_СпецификацияПокупателя
	|		ПО ВТ_Спецификации.Ссылка = гф_СпецификацияПокупателя.Ссылка"; 
	
	Запрос.Параметры.Вставить("Организация", Объект.Организация);
	Запрос.Параметры.Вставить("Склад", Объект.Склад);
	Запрос.Параметры.Вставить("Сезон", Объект.Сезон);
	Запрос.Параметры.Вставить("ВидНоменклатуры", Объект.ВидНоменклатуры); 
	
	МассивСвойствДоговора = УправлениеСвойствами.СвойстваОбъекта(Справочники.ДоговорыКонтрагентов.ПустаяСсылка(), , Ложь);
	
	Для Каждого СвойствоДоговора Из МассивСвойствДоговора Цикл
		
		Если СвойствоДоговора.Имя = "гф_ДоговорыКонтрагентовСтраховойДепозит" Тогда 
			
			Запрос.Параметры.Вставить("СвойствоДепозит", СвойствоДоговора.Ссылка);
			
		ИначеЕсли СвойствоДоговора.Имя = "гф_ДоговорыКонтрагентовПредоплата" Тогда
			
			Запрос.Параметры.Вставить("СвойствоПредоплата", СвойствоДоговора.Ссылка);
			
		Иначе 
			
			Продолжить;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	СмещениеЗаказов = 2;
	СмещениеСпецификаций = 1;
	
	ИндексЗаказов = ПакетРезультатов.Количество() - СмещениеЗаказов;
	ИндексСпецификаций = ПакетРезультатов.Количество() - СмещениеСпецификаций;
	
	Объект.Заказы.Загрузить(ПакетРезультатов[ИндексЗаказов].Выгрузить());
	Объект.Спецификации.Загрузить(ПакетРезультатов[ИндексСпецификаций].Выгрузить());
	
КонецПроцедуры

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Вызывает серверный метод
// выбранными на форме параметрами
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	КомандаЗаполнитьНаСервере();
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Создает документы спецификаций по таблицам спецификации и заказы
// выбранными на форме параметрами
//
&НаСервере
Процедура КомандаСоздатьСпецификацииНаСервере()
	
	Для Каждого СтрокаСпецификации Из Объект.Спецификации Цикл
		
		Если Не СтрокаСпецификации.Флаг Тогда
			
			Продолжить;
			
		КонецЕсли;	
		
		Если СтрокаСпецификации.Проведен Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("По контрагенту " + 
			СтрокаСпецификации.Контрагент + " в табличной части присутствуют проведенные спецификации");
			
			Продолжить;
			
		КонецЕсли;	 
		
		Если ЗначениеЗаполнено(СтрокаСпецификации.СпецификацияПокупателя) Тогда
			
			СпецификацияОбъект = СтрокаСпецификации.СпецификацияПокупателя.ПолучитьОбъект();
			
			СпецификацияОбъект.ПометкаУдаления = Ложь;
			
			СпецификацияОбъект.ЗаказыКлиентов.Очистить();
			
		Иначе	
			
			СпецификацияОбъект = Документы.гф_СпецификацияПокупателя.СоздатьДокумент();
			
		КонецЕсли;
		
		ОтборЗаказов = Новый Структура("Контрагент, ДоговорКонтрагента", 
		СтрокаСпецификации.Контрагент, 
		СтрокаСпецификации.ДоговорКонтрагента);
		
		СтрокиЗаказов = Объект.Заказы.НайтиСтроки(ОтборЗаказов);										
		
		СпецификацияОбъект.Организация		= Объект.Организация;
		СпецификацияОбъект.Контрагент		= СтрокаСпецификации.Контрагент;
		СпецификацияОбъект.Договор			= СтрокаСпецификации.ДоговорКонтрагента;
		СпецификацияОбъект.Ответственный	= Пользователи.ТекущийПользователь();
		СпецификацияОбъект.Дата				= ТекущаяДатаСеанса();
		
		Для Каждого СтрокаЗаказа Из СтрокиЗаказов Цикл
			
			СтрокаДокумента = СпецификацияОбъект.ЗаказыКлиентов.Добавить();
			
			СтрокаДокумента.ЗаказКлиента = СтрокаЗаказа.ЗаказКлиента;
			
		КонецЦикла;	
		
		СпецификацияОбъект.Записать();
		
		СтрокаСпецификации.СпецификацияПокупателя = СпецификацияОбъект.Ссылка;
		
	КонецЦикла;	
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Вызывает серверный метод
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаСоздатьСпецификации(Команда)
	
	КомандаСоздатьСпецификацииНаСервере();
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Проводит отмеченные документы
//
&НаСервере
Процедура КомандаПровестиНаСервере()
	
	Для Каждого СтрокаСпецификации Из Объект.Спецификации Цикл
		
		Если Не СтрокаСпецификации.Флаг Тогда
			
			Продолжить;
			
		КонецЕсли;	
		
		СпецификацияОбъект = СтрокаСпецификации.СпецификацияПокупателя.ПолучитьОбъект();
		
		Если Не СпецификацияОбъект.Проведен Тогда 
			
			СпецификацияОбъект.ПометкаУдаления = Ложь;
			
			СпецификацияОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			УстановитьФлажки(СпецификацияОбъект, СтрокаСпецификации);
			
		КонецЕсли;	
		
	КонецЦикла; 
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Вызывает серверный метод
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаПровести(Команда)
	
	КомандаПровестиНаСервере();
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.гф_СпецификацияПокупателя"));
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Отменяет проведение отмеченных документов
//
&НаСервере
Процедура КомандаОтменитьПроведениеНаСервере()
	
	Для Каждого СтрокаСпецификации Из Объект.Спецификации Цикл
		
		Если Не СтрокаСпецификации.Флаг Тогда
			
			Продолжить;
			
		КонецЕсли;	
		
		СпецификацияОбъект = СтрокаСпецификации.СпецификацияПокупателя.ПолучитьОбъект();
		
		Если СпецификацияОбъект.Проведен Тогда 
			
			СпецификацияОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			
			УстановитьФлажки(СпецификацияОбъект, СтрокаСпецификации);
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Вызывает серверный метод, 
// оповещает об изменении документа
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаОтменитьПроведение(Команда)
	
	КомандаОтменитьПроведениеНаСервере();
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.гф_СпецификацияПокупателя"));
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Помечает на удаление отмеченные документы
//
&НаСервере
Процедура КомандаПометитьНаУдалениеНаСервере()
	
	Для Каждого СтрокаСпецификации Из Объект.Спецификации Цикл
		
		Если Не СтрокаСпецификации.Флаг Тогда
			
			Продолжить;
			
		КонецЕсли;	
		
		СпецификацияОбъект = СтрокаСпецификации.СпецификацияПокупателя.ПолучитьОбъект();
		
		Если Не СпецификацияОбъект.ПометкаУдаления Тогда 
			
			СпецификацияОбъект.УстановитьПометкуУдаления(Истина);
			
			УстановитьФлажки(СпецификацияОбъект, СтрокаСпецификации);
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Вызывает серверный метод, 
// оповещает об изменении документа
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаПометитьНаУдаление(Команда)
	
	КомандаПометитьНаУдалениеНаСервере();
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.гф_СпецификацияПокупателя"));
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Снимает пометки на удаление отмеченных документов
//
&НаСервере
Процедура КомандаСнятьПометкиНаУдалениеНаСервере()
	
	Для Каждого СтрокаСпецификации Из Объект.Спецификации Цикл
		
		Если Не СтрокаСпецификации.Флаг Тогда
			
			Продолжить;
			
		КонецЕсли;	
		
		СпецификацияОбъект = СтрокаСпецификации.СпецификацияПокупателя.ПолучитьОбъект();
		
		Если СпецификацияОбъект.ПометкаУдаления Тогда 
			
			СпецификацияОбъект.УстановитьПометкуУдаления(Ложь);
			
			УстановитьФлажки(СпецификацияОбъект, СтрокаСпецификации);
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Вызывает серверный метод, 
// оповещает об изменении документа
//
// Параметры:
//
// Команда - команда формы
//
&НаКлиенте
Процедура КомандаСнятьПометкиНаУдаление(Команда)
	
	КомандаСнятьПометкиНаУдалениеНаСервере();
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.гф_СпецификацияПокупателя"));
	
КонецПроцедуры// } #wortmann

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Устанавливает реквизы индекса картинки в строке спецификации
// устанавливает реквизиты проведен и пометка удаления
// для минимизации обращения к серверу
//
// Параметры:
//
// Команда - команда формы
//
&НаСервере
Процедура УстановитьФлажки(СпецификацияОбъект, СтрокаСпецификации)
	
	СтрокаСпецификации.Проведен			= СпецификацияОбъект.Проведен;
	СтрокаСпецификации.ПометкаУдаления	= СпецификацияОбъект.ПометкаУдаления;
	
	Если СтрокаСпецификации.Проведен Тогда
		
		СтрокаСпецификации.ИндексКартинки = 1;
		
	ИначеЕсли СтрокаСпецификации.ПометкаУдаления Тогда
		
		СтрокаСпецификации.ИндексКартинки = 2;
		
	Иначе
		
		СтрокаСпецификации.ИндексКартинки = 0;
		
	КонецЕсли;	
	
КонецПроцедуры// } #wortmann

#КонецОбласти

