#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	КонецПериода = КонецДня(ТекущаяДатаСеанса());
	НачалоПериода = НачалоДня(ДобавитьМесяц(КонецПериода, -1));
	Объект.Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	Объект.Период.ДатаНачала = НачалоПериода;
	Объект.Период.ДатаОкончания = КонецПериода;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьСвойствоПроверкаПТУ();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьСписокВыбораСклада();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбновитьСписокВыбораСклада();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивСсылокПТУ = ПолучитьМассивСсылокПТУ();
	ЗначениеОтбора  = Новый Структура("Ссылка", МассивСсылокПТУ);
	
	ОткрытьФорму("Документ.ПриобретениеТоваровУслуг.ФормаВыбора", Новый Структура("Отбор", ЗначениеОтбора), Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставкаУдалениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивСсылокПТУ = ПолучитьМассивСсылокУдалениеПТУ();
	ЗначениеОтбора  = Новый Структура("Ссылка", МассивСсылокПТУ);
	
	ОткрытьФорму("Документ.ПриобретениеТоваровУслуг.ФормаВыбора", Новый Структура("Отбор", ЗначениеОтбора), Элемент);
КонецПроцедуры


&НаКлиенте
Процедура ДанныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Данные.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.УпаковочныйЛист) Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.УпаковочныйЛист);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаКорректировокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.ОтменаКорректировок.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументКорректировки) Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.ДокументКорректировки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковочныеЛистыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.УпаковочныеЛисты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.УпаковочныйЛист) Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.УпаковочныйЛист);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаКорректировокПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.ОтменаКорректировок.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураОтбора = Новый Структура("ДокументКорректировки", ТекущиеДанные.ДокументКорректировки);
	Если НеОтбиратьПриПозиционировании Тогда
		Элементы.УпаковочныеЛисты.ОтборСтрок = Неопределено;
	Иначе
		Элементы.УпаковочныеЛисты.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НеОтбиратьПриПозиционированииПриИзменении(Элемент)
	ОтменаКорректировокПриАктивизацииСтроки(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура УпаковочныеЛистыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура УпаковочныеЛистыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтменаКорректировокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтменаКорректировокПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Поставка) Тогда
		ТекстОшибки = "Не заполнен документ поставки.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,, "Объект.Поставка");
		Возврат;
	КонецЕсли;
	ЗаполнитьНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУдаление(Команда)
	Если Не ЗначениеЗаполнено(Объект.ПоставкаУдаление) Тогда
		ТекстОшибки = "Не заполнен документ поставки.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,, "Объект.ПоставкаУдаление");
	КонецЕсли;
	ЗаполнитьУдалениеНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	ВыполнитьОбработкуНаСервере();	
	Заполнить(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьУдаление(Команда)
	ВыполнитьУдалениеНаСервере();
	ЗаполнитьУдаление(Неопределено);
КонецПроцедуры


&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для Каждого СтрокаД Из Объект.ДанныеДляОтображения Цикл
		ИД = СтрокаД.ПолучитьИдентификатор();
		Если Элементы.Данные.ПроверитьСтроку(ИД) Тогда
			СтрокаД.Отметка = Истина;	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого СтрокаД Из Объект.ДанныеДляОтображения Цикл
		ИД = СтрокаД.ПолучитьИдентификатор();
		Если Элементы.Данные.ПроверитьСтроку(ИД) Тогда
			СтрокаД.Отметка = Ложь;	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажкиУдаление(Команда)
	Для Каждого СтрокаД Из Объект.ОтменаКорректировок Цикл
		ИД = СтрокаД.ПолучитьИдентификатор();
		Если Элементы.ОтменаКорректировок.ПроверитьСтроку(ИД) Тогда
			СтрокаД.Отметка = Ложь;	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажкиУдаление(Команда)
	Для Каждого СтрокаД Из Объект.ОтменаКорректировок Цикл
		ИД = СтрокаД.ПолучитьИдентификатор();
		Если Элементы.ОтменаКорректировок.ПроверитьСтроку(ИД) Тогда
			СтрокаД.Отметка = Истина;	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере();
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьНаСервере();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУдалениеНаСервере();
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьУдалениеНаСервере();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры


&НаСервере
Процедура ВыполнитьОбработкуНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ВыполнитьОбработкуНаСервере(Истина);
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры
&НаСервере
Процедура ВыполнитьУдалениеНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ВыполнитьУдалениеНаСервере(Истина);
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВыбораСклада()
	
	Запрос       = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	гф_ГлобальныеЗначенияСписок.Значение КАК Склад
	|ПОМЕСТИТЬ ВТ_Склады
	|ИЗ
	|	ПланВидовХарактеристик.гф_ГлобальныеЗначения.Список КАК гф_ГлобальныеЗначенияСписок
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.гф_ГлобальныеЗначения КАК гф_ГлобальныеЗначения
	|		ПО гф_ГлобальныеЗначенияСписок.Ссылка = гф_ГлобальныеЗначения.Ссылка
	|ГДЕ
	|	гф_ГлобальныеЗначения.Ключ = &Ключ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Склады.Склад КАК Значение
	|ИЗ
	|	ВТ_Склады КАК ВТ_Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ВТ_Склады.Склад = Склады.Ссылка
	|ГДЕ
	|	Склады.гф_Организация = &Организация";
	
	Запрос.УстановитьПараметр("Ключ",        "гф_ГлобальныеЗначенияОсновнойСклад");
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	ГлобальныеЗначенияОсновнойСклад = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Значение");
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Склады.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.гф_Организация = &Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	МассивСкладов    = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	Элементы.Склад.СписокВыбора.ЗагрузитьЗначения(МассивСкладов);
	
	// проставим первым по умолчанию
	Если ГлобальныеЗначенияОсновнойСклад.Количество() Тогда
		
		Объект.Склад = ГлобальныеЗначенияОсновнойСклад[0];
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивСсылокПТУ()
	
	Запрос       = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппыДоступаПартнеров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ГруппыДоступаПартнеров
	|ИЗ
	|	Справочник.ГруппыДоступаПартнеров КАК ГруппыДоступаПартнеров
	|;
	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПриобретениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПриобретениеТоваровУслуг.Партнер КАК Партнер,
	|	ПриобретениеТоваровУслуг.Контрагент КАК Контрагент,
	|	Партнеры.ГруппаДоступа КАК ГруппаДоступа,
	|	ПриобретениеТоваровУслугДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ПриобретениеТоваровУслугДополнительныеРеквизиты.Значение КАК Значение
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО ПриобретениеТоваровУслуг.Партнер = Партнеры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.ДополнительныеРеквизиты КАК ПриобретениеТоваровУслугДополнительныеРеквизиты
	|		ПО ПриобретениеТоваровУслуг.Ссылка = ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтборПоОрганизации
	|				ТОГДА ПриобретениеТоваровУслуг.Ссылка.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ПриобретениеТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Партнеры.ГруппаДоступа В
	|			(ВЫБРАТЬ
	|				ГруппыДоступаПартнеров.Ссылка
	|			ИЗ
	|				ГруппыДоступаПартнеров КАК ГруппыДоступаПартнеров)
	|	И НЕ ПриобретениеТоваровУслугДополнительныеРеквизиты.Свойство ЕСТЬ NULL
	|;
	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Документы.Ссылка КАК Ссылка,
	|	ВТ_Документы.Партнер КАК Партнер,
	|	ВТ_Документы.Контрагент КАК Контрагент
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО ВТ_Документы.Значение = ЗначенияСвойствОбъектов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_Документы.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.ИдентификаторДляФормул = ""гф_ПТУПроверкаПТУ""
	|	И ЗначенияСвойствОбъектов.Наименование = ""Загружен""
	
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Документы.Ссылка,
	|	ВТ_Документы.Партнер,
	|	ВТ_Документы.Контрагент";
	
	ДатаОкончания = Объект.Период.ДатаОкончания;
	Если НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда		
		ДатаОкончания = Дата(2999,12,31);
	КонецЕсли;	
	Запрос.УстановитьПараметр("ОтборПоОрганизации", ЗначениеЗаполнено(Объект.Организация));
	Запрос.УстановитьПараметр("Организация",        Объект.Организация);
	Запрос.УстановитьПараметр("ДатаНачала",         Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",      КонецДня(ДатаОкончания));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Функция ПолучитьМассивСсылокУдалениеПТУ()
	
	Запрос       = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппыДоступаПартнеров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ГруппыДоступаПартнеров
	|ИЗ
	|	Справочник.ГруппыДоступаПартнеров КАК ГруппыДоступаПартнеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПриобретениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПриобретениеТоваровУслуг.Партнер КАК Партнер,
	|	ПриобретениеТоваровУслуг.Контрагент КАК Контрагент,
	|	Партнеры.ГруппаДоступа КАК ГруппаДоступа,
	|	ПриобретениеТоваровУслугДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ПриобретениеТоваровУслугДополнительныеРеквизиты.Значение КАК Значение
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО ПриобретениеТоваровУслуг.Партнер = Партнеры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.ДополнительныеРеквизиты КАК ПриобретениеТоваровУслугДополнительныеРеквизиты
	|		ПО ПриобретениеТоваровУслуг.Ссылка = ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтборПоОрганизации
	|				ТОГДА ПриобретениеТоваровУслуг.Ссылка.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И Партнеры.ГруппаДоступа В
	|			(ВЫБРАТЬ
	|				ГруппыДоступаПартнеров.Ссылка
	|			ИЗ
	|				ГруппыДоступаПартнеров КАК ГруппыДоступаПартнеров)
	|	И НЕ ПриобретениеТоваровУслугДополнительныеРеквизиты.Свойство ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Документы.Ссылка КАК Ссылка,
	|	ВТ_Документы.Партнер КАК Партнер,
	|	ВТ_Документы.Контрагент КАК Контрагент
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Документы.Ссылка,
	|	ВТ_Документы.Партнер,
	|	ВТ_Документы.Контрагент";
	
	Запрос.УстановитьПараметр("ОтборПоОрганизации", ЗначениеЗаполнено(Объект.Организация));
	Запрос.УстановитьПараметр("Организация",        Объект.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти
