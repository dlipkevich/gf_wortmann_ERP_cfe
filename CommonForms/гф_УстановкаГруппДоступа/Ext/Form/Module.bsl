#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КомандаОбновитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаДоступаКонтрагентаПриИзменении(Элемент)
	
	КомандаОбновитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОрганизациииГруппы

&НаКлиенте
Процедура ТаблицаОрганизацийГруппГруппаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ТекущиеДанные = Элементы.ТаблицаОрганизацийГрупп.ТекущиеДанные;
	
	Отбор = Новый Структура("Организация",ТекущиеДанные.Организация);
	
	СтрокиГруппОрганизации = ТаблицаОрганизацийГруппДляВыбора.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаГруппы Из СтрокиГруппОрганизации Цикл
		
		ДанныеВыбора.Добавить(СтрокаГруппы.Группа);
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОбновить(Команда)
	
	КомандаОбновитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура КомандаЗаписатьНаСервере()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Подзапрос.ГруппаДоступа КАК ГруппаДоступа
	               |ПОМЕСТИТЬ ВТ_Есть
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ГруппыДоступа.Ссылка КАК ГруппаДоступа
	               |	ИЗ
	               |		Константы КАК Константы
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа КАК Организации
	               |			ПО Константы.гф_ГруппыДоступаДляКонтрагентов = Организации.Родитель
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа КАК ГруппыДоступа
	               |			ПО (Организации.Ссылка = ГруппыДоступа.Родитель)
	               |	ГДЕ
	               |		Организации.ЭтоГруппа) КАК Подзапрос
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа.ЗначенияДоступа КАК ГруппыДоступаЗначенияДоступа
	               |		ПО Подзапрос.ГруппаДоступа = ГруппыДоступаЗначенияДоступа.Ссылка
	               |			И (ГруппыДоступаЗначенияДоступа.ЗначениеДоступа = &ГруппаДоступаПартнеров)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ГруппыДоступа.Группа КАК ГруппаДоступа
	               |ПОМЕСТИТЬ ВТ_Нужно
	               |ИЗ
	               |	&ТаблицаГрупп КАК ГруппыДоступа
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Подзапрос.ГруппаДоступа КАК ГруппаДоступа,
	               |	Подзапрос.СоздатьУдалить КАК СоздатьУдалить
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ЕСТЬNULL(ВТ_Есть.ГруппаДоступа, ВТ_Нужно.ГруппаДоступа) КАК ГруппаДоступа,
	               |		НЕ ВТ_Нужно.ГруппаДоступа ЕСТЬ NULL КАК СоздатьУдалить
	               |	ИЗ
	               |		ВТ_Нужно КАК ВТ_Нужно
	               |			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Есть КАК ВТ_Есть
	               |			ПО ВТ_Нужно.ГруппаДоступа = ВТ_Есть.ГруппаДоступа
	               |	ГДЕ
	               |		(ВТ_Есть.ГруппаДоступа ЕСТЬ NULL
	               |				ИЛИ ВТ_Нужно.ГруппаДоступа ЕСТЬ NULL)) КАК Подзапрос
	               |ГДЕ
	               |	Подзапрос.ГруппаДоступа <> ЗНАЧЕНИЕ(Справочник.ГруппыДоступа.ПустаяСсылка)";
	
	Запрос.Параметры.Вставить("ГруппаДоступаПартнеров",ГруппаДоступаПартнера);
	Запрос.Параметры.Вставить("ТаблицаГрупп",ТаблицаОрганизацийГрупп.Выгрузить());
	
	Результат = Запрос.Выполнить();   
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ГруппаОбъект = Выборка.ГруппаДоступа.ПолучитьОбъект();
			
			Если Выборка.СоздатьУдалить Тогда
				
				СтрокаЗначенияДоступа = ГруппаОбъект.ЗначенияДоступа.Добавить();						
				
				СтрокаЗначенияДоступа.ЗначениеДоступа = ГруппаДоступаПартнера;
				
			Иначе
				
				СтрокаЗначенияДоступа = ГруппаОбъект.ЗначенияДоступа.Найти(ГруппаДоступаПартнера,"ЗначениеДоступа");
				
				Если СтрокаЗначенияДоступа <> Неопределено Тогда
					
					ГруппаОбъект.ЗначенияДоступа.Удалить(ГруппаОбъект.ЗначенияДоступа.Индекс(СтрокаЗначенияДоступа));	
					
				КонецЕсли;	
				
			КонецЕсли;	
			
			ГруппаОбъект.Записать();
			
		КонецЦикла;	
		
	КонецЕсли;	 
	
	КомандаОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	КомандаЗаписатьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура КомандаЗаписатьЗакрытьНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьЗакрыть(Команда) 
	
	КомандаЗаписатьЗакрытьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура КомандаОбновитьНаСервере()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	ГруппыДоступа.Ссылка КАК ГруппаДоступа
	|ПОМЕСТИТЬ ВТ_ОрганизацииГруппыДоступа
	|ИЗ
	|	Константы КАК Константы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа КАК Организации
	|		ПО Константы.гф_ГруппыДоступаДляКонтрагентов = Организации.Родитель
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа КАК ГруппыДоступа
	|		ПО (Организации.Ссылка = ГруппыДоступа.Родитель)
	|ГДЕ
	|	Организации.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ОрганизацииГруппыДоступа.Организация КАК Организация
	|ИЗ
	|	ВТ_ОрганизацииГруппыДоступа КАК ВТ_ОрганизацииГруппыДоступа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ОрганизацииГруппыДоступа.Организация КАК Организация,
	|	ВТ_ОрганизацииГруппыДоступа.ГруппаДоступа КАК Группа
	|ИЗ
	|	ВТ_ОрганизацииГруппыДоступа КАК ВТ_ОрганизацииГруппыДоступа
	|ГДЕ
	|	НЕ ВТ_ОрганизацииГруппыДоступа.ГруппаДоступа.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОрганизацииГруппыДоступа.Организация КАК Организация,
	|	ВТ_ОрганизацииГруппыДоступа.ГруппаДоступа КАК Группа
	|ИЗ
	|	Справочник.ГруппыДоступа.ЗначенияДоступа КАК ГруппыДоступаЗначенияДоступа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОрганизацииГруппыДоступа КАК ВТ_ОрганизацииГруппыДоступа
	|		ПО ГруппыДоступаЗначенияДоступа.Ссылка = ВТ_ОрганизацииГруппыДоступа.ГруппаДоступа
	|			И (ГруппыДоступаЗначенияДоступа.ЗначениеДоступа = &ГруппаДоступаПартнеров)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ОрганизацииГруппыДоступа.Организация.Наименование,
	|	ВТ_ОрганизацииГруппыДоступа.ГруппаДоступа.Наименование";
	
	Запрос.Параметры.Вставить("ГруппаДоступаПартнеров",ГруппаДоступаПартнера);
	
	Результат = Запрос.ВыполнитьПакет(); 
	
	ТаблицаОрганизацийГрупп.Загрузить(Результат[Результат.Количество()-1].Выгрузить());
	
	Элементы.ТаблицаОрганизацийГруппОрганизация.СписокВыбора.Очистить();
	
	ВыборкаОрганизаций = Результат[Результат.Количество()-3].Выбрать();
	
	Пока ВыборкаОрганизаций.Следующий() Цикл
		
		Элементы.ТаблицаОрганизацийГруппОрганизация.СписокВыбора.Добавить(ВыборкаОрганизаций.Организация);
		
	КонецЦикла;	   
	
	ТаблицаОрганизацийГруппДляВыбора.Загрузить(Результат[Результат.Количество()-2].Выгрузить());
	
КонецПроцедуры

#КонецОбласти
