#Область ОбработчикиСобытийФормы
  
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Данные = ТекущийОбъект.ДанныеХЗ.Получить();
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Данные);
		Если Порт = 0 Тогда
			Порт = 21;		
		КонецЕсли;
		Если Таймаут = 0 Тогда
			Таймаут = 10;		
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//Если Прав(Каталог, 1) <> "/" Тогда
	//	Каталог = Каталог + "/";	
	//КонецЕсли;
	ТекущийОбъект.ДанныеХЗ = вСформироватьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции  

&НаСервере
Функция СтруктураФТП()
	
	Данные = Новый Структура;
	Данные.Вставить("Сервер", "");
	Данные.Вставить("Порт", 21);
	Данные.Вставить("Пользователь", "");
	Данные.Вставить("Пароль", "");
	Данные.Вставить("ПользовательПрокси", "");
	Данные.Вставить("ПарольПрокси", "");
	Данные.Вставить("Каталог", "");
	Данные.Вставить("ПассивноеСоединение", Ложь);
	Данные.Вставить("Таймаут", 10);
		
	Возврат Данные;
	
КонецФункции

&НаСервере
Функция вСформироватьДанные()
	
	Данные = СтруктураФТП();
	ЗаполнитьЗначенияСвойств(Данные, ЭтотОбъект);
	ДанныеХЗ = Новый ХранилищеЗначения(Данные, Новый СжатиеДанных(6));
	Возврат ДанныеХЗ;
	
КонецФункции

&НаСервере
Процедура ПроверитьСоединениеНаСервере()
	
	Записать();
	
	// vvv Галфинд \ Sakovich 18.01.2024
	//FTP = Новый FTPСоединение(Сервер, Порт, Пользователь, Пароль, , ПассивноеСоединение, Таймаут);
	ЭтоЗащищенноеСоединение = SSL;
	Если ЭтоЗащищенноеСоединение Тогда
		СисИнфо = Новый СистемнаяИнформация();
		Если (СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86) 
			Или (СисИнфо.ТипПлатформы = ТипПлатформы.Windows_x86_64) Тогда
			// Windows
			СертификатКлиента = Новый СертификатКлиентаWindows();
			СертификатыУдостоверяющихЦентров = Новый СертификатыУдостоверяющихЦентровWindows;
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(
			СертификатКлиента, 
			СертификатыУдостоверяющихЦентров);
		Иначе
			// пока не реализовано
			// Linux_ARM64 (Linux_ARM64)
			// Linux_E2K (Linux_E2K)
			// Linux_x86 (Linux_x86)
			// Linux_x86_64 (Linux_x86_64)
			// MacOS_x86 (MacOS_x86)
			// MacOS_x86_64 (MacOS_x86_64)
			ЗащищенноеСоединение = Неопределено;
		КонецЕсли;
		
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	FTP = Новый FTPСоединение(
	Сервер, 
	Порт, 
	Пользователь,
	Пароль, , 
	ПассивноеСоединение, 
	Таймаут, 
	ЗащищенноеСоединение);
	// ^^^ Галфинд \ Sakovich 18.01.2024
	
	ИмяВременногоФайлаПроверкиПодключения = ПолучитьИмяВременногоФайла("tmp");
	ИмяФайлаНаСторонеПриемника = ОбменДаннымиСервер.ИмяВременногоФайлаПроверкиПодключения();
	
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайлаПроверкиПодключения);
	ЗаписьТекста.ЗаписатьСтроку(ИмяФайлаНаСторонеПриемника);
	ЗаписьТекста.Закрыть();
	
	// Изменено  Галфинд \ Sakovich 11.01.2024 old:< ИмяФайлаНаСторонеПриемника = Каталог + ИмяФайлаНаСторонеПриемника;>
	ПолноеИмяФайлаНаСторонеПриемника = Каталог + ИмяФайлаНаСторонеПриемника; 
	
	Попытка
		//   Галфинд \ Sakovich 11.01.2024 17:37:34   old:<FTP.Записать(ИмяВременногоФайлаПроверкиПодключения, ИмяФайлаНаСторонеПриемника);>
		FTP.Записать(ИмяВременногоФайлаПроверкиПодключения, ПолноеИмяФайлаНаСторонеПриемника); 
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецПопытки;

	Попытка
		УдалитьФайлы(ИмяВременногоФайлаПроверкиПодключения);
		// Изменено  Галфинд \ Sakovich 11.01.2024 old:<FTP.Удалить(ИмяФайлаНаСторонеПриемника)>
		FTP.Удалить(Каталог, ИмяФайлаНаСторонеПриемника);
	Исключение
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецПопытки;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Успешно!");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСоединение(Команда)
	ПроверитьСоединениеНаСервере();
КонецПроцедуры

 #КонецОбласти