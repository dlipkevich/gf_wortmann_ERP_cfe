#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	пЕстьПраваАдминистратора = ПравоДоступа("Администрирование", Метаданные);
	Элементы.Форма_ЗаполнитьКлючиДляПредопределенных.Доступность = пЕстьПраваАдминистратора;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
&НаКлиенте
Процедура _ЗаполнитьКлючиДляПредопределенных(Команда)
	
	пТекст = "Для предопределенных элементов будут заполнены пустые значения реквизита ""Ключ"".
	|Продолжить?";
	ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ЗаполнитьКлючиДляПредопределенныхОтвет", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещенияВопрос, пТекст, РежимДиалогаВопрос.ДаНетОтмена, 20);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКлючиДляПредопределенныхОтвет(Ответ, ДопПарам = Неопределено) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		вЗаполнитьЗначенияКлючаДляПредопределенных();
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура вЗаполнитьЗначенияКлючаДляПредопределенных()
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тДанные.Ссылка КАК Ссылка,
	|	тДанные.ИмяПредопределенныхДанных КАК Ключ
	|ИЗ
	|	ПланВидовХарактеристик.гф_ГлобальныеЗначения КАК тДанные
	|ГДЕ
	|	тДанные.Предопределенный = истина
	|	И тДанные.Ключ = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		пОбъект = Выборка.Ссылка.ПолучитьОбъект();
		пОбъект.Ключ = Выборка.Ключ;
		
		Попытка
			пОбъект.Записать();
		Исключение
			пТекст = "Ошибка при записи объекта: " + Выборка.Ссылка + Символы.ПС + ОписаниеОшибки();
			_омОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(пТекст);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
