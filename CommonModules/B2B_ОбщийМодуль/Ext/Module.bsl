
Функция ПереводОбъекта(ссОбъект, Язык) Экспорт
    Если Не ЗначениеЗаполнено(ссОбъект) Тогда
        Возврат "";
    КонецЕсли;
    
    запрос = Новый Запрос("ВЫБРАТЬ
                          | RC_Переводы.Значение
                          |ИЗ
                          | РегистрСведений.RC_Переводы КАК RC_Переводы
                          |ГДЕ
                          | RC_Переводы.Объект = &Объект
                          | И RC_Переводы.Язык = &Язык");
    запрос.УстановитьПараметр("Объект", ссОбъект);
    запрос.УстановитьПараметр("Язык", Язык);
    
    выборка = запрос.Выполнить().Выбрать();
    Если выборка.Следующий() Тогда
        Возврат выборка.Значение;
    КонецЕсли;
    
    Возврат "" + ссОбъект;	
КонецФункции


Функция B2B_ВернутьСвойствоКороба(Номенклатура, ИмяСвойства)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КомплектующиеНоменклатуры.Комплектующая КАК Комплектующая
	|ПОМЕСТИТЬ Номенклатура
	|ИЗ
	|	РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
	|ГДЕ
	|	КомплектующиеНоменклатуры.Номенклатура = &Номенклатура
	|;
    |
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗначенияСвойствОбъектов.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|ЗначенияСвойствОбъектов.Свойство В
	|		(ВЫБРАТЬ
	|			РИ_ИдентификаторыСвойствНоменклатурыБитрикс.Объект КАК Объект
	|		ИЗ
	|			РегистрСведений.РИ_ИдентификаторыСвойствНоменклатурыБитрикс КАК РИ_ИдентификаторыСвойствНоменклатурыБитрикс
	|		ГДЕ
	|			РИ_ИдентификаторыСвойствНоменклатурыБитрикс.Идентификатор = &ИмяСвойства)
	|	И ЗначенияСвойствОбъектов.Объект В
	|			(ВЫБРАТЬ
	|				Номенклатура.Комплектующая КАК Комплектующая
	|			ИЗ
	|				Номенклатура КАК Номенклатура)";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ИмяСвойства", ИмяСвойства);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Лев(ПереводОбъекта(Выборка.Значение, Справочники.RC_Язык.Russian), 1000);	
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции         

Функция B2B_ПолучитьКомплектующие(Номенклатура)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КомплектующиеНоменклатуры.Комплектующая КАК Комплектующая
	|ИЗ
	|	РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
	|ГДЕ
	|	КомплектующиеНоменклатуры.Номенклатура = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура B2B_ЗарегистрироватьИзменения(Объект) Экспорт
	
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.Номенклатура") ИЛИ ТипЗнч(Объект) = Тип("СправочникСсылка.Номенклатура") Тогда
		Если НЕ ЗначениеЗаполнено(Объект.B2B_Портал) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);   
	
	
	МассивУзлов = B2B_ПолучитьМассивУзлов();
	
	ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, Объект.Ссылка);
	
	
	УстановитьПривилегированныйРежим(Ложь);   
	
КонецПроцедуры

функция B2B_ПолучитьМассивУзлов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Б_ОбменССайтом.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.Б_ОбменССайтом КАК Б_ОбменССайтом
	|ГДЕ
	|	(Б_ОбменССайтом.Ссылка <> &ЭтотУзел) и (Б_ОбменССайтом.B2B_Портал <> Значение(Справочник.B2B_Портал.ПустаяСсылка))	
	|";
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.Б_ОбменССайтом.ЭтотУзел());
	МассивУзлов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

	Возврат МассивУзлов; 
	
КонецФункции

//1БЦ++ АК заявка 32347 выгрузка свойства количество в коробе 05.07.2022
Функция B2B_ПолучитьКоличествоВКоробе(Номенклатура)
	Попытка
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(КомплектующиеНоменклатуры.Количество) КАК КоличествоКомплектующиих
		|ИЗ
		|	РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
		|ГДЕ
		|	КомплектующиеНоменклатуры.Номенклатура = &Номенклатура";
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		Возврат Запрос.Выполнить().Выгрузить()[0].КоличествоКомплектующиих;
	Исключение
		Возврат 0;
	КонецПопытки;
КонецФункции
//1БЦ-- АК заявка 32347 выгрузка свойства количество в коробе 05.07.2022