
&ИзменениеИКонтроль("ОписанияОбластейПоиска")
Функция гф_ОписанияОбластейПоиска(Форма, ПараметрыПоиска)
	
	// В зависимости от того, из какой формы выполняется вызов функции поиска,
	// список областей поиска будет различаться.
	ОбластиПоиска = Новый Массив;
	
	Если ПараметрыПоиска.ИмяСпискаПоиска = "Номенклатура" Тогда
		
		ОписаниеОбластиПоиска                             = ОписаниеОбластиПоиска();
		ОписаниеОбластиПоиска.ОбъектМетаданных            = Метаданные.Справочники.Номенклатура;
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаПоОбласти =
		#Удаление
		"ВЫБРАТЬ ПЕРВЫЕ 999
		#КонецУдаления
		#Вставка
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 999
		#КонецВставки
		|	СправочникНоменклатура.Ссылка КАК Значение,
		|	""Номенклатура"" КАК Метаданные
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|ГДЕ
		|	НЕ СправочникНоменклатура.ЭтоГруппа И
		|	(СправочникНоменклатура.Наименование = &УсловиеСравнения
		|			ИЛИ СправочникНоменклатура.НаименованиеПолное = &УсловиеСравнения
		|			ИЛИ СправочникНоменклатура.Код = &УсловиеСравнения
		|			ИЛИ СправочникНоменклатура.КодДляПоиска = &УсловиеСравнения
		|			ИЛИ СправочникНоменклатура.Артикул = &УсловиеСравнения)";

		Если ПараметрыПоиска.Свойство("ДополнительныеУсловия") Тогда
			ОписаниеОбластиПоиска.ТекстЗапросаПоискаПоОбласти = ОписаниеОбластиПоиска.ТекстЗапросаПоискаПоОбласти +
				"
				|	И " + СтрСоединить(ПараметрыПоиска.ДополнительныеУсловия, " И ");
		КонецЕсли;
		
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаНоменклатуры = "";
		ОбластиПоиска.Добавить(ОписаниеОбластиПоиска);
		
		ОписаниеОбластиПоиска                             = ОписаниеОбластиПоиска();
		ОписаниеОбластиПоиска.ОбъектМетаданных            = Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры;
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаПоОбласти =
		"ВЫБРАТЬ ПЕРВЫЕ 999
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Значение,
		|	""ШтрихкодыНоменклатуры"" КАК Метаданные
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод = &СтрокаПоискаПоШтрихкоду";
		ОписаниеОбластиПоиска.ПараметрыЗапросаПоискаПоОбласти.Вставить("СтрокаПоискаПоШтрихкоду",
		ИсходнаяСтрокаПоиска(ПараметрыПоиска.СтрокаПоиска, Форма.НайтиНоменклатуруПоТочномуСоответствию));
		
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаНоменклатуры =
		"ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод В(&ШтрихкодыНоменклатуры)";
		ОбластиПоиска.Добавить(ОписаниеОбластиПоиска);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПродажиНаЭкспорт")
			Или ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки") Тогда
			
			ОписаниеОбластиПоиска                             = ПодборТоваровСервер.ОписаниеОбластиПоиска();
			ОписаниеОбластиПоиска.ОбъектМетаданных            = Метаданные.Справочники.КлассификаторТНВЭД;
			ОписаниеОбластиПоиска.ТекстЗапросаПоискаПоОбласти =
			"ВЫБРАТЬ ПЕРВЫЕ 999
			|	КлассификаторТНВЭД.Ссылка КАК Значение,
			|	""КлассификаторТНВЭД"" КАК Метаданные
			|ИЗ
			|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
			|ГДЕ
			|	КлассификаторТНВЭД.Код = &УсловиеСравнения";
			ОписаниеОбластиПоиска.ТекстЗапросаПоискаНоменклатуры =
			"ВЫБРАТЬ
			|	Номенклатура.Ссылка КАК Номенклатура
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
			|		ПО Номенклатура.КодТНВЭД = КлассификаторТНВЭД.Ссылка
			|ГДЕ
			|	КлассификаторТНВЭД.Ссылка В(&КлассификаторТНВЭД)";
			ОбластиПоиска.Добавить(ОписаниеОбластиПоиска);
			
		КонецЕсли;
		
	КонецЕсли;
		
	Если (ПараметрыПоиска.ИмяСпискаПоиска = "Номенклатура"
		Или ПараметрыПоиска.ИмяСпискаПоиска = "НоменклатураКонтрагентов")
		И ПодборТоваровКлиентСервер.ЭтоФормаПодбораВДокументыЗакупки(Форма) Тогда
		
		ОписаниеОбластиПоиска                             = ОписаниеОбластиПоиска();
		ОписаниеОбластиПоиска.ОбъектМетаданных            = Метаданные.Справочники.НоменклатураКонтрагентов;
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаПоОбласти =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 999
		|	НоменклатураКонтрагентов.Ссылка КАК Значение,
		|	""НоменклатураКонтрагентов"" КАК Метаданные
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|ГДЕ
		|	НЕ НоменклатураКонтрагентов.ЭтоГруппа И
		|	(НоменклатураКонтрагентов.Артикул = &УсловиеСравнения
		|		ИЛИ НоменклатураКонтрагентов.Наименование = &УсловиеСравнения)
		|	И НоменклатураКонтрагентов.Владелец = &Партнер";
		
		ОписаниеОбластиПоиска.ПараметрыЗапросаПоискаПоОбласти.Вставить("Партнер", Форма.ПоставщикПартнер);
		
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаНоменклатуры =
		"ВЫБРАТЬ
		|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|ГДЕ
		|	НоменклатураКонтрагентов.Ссылка В(&НоменклатураКонтрагентов)";
		ОбластиПоиска.Добавить(ОписаниеОбластиПоиска);
	КонецЕсли;

	Если ПараметрыПоиска.ИмяСпискаПоиска = "ХарактеристикиНоменклатуры" Тогда
		ОписаниеОбластиПоиска                             = ОписаниеОбластиПоиска();
		ОписаниеОбластиПоиска.ОбъектМетаданных            = Метаданные.Справочники.ХарактеристикиНоменклатуры;
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаПоОбласти =
		"ВЫБРАТЬ ПЕРВЫЕ 999
		|	ХарактеристикиНоменклатуры.Ссылка КАК Значение,
		|	""ХарактеристикиНоменклатуры"" КАК Метаданные
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|ГДЕ
		|	ХарактеристикиНоменклатуры.Наименование = &УсловиеСравнения
		|	ИЛИ ХарактеристикиНоменклатуры.НаименованиеПолное = &УсловиеСравнения";
		ОписаниеОбластиПоиска.ТекстЗапросаПоискаНоменклатуры = "";
		ОбластиПоиска.Добавить(ОписаниеОбластиПоиска);
		
	КонецЕсли;
	
	НоменклатураЛокализация.ОписанияОбластейПоиска(ОбластиПоиска, Форма, ПараметрыПоиска);	
	
	Возврат ОбластиПоиска;
	
КонецФункции
