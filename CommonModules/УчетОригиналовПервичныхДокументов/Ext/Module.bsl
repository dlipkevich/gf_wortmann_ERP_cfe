#Область ПрограммныйИнтерфейс

// #wortmann {
// #
// Галфинд Окунев 2022/08/14
// Удаляет из списков объекты и формы, отсутствующие в регистре
// гф_ОтслеживаемыеОригиналыПервичныхДокументов, при условии, что в нем есть записи
//
// Параметры:
//
// ОбъектыПечати	- СписокЗначений
// СписокПечати		- СписокЗначений
//
&После("ПриОпределенииСпискаПечатныхФорм")
Процедура гф_ПриОпределенииСпискаПечатныхФорм(ОбъектыПечати, СписокПечати) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ИдентификаторыОбъектовМетаданных.Имя КАК ВидДокумента,
	|	гф_ОтслеживаемыеОригиналыПервичныхДокументов.ПервичныйДокумент КАК ПервичныйДокумент
	|ИЗ
	|	РегистрСведений.гф_ОтслеживаемыеОригиналыПервичныхДокументов КАК гф_ОтслеживаемыеОригиналыПервичныхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|		ПО гф_ОтслеживаемыеОригиналыПервичныхДокументов.ВидДокумента = ИдентификаторыОбъектовМетаданных.Ссылка";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;	      
	
	ТаблицаРегистрируемых = Результат.Выгрузить();
	
	ФормыКУдалению = Новый Массив;

	Для Каждого Документ Из ОбъектыПечати Цикл

		Для Каждого ПечатнаяФорма Из СписокПечати Цикл
			
			ОтборДокументовФорм = Новый Структура("ВидДокумента, ПервичныйДокумент",
				 Документ.Значение.Метаданные().Имя, ПечатнаяФорма.Значение);
				 
			СтрокиРегистрируемых = ТаблицаРегистрируемых.НайтиСтроки(ОтборДокументовФорм);	 
			
			Если СтрокиРегистрируемых.Количество() = 0 Тогда
				
				ФормыКУдалению.Добавить(ПечатнаяФорма);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЦикла;	
	
	Для Каждого ПечатнаяФорма Из ФормыКУдалению Цикл
		
		СписокПечати.Удалить(ПечатнаяФорма);
		
	КонецЦикла;	
	
	Если СписокПечати.Количество() = 0 Тогда   
		
		ОбъектыПечати.Очистить();
		
	КонецЕсли;	
	
КонецПроцедуры// } #wortmann

#КонецОбласти