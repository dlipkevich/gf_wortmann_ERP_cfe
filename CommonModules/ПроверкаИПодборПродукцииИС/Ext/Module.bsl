
&ИзменениеИКонтроль("ДополнитьКешИТаблицуШтрихкодовУпаковок")
Процедура гф_ДополнитьКешИТаблицуШтрихкодовУпаковок(Форма, ОбновляемаяСтрока, ДобавленныеШтрихкодыУпаковок)

	ДанныеШтрихкодовУпаковокГосИС = Форма.ДанныеШтрихкодовУпаковокГосИС;

	// Дополнительные ключи связи, специфика: поля поиска для незаполненной номенклатуры.
	#Вставка
	Если ТипЗнч(Форма) = Тип("Структура") Тогда
		КолонкиКеша = ДанныеШтрихкодовУпаковокГосИС.Колонки;
	Иначе
	#КонецВставки
	КолонкиКеша = ДанныеШтрихкодовУпаковокГосИС.Выгрузить(Новый Массив).Колонки;
	#Вставка
	КонецЕсли;
	#КонецВставки
	ЕстьGTIN = КолонкиКеша.Найти("GTIN") <> Неопределено;
	// Дополнительное поле кеша, КМ на переменное количество: должны быть и колонка и переменное количество
	ЕстьКоличествоКодов = КолонкиКеша.Найти("КоличествоПотребительскихУпаковок") <> Неопределено
	И ОбновляемаяСтрока.Свойство("КоличествоПотребительскихУпаковок");
	// Дополнительные ключи связи, специфика: поля поиска для маркировки молочной продукции
	ДобавитьШтрихкодОднороднойУпаковки = ОбновляемаяСтрока.Свойство("ШтрихкодОднороднойУпаковки");

	ПоляПоиска = ИнтеграцияИС.ПоляПоискаМаркируемойПродукции(, ЕстьGTIN, ДобавитьШтрихкодОднороднойУпаковки);

	ДобавитьШтрихкодУпаковки = Истина;

	ЗаполнитьЗначенияСвойств(ПоляПоиска, ОбновляемаяСтрока);
	Если ОбновляемаяСтрока.ЧастичноеВыбытиеОтдельнойНоменклатуры Тогда
		ПоляПоиска.Серия = ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("СерияНоменклатуры");
	КонецЕсли;

	ДанныеКеша = ДанныеШтрихкодовУпаковокГосИС.НайтиСтроки(ПоляПоиска);
	Если ДанныеКеша.Количество() Тогда
		СтрокаКеша = ДанныеКеша[0];
		ШтрихкодыУпаковок = Новый Соответствие(СтрокаКеша.ШтрихкодыУпаковок);
		Если ЕстьКоличествоКодов Тогда
			СтрокаКеша.Количество = СтрокаКеша.Количество + ОбновляемаяСтрока.Количество;
			СтрокаКеша.КоличествоПотребительскихУпаковок = СтрокаКеша.КоличествоПотребительскихУпаковок + ОбновляемаяСтрока.КоличествоПотребительскихУпаковок;
		Иначе
			СтрокаКеша.Количество = СтрокаКеша.Количество + ?(ОбновляемаяСтрока.Количество = 0, 1, ОбновляемаяСтрока.Количество);
		КонецЕсли;
	Иначе
		СтрокаКеша = ДанныеШтрихкодовУпаковокГосИС.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКеша, ОбновляемаяСтрока);
		Если ОбновляемаяСтрока.ЧастичноеВыбытиеОтдельнойНоменклатуры Тогда
			СтрокаКеша.ПодменнаяНоменклатура = Истина;
		КонецЕсли;
		ШтрихкодыУпаковок = Новый Соответствие;
	КонецЕсли;

	Если ОбновляемаяСтрока.Количество = 0 Тогда
		СтрокаКеша.БезКоличества = Истина;
	КонецЕсли;

	ДобавитьШтрихкодУпаковки = ДобавленныеШтрихкодыУпаковок.Найти(ОбновляемаяСтрока.ШтрихкодУпаковки) = Неопределено;

	// Добавление штрихкодов упаковок, специфика маркировки молочной продукции
	Если ДобавитьШтрихкодОднороднойУпаковки
		И ЗначениеЗаполнено(ОбновляемаяСтрока.ШтрихкодОднороднойУпаковки) Тогда

		ШтрихкодыУпаковок.Вставить(ОбновляемаяСтрока.ШтрихкодОднороднойУпаковки);

		НоваяСтрока = Форма.Объект.ШтрихкодыУпаковок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбновляемаяСтрока);
		НоваяСтрока.ШтрихкодУпаковки = ОбновляемаяСтрока.ШтрихкодОднороднойУпаковки;

		ДобавитьШтрихкодУпаковки = ДобавитьШтрихкодУпаковки
		И ОбновляемаяСтрока.ШтрихкодОднороднойУпаковки <> ОбновляемаяСтрока.ШтрихкодУпаковки;

	КонецЕсли;

	ШтрихкодыУпаковок.Вставить(ОбновляемаяСтрока.ШтрихкодУпаковки);

	Если ДобавитьШтрихкодУпаковки Тогда

		НоваяСтрока = Форма.Объект.ШтрихкодыУпаковок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбновляемаяСтрока);
		Если ДобавитьШтрихкодОднороднойУпаковки Тогда
			ОчищаемыеРеквизиты = Новый Структура("СрокГодности,ИдентификаторПроисхожденияВЕТИС");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОчищаемыеРеквизиты);
			ДобавленныеШтрихкодыУпаковок.Добавить(ОбновляемаяСтрока.ШтрихкодУпаковки);
		КонецЕсли;

	КонецЕсли;

	СтрокаКеша.ШтрихкодыУпаковок = Новый ФиксированноеСоответствие(ШтрихкодыУпаковок);

КонецПроцедуры
