#Область СлужебныйПрограммныйИнтерфейс

// Возвращает таблицу кодов и атрибутов операций
// из табличного документа Обработки.гф_ПочтаРоссииЗапросКТрекеру.КодыОпераций
Функция ПолучитьТаблицуСоответствияКодовОпераций() Экспорт          
	
	ИнициализироватьСправочник_гф_ПочтаРоссииОперацииОтслеживания();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	гф_ПочтаРоссииОперацииОтслеживания.Ссылка КАК Ссылка,
	               |	гф_ПочтаРоссииОперацииОтслеживания.КодОперации КАК КодОперации,
	               |	гф_ПочтаРоссииОперацииОтслеживания.КодАтрибута КАК КодАтрибута,
	               |	гф_ПочтаРоссииОперацииОтслеживания.Операция КАК Операция,
	               |	гф_ПочтаРоссииОперацииОтслеживания.Атрибут КАК Атрибут,
	               |	гф_ПочтаРоссииОперацииОтслеживания.КонечнаяОперация КАК КонечнаяОперация
	               |ИЗ
	               |	Справочник.гф_ПочтаРоссииОперацииОтслеживания КАК гф_ПочтаРоссииОперацииОтслеживания";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;	 
	
	ТаблицаСоответствийКодовОпераций = Результат.Выгрузить();
	
	Возврат ТаблицаСоответствийКодовОпераций; 
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции  

Процедура ИнициализироватьСправочник_гф_ПочтаРоссииОперацииОтслеживания()
	
	ТаблицаСоответствий = гф_ПочтаРоссииПовтИсп.ПолучитьТаблицуСоответствияКодовОперацийСлужебный();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТ_Соответствия.КодОперации КАК КодОперации,
	               |	ВТ_Соответствия.КодАтрибута КАК КодАтрибута,
	               |	ВТ_Соответствия.Операция КАК Операция,
	               |	ВТ_Соответствия.Атрибут КАК Атрибут,
	               |	ВТ_Соответствия.КонечнаяОперация КАК КонечнаяОперация
	               |ПОМЕСТИТЬ ВТ_Соответствия
	               |ИЗ
	               |	&ТаблицаСоответствий КАК ВТ_Соответствия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ   
				   |	гф_ПочтаРоссииОперацииОтслеживания.Ссылка КАК Ссылка,
	               |	ВТ_Соответствия.КодОперации КАК КодОперации,
	               |	ВТ_Соответствия.КодАтрибута КАК КодАтрибута,
	               |	ВТ_Соответствия.Операция КАК Операция,
	               |	ВТ_Соответствия.Атрибут КАК Атрибут,
	               |	ВТ_Соответствия.КонечнаяОперация КАК КонечнаяОперация
	               |ИЗ
	               |	ВТ_Соответствия КАК ВТ_Соответствия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гф_ПочтаРоссииОперацииОтслеживания КАК гф_ПочтаРоссииОперацииОтслеживания
	               |		ПО ВТ_Соответствия.КодОперации = гф_ПочтаРоссииОперацииОтслеживания.КодОперации
	               |			И ВТ_Соответствия.КодАтрибута = гф_ПочтаРоссииОперацииОтслеживания.КодАтрибута
	               |ГДЕ
	               |	гф_ПочтаРоссииОперацииОтслеживания.Ссылка ЕСТЬ NULL";
	
	Запрос.Параметры.Вставить("ТаблицаСоответствий", ТаблицаСоответствий);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;	 
	
	Таб = Результат.Выгрузить();
	
	Выборка = Результат.Выбрать();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Справочники.гф_ПочтаРоссииОперацииОтслеживания.СоздатьЭлемент();
		
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		
		ЗаполнитьЗначенияСвойств(СправочникОбъект, Выборка);     
		
		Если Выборка.КодАтрибута = 0  Тогда
			
			СправочникОбъект.Наименование = СправочникОбъект.Операция + ".";
			
		Иначе	
		
			СправочникОбъект.Наименование = СправочникОбъект.Операция + ". " + СправочникОбъект.Атрибут + ".";
		
		КонецЕсли;
		
		СправочникОбъект.Записать();
		
	КонецЦикла;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПолучитьТаблицуСоответствияКодовОперацийСлужебный() Экспорт
	
	ТаблицаСоответствийКодовОпераций = Новый ТаблицаЗначений;    
	
	КвалификаторыЧисла4		= Новый КвалификаторыЧисла(4, 0, ДопустимыйЗнак.Неотрицательный);
	КвалификаторыСтроки64	= Новый КвалификаторыСтроки(64, ДопустимаяДлина.Переменная);
	КвалификаторыСтроки120	= Новый КвалификаторыСтроки(120, ДопустимаяДлина.Переменная);
	
	ОписаниеТиповБулево		= Новый ОписаниеТипов("Булево");
	ОписаниеТиповЧисло4		= Новый ОписаниеТипов("Число", , , КвалификаторыЧисла4);
	ОписаниеТиповСтрока64	= Новый ОписаниеТипов("Строка", , , КвалификаторыСтроки64);
	ОписаниеТиповСтрока120	= Новый ОписаниеТипов("Строка", , , КвалификаторыСтроки120);
	
	ТаблицаСоответствийКодовОпераций.Колонки.Добавить("КодОперации",		ОписаниеТиповЧисло4);
	ТаблицаСоответствийКодовОпераций.Колонки.Добавить("КодАтрибута",		ОписаниеТиповЧисло4);
	ТаблицаСоответствийКодовОпераций.Колонки.Добавить("Операция",			ОписаниеТиповСтрока64);
	ТаблицаСоответствийКодовОпераций.Колонки.Добавить("Атрибут",			ОписаниеТиповСтрока120);
	ТаблицаСоответствийКодовОпераций.Колонки.Добавить("КонечнаяОперация",	ОписаниеТиповБулево);
	
	ДокументКоды = Обработки.гф_ПочтаРоссииЗапросКТрекеру.ПолучитьМакет("КодыОпераций"); 
	
	КолонкаКодОперации		= 1; 
	КолонкаОперация			= 2; 
	КолонкаКодАтрибута		= 3; 
	КолонкаАтрибут			= 4; 
	КолонкаКонечнаяОперация	= 5;  
	
	ПерваяСтрокаДанных		= 2;
	
	КодОперации = Неопределено;
	
	Для НомерСтроки = ПерваяСтрокаДанных По ДокументКоды.ВысотаТаблицы Цикл
		
		ОбластьКодОперации		= ДокументКоды.Область(НомерСтроки, КолонкаКодОперации);
		ОбластьОперация			= ДокументКоды.Область(НомерСтроки, КолонкаОперация);
		ОбластьКодАтрибута		= ДокументКоды.Область(НомерСтроки, КолонкаКодАтрибута);
		ОбластьАтрибут			= ДокументКоды.Область(НомерСтроки, КолонкаАтрибут);
		ОбластьКонечнаяОперация	= ДокументКоды.Область(НомерСтроки, КолонкаКонечнаяОперация);
		
		КодСледующейОперации = гф_ПочтаРоссии.СтрокаВЧисло(ОбластьКодОперации.Текст);
		
		Если ЗначениеЗаполнено(КодСледующейОперации) Тогда       
			
			КодОперации	= КодСледующейОперации; 
			Операция	= ОбластьОперация.Текст;
			
		КонецЕсли;	 
		
		СтрокаСоответствия = ТаблицаСоответствийКодовОпераций.Добавить();
		
		СтрокаСоответствия.КодОперации		= КодОперации;
		СтрокаСоответствия.КодАтрибута		= гф_ПочтаРоссии.СтрокаВЧисло(ОбластьКодАтрибута.Текст);
		СтрокаСоответствия.Операция			= Операция;
		СтрокаСоответствия.Атрибут			= ?(СтрокаСоответствия.КодАтрибута = 0, "", ОбластьАтрибут.Текст);
		СтрокаСоответствия.КонечнаяОперация	= ЗначениеЗаполнено(ОбластьКонечнаяОперация.Текст);
		
	КонецЦикла;	
	
	Возврат ТаблицаСоответствийКодовОпераций; 
	
КонецФункции	

#КонецОбласти

