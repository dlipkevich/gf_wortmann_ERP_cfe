#Область ПрограммныйИнтерфейс

Функция ПолучитьДанныеОтслеживания(УчетнаяЗапись, ТрекНомер, Ошибки = Неопределено, Записывать = Истина) Экспорт 
	
	Если Ошибки = Неопределено Тогда
		
		Ошибки = Новый Массив;
		
	КонецЕсли;	
	
	Логин = УчетнаяЗапись.Логин;
	
	УстановитьПривилегированныйРежим(Истина);
	
   	Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(УчетнаяЗапись, "Пароль");
	
	УстановитьПривилегированныйРежим(Ложь);  
	
	ОтветОтСервераXDTO = ПолучитьДанныеОтслеживанияСлужебный(Логин, Пароль, ТрекНомер, Ошибки); 
	
	ОтветОтСервера = ДанныеОтслеживанияВМассивСтруктур(ОтветОтСервераXDTO);  
	
	Если Записывать Тогда
		
		ЗаписатьИсториюОтслеживания(ТрекНомер, ОтветОтСервера);
		
	КонецЕсли;	
	
	Возврат ОтветОтСервера;
	
КонецФункции	

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьДанныеОтслеживанияСлужебный(Логин, Пароль, ТрекНомер, Ошибки = Неопределено) Экспорт 
	
	Если Ошибки = Неопределено Тогда
		
		Ошибки = Новый Массив;
		
	КонецЕсли;	
	
	URI						= "http://russianpost.org/operationhistory";
	НаименованиеВебСервиса	= "OperationHistory12";
	НаименованиеПорта		= "OperationHistory12Port";     
	ТипЗначенияXDTO			= "http://russianpost.org/operationhistory/data";
	WSСсылка				= WSСсылки.гф_ПочтаРоссииЕдиничныйДоступКТрекеру;
	
	Соединение = Новый ЗащищенноеСоединениеOpenSSL;
	
	Попытка
		
		Прокси = WSСсылка.СоздатьWSПрокси(URI, НаименованиеВебСервиса, НаименованиеПорта, , , Соединение);
		
	Исключение   
		
		Ошибки.Добавить(ОписаниеОшибки());
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	Фабрика = Прокси.ФабрикаXDTO;        
	
	ЗаголовокАвторизации = Фабрика.Создать(ТипЗначенияXDTO, "AuthorizationHeader");
	
	ЗаголовокАвторизации.login		= Логин;
	ЗаголовокАвторизации.password	= Пароль;
	
	ЗапросИстории = Фабрика.Создать(ТипЗначенияXDTO, "OperationHistoryRequest");
	
	ЗапросИстории.Barcode		= ТрекНомер;
	ЗапросИстории.MessageType	= 0;
	ЗапросИстории.Language		= "RUS";
	
	Пакеты = Фабрика.Пакеты.Получить(URI);    
	
	ПакетЗапроса = Пакеты.Получить("getOperationHistory");
	
	Запрос = Фабрика.Создать(ПакетЗапроса);
	
	Запрос.OperationHistoryRequest	= ЗапросИстории;
	Запрос.AuthorizationHeader		= ЗаголовокАвторизации;
	
	Попытка
		
		ОтветОтСервера = Прокси.getOperationHistory(Запрос);  
		
	Исключение   
		
		Ошибки.Добавить(ОписаниеОшибки());
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	Возврат ОтветОтСервера;
	
КонецФункции	

Функция ДанныеОтслеживанияВМассивСтруктур(ДанныеXDTO) Экспорт  
	
	Результат = Новый Массив;           
	
	ТаблицаСоответствияКодовОпераций = гф_ПочтаРоссииПовтИсп.ПолучитьТаблицуСоответствияКодовОпераций();	
	
	Для Каждого Элемент Из ДанныеXDTO.OperationHistoryData.historyRecord Цикл
		
		Запись = ОтветОтТрекераСтруктураЗапись();
		
		Операция = ОтветОтТрекераСтруктураОперация();  
		
		КодОперации = СтрокаВЧисло(Элемент.OperationParameters.OperType.id);
		КодАтрибута = СтрокаВЧисло(Элемент.OperationParameters.OperAttr.id);  
		
		Операция.Дата			= Элемент.OperationParameters.OperDate;
		Операция.КодОперации	= КодОперации;
		Операция.КодАтрибута	= КодАтрибута;
													
		Отбор = Новый Структура("КодОперации, КодАтрибута", КодОперации, КодАтрибута);
		
		СтрокиОпераций = ТаблицаСоответствияКодовОпераций.НайтиСтроки(Отбор);
		
		Если СтрокиОпераций.Количество() Тогда   
			
			ЗаполнитьЗначенияСвойств(Операция, СтрокиОпераций[0]);
			
		КонецЕсли;	
		
		Запись.Операция = Операция;
		
		Результат.Добавить(Запись);
		
	КонеЦЦикла;              
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции   

Процедура ЗаписатьИсториюОтслеживания(ТрекНомер, ОтветОтСервера, Ошибки = Неопределено) 
	
	Если Ошибки = Неопределено Тогда
		
		Ошибки = Новый Массив;
		
	КонецЕсли;	

	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	гф_ПочтаРоссииПочтовыеОтправления.Ссылка КАК Ссылка,
	               |	гф_ПочтаРоссииПочтовыеОтправления.ДоставкаЗавершена КАК ДоставкаЗавершена
	               |ИЗ
	               |	Справочник.гф_ПочтаРоссииПочтовыеОтправления КАК гф_ПочтаРоссииПочтовыеОтправления
	               |ГДЕ
	               |	гф_ПочтаРоссииПочтовыеОтправления.ТрекНомер = &ТрекНомер";
	
	Запрос.Параметры.Вставить("ТрекНомер", ТрекНомер);   
	
	Результат = Запрос.Выполнить();
	
	ДоставкаЗавершена = Ложь;
	
	Если Результат.Пустой() Тогда
		
		ПочтовоеОтправлениеОбъект = Справочники.гф_ПочтаРоссииПочтовыеОтправления.СоздатьЭлемент();
		
		ПочтовоеОтправлениеОбъект.ТрекНомер		= ТрекНомер;
		ПочтовоеОтправлениеОбъект.Наименование	= ТрекНомер;
		
		ПочтовоеОтправлениеОбъект.Записать();  
		
		ПочтовоеОтправлениеСсылка = ПочтовоеОтправлениеОбъект.Ссылка;
		
	Иначе    
		
		Выборка = Результат.Выбрать();
		
		Выборка.Следующий();
		
		ПочтовоеОтправлениеСсылка = Выборка.Ссылка;
		ДоставкаЗавершена = Выборка.ДоставкаЗавершена;
		
	КонецЕсли;	
	
	Если ДоставкаЗавершена Тогда
		
		Возврат;
		
	КонецЕсли;	       
	
	ДоставкаЗавершена = Ложь;
	
	Для Каждого СтрокаОтвета Из ОтветОтСервера Цикл           
		
		Запись = РегистрыСведений.гф_ПочтаРоссииИсторияОтслеживания.СоздатьМенеджерЗаписи();
		
		Операция = СтрокаОтвета.Операция;
		
		Запись.ПочтовоеОтправление	= ПочтовоеОтправлениеСсылка;
		Запись.Период				= Операция.Дата;
		Запись.Операция				= Операция.Ссылка;
		Запись.КонечнаяОперация		= Операция.КонечнаяОперация;
		Запись.Активность			= Истина;   
		
		ДоставкаЗавершена			= Операция.КонечнаяОперация;
		
		Запись.Записать();
		
	КонецЦикла;   
	
	Если ДоставкаЗавершена <> ПочтовоеОтправлениеСсылка.ДоставкаЗавершена Тогда
		
		ПочтовоеОтправлениеОбъект = ПочтовоеОтправлениеСсылка.ПолучитьОбъект();	
		
		ПочтовоеОтправлениеОбъект.ДоставкаЗавершена = ДоставкаЗавершена;
		
		ПочтовоеОтправлениеОбъект.Записать();  
		
	КонецЕсли;	
	
КонецПроцедуры	

Функция СтрокаВЧисло(Значение) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		
		Результат = Значение;
		
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЭтоЧисло(Значение) Тогда
		
		Результат = Число(Значение);
		
	Иначе
		
		Результат = 0;
		
	КонецЕсли;	 
	
	Возврат Результат;
	
КонецФункции

Функция ОтветОтТрекераСтруктураОперация()

	Результат = Новый Структура(
	"Дата,
	|Ссылка,
	|КодОперации,
	|КодАтрибута,
	|Операция,
	|Атрибут,
	|КонечнаяОперация");	
	
	Возврат Результат;
	
КонецФункции

Функция ОтветОтТрекераСтруктураЗапись()

	Результат = Новый Структура(
	"ПараметрыАдреса,
	|ПараметрыФинансовые,
	|ПараметрыОтправления,
	|Операция,
	|ПараметрыПользователя");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
