#Область ПрограммныйИнтерфейс
 
// #wortmann {
// #Спецификация покупателя
// Обработчик команды печати, открывает форму для ввода дополнительных параметров печати
// Галфинд Окунев 2022/07/10
//
// Параметры:
//  ПараметрыПечати - Массив - менеджеры объектов с процедурой ДобавитьКомандыПечати.
//
// Возвращаемое значение: 
//  - Булево
//
Функция ПараметрыСпецификацииПоНовойЦене(ПараметрыПечати) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПараметрыСпецификацииПоНовойЦенеПродолжение",
		ЭтотОбъект,
		ПараметрыПечати);
		
	Если ПараметрыПечати.Форма.Элементы.Найти("Список") <> Неопределено Тогда
		
		Договор = ПараметрыПечати.Форма.Элементы.Список.ТекущиеДанные.Договор;
		
	ИначеЕсли ПараметрыПечати.Форма.Элементы.Найти("Договор") <> Неопределено Тогда
		
		Договор = ПараметрыПечати.Форма.Объект.Договор;
		
	Иначе	
		
		Договор = Неопределено;
		
	КонецЕсли;	
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("Договор", Договор);
	
	ОткрытьФорму("Документ.гф_СпецификацияПокупателя.Форма.ПараметрыСпецификацииПоНовойЦене", ПараметрыОткрытия,
		ПараметрыПечати.Форма, , , ,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);   
		
	Возврат Истина;	
	
КонецФункции// } #wortmann

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// #wortmann {
// #Спецификация покупателя
//
// Галфинд Окунев 2022/07/10
//
// Параметры:
//	Результат - Структура - параметры печати, установленные в 
//	форме параметров спецификации по новой цене
//	ПараметрыПечати - Массив - менеджеры объектов с процедурой ДобавитьКомандыПечати.
//
Процедура ПараметрыСпецификацииПоНовойЦенеПродолжение(Результат, ПараметрыПечати) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда 
		
		ПараметрыПечати.ДополнительныеПараметры.Вставить("гф_Параметры", Результат);
		
		Форма = ПараметрыПечати.Форма;
		
		ПараметрыПечати.Удалить("Форма");
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			ПараметрыПечати.МенеджерПечати,
			ПараметрыПечати.Идентификатор,
			ПараметрыПечати.ОбъектыПечати,
			Форма,
			ПараметрыПечати);
		
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти
