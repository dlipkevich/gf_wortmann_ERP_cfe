Функция гф_СоздатьМаркировкаТоваровИСМП(СтрокаТЗ, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВозврата = Новый Структура("ОписаниеОшибки,"
	+ "МаркировкаТоваровИСМП,"
	+ "Организация,"
	+ "ЕстьОшибкиЗаполнения,"
	+ "Статус,"
	+ "ДальнейшееДействие,"
	+ "ЗаказКодовВозможен");
	
	Если Не ЗначениеЗаполнено(СтрокаТЗ.ДокументПоступления) Тогда
		СтруктураВозврата["ОписаниеОшибки"] = "Не заполнено основание для создания ""Маркировки товаров ИС МП""";
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Если ТипЗнч(СтрокаТЗ.ДокументПоступления) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		МаркировкаТоваровИСМП = Документы.МаркировкаТоваровИСМП.СоздатьДокумент();
		МаркировкаТоваровИСМП.Заполнить(СтрокаТЗ.ДокументПоступления);
		// заполняем тч ШтрихкодыУпаковок
		Запрос = Новый Запрос;
		Запрос.Текст = Документы.ПриобретениеТоваровУслуг.гф_ПолучитьТекстЗапросаКодыМаркировки();
		Запрос.УстановитьПараметр("Ссылка", СтрокаТЗ.ДокументПоступления);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Запрос.Текст = "ВЫБРАТЬ
		|	ШтрихкодыУпаковокТоваров.Ссылка КАК ШтрихкодУпаковки
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|ГДЕ
		|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода = &ЗначениеШтрихкода";
		Пока Выборка.Следующий() Цикл
			Запрос.УстановитьПараметр("ЗначениеШтрихкода", СокрЛП(Выборка.КМ));
			РезультатШК = Запрос.Выполнить();
			ВыборкаШК = РезультатШК.Выбрать();
			Если ВыборкаШК.Следующий() Тогда
				нс = МаркировкаТоваровИСМП.ШтрихкодыУпаковок.Добавить();
				нс.ШтрихкодУпаковки = ВыборкаШК.ШтрихкодУпаковки;
			КонецЕсли;
		КонецЦикла;
		МаркировкаТоваровИСМП.Дата = ТекущаяДатаСеанса();
		МаркировкаТоваровИСМП.Операция = Перечисления.ВидыОперацийИСМП.ВводВОборотИмпортСФТС;
		МаркировкаТоваровИСМП.Комментарий = "Создан автоматически регл. заданием Маркировка товаров (ввод в оборот) " + ТекущаяДатаСеанса();
		МаркировкаТоваровИСМП.РегистрационныйНомерДекларации = СокрЛП(СтрокаТЗ.ГТД.НомерДекларации);
		МаркировкаТоваровИСМП.ДатаДекларации = гф_ПолучитьДатуИзНомераДекларации(МаркировкаТоваровИСМП.РегистрационныйНомерДекларации);
		//РезультатПроверки = МаркировкаТоваровИСМП.ПроверитьЗаполнение();
		// 1-я проверка
		РезультатПроверки = ИнтеграцияИСМПСлужебный.гф_ПроверитьЗаполнениеШтрихкодовУпаковок(МаркировкаТоваровИСМП);
		Если РезультатПроверки.ДанныеСоответствуют Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			СтруктураВозврата["ЕстьОшибкиЗаполнения"] = Ложь;
		Иначе
			// Галфинд СадомцевСА 20.06.2023 Реализовал "удаление" строки тч при "нулевом" количестве
			//e1cib/data/Задача.ЗадачаИсполнителя?ref=8ea8b083fed1320811ee09e0e1b3a4bf
			МассивУдалитьСтроки = Новый Массив;
			Для Каждого СтрокаСРасхожнением Из РезультатПроверки.СтрокиСРасхождением Цикл
				Если СтрокаСРасхожнением.НомерСтроки > МаркировкаТоваровИСМП.Товары.Количество() Тогда
					Продолжить;
				КонецЕсли;
				СтрокаТЧ = МаркировкаТоваровИСМП.Товары[СтрокаСРасхожнением.НомерСтроки-1];
				СтрокаТЧ[СтрокаСРасхожнением.Поле] = СтрокаСРасхожнением.Указано;
				СтрокаТЧ["Количество"] = СтрокаТЧ[СтрокаСРасхожнением.Поле];
				СтрокаТЧ.Сумма = СтрокаТЧ.Цена * СтрокаТЧ["Количество"];
				СтрокаТЧ.СуммаСНДС = СтрокаТЧ.Цена * СтрокаТЧ["Количество"];
				Если Не ЗначениеЗаполнено(СтрокаТЧ["Количество"]) Тогда
					МассивУдалитьСтроки.Добавить(СтрокаТЧ);
				КонецЕсли;
			КонецЦикла;
			Для Каждого УдалитьСтроку Из МассивУдалитьСтроки Цикл
				МаркировкаТоваровИСМП.Товары.Удалить(УдалитьСтроку);
			КонецЦикла;
			// 2-я проверка
			РезультатПроверки = ИнтеграцияИСМПСлужебный.гф_ПроверитьЗаполнениеШтрихкодовУпаковок(МаркировкаТоваровИСМП);
			Если РезультатПроверки.ДанныеСоответствуют Тогда
				РежимЗаписи = РежимЗаписиДокумента.Проведение;
				СтруктураВозврата["ЕстьОшибкиЗаполнения"] = Ложь;
			Иначе
				РежимЗаписи = РежимЗаписиДокумента.Запись;
				СтруктураВозврата["ЕстьОшибкиЗаполнения"] = Истина;
				МаркировкаТоваровИСМП.Комментарий = МаркировкаТоваровИСМП.Комментарий + Символы.ПС + "Есть ошибки при проверке документа (НЕ проведен)!";
			КонецЕсли;
		КонецЕсли;
		Попытка
			МаркировкаТоваровИСМП.Записать(РежимЗаписи);
			Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				СтруктураВозврата["МаркировкаТоваровИСМП"] = МаркировкаТоваровИСМП.Ссылка;
			КонецЕсли;
		Исключение
			СтруктураВозврата["ОписаниеОшибки"] = "Ошибка при записи документа:" + ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

//Параметры:
// МассивВходящихДанных - массив ссылок Документ.МаркировкаТоваровИСМП
Процедура гф_ПодготовитьКПередачеИСМП(МассивВходящихДанных) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическуюОтправкуПолучениеДанныхИСМП") Тогда
		Возврат;
	КонецЕсли;
	
	ВходящиеДанные = Новый Массив;
	Для Каждого Эл Из МассивВходящихДанных Цикл
		ПараметрыОбработкиДокументов = ИнтеграцияИСМПСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка = Эл;
		Организация = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Эл, "Организация");
		ПараметрыОбработкиДокументов.Организация = Организация;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение(
		"Перечисление.ДальнейшиеДействияПоВзаимодействиюИСМП.ПередайтеДанные");
		// ++ Галфинд СадомцевСА 04.07.2023 Дальнейшее действие для "подписания" документа зависит от Операции
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Эл);
		СтруктураПараметров = Новый Структура("Операция", Эл.Операция);
		ПараметрыОбработкиДокументов.ДальнейшееДействие = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию(СтруктураПараметров);
		// --
		ВходящиеДанные.Добавить(ПараметрыОбработкиДокументов);
	КонецЦикла;
	РезультатОбмена = ИнтеграцияИСМПВызовСервера.ПодготовитьКПередаче(ВходящиеДанные);
	
	Если РезультатОбмена.Свойство("ПараметрыОбмена") Тогда
		СвойстваПодписи = гф_ЭлектроннаяПодписьВызовСервера.ПолучитьЗначениеКонстанты();
		Если ТипЗнч(СвойстваПодписи) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		Если РезультатОбмена.ПараметрыОбмена.СообщенияКПодписанию = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ *
		|ИЗ
		|	РегистрСведений.ОчередьСообщенийИСМП КАК ОчередьСообщенийИСМП
		|ГДЕ
		|	ОчередьСообщенийИСМП.Документ = &Документ";
		Для Каждого КлючИЗначение Из РезультатОбмена.ПараметрыОбмена.СообщенияКПодписанию Цикл
			Для Каждого Сообщение Из КлючИЗначение.Значение Цикл
				Запрос.УстановитьПараметр("Документ", Сообщение.Документ);
				Результат = Запрос.Выполнить();
				Выборка = Результат.Выбрать();
				Если Выборка.Следующий() Тогда
					РеквизитыИсходящегоСообщения = Выборка.РеквизитыИсходящегоСообщения.Получить();
					ЗаполнитьЗначенияСвойств(РеквизитыИсходящегоСообщения, Сообщение);
					РеквизитыИсходящегоСообщения.Вставить("СвойстваПодписи", СвойстваПодписи);
					НаборЗаписей = РегистрыСведений.ОчередьСообщенийИСМП.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Сообщение.Установить(Сообщение.Идентификатор);
					Запись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, Сообщение);
					Запись.ДатаМодификацииУниверсальная = Дата(1,1,1);
					Запись.ДатаСоздания = Выборка.ДатаСоздания + 600;
					Запись.Сообщение = Сообщение.Идентификатор;
					Запись.РеквизитыИсходящегоСообщения = Новый ХранилищеЗначения(РеквизитыИсходящегоСообщения);
					НаборЗаписей.Записать();
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Функция гф_ПолучитьДатуИзНомераДекларации(НомерДекларации)
	_День = Сред(НомерДекларации, 10, 2);
	Если ЗначениеЗаполнено(_День) Тогда
		День = Число(_День);
	Иначе
		День = 1;
	КонецЕсли;
	_Месяц = Сред(НомерДекларации, 12, 2);
	Если ЗначениеЗаполнено(_Месяц) Тогда
		Месяц = Число(_Месяц);
	Иначе
		Месяц = 1;
	КонецЕсли;
	_Год = Сред(НомерДекларации, 14, 2);
	Если ЗначениеЗаполнено(_Год) Тогда
		Год = Число("20"+_Год);
	Иначе
		Год = 1;
	КонецЕсли;
	Возврат Дата(Год, Месяц, День);
КонецФункции

// ++ Галфинд СадомцевСА 04.07.2023 Реализовал создание документа Маркировка товаров ИСМП на основании РТУ
//e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee166b434dc8ce
//Параметры:
// СсылкаРТУ - ссылка на Документ.РеализацияТоваровУслуг
Функция гф_СоздатьМаркировкаТоваровИСМП_ОснованиеРТУ(СсылкаРТУ) Экспорт
	
	СтруктураВозврата = Новый Структура("ОписаниеОшибки,"
	+ "МаркировкаТоваровИСМП,"
	+ "Организация,"
	+ "ЕстьОшибкиЗаполнения,"
	+ "Статус,"
	+ "ДальнейшееДействие,"
	+ "ЗаказКодовВозможен");
	
	Если Не ЗначениеЗаполнено(СсылкаРТУ) Тогда
		СтруктураВозврата["ОписаниеОшибки"] = "Не заполнено основание для создания ""Маркировки товаров ИС МП""";
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СсылкаРТУ.гф_МаркировкаТоваровИСМП) Тогда
		МаркировкаТоваровИСМП = СсылкаРТУ.гф_МаркировкаТоваровИСМП.ПолучитьОбъект();
		МаркировкаТоваровИСМП.Товары.Очистить();
		МаркировкаТоваровИСМП.ШтрихкодыУпаковок.Очистить();
	Иначе
		МаркировкаТоваровИСМП = Документы.МаркировкаТоваровИСМП.СоздатьДокумент();
	КонецЕсли;
		МаркировкаТоваровИСМП.Дата = ТекущаяДатаСеанса();
		МаркировкаТоваровИСМП.Операция = Перечисления.ВидыОперацийИСМП.Агрегация;
		МаркировкаТоваровИСМП.Организация = СсылкаРТУ.Организация;
		//МаркировкаТоваровИСМП.ДокументОснование = СсылкаРТУ;
		
		МаркировкаТоваровИСМП.ВариантЗаполненияДекларации = Перечисления.ВариантыЗаполненияДекларацииИСМП.КодыМаркировки;
		МаркировкаТоваровИСМП.ВариантФормированияАТКИСМП = Перечисления.ВариантыФормированияАТКИСМП.КодТНВЭД;
		//МаркировкаТоваровИСМП.КодТаможенногоОргана = ;
		//МаркировкаТоваровИСМП.ПринятоеРешение = ;
		
		МаркировкаТоваровИСМП.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		МаркировкаТоваровИСМП.Комментарий = "#Создано автоматически для отгрузки в коробах " + ТекущаяДатаСеанса();
		//МаркировкаТоваровИСМП.РегистрационныйНомерДекларации = СокрЛП(СтрокаТЗ.ГТД.НомерДекларации);
		//МаркировкаТоваровИСМП.ДатаДекларации = гф_ПолучитьДатуИзНомераДекларации(МаркировкаТоваровИСМП.РегистрационныйНомерДекларации);
		Для Каждого СтрокаТЗ Из СсылкаРТУ.Товары Цикл
			нс = МаркировкаТоваровИСМП.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(нс, СтрокаТЗ);
		КонецЦикла;
		КолонкиГруппировки = "";
		КолонкиСуммирования = "";
		Для Каждого Реквизит Из Метаданные.Документы.МаркировкаТоваровИСМП.ТабличныеЧасти.Товары.Реквизиты Цикл
			Если Реквизит.Тип.СодержитТип(Тип("Число")) И Реквизит.Имя <> "Цена" Тогда
				Если КолонкиСуммирования <> "" Тогда
					КолонкиСуммирования = КолонкиСуммирования + ",";
				КонецЕсли;
				КолонкиСуммирования = КолонкиСуммирования + Реквизит.Имя;
			Иначе
				Если КолонкиГруппировки <> "" Тогда
					КолонкиГруппировки = КолонкиГруппировки + ",";
				КонецЕсли;
				КолонкиГруппировки = КолонкиГруппировки + Реквизит.Имя;
			КонецЕсли;
		КонецЦикла;
		МаркировкаТоваровИСМП.Товары.Свернуть(КолонкиГруппировки, КолонкиСуммирования);
		ИнтеграцияИСМП.ЗаполнитьВидПродукцииПоТабличнойЧасти(МаркировкаТоваровИСМП);
		
		Для Каждого СтрокаТЗ Из СсылкаРТУ.ШтрихкодыУпаковок Цикл
			нс = МаркировкаТоваровИСМП.ШтрихкодыУпаковок.Добавить();
			ЗаполнитьЗначенияСвойств(нс, СтрокаТЗ);
		КонецЦикла;
		
		//РезультатПроверки = МаркировкаТоваровИСМП.ПроверитьЗаполнение();
		// 1-я проверка
		РезультатПроверки = ИнтеграцияИСМПСлужебный.гф_ПроверитьЗаполнениеШтрихкодовУпаковок(МаркировкаТоваровИСМП);
		Если РезультатПроверки.ДанныеСоответствуют Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			СтруктураВозврата["ЕстьОшибкиЗаполнения"] = Ложь;
		Иначе
			// Галфинд СадомцевСА 20.06.2023 Реализовал "удаление" строки тч при "нулевом" количестве
			//e1cib/data/Задача.ЗадачаИсполнителя?ref=8ea8b083fed1320811ee09e0e1b3a4bf
			МассивУдалитьСтроки = Новый Массив;
			Для Каждого СтрокаСРасхожнением Из РезультатПроверки.СтрокиСРасхождением Цикл
				Если СтрокаСРасхожнением.НомерСтроки > МаркировкаТоваровИСМП.Товары.Количество() Тогда
					Продолжить;
				КонецЕсли;
				СтрокаТЧ = МаркировкаТоваровИСМП.Товары[СтрокаСРасхожнением.НомерСтроки-1];
				СтрокаТЧ[СтрокаСРасхожнением.Поле] = СтрокаСРасхожнением.Указано;
				СтрокаТЧ["Количество"] = СтрокаТЧ[СтрокаСРасхожнением.Поле];
				СтрокаТЧ.Сумма = СтрокаТЧ.Цена * СтрокаТЧ["Количество"];
				СтрокаТЧ.СуммаСНДС = СтрокаТЧ.Цена * СтрокаТЧ["Количество"];
				Если Не ЗначениеЗаполнено(СтрокаТЧ["Количество"]) Тогда
					МассивУдалитьСтроки.Добавить(СтрокаТЧ);
				КонецЕсли;
			КонецЦикла;
			Для Каждого УдалитьСтроку Из МассивУдалитьСтроки Цикл
				МаркировкаТоваровИСМП.Товары.Удалить(УдалитьСтроку);
			КонецЦикла;
			// 2-я проверка
			РезультатПроверки = ИнтеграцияИСМПСлужебный.гф_ПроверитьЗаполнениеШтрихкодовУпаковок(МаркировкаТоваровИСМП);
			Если РезультатПроверки.ДанныеСоответствуют Тогда
				РежимЗаписи = РежимЗаписиДокумента.Проведение;
				СтруктураВозврата["ЕстьОшибкиЗаполнения"] = Ложь;
			Иначе
				РежимЗаписи = РежимЗаписиДокумента.Запись;
				СтруктураВозврата["ЕстьОшибкиЗаполнения"] = Истина;
				МаркировкаТоваровИСМП.Комментарий = МаркировкаТоваровИСМП.Комментарий + Символы.ПС + "Есть ошибки при проверке документа (НЕ проведен)!";
			КонецЕсли;
		КонецЕсли;
		Попытка
			МаркировкаТоваровИСМП.Записать(РежимЗаписи);
			Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				СтруктураВозврата["МаркировкаТоваровИСМП"] = МаркировкаТоваровИСМП.Ссылка;
			КонецЕсли;
		Исключение
			СтруктураВозврата["ОписаниеОшибки"] = "Ошибка при записи документа:" + ОписаниеОшибки();
		КонецПопытки;
	
	Возврат СтруктураВозврата;
	
КонецФункции
// -- Галфинд СадомцевСА 04.07.2023
