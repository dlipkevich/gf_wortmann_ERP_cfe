
&ИзменениеИКонтроль("ПроверитьЗаполнитьОбеспечениеВСтрокеТЧДокументаПродажи")
Процедура гф_ПроверитьЗаполнитьОбеспечениеВСтрокеТЧДокументаПродажи(ТекущаяСтрока, ДанныеПрограммногоОбъекта, ПараметрыДействия)
	
	ПараметрыВстраивания = ПараметрыДействия.ПараметрыВстраивания;
	Таблицы = ПолучитьИзВременногоХранилища(ПараметрыВстраивания.СлужебныеПараметры.АдресТаблицыДанныхФормы);
	ЗаполнитьТаблицыИзСериализованногоОбъекта(Таблицы, ДанныеПрограммногоОбъекта, ПараметрыВстраивания, ТекущаяСтрока);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	ДобавитьВременнуюТаблицуДанныхДокумента(МенеджерВременныхТаблиц, Таблицы, ПараметрыВстраивания);
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Номенклатура                            КАК Номенклатура,
		|	ТаблицаДокумента.Номенклатура.ТипНоменклатуры            КАК ТипНоменклатуры,
		|	ТаблицаДокумента.Номенклатура.ОбособленнаяЗакупкаПродажа КАК ОбособленнаяЗакупкаПродажа,
		|	ТаблицаДокумента.ВариантОбеспечения                      КАК ВариантОбеспечения
		|ИЗ
		|	ВременнаяТаблицаДанныхДокумента КАК ТаблицаДокумента";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Не ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение варианта обеспечения.
	ВариантОбеспеченияИФлагОбособленно = ПараметрыДействия.ВариантОбеспеченияИФлагОбособленно;
	
	Если ВариантОбеспеченияИФлагОбособленно = Неопределено Тогда
		
		ВариантОбеспеченияИФлагОбособленно = ОбеспечениеВДокументахКлиентСервер.ВариантОбеспеченияИФлагОбособленноПоУмолчанию(
			Выборка.ОбособленнаяЗакупкаПродажа);
		
	КонецЕсли;
	
	Если Выборка.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		ИмяСвойства = "Товар";
	ИначеЕсли Выборка.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа") Тогда
		ИмяСвойства = "Работа";
	ИначеЕсли Выборка.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
		ИмяСвойства = "Услуга";
	ИначеЕсли Выборка.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара") Тогда
		ИмяСвойства = "Тара";
	КонецЕсли;
	
	ВариантОбеспечения = ВариантОбеспеченияИФлагОбособленно[ИмяСвойства].ВариантОбеспечения;
	Обособленно        = ВариантОбеспеченияИФлагОбособленно[ИмяСвойства].Обособленно;
	
	ТекущаяСтрока.ВариантОбеспечения = ВариантОбеспечения;
	#Удаление
	ТекущаяСтрока.Обособленно        = Обособленно;
	#КонецУдаления
	#Вставка
	// #wortmann {
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=812ebcee7bda45d711edbc2b84b07f12
	// не сбрасывать автоматически признак "Обособленно" в строках для товаров, если он установлен
	// Галфинд Sakovich 2023/03/06
	Если Не (ИмяСвойства = "Товар" И ТекущаяСтрока.Обособленно) Тогда
		ТекущаяСтрока.Обособленно        = Обособленно;
	КонецЕсли;
	// } #wortmann
	#КонецВставки
	
КонецПроцедуры
