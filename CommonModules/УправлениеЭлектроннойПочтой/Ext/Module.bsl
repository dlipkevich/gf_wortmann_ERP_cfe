
&Вместо("ПолучитьУчетнуюЗаписьДляОтправкиПоУмолчанию")
Функция гф_ПолучитьУчетнуюЗаписьДляОтправкиПоУмолчанию()
	// vvv Галфинд \ Sakovich 07.03.2024
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eedbbb98f1041d
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПользователиКонтактнаяИнформация.АдресЭП КАК АдресЭП,
	|	Пользователи.Наименование КАК Наименование
	|ПОМЕСТИТЬ ПочтаПользователя
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО (ПользователиКонтактнаяИнформация.Ссылка = Пользователи.Ссылка)
	|ГДЕ
	|	Пользователи.Ссылка = &ТекущийПользователь
	|	И ПользователиКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailПользователя)
	|	И ПользователиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка КАК УчетнаяЗапись
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПочтаПользователя КАК ПочтаПользователя
	|		ПО УчетныеЗаписиЭлектроннойПочты.ИмяПользователя = ПочтаПользователя.Наименование
	|			И УчетныеЗаписиЭлектроннойПочты.АдресЭлектроннойПочты = ПочтаПользователя.АдресЭП
	|";
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.УчетнаяЗапись;
	КонецЕсли;
	// ^^^ Галфинд \ Sakovich 07.03.2024
	Результат = ПродолжитьВызов();
	Возврат Результат;
КонецФункции
