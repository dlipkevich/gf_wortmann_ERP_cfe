
#Область ПрограммныйИнтерфейс

&ИзменениеИКонтроль("ВывестиЛоготипВТабличныйДокумент")
Процедура гф_ВывестиЛоготипВТабличныйДокумент(Макет, ОбластьМакета, ИмяОбласти, Организация)

	ФайлКартинки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ФайлЛоготип");

	Если Не ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ОбластьМакета.Рисунки, "Логотип") Тогда
		// Картинки логотипа в этой области макета нет.

		Если Макет.Области.Найти(ИмяОбласти) <> Неопределено Тогда

			// Проверка картинки логотипа в области с логотипом
			ОбластьМакетаЛоготип = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ОбластьМакетаЛоготип.Рисунки, "Логотип") 
				И ЗначениеЗаполнено(ФайлКартинки) Тогда

				ОбластьМакета = ОбластьМакетаЛоготип;
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;

	КонецЕсли;

	Если Не ЗначениеЗаполнено(ФайлКартинки) Тогда
		ОбластьМакета.Рисунки.Удалить(ОбластьМакета.Рисунки.Логотип);
		Возврат;
	КонецЕсли;

	#Удаление
	ДвоичныеДанныеФайла = РаботаСФайлами.ДвоичныеДанныеФайла(ФайлКартинки);
	#КонецУдаления
	#Вставка
	// vvv Галфинд \ Sakovich 12.04.2024
	ДвоичныеДанныеФайла = гф_ПолучитьДвоичныеДанныеФайла(ФайлКартинки);
	// ^^^ Галфинд \ Sakovich 12.04.2024
	#КонецВставки

	Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("Картинка") Тогда
		Картинка = ДвоичныеДанныеФайла;
	ИначеЕсли ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
		Картинка = Новый Картинка(ДвоичныеДанныеФайла);
	Иначе
		Картинка = Новый Картинка;
	КонецЕсли; 

	ОбластьМакета.Рисунки.Логотип.Картинка = Картинка;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция гф_ПолучитьДвоичныеДанныеФайла(ФайлКартинки)
	Попытка
		Возврат РаботаСФайлами.ДвоичныеДанныеФайла(ФайлКартинки);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
КонецФункции 

#КонецОбласти
