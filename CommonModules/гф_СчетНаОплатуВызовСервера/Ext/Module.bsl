Процедура ПараметрыФормыДобавитьТЧТоварыВКоробах(ПараметрыФормы, ПараметрКоманды) Экспорт
	// Галфинд СадомцевСА 12.02.2024
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eec96fbdb98554
	СкладКоробной = УправлениеСвойствами.ЗначениеСвойства(ПараметрКоманды.Склад, "гф_СкладыТоварыВКоробах") = Истина;
	Если НЕ СкладКоробной Тогда
		Возврат;
	КонецЕсли;
	тчТоварыВКоробах = Новый ТаблицаЗначений;
	тчТоварыВКоробах.Колонки.Добавить("ВариантКомплектации");
	тчТоварыВКоробах.Колонки.Добавить("Количество");
	тчТоварыВКоробах.Колонки.Добавить("Цена");
	тчТоварыВКоробах.Колонки.Добавить("СтавкаНДС");
	тчТоварыВКоробах.Колонки.Добавить("СуммаНДС");
	тчТоварыВКоробах.Колонки.Добавить("Сумма");
	тчТоварыВКоробах.Колонки.Добавить("ЗаказКлиента");
	// СадомцевСА 07.03.2024
	тчТоварыВКоробах.Колонки.Добавить("Номенклатура");
	ОтборСтрок = Новый Структура("Отменено", Ложь);
	НайденныеСтроки = ПараметрКоманды.гф_ТоварыВКоробах.НайтиСтроки(ОтборСтрок);
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		Если СтрокаТЧ.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		нс = тчТоварыВКоробах.Добавить();
		нс.ВариантКомплектации = СтрокаТЧ.ВариантКомплектации;
		нс.Количество = СтрокаТЧ.Количество;
		нс.Цена = СтрокаТЧ.ЦенаКоробаСоСкидкой;
		нс.СтавкаНДС = СтрокаТЧ.СтавкаНДС;
		нс.СуммаНДС = СтрокаТЧ.СуммаНДС;
		нс.Сумма = СтрокаТЧ.СуммаСНДС;
		нс.ЗаказКлиента = ПараметрКоманды;
		// СадомцевСА 07.03.2024
		нс.Номенклатура = СтрокаТЧ.ВариантКомплектации.Владелец;
	КонецЦикла;
	ХЗ = Новый ХранилищеЗначения(тчТоварыВКоробах);
	ПараметрыФормы.Вставить("гф_ТоварыВКоробах", ХЗ);
КонецПроцедуры

Процедура ПараметрыФормыДобавитьТЧТоварыВКоробахРТУ(ПараметрыФормы, ПараметрКоманды) Экспорт
	
	// ++ Галфинд_ДомнышеваКР_02_04_2024
	СкладКоробной = УправлениеСвойствами.ЗначениеСвойства(ПараметрКоманды.Склад, "гф_СкладыТоварыВКоробах") = Истина;
	Если НЕ СкладКоробной Тогда
		Возврат;
	КонецЕсли;
	тчТоварыВКоробах = Новый ТаблицаЗначений;
	тчТоварыВКоробах.Колонки.Добавить("ВариантКомплектации");
	тчТоварыВКоробах.Колонки.Добавить("Количество");
	тчТоварыВКоробах.Колонки.Добавить("Цена");
	тчТоварыВКоробах.Колонки.Добавить("СтавкаНДС");
	тчТоварыВКоробах.Колонки.Добавить("СуммаНДС");
	тчТоварыВКоробах.Колонки.Добавить("Сумма");
	тчТоварыВКоробах.Колонки.Добавить("ЗаказКлиента");
	тчТоварыВКоробах.Колонки.Добавить("Номенклатура");
	
	ТаблицаЗаполнения = ПолучитьТЧТоварыВКоробахРТУ(ПараметрКоманды);
	Для Каждого СтрокаТЧ Из ТаблицаЗаполнения Цикл
		Если СтрокаТЧ.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		нс = тчТоварыВКоробах.Добавить();
		нс.ВариантКомплектации = СтрокаТЧ.ВариантКомплектации;
		нс.Количество = СтрокаТЧ.Количество;
		нс.Цена = СтрокаТЧ.Цена;
		нс.СтавкаНДС = СтрокаТЧ.СтавкаНДС;
		нс.СуммаНДС = СтрокаТЧ.СуммаНДС;
		нс.Сумма = СтрокаТЧ.Сумма;
		нс.ЗаказКлиента = СтрокаТЧ.ЗаказКлиента;
		нс.Номенклатура = СтрокаТЧ.Номенклатура;
	КонецЦикла;
  	
	ХЗ = Новый ХранилищеЗначения(тчТоварыВКоробах);
	ПараметрыФормы.Вставить("гф_ТоварыВКоробах", ХЗ);
	
    // -- Галфинд_ДомнышеваКР_02_04_2024
КонецПроцедуры  

Функция ПолучитьТЧТоварыВКоробахРТУ(ПараметрКоманды)
	
	// ++ Галфинд_ДомнышеваКР_02_04_2024
	МассивЗаказы = Новый Массив();
	МассивЗаказы.Добавить(ПараметрКоманды.ЗаказКлиента);
	МассивУЛ = ПараметрКоманды.гф_ТоварыВКоробах.ВыгрузитьКолонку("УпаковочныйЛист");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УпаковочныйЛистТовары.Ссылка КАК УпаковочныйЛист,
		|	УпаковочныйЛистТовары.Ссылка.гф_Комплектация КАК ВариантКомплектации,
		|	УпаковочныйЛистТовары.Номенклатура КАК Номенклатура,
		|	УпаковочныйЛистТовары.Характеристика КАК Характеристика,
		|	УпаковочныйЛистТовары.Серия КАК Серия,
		|	СУММА(УпаковочныйЛистТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ ВтДанныеПоУпаковочнымЛистам
		|ИЗ
		|	Документ.УпаковочныйЛист.Товары КАК УпаковочныйЛистТовары
		|ГДЕ
		|	УпаковочныйЛистТовары.Ссылка В(&МассивУЛ)
		|
		|СГРУППИРОВАТЬ ПО
		|	УпаковочныйЛистТовары.Ссылка,
		|	УпаковочныйЛистТовары.Номенклатура,
		|	УпаковочныйЛистТовары.Характеристика,
		|	УпаковочныйЛистТовары.Серия,
		|	УпаковочныйЛистТовары.Ссылка.гф_Комплектация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказКлиентаТовары.Ссылка КАК ЗаказКлиента,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	ЗаказКлиентаТовары.Серия КАК Серия,
		|	ЗаказКлиентаТовары.СтавкаНДС КАК СтавкаНДС,
		|	МАКСИМУМ(ЗаказКлиентаТовары.гф_ЦенаСоСкидкой) КАК ЦенаСоСкидкой
		|ПОМЕСТИТЬ ВтДанныеПоЗаказам
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка В(&МассивЗаказы)
		|	И (ЗаказКлиентаТовары.Номенклатура, ЗаказКлиентаТовары.Характеристика, ЗаказКлиентаТовары.Серия) В
		|			(ВЫБРАТЬ
		|				УпаковочныйЛисты.Номенклатура,
		|				УпаковочныйЛисты.Характеристика,
		|				УпаковочныйЛисты.Серия
		|			ИЗ
		|				ВтДанныеПоУпаковочнымЛистам КАК УпаковочныйЛисты)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаТовары.Ссылка,
		|	ЗаказКлиентаТовары.Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика,
		|	ЗаказКлиентаТовары.Серия,
		|	ЗаказКлиентаТовары.СтавкаНДС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка) КАК ПТУ,
		|	ВтДанныеПоЗаказам.ЗаказКлиента КАК ЗаказКлиента,
		|	ВтДанныеПоУпаковочнымЛистам.ВариантКомплектации КАК ВариантКомплектации,
		|	ВтДанныеПоУпаковочнымЛистам.УпаковочныйЛист КАК УпаковочныйЛист,
		|	ВтДанныеПоЗаказам.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ВтДанныеПоУпаковочнымЛистам.Количество * ВтДанныеПоЗаказам.ЦенаСоСкидкой) КАК ЦенаУпаковочногоЛиста
		|ПОМЕСТИТЬ ВтЦеныУпаоковочныхЛистов
		|ИЗ
		|	ВтДанныеПоУпаковочнымЛистам КАК ВтДанныеПоУпаковочнымЛистам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеПоЗаказам КАК ВтДанныеПоЗаказам
		|		ПО ВтДанныеПоУпаковочнымЛистам.УпаковочныйЛист.гф_Заказ = ВтДанныеПоЗаказам.ЗаказКлиента
		|			И ВтДанныеПоУпаковочнымЛистам.Номенклатура = ВтДанныеПоЗаказам.Номенклатура
		|			И ВтДанныеПоУпаковочнымЛистам.Характеристика = ВтДанныеПоЗаказам.Характеристика
		|			И ВтДанныеПоУпаковочнымЛистам.Серия = ВтДанныеПоЗаказам.Серия
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтДанныеПоЗаказам.ЗаказКлиента,
		|	ВтДанныеПоЗаказам.СтавкаНДС,
		|	ВтДанныеПоУпаковочнымЛистам.ВариантКомплектации,
		|	ВтДанныеПоУпаковочнымЛистам.УпаковочныйЛист
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВтЦеныУпаоковочныхЛистов.ПТУ КАК ПТУ,
		|	ВтЦеныУпаоковочныхЛистов.ЗаказКлиента КАК ЗаказКлиента,
		|	ВтЦеныУпаоковочныхЛистов.ВариантКомплектации КАК ВариантКомплектации,
		|	ВтЦеныУпаоковочныхЛистов.ЦенаУпаковочногоЛиста КАК ЦенаУпаковочногоЛиста,
		|	ВтЦеныУпаоковочныхЛистов.СтавкаНДС КАК СтавкаНДС,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтЦеныУпаоковочныхЛистов.УпаковочныйЛист) КАК КоличествоУпаковочныхЛистов
		|ПОМЕСТИТЬ ВтЦеныПоВариантам
		|ИЗ
		|	ВтЦеныУпаоковочныхЛистов КАК ВтЦеныУпаоковочныхЛистов
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтЦеныУпаоковочныхЛистов.ПТУ,
		|	ВтЦеныУпаоковочныхЛистов.ЗаказКлиента,
		|	ВтЦеныУпаоковочныхЛистов.ЦенаУпаковочногоЛиста,
		|	ВтЦеныУпаоковочныхЛистов.СтавкаНДС,
		|	ВтЦеныУпаоковочныхЛистов.ВариантКомплектации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВтЦеныПоВариантам.ПТУ КАК ПТУ,
		|	ВтЦеныПоВариантам.ЗаказКлиента КАК ЗаказКлиента,
		|	ВтЦеныПоВариантам.ЗаказКлиента.Организация КАК Организация,
		|	ВтЦеныПоВариантам.ЗаказКлиента.Контрагент КАК Контрагент,
		|	ВтЦеныПоВариантам.ЗаказКлиента.Договор КАК Договор,
		|	ВтЦеныПоВариантам.ВариантКомплектации КАК ВариантКомплектации,
		|	ВтЦеныПоВариантам.ЦенаУпаковочногоЛиста КАК Цена,
		|	ВтЦеныПоВариантам.КоличествоУпаковочныхЛистов КАК Количество,
		|	ВтЦеныПоВариантам.СтавкаНДС КАК СтавкаНДС,
		|	ВтЦеныПоВариантам.КоличествоУпаковочныхЛистов * ВтЦеныПоВариантам.ЦенаУпаковочногоЛиста * ВтЦеныПоВариантам.СтавкаНДС.Ставка / 100 КАК СуммаНДС,
		|	ВтЦеныПоВариантам.КоличествоУпаковочныхЛистов * ВтЦеныПоВариантам.ЦенаУпаковочногоЛиста КАК СуммаБезНДС,
		|	ВтЦеныПоВариантам.КоличествоУпаковочныхЛистов * ВтЦеныПоВариантам.ЦенаУпаковочногоЛиста * ВтЦеныПоВариантам.СтавкаНДС.Ставка / 100 + ВтЦеныПоВариантам.КоличествоУпаковочныхЛистов * ВтЦеныПоВариантам.ЦенаУпаковочногоЛиста КАК Сумма
		|ПОМЕСТИТЬ ВтДанныеКВыставлению
		|ИЗ
		|	ВтЦеныПоВариантам КАК ВтЦеныПоВариантам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВтДанныеКВыставлению.ПТУ КАК ПТУ,
		|	ВтДанныеКВыставлению.ЗаказКлиента КАК ЗаказКлиента,
		|	ВтДанныеКВыставлению.Организация КАК Организация,
		|	ВтДанныеКВыставлению.Контрагент КАК Контрагент,
		|	ВтДанныеКВыставлению.Договор КАК Договор,
		|	ВтДанныеКВыставлению.ВариантКомплектации КАК ВариантКомплектации,
		|	ВтДанныеКВыставлению.ВариантКомплектации.Владелец КАК Номенклатура,
		|	ВтДанныеКВыставлению.Цена КАК Цена,
		|	ВтДанныеКВыставлению.Количество КАК Количество,
		|	ВтДанныеКВыставлению.СтавкаНДС КАК СтавкаНДС,
		|	ВтДанныеКВыставлению.СуммаНДС КАК СуммаНДС,
		|	ВтДанныеКВыставлению.СуммаБезНДС КАК СуммаБезНДС,
		|	ВтДанныеКВыставлению.Сумма КАК Сумма
		|ИЗ
		|	ВтДанныеКВыставлению КАК ВтДанныеКВыставлению
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВтДанныеКВыставлению.Контрагент.Наименование,
		|	ВтДанныеКВыставлению.Договор.Наименование,
		|	ВтДанныеКВыставлению.ПТУ.Представление,
		|	ВтДанныеКВыставлению.ВариантКомплектации.Наименование";
	
	Запрос.УстановитьПараметр("МассивЗаказы", МассивЗаказы);
	Запрос.УстановитьПараметр("МассивУЛ", МассивУЛ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	// -- Галфинд_ДомнышеваКР_02_04_2024
	
КонецФункции
