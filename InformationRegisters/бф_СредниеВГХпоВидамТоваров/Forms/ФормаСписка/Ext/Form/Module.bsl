
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ГруппаРегЗадание.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	МассивРегЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.бф_ЗаполнениеВГХ));
	Если МассивРегЗаданий.Количество() Тогда
		ИдентификаторРегЗадания = Строка(МассивРегЗаданий[0].УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРегЗаданиеВручную(Команда)
	Если НЕ ЗначениеЗаполнено(ИдентификаторРегЗадания) Тогда
		ПоказатьПредупреждение(,"Регламентное задание не найдено");
		Возврат;
	КонецЕсли;
	ПараметрыВыполнения = ВыполнитьРегламентноеЗаданиеВручнуюНаСервере(ИдентификаторРегЗадания);
	Если ПараметрыВыполнения.ЗапускВыполнен Тогда
		
		ПоказатьОповещениеПользователя("Запущена процедура регламентного задания",
										,
										СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Процедура запущена в фоновом задании %1", Строка(ПараметрыВыполнения.МоментЗапуска)),
										БиблиотекаКартинок.ВыполнитьРегламентноеЗаданиеВручную);
	КонецЕсли;									
КонецПроцедуры

&НаСервере
Функция ВыполнитьРегламентноеЗаданиеВручнуюНаСервере(Знач ИдентификаторРегламентногоЗадания)
	
	Результат = РегламентныеЗаданияСлужебный.ВыполнитьРегламентноеЗаданиеВручную(ИдентификаторРегламентногоЗадания);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура НастроитьРегЗадание(Команда)
	Если НЕ ЗначениеЗаполнено(ИдентификаторРегЗадания) Тогда         
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПослеЗавершения", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Регламентное задание не найдено. Создать?", РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	ОткрытьФормуНастройкиРегЗадания();
КонецПроцедуры 

&НаКлиенте
Процедура ВопросПослеЗавершения(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		СоздатьРегЗаданиеНаСервере();
		ОткрытьФормуНастройкиРегЗадания();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьРегЗаданиеНаСервере()
	РегЗаданиеОбъект = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.бф_ЗаполнениеВГХ);
	РегЗаданиеОбъект.Записать();
	ИдентификаторРегЗадания = Строка(РегЗаданиеОбъект.УникальныйИдентификатор);
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьФормуНастройкиРегЗадания()
	Если НЕ ЗначениеЗаполнено(ИдентификаторРегЗадания) Тогда         
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Действие", "Изменить");
	ПараметрыФормы.Вставить("Идентификатор", ИдентификаторРегЗадания);
	ОткрытьФорму("Обработка.РегламентныеИФоновыеЗадания.Форма.РегламентноеЗадание", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

