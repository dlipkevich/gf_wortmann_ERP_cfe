#Область ОбработчикиСобытийФормы

&НаСервере
Процедура гф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	// ++ Галфинд СадомцевСА 17.10.2022 добавляем новые реквизиты формы
	ДобавляемыеРеквизиты = Новый Массив;
	
    // Описание таблицы значений как реквизита
    МассивТипаТЗ = Новый Массив;
    МассивТипаТЗ.Добавить(Тип("ТаблицаЗначений"));
	
    // Добавление ТаблицыЗначений в массив реквизитов
    ОписаниеТипаТЗ = Новый ОписаниеТипов(МассивТипаТЗ);
	НовыйРеквизит = Новый РеквизитФормы("гф_ИсторияИзмененияСкидок", ОписаниеТипаТЗ, "",
		"Актуальные скидки");
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	
	// Создание ТаблицыЗначений с описанием колонок
	ТЗ = ЗаполнитьТаблицуИсторияИзмененияСкидок(Объект.Ссылка, Неопределено);
	
    // Добавление в массив реквизитов колонок ТаблицыЗначений
    Для Каждого Колонка Из ТЗ.Колонки Цикл
		НовыйРеквизит = Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения,
			"гф_ИсторияИзмененияСкидок");
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
    КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	// -- Галфинд СадомцевСА 17.10.2022
	
	// ++ Галфинд_Домнышева_01_07_2022
	
	ТипГруппаФормы = Тип("ГруппаФормы");
	ТипПолеФормы = Тип("ПолеФормы");
	ТипКнопкаФормы = Тип("КнопкаФормы");
	ТипТаблицаФормы = Тип("ТаблицаФормы");
	
	НовыйЭлемент = Элементы.Вставить("гф_СтраницаТипСкидки", ТипГруппаФормы, Элементы.ГруппаСтраницы, 
					Элементы.СтраницаУчетнаяИнформация);
	НовыйЭлемент.Вид = ВидГруппыФормы.Страница;
	НовыйЭлемент.Заголовок = "Вид скидки";
	НовыйЭлемент.Подсказка = "Вид скидки";
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;

	НовыйЭлемент = Элементы.Вставить("гф_Сезон", ТипПолеФормы, Элементы.гф_СтраницаТипСкидки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_Сезон";
	НовыйЭлемент.Заголовок = "Сезон";
	// ++ Галфинд СадомцевСА 17.10.2022
	ТаблицаФормы = Элементы.Вставить("гф_Скидки", ТипТаблицаФормы, Элементы.гф_СтраницаТипСкидки);
    ТаблицаФормы.ПутьКДанным = "гф_ИсторияИзмененияСкидок";
    ТаблицаФормы.Отображение = ОтображениеТаблицы.Список;
    ТаблицаФормы.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
    ТаблицаФормы.Заголовок = "История изменения скидок";
    ТаблицаФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	ТаблицаФормы.ИзменятьПорядокСтрок = Ложь;
	ТаблицаФормы.ИзменятьСоставСтрок = Ложь;
	ТаблицаФормы.АвтоМаксимальнаяВысота = Ложь;
	ТаблицаФормы.АвтоМаксимальнаяШирина = Ложь;
	ТаблицаФормы.МаксимальнаяВысота = 12;
	ТаблицаФормы.МаксимальнаяШирина = 60;

    Для Каждого Колонка Из ТЗ.Колонки Цикл

        НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), ТаблицаФормы);
        НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
        НовыйЭлемент.ПутьКДанным = "гф_ИсторияИзмененияСкидок." + Колонка.Имя;
		НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
		НовыйЭлемент.МаксимальнаяШирина = 20;
		НовыйЭлемент.РастягиватьПоГоризонтали = Ложь; 
		НовыйЭлемент.ТолькоПросмотр = Истина;

	КонецЦикла;
	ЗначениеВРеквизитФормы(ТЗ, "гф_ИсторияИзмененияСкидок");
	// -- Галфинд СадомцевСА 17.10.2022
	
    НовыйЭлемент = Элементы.Вставить("гф_СтраницаДополнительно", ТипГруппаФормы, Элементы.ГруппаСтраницы,
					Элементы.СтраницаУчетнаяИнформация);
	НовыйЭлемент.Вид = ВидГруппыФормы.Страница;
	НовыйЭлемент.Заголовок = "Служебное";
	НовыйЭлемент.Подсказка = "Служебное";
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НовыйЭлемент = Элементы.Вставить("Код", ТипПолеФормы, Элементы.ГруппаНомерДата);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Код"; 
	
	НовыйЭлемент = Элементы.Вставить("гф_УстановленСрокОплатыДляРезервов", ТипПолеФормы,
					Элементы.гф_СтраницаДополнительно);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_УстановленСрокОплатыДляРезервов";
	
	НовыйЭлемент = Элементы.Вставить("гф_РазмерПредоплатыПоЗаказуПокупателя", ТипПолеФормы,
					Элементы.гф_СтраницаДополнительно);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_РазмерПредоплатыПоЗаказуПокупателя";
	НовыйЭлемент.ТолькоПросмотр = Истина;
	
	НовыйЭлемент = Элементы.Вставить("гф_ВестиУчетАвансовПоДоговоруВРазрезеНоменклатуры", ТипПолеФормы, 
					Элементы.гф_СтраницаДополнительно);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_ВестиУчетАвансовПоДоговоруВРазрезеНоменклатуры";
	  	
    НовыйЭлемент = Элементы.Добавить("гф_ДатыЗагрузки", ТипГруппаФормы, Элементы.гф_СтраницаДополнительно);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	НовыйЭлемент.ОтображатьЗаголовок = Истина;
	НовыйЭлемент.Заголовок = "Загрузка данных";
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НовыйЭлемент = Элементы.Вставить("гф_ДатаНачалаИспользованияДоговораПриЗагрузкеДанных", ТипПолеФормы, 
					Элементы.гф_ДатыЗагрузки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_ДатаНачалаИспользованияДоговораПриЗагрузкеДанных"; 
	
    НовыйЭлемент = Элементы.Вставить("гф_ДатаОкончанияИспользованияДоговораПриЗагрузкеДанных", ТипПолеФормы, 
					Элементы.гф_ДатыЗагрузки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_ДатаОкончанияИспользованияДоговораПриЗагрузкеДанных"; 

    НовыйЭлемент = Элементы.Добавить("гф_Блокировки", ТипГруппаФормы, Элементы.гф_СтраницаДополнительно);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.СильноеВыделение;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НовыйЭлемент = Элементы.Вставить("гф_Заблокирован", ТипПолеФормы, Элементы.гф_Блокировки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_Заблокирован";
	НовыйЭлемент.ТолькоПросмотр = Истина;
	
    НовыйЭлемент = Элементы.Вставить("гф_ОписаниеБлокировки", ТипПолеФормы, Элементы.гф_Блокировки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_ОписаниеБлокировки";  
    // ++ Галфинд(Просто)_Боцманова_17_09_2022	
	НовыйЭлемент.ТолькоПросмотр = Истина; 
    // ++ Галфинд(Просто)_Боцманова_17_09_2022	
	
	Команда = Команды.Добавить("гф_КомандаПричиныБлокировки");
	Команда.Действие = "гф_КомандаПричиныБлокировки";
	
	НовыйЭлемент = Элементы.Вставить("гф_ПричиныБлокировки", ТипКнопкаФормы, КоманднаяПанель);
	НовыйЭлемент.Заголовок = "Причины блокировки";
	НовыйЭлемент.ИмяКоманды = "гф_КомандаПричиныБлокировки";
	НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	// -- Галфинд_Домнышева_04_07_2022
	
	// vvv Галфинд \ Sakovich 08.08.2023
	// табличная часть "гф_GLN"
	НовыйЭлемент = Элементы.Вставить("гф_СтраницаGLN", ТипГруппаФормы, Элементы.ГруппаСтраницы,
					Элементы.СтраницаУчетнаяИнформация);
	НовыйЭлемент.Вид = ВидГруппыФормы.Страница;
	НовыйЭлемент.Заголовок = "GLN";
	
	НовыйЭлемент = Элементы.Вставить("гф_GLN", ТипТаблицаФормы, Элементы.гф_СтраницаGLN);
	НовыйЭлемент.ПутьКДанным = "Объект.гф_GLN";
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.АвтоМаксимальнаяВысота = Ложь;
	НовыйЭлемент.МаксимальнаяВысота = 20;

	НовыйЭлемент.УстановитьДействие("ПередНачаломДобавления", "гф_GLN_ПередНачаломДобавления");
	НовыйЭлемент.УстановитьДействие("ПередНачаломИзменения", "гф_GLN_ПередНачаломИзменения");
	
	НовыйЭлемент = Элементы.Вставить("GLNНомерСтроки", ТипПолеФормы, Элементы.гф_GLN);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_GLN.НомерСтроки";
	НовыйЭлемент.Заголовок = "N п/п";

	НовыйЭлемент = Элементы.Вставить("GLNНомер", ТипПолеФормы, Элементы.гф_GLN);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_GLN.GLNНомер";
	НовыйЭлемент.Заголовок = "GLN";

	НовыйЭлемент = Элементы.Вставить("НомерАдреса", ТипПолеФормы, Элементы.гф_GLN);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_GLN.НомерАдреса";
	НовыйЭлемент.Заголовок = "Номер адреса";
	// ^^^ Галфинд \ Sakovich 08.08.2023
	
КонецПроцедуры

// vvv Галфинд \ Sakovich 08.08.2023
&НаКлиенте
Процедура гф_GLN_ПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	Отказ = Истина;
	ВыборАдресовДоставки("Добавление");
КонецПроцедуры

&НаКлиенте
Процедура гф_GLN_ПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ВыборАдресовДоставки("Изменение");
КонецПроцедуры 
// ^^^ Галфинд \ Sakovich 08.08.2023

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// vvv Галфинд \ Sakovich 08.08.2023
&НаКлиенте
Процедура ВыборАдресовДоставки(Действие)
	Отказ = Истина;
	Парам = Новый Структура;
	ПараметрОтбораПоВладельцу = Новый Структура;
	ПараметрОтбораПоВладельцу.Вставить("Владелец", Объект.Контрагент);
	Парам.Вставить("Отбор", ПараметрОтбораПоВладельцу);
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Действие", Действие);
	Оповещение = Новый ОписаниеОповещения("ВыборАдресовДоставкиЗавешенение", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("Справочник.гф_АдресаДоставки.ФормаВыбора", Парам, ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыборАдресовДоставкиЗавешенение(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		СтруктураДанныхАдреса = ПолучитьДанныеАдреса(Результат);
		ТекДанные = Неопределено;
		Если ДопПараметры["Действие"] = "Добавление" Тогда
			ТекДанные = Объект.гф_GLN.Добавить();
		КонецЕсли;
		Если  ДопПараметры["Действие"] = "Изменение" Тогда
			ТекДанные = Элементы.гф_GLN.ТекущиеДанные;
		КонецЕсли;
		Если ТекДанные <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТекДанные, СтруктураДанныхАдреса);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеАдреса(Результат)
	СтруктураДанныхАдреса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Результат, "GLNНомер, НомерАдреса");
	Возврат СтруктураДанныхАдреса;
КонецФункции
// ^^^ Галфинд \ Sakovich 08.08.2023

&НаКлиенте
Процедура гф_КомандаПричиныБлокировки(Команда)
	// ++ Галфинд_Домнышева_04_07_2022
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Открывать РС ""Причины блокировок"" (ТЗ 4.1.15)");
	// Команда будет заполнена после проектирования РС ПричиныБлокировок
	// -- Галфинд_Домнышева_04_07_2022

КонецПроцедуры  

// ++ Галфинд СадомцевСА 17.10.2022
// Заполняет таблицу гф_ИсторияИзмененияСкидок
//
// Параметры:
//   Договор - СправочникСсылка.ДоговорыКонтрагентов
//   Сезон - СправочникСсылка.КоллекцииНоменклатуры
//
// Возвращаемое значение:
//   ТаблицаЗначений
//

&НаСервереБезКонтекста
Функция ЗаполнитьТаблицуИсторияИзмененияСкидок(Договор, Сезон)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	гф_ИсторияИзмененияСкидокСрезПоследних.Сезон КАК Сезон,
	               |	гф_ИсторияИзмененияСкидокСрезПоследних.ВидСкидки КАК ВидСкидки,
	               |	гф_ИсторияИзмененияСкидокСрезПоследних.Скидка КАК Скидка
	               |ИЗ
	               |	РегистрСведений.гф_ИсторияИзмененияСкидок.СрезПоследних(
	               |			,
	               |			Договор = &Договор
	               |				И Сезон = &Сезон) КАК гф_ИсторияИзмененияСкидокСрезПоследних";
	Если Не ЗначениеЗаполнено(Сезон) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Сезон = &Сезон", "Истина");
	КонецЕсли;
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Сезон", Сезон);
	Результат = Запрос.Выполнить();
	ТЗ = Результат.Выгрузить();
	Возврат ТЗ;
КонецФункции

#КонецОбласти