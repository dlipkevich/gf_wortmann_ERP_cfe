#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	гф_ГлобальныеЗначения.Значение КАК Значение
	|ИЗ
	|	ПланВидовХарактеристик.гф_ГлобальныеЗначения КАК гф_ГлобальныеЗначения
	|ГДЕ
	|	гф_ГлобальныеЗначения.Ключ = &Ключ"; 
	
	Ключ = "гф_ГлобальныеЗначенияОптоваяЗакупочнаяЦена";
	
	Запрос.Параметры.Вставить("Ключ", Ключ);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда  
		
		ОптоваяЦенаОбъект = ПланыВидовХарактеристик.гф_ГлобальныеЗначения.СоздатьЭлемент();
		
		ТипЦена = Новый ОписаниеТипов("СправочникСсылка.ВидыЦен");
		
		ОптоваяЦенаОбъект.Ключ			= Ключ;
		ОптоваяЦенаОбъект.Наименование	= "Тип цен оптовая";
		ОптоваяЦенаОбъект.ТипЗначения	= ТипЦена;
		ОптоваяЦенаОбъект.Родитель		= ПланыВидовХарактеристик.гф_ГлобальныеЗначения.ОбщегоНазначения;
		
		ОптоваяЦенаОбъект.Записать();
		
		ПрайсЛист = Неопределено;
		
	Иначе
		
		Выборка = Результат.Выбрать();  
		
		Выборка.Следующий();
		
		ПрайсЛист = Выборка.Значение;
		
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ПрайсЛист) Тогда 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлено глобальное значение ""Тип цен оптовая"".");
		
	КонецЕсли;	  
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(B2B_w_СкидкиКлиентаПоДоговоруСрезПоследних.Скидка, 0)) КАК Скидка
	|ИЗ
	|	ПланВидовХарактеристик.гф_ГлобальныеЗначения КАК гф_ГлобальныеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.гф_ГлобальныеЗначения.Список КАК гф_ГлобальныеЗначенияСписок
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.B2B_w_СкидкиКлиентаПоДоговору.СрезПоследних(&ДатаСкидки, ДоговорКонтрагента = &Договор) КАК B2B_w_СкидкиКлиентаПоДоговоруСрезПоследних
	|			ПО гф_ГлобальныеЗначенияСписок.Значение = B2B_w_СкидкиКлиентаПоДоговоруСрезПоследних.ТипСкидки
	|		ПО гф_ГлобальныеЗначения.Ссылка = гф_ГлобальныеЗначенияСписок.Ссылка
	|ГДЕ
	|	гф_ГлобальныеЗначения.Ключ = &Ключ";
	
	
	Договор = Неопределено;
	
	Параметры.Свойство("Договор", Договор);
	
	Запрос.Параметры.Вставить("ДатаСкидки", ТекущаяДатаСеанса());
	Запрос.Параметры.Вставить("Ключ", "гф_ГлобальныеЗначенияСкидкиДляЗаказаКлиента");
	Запрос.Параметры.Вставить("Договор", Договор);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка= Результат.Выбрать();
		
		Выборка.Следующий();
		
		СкидкаКлиента = Выборка.Скидка;
		
	КонецЕсли;	

	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПредоплатныйКлиентПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ПредоплатныйКлиентПриИзмененииНаСервере()    
	
	Если ПредоплатныйКлиент Тогда   
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	гф_ГлобальныеЗначения.Значение КАК Значение
		|ИЗ
		|	ПланВидовХарактеристик.гф_ГлобальныеЗначения КАК гф_ГлобальныеЗначения
		|ГДЕ
		|	гф_ГлобальныеЗначения.Ключ = &Ключ"; 
		
		Ключ = "гф_ГлобальныеЗначенияДатаЦенДляПредоплатныхКлиентов";
		
		Запрос.Параметры.Вставить("Ключ", Ключ);
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда  
			
			ДатаЦенОбъект = ПланыВидовХарактеристик.гф_ГлобальныеЗначения.СоздатьЭлемент();
			
			КвалификаторыДаты = Новый КвалификаторыДаты(ЧастиДаты.Дата);
			ТипДата = Новый ОписаниеТипов("Дата", , , , , КвалификаторыДаты);
			
			ДатаЦенОбъект.Ключ			= Ключ;
			ДатаЦенОбъект.Наименование	= "Дата цен для предоплатных клиентов";
			ДатаЦенОбъект.ТипЗначения	= ТипДата;
			ДатаЦенОбъект.Родитель		= ПланыВидовХарактеристик.гф_ГлобальныеЗначения.ОбщегоНазначения;
			
			ДатаЦенОбъект.Записать();
			
			ДатаЦен = Неопределено;
			
		Иначе
			
			Выборка = Результат.Выбрать();  
			
			Выборка.Следующий();
			
			ДатаЦен = Выборка.Значение;
			
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(ДатаЦен) Тогда 
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не установлено глобальное значение ""Дата цен для предоплатных клиентов"".");
			
		КонецЕсли;	
		
	Иначе	
		
		ДатаЦен = ТекущаяДатаСеанса();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПредоплатныйКлиентПриИзменении(Элемент)
	
	ПредоплатныйКлиентПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)   
	
	Если Не ЗначениеЗаполнено(ПрайсЛист) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для печати необходимо заполнить Прайс-лист");
		
		Возврат;			
		
	КонецЕсли;	
	
	ПараметрыПечати = Новый Структура("ДатаЦен, ПрайсЛист, ПредоплатныйКлиент, СкидкаКлиента");
	
	ЗаполнитьЗначенияСвойств(ПараметрыПечати, ЭтотОбъект);
	
	Закрыть(ПараметрыПечати);
	
КонецПроцедуры

#КонецОбласти

