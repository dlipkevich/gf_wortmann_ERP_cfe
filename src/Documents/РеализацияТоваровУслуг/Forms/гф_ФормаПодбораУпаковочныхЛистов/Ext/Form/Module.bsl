#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Реализация",	Реализация);
	Параметры.Свойство("Склад",			Склад);
	
	ПрочитатьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУпаковочныеЛисты

&НаКлиенте
Процедура УпаковочныеЛистыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.УпаковочныеЛисты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		УпаковочныйЛист = ТекущиеДанные.УпаковочныйЛист;
		
	Иначе	
		
		УпаковочныйЛист = ПредопределенноеЗначение("Документ.УпаковочныйЛист.ПустаяСсылка");
		
	КонецЕсли;	
	
	ОтборПоЛисту = Новый ФиксированнаяСтруктура("УпаковочныйЛист", УпаковочныйЛист);
	
	Элементы.Товары.ОтборСтрок = ОтборПоЛисту;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	Результат = Новый Массив;
	
	Для Каждого СтрокаЛиста Из УпаковочныеЛисты Цикл
		
		Если СтрокаЛиста.Флаг Тогда
			
			Результат.Добавить(СтрокаЛиста.УпаковочныйЛист);
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтметить(Команда)
	
	Флаг = (Команда = Команды["ОтметитьВсе"]);
	
	Для Каждого СтрокаУпаковочныхЛистов Из УпаковочныеЛисты Цикл
		
		СтрокаУпаковочныхЛистов.Флаг = Флаг;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ПрочитатьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьДанные()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Подзапрос.Номенклатура КАК Номенклатура,
	|	Подзапрос.Характеристика КАК Характеристика,
	|	Подзапрос.Серия КАК Серия,
	|	СУММА(Подзапрос.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|		ТоварыНаСкладахОстатки.Серия КАК Серия,
	|		ТоварыНаСкладахОстатки.ВНаличииОстаток + ТоварыНаСкладахОстатки.КОтгрузкеОстаток КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНаСкладах.Номенклатура,
	|		ТоварыНаСкладах.Характеристика,
	|		ТоварыНаСкладах.Серия,
	|		ТоварыНаСкладах.ВНаличии + ТоварыНаСкладах.КОтгрузке
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|			ПРАВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
	|				ПО РеализацияТоваровУслуг.ЗаказКлиента = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение
	|			ПО (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка = ТоварыНаСкладах.Регистратор)
	|	ГДЕ
	|		РеализацияТоваровУслуг.Ссылка = &Ссылка
	|		И ТоварыНаСкладах.Склад = &Склад) КАК Подзапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	Подзапрос.Номенклатура,
	|	Подзапрос.Характеристика,
	|	Подзапрос.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УпаковочныйЛистТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_УпаковочниыеЛисты
	|ИЗ
	|	Документ.УпаковочныйЛист.Товары КАК УпаковочныйЛистТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остаткм
	|		ПО УпаковочныйЛистТовары.Номенклатура = ВТ_Остаткм.Номенклатура
	|			И УпаковочныйЛистТовары.Серия = ВТ_Остаткм.Серия
	|			И УпаковочныйЛистТовары.Характеристика = ВТ_Остаткм.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураСправочник.Артикул КАК Артикул,
	|	УпаковочныйЛистШапка.Код КАК IDКороба,
	|	ВТ_УпаковочниыеЛисты.Ссылка КАК УпаковочныйЛист,
	|	НоменклатураСправочник.Ссылка КАК Номенклатура,
	|	УпаковочныйЛистТовары.Характеристика КАК Характеристика,
	|	УпаковочныйЛистТовары.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_УпаковочныеЛистыНоменклатура
	|ИЗ
	|	ВТ_УпаковочниыеЛисты КАК ВТ_УпаковочниыеЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УпаковочныйЛист.Товары КАК УпаковочныйЛистТовары
	|		ПО (УпаковочныйЛистТовары.Ссылка = ВТ_УпаковочниыеЛисты.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО (УпаковочныйЛистТовары.Номенклатура = НоменклатураСправочник.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УпаковочныйЛист КАК УпаковочныйЛистШапка
	|		ПО ВТ_УпаковочниыеЛисты.Ссылка = УпаковочныйЛистШапка.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_УпаковочныеЛистыНоменклатура.УпаковочныйЛист КАК УпаковочныйЛист,
	|	ВТ_УпаковочныеЛистыНоменклатура.Номенклатура КАК Номенклатура,
	|	ВТ_УпаковочныеЛистыНоменклатура.Характеристика КАК Характеристика,
	|	ВТ_УпаковочныеЛистыНоменклатура.Количество КАК Количество
	|ИЗ
	|	ВТ_УпаковочныеЛистыНоменклатура КАК ВТ_УпаковочныеЛистыНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_УпаковочныеЛистыНоменклатура.Артикул КАК Артикул,
	|	ВТ_УпаковочныеЛистыНоменклатура.IDКороба КАК IDКороба,
	|	ВТ_УпаковочныеЛистыНоменклатура.УпаковочныйЛист КАК УпаковочныйЛист,
	|	СУММА(ВТ_УпаковочныеЛистыНоменклатура.Количество) КАК Количество
	|ИЗ
	|	ВТ_УпаковочныеЛистыНоменклатура КАК ВТ_УпаковочныеЛистыНоменклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_УпаковочныеЛистыНоменклатура.Артикул,
	|	ВТ_УпаковочныеЛистыНоменклатура.IDКороба,
	|	ВТ_УпаковочныеЛистыНоменклатура.УпаковочныйЛист";
	
	Запрос.Параметры.Вставить("Ссылка", Реализация);
	Запрос.Параметры.Вставить("Склад", Склад);
	
	Товары.Очистить();
	УпаковочныеЛисты.Очистить();
	
	Результат = Запрос.ВыполнитьПакет(); 
	
	ИндексУпаковочныхЛистов	= Результат.Количество() - 1;
	ИндексТоваров			= Результат.Количество() - 2;
	
	УпаковочныеЛисты.Загрузить(Результат[ИндексУпаковочныхЛистов].Выгрузить());
	Товары.Загрузить(Результат[ИндексТоваров].Выгрузить());
	
КонецПроцедуры

#КонецОбласти