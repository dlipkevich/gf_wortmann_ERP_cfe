#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&Перед("ПередЗаписью")
Процедура гф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	// #wortmann {
	// Галфинд Sakovich 2022/11/30
	гф_УстановитьПризнакЗаказатьКМ(Отказ, РежимЗаписи);
	// } #wortmann
	
	// #wortmann {
	// Галфинд Sakovich 2022/12/19
	Если ЭтоНовый() Тогда
		гф_NVE_назначен = Истина;
	КонецЕсли;
	// } #wortmann
	
КонецПроцедуры

&После("ПриКопировании")
Процедура гф_ПриКопировании(ОбъектКопирования)
	// #wortmann {
	// Галфинд Sakovich 2022/11/30
	гф_ЗаказатьКМ = Ложь;
	гф_КМЭмитированы = Ложь;
	// } #wortmann
	
	// #wortmann {
	// Галфинд Sakovich 2022/12/19
		гф_NVE_назначен = Истина;
	// } #wortmann
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура гф_ПересчитатьТЧТоварыНаОсновнииКоробов() Экспорт
	
	ВременнаяТаблицаТовары = Товары.Выгрузить();
	
	ВременнаяТаблицаТовары.Очистить();
	
	Для каждого СтрокаВКоробах из гф_ПродукцияВКоробах Цикл
		
		Если ЗначениеЗаполнено(СтрокаВКоробах.IDКороба) Тогда
			
			гф_ДобавитьСтрокиПоУпаковочномуЛисту(СтрокаВКоробах, ВременнаяТаблицаТовары);
			
			
		ИначеЕсли ЗначениеЗаполнено(СтрокаВКоробах.ВариантКомплектации) Тогда 
			
			гф_ДобавитьСтрокиПоВариантуКомплектации(СтрокаВКоробах, ВременнаяТаблицаТовары);
			
		Иначе
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;	
	// #wortmann {
	// Галфинд_Домнышева_26_04_2023 - 26_06_2023 - 19_09_2023
    // добавила в свертку гф_IDКороба (необходимо заполнять ТЧ Товары с учетом УЛ) и СтавкаНДС(26_06_2023) и гф_ЗаказКлиента(19_09_2023)
	ВременнаяТаблицаТовары.Свернуть("Номенклатура,Характеристика,Назначение,Упаковка,гф_РазмерностьОбуви,Цена, гф_IDКороба, гф_ЗаказКлиента, СтавкаНДС",
											"КоличествоУпаковок,Количество,Сумма");
	// } #wortmann
	Товары.Загрузить(ВременнаяТаблицаТовары);
	
	СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(Склад, Ложь, Товары, Истина);
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЭтотОбъект);
	
	СтруктураДействий = Новый Структура; 
	// #wortmann {
	// Галфинд_Домнышева_26_06_2023
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8132bcee7bda45d711ede3300f56c63c
	// При хоз.операции Импорт СтавкаНДС заполняется значением "Без НДС", так оно добавилось в сворачиваемую таблицу перезаполнять не нужно.
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", 
			ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(ЭтотОбъект, Истина));
	КонецЕсли;
	// } #wortmann
																		
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Товары, СтруктураДействий, Неопределено);
	
	СуммаДокумента = ПолучитьСуммуДокумента();
	
КонецПроцедуры

Процедура гф_ДобавитьСтрокиПоУпаковочномуЛисту(СтрокаВКоробах, ВременнаяТаблицаТовары)
	
	РабочаяТаблица = Товары.Выгрузить();
	РабочаяТаблица.Очистить();
	
	Для каждого Строка из СтрокаВКоробах.IDКороба.Товары Цикл															
		
		НоваяСтрокаРТ = РабочаяТаблица.Добавить();					
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРТ, Строка);										
		
	КонецЦикла;
	ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(СтрокаВКоробах.СтоимостьКороба, 
												РабочаяТаблица, "Сумма", "Количество");
	
	Для каждого Строка из РабочаяТаблица Цикл
		
		НоваяСтрока = ВременнаяТаблицаТовары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
		НоваяСтрока.Цена = НоваяСтрока.Сумма / НоваяСтрока.Количество;
		НоваяСтрока.гф_IDКороба = СтрокаВКоробах.IDКороба;
		НоваяСтрока.гф_ЗаказКлиента = СтрокаВКоробах.IDКороба.гф_Заказ; 
		НоваяСтрока.гф_РазмерностьОбуви = СтрокаВКоробах.IDКороба.гф_Комплектация.Характеристика;
		
		// Убрано Галфинд_ДомнышеваКР_26_06_2023
		// e1cib/data/Задача.ЗадачаИсполнителя?ref=8132bcee7bda45d711ede3300f56c63c
		//Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		//КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура гф_ДобавитьСтрокиПоВариантуКомплектации(СтрокаВКоробах, ВременнаяТаблицаТовары)
	
	РабочаяТаблица = Товары.Выгрузить();
	РабочаяТаблица.Очистить();
	
	Для каждого Строка из СтрокаВКоробах.ВариантКомплектации.Товары Цикл															
		
		НоваяСтрокаРТ = РабочаяТаблица.Добавить();					
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРТ, Строка);										
		НоваяСтрокаРТ.Назначение = Справочники.Назначения.НайтиПоНаименованию("Техническое");
		
	КонецЦикла;
	ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(СтрокаВКоробах.СтоимостьКороба, 
												РабочаяТаблица, "Сумма", "Количество");
	
	Для каждого Строка из РабочаяТаблица Цикл
		
		НоваяСтрока = ВременнаяТаблицаТовары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
		НоваяСтрока.Цена = НоваяСтрока.Сумма / НоваяСтрока.Количество;
			
		НоваяСтрока.гф_РазмерностьОбуви = СтрокаВКоробах.ВариантКомплектации.Характеристика; 
		
		Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры	

Процедура гф_УстановитьПризнакЗаказатьКМ(Отказ, РежимЗаписи)
	
	Если Не (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту) Тогда
		Возврат;
	КонецЕсли;
	тзТовары = Товары.Выгрузить();
	Если Не Документы.ПриобретениеТоваровУслуг.гф_ОпределитьНаличиеМаркируемойПродукции(тзТовары) Тогда
		Возврат;
	КонецЕсли;

	гф_ЗаказатьКМ = Истина;
	
КонецПроцедуры	

#КонецОбласти

#КонецЕсли