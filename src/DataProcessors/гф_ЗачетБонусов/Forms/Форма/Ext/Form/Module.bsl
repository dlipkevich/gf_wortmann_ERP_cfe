
&НаСервере
Процедура ЗаполнитьНаСервере()
	ТаблицаБонусов.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИСТИНА КАК Отметка,
	               |	гф_БонусыКлиентовОстатки.Организация КАК Организация,
	               |	гф_БонусыКлиентовОстатки.Контрагент КАК Контрагент,
	               |	гф_БонусыКлиентовОстатки.Договор КАК Договор,
	               |	гф_БонусыКлиентовОстатки.ВидБонуса КАК ВидБонуса,
	               |	гф_БонусыКлиентовОстатки.СуммаОстаток КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.гф_БонусыКлиентов.Остатки(
	               |			&Период,
	               |			Организация = &Организация
	               |				И Контрагент = &Контрагент) КАК гф_БонусыКлиентовОстатки";
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация", "ИСТИНА");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Контрагент = &Контрагент", "ИСТИНА");
	КонецЕсли;
	Результат = Запрос.Выполнить();
	ТаблицаБонусов.Загрузить(Результат.Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Дата = ТекущаяДатаСеанса();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для Каждого Стр Из ТаблицаБонусов Цикл
		Стр.Отметка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого Стр Из ТаблицаБонусов Цикл
		Стр.Отметка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СоздатьКорректировкиЗадолженностейНаСервере()
	Для Каждого Стр Из ТаблицаБонусов Цикл
		Если Не Стр.Отметка Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(Стр.КорректирокаЗадолженности) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокОбъект = Документы.КорректировкаЗадолженности.СоздатьДокумент();
		ДокОбъект.Дата = Дата - 1;
		ДокОбъект.Организация = Стр.Организация;
		ДокОбъект.Контрагент = Стр.Контрагент;
		ДокОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности;
		ДокОбъект.Автор = ПараметрыСеанса.ТекущийПользователь;
		ДокОбъект.Комментарий = "#Создан обработкой Зачет бонусов";
		
		нс = ДокОбъект.Задолженность.Добавить();
		нс.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		нс.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
		нс.ВалютаВзаиморасчетов = Стр.Договор.ВалютаВзаиморасчетов;
		нс.Партнер = Стр.Контрагент.Партнер;
		нс.ОбъектРасчетов = НайтиОбъектРасчетов(Стр.Договор);
		нс.Сумма = Стр.Сумма;
		
		нс = ДокОбъект.ДоходыРасходыАктивыПассивы.Добавить();
		нс.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		нс.Статья = СтатьяРасходов;
		нс.Подразделение = Подразделение;
		
		Попытка
			ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
			
			Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("ИдентификаторДляФормул", "гф_КорректировкаЗадолженностиВидКорректировкиДолга");
			НаборЗаписей = Регистрысведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ДокОбъект.Ссылка);
			НаборЗаписей.Отбор.Свойство.Установить(Свойство);
			Запись = НаборЗаписей.Добавить();
			Запись.Объект = ДокОбъект.Ссылка;
			Запись.Свойство = Свойство;
			Запись.Значение = Стр.ВидБонуса;
			НаборЗаписей.Записать();
			
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Стр.КорректирокаЗадолженности = ДокОбъект.Ссылка;
			
		Исключение
			Сообщить("Ошибка:" + ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКорректировкиЗадолженностей(Команда)
	Если Не ЗначениеЗаполнено(СтатьяРасходов) Тогда
		Сообщить("Надо указать Статью расходов!");
		Возврат;
	КонецЕсли;
	СоздатьКорректировкиЗадолженностейНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиОбъектРасчетов(Договор)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбъектыРасчетов.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	               |ГДЕ
	               |	ОбъектыРасчетов.Договор = &Договор";
				   //|	И НЕ ОбъектыРасчетов.ПометкаУдаления";
	Запрос.УстановитьПараметр("Договор", Договор);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции
