
&НаКлиенте
Процедура КомандаЗагрузитьФайлы(Команда)
	
	Состояние("Подождите..", , "Выполняется получение почтовых сообщений", БиблиотекаКартинок.Получить);

	Если ОчищатьКаталогиЗагрузки Тогда
		УдалитьФайлы(КаталогЗагрузки, "*.*");
		Для Каждого ТекЯщик Из ПочтовыеЯщики Цикл
			Если ЗначениеЗаполнено(ТекЯщик.КаталогЗагрузки)
				Тогда
				УдалитьФайлы(ТекЯщик.КаталогЗагрузки, "*.*");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИнформацияОЗагруженныхФайлах = ПолучитьОписаниеИнформацииОФайлах();
	
	ЗагрузитьФайлыИзПочтовыхЯщиков(ИнформацияОЗагруженныхФайлах);
	
	Если ИнформацияОЗагруженныхФайлах.ВсегоЗагруженоФайлов > 0
		Тогда                           
			Для Каждого ТекФайл Из ИнформацияОЗагруженныхФайлах.Файлы Цикл
				
				ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьПослеПолученияФайла", ЭтотОбъект, ТекФайл);
				
				Если (Не ЗначениеЗаполнено(ТекФайл.ИмяФайлаНаКлиенте)) или (Не ЗначениеЗаполнено(ТекФайл.АдресВХранилище))
					Тогда
						Продолжить;
				КонецЕсли;
				
				ОписанияФайлов = Новый Массив;
				ОписанияФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(ТекФайл.ИмяФайлаНаКлиенте, ТекФайл.АдресВХранилище));
				
				Попытка
					НачатьПолучениеФайловССервера(ОповещениеОЗавершении, ОписанияФайлов, ТекФайл.КаталогВыгрузки);
				Исключение
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
				КонецПопытки;
			КонецЦикла;
			
			НомерСтроки = 1;
			
			ТекстСообщения = "Всего загружено файлов: " + Формат(ИнформацияОЗагруженныхФайлах.ВсегоЗагруженоФайлов, "ЧДЦ=0");
			Для Каждого ТекЯщик Из ИнформацияОЗагруженныхФайлах.ПочтовыеЯщики Цикл
				ТекстСообщения = ТекстСообщения
									+ Символы.ПС
									+ " " + Формат(НомерСтроки, "ЧДЦ=0") + ") Из почтового ящика """ + ТекЯщик.EMail + """: "
									+ Формат(ТекЯщик.ЗагруженоФайлов, "ЧДЦ=0");
									
				НомерСтроки = НомерСтроки + 1;
			КонецЦикла;
			
			Состояние(); 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

		Иначе                                            
			УдалитьНаСервереВсеФайлы(ИнформацияОЗагруженныхФайлах.Файлы);
			ТекстСообщения = "Нет файлов для загрузки";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Состояние();
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ЗагрузитьФайлыИзПочтовыхЯщиков(ИнформацияОЗагруженныхФайлах)
	Обработка = РеквизитФормыВЗначение("Объект");
	
	Обработка.ЗагрузитьФайлыИзПочтовыхЯщиков(ИнформацияОЗагруженныхФайлах, ПочтовыеЯщики.Выгрузить());
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыполнитьПослеПолученияФайла(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	УдалитьНаСервереФайл(ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНаСервереВсеФайлы(УдаляемыеФайлы)
	
	Для Каждого ТекФайл Из УдаляемыеФайлы Цикл
		Попытка
			Если ТекФайл.ИмяФайлаНаСервере <> ""
				Тогда
					УдалитьФайлы(ТекФайл.ИмяФайлаНаСервере);
			КонецЕсли
		Исключение
		КонецПопытки;
		
		Попытка
			Если ТекФайл.АдресВХранилище <> ""
				Тогда
					УдалитьИзВременногоХранилища(ТекФайл.АдресВХранилище);
			КонецЕсли
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНаСервереФайл(УдаляемыйФайл)
	
	Попытка
		Если УдаляемыйФайл.ИмяФайлаНаСервере <> ""
			Тогда
				УдалитьФайлы(УдаляемыйФайл.ИмяФайлаНаСервере);
		КонецЕсли
	Исключение
	КонецПопытки;
		
	Попытка
		Если УдаляемыйФайл.АдресВХранилище <> ""
			Тогда
				УдалитьИзВременногоХранилища(УдаляемыйФайл.АдресВХранилище);
		КонецЕсли
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОписаниеИнформацииОФайлах()

	ИнформацияОЗагруженныхФайлах = Новый Структура;
	ИнформацияОЗагруженныхФайлах.Вставить("ПочтовыеЯщики", Новый Массив);
	ИнформацияОЗагруженныхФайлах.Вставить("Файлы", Новый Массив);
	ИнформацияОЗагруженныхФайлах.Вставить("ВсегоЗагруженоФайлов", 0);

	Возврат ИнформацияОЗагруженныхФайлах;
КонецФункции

&НаКлиенте
Процедура УправлениеФормой()

	ЭтотОбъект.Элементы.КомандаЗагрузитьФайлы.Доступность =
			(ЭтотОбъект.ПочтовыеЯщики.Количество() > 0);
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
	ДатаОтбораПисем = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	ТолькоНовыеПисьма = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПочтовыеЯщикиПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПочтовыеЯщикиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Порт = 993;
		Элемент.ТекущиеДанные.IMAP = Истина;
		Элемент.ТекущиеДанные.SSL = Истина;
		Элемент.ТекущиеДанные.Таймаут = 50;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПочтовыеЯщикиКаталогЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ВыборКаталогаДляЗагрузки(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаДляЗагрузки(Элемент) Экспорт
	
	ТекЗаголовок = "Выберите каталог для загрузки файлов из почтового ящика """ + Элементы.ПочтовыеЯщики.ТекущиеДанные.EMail + """";
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбора.Заголовок = ТекЗаголовок;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКаталогаДляЗагрузкиЗавершение", ЭтотОбъект, Элемент);
	ДиалогВыбора.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаДляЗагрузкиЗавершение(ВыбранныеКаталоги, ДополнительныеПараметры) Экспорт
	
	Если (ВыбранныеКаталоги <> Неопределено) и (ВыбранныеКаталоги.Количество() > 0)
		Тогда
			ВыбКаталогЗагрузки = ВыбранныеКаталоги.Получить(0);
		Иначе
			Возврат;
	КонецЕсли;
	
	Элементы.ПочтовыеЯщики.ТекущиеДанные.КаталогЗагрузки = ВыбКаталогЗагрузки;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиИзСправочникаНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.EMail КАК EMail,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.Логин КАК Логин,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.Пароль КАК Пароль,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.АдресСервера КАК АдресСервера,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.Порт КАК Порт,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.IMAP КАК IMAP,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.SSL КАК SSL,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.Таймаут КАК Таймаут,
		|	гф_РеквизитыПочтыДляЗагрузкиВКаталоги.КаталогЗагрузки КАК КаталогЗагрузки
		|ИЗ
		|	Справочник.гф_РеквизитыПочтыДляЗагрузкиВКаталоги КАК гф_РеквизитыПочтыДляЗагрузкиВКаталоги";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ПочтовыеЯщики.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДаннымиИзСправочника(Команда)
	
	ЗаполнитьДаннымиИзСправочникаНаСервере();
	УправлениеФормой();
	
КонецПроцедуры
