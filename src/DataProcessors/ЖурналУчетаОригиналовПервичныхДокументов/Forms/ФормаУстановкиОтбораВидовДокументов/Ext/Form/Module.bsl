#Область СлужебныеПроцедурыИФункции

&НаСервере
&Вместо("ЗаполнитьСписокВыбораВидовДокумента")
Процедура гф_ЗаполнитьСписокВыбораВидовДокумента()
	
	ТаблицаВидов = РеквизитФормыВЗначение("ТаблицаВидыДокументов");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаВидов.ВидДокумента КАК ВидДокумента,
	               |	ТаблицаВидов.Отбор КАК Отбор,
	               |	ТаблицаВидов.Представление КАК Представление
	               |ПОМЕСТИТЬ ВТ_ТаблицаВидов
	               |ИЗ
	               |	&ТаблицаВидов КАК ТаблицаВидов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	гф_ОтслеживаемыеОригиналыПервичныхДокументов.ВидДокумента КАК ВидДокумента,
	               |	ИдентификаторыОбъектовМетаданных.Синоним КАК Представление
	               |ПОМЕСТИТЬ ВТ_Отслеживаемые
	               |ИЗ
	               |	РегистрСведений.гф_ОтслеживаемыеОригиналыПервичныхДокументов КАК гф_ОтслеживаемыеОригиналыПервичныхДокументов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	               |		ПО гф_ОтслеживаемыеОригиналыПервичныхДокументов.ВидДокумента = ИдентификаторыОбъектовМетаданных.Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	гф_ОтслеживаемыеОригиналыПервичныхДокументов.ВидДокумента,
	               |	ИдентификаторыОбъектовМетаданных.Синоним
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТ_Отслеживаемые.ВидДокумента КАК ВидДокумента,
	               |	ISNULL(ВТ_ТаблицаВидов.Отбор, ЛОЖЬ) КАК Отбор,
	               |	ВТ_Отслеживаемые.Представление КАК Представление
	               |ИЗ
	               |	ВТ_Отслеживаемые КАК ВТ_Отслеживаемые
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаВидов КАК ВТ_ТаблицаВидов
	               |		ПО ВТ_Отслеживаемые.ВидДокумента = ВТ_ТаблицаВидов.ВидДокумента
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Представление";
	
	Запрос.Параметры.Вставить("ТаблицаВидов", ТаблицаВидов);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		ТаблицаВидов.Очистить();                   
		
	Иначе
		
		ТаблицаВидов = Результат.Выгрузить();
		
	КонецЕсли;	
	
	ЗначениеВРеквизитФормы(ТаблицаВидов, "ТаблицаВидыДокументов");
	
КонецПроцедуры

#КонецОбласти

