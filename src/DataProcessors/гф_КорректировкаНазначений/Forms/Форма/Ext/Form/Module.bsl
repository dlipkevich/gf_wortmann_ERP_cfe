#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьСвойствоПроверкаПТУ();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗначенияПоУмолчанию();
	ОбновитьСписокВыбораСклада();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбновитьСписокВыбораСклада();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивСсылокПТУ = ПолучитьМассивСсылокПТУ();
	ЗначениеОтбора  = Новый Структура("Ссылка", МассивСсылокПТУ);
	
	ОткрытьФорму("Документ.ПриобретениеТоваровУслуг.ФормаВыбора", Новый Структура("Отбор", ЗначениеОтбора), Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Данные.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.УпаковочныйЛист) Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.УпаковочныйЛист);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	ВыполнитьОбработкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(Объект,
	Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачала", "ДатаОкончания"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере();
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьНаСервере();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ВыполнитьОбработкуНаСервере();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	Объект.ДатаОкончания = ТекущаяДатаСеанса();
	Объект.ДатаНачала    = Объект.ДатаОкончания - 7*86400; // 7 дней
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВыбораСклада()
	
	Запрос       = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	гф_ГлобальныеЗначенияСписок.Значение КАК Значение
	|ИЗ
	|	ПланВидовХарактеристик.гф_ГлобальныеЗначения.Список КАК гф_ГлобальныеЗначенияСписок
	|ГДЕ
	|	гф_ГлобальныеЗначенияСписок.Ссылка.Ключ = &Ключ
	|	И гф_ГлобальныеЗначенияСписок.Значение.гф_Организация = &Организация";
	
	Запрос.УстановитьПараметр("Ключ",        "гф_ГлобальныеЗначенияОсновнойСклад");
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	ГлобальныеЗначенияОсновнойСклад = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Значение");
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Склады.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.гф_Организация = &Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	МассивСкладов    = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	Элементы.Склад.СписокВыбора.ЗагрузитьЗначения(МассивСкладов);
	
	// проставим первым по умолчанию
	Если ГлобальныеЗначенияОсновнойСклад.Количество() Тогда
		
		Объект.Склад = ГлобальныеЗначенияОсновнойСклад[0];
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивСсылокПТУ()
	
	Запрос       = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппыДоступаПартнеров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ГруппыДоступаПартнеров
	|ИЗ
	|	Справочник.ГруппыДоступаПартнеров КАК ГруппыДоступаПартнеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка КАК Ссылка,
	|	ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка.Партнер КАК Партнер,
	|	ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.ДополнительныеРеквизиты КАК ПриобретениеТоваровУслугДополнительныеРеквизиты
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтборПоОрганизации
	|				ТОГДА ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ПриобретениеТоваровУслугДополнительныеРеквизиты.Ссылка.Партнер.ГруппаДоступа В
	|			(ВЫБРАТЬ
	|				ГруппыДоступаПартнеров.Ссылка
	|			ИЗ
	|				ГруппыДоступаПартнеров КАК ГруппыДоступаПартнеров)
	|	И ПриобретениеТоваровУслугДополнительныеРеквизиты.Свойство.Имя = ""гф_ПТУПроверкаПТУ""
	|	И ПриобретениеТоваровУслугДополнительныеРеквизиты.Значение.Наименование = ""Загружен""";
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		
		ДатаОкончания = Объект.ДатаОкончания;
		
	Иначе
		
		ДатаОкончания = Дата(2999,12,31);
		
	КонецЕсли;	
	Запрос.УстановитьПараметр("ОтборПоОрганизации", ЗначениеЗаполнено(Объект.Организация));
	Запрос.УстановитьПараметр("Организация",        Объект.Организация);
	Запрос.УстановитьПараметр("ДатаНачала",         Объект.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",      ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти
