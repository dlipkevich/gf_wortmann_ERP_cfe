
&Вместо("ЗаписатьСостояниеОригиналаДокументаПоПечатнымФормам")
Процедура гф_ЗаписатьСостояниеОригиналаДокументаПоПечатнымФормам(Документ, ПечатнаяФорма, Представление, Состояние, Извне, Сотрудник)
	
	УстановитьПривилегированныйРежим(Истина);

	ВидДокумента = Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоРеквизиту("ПолноеИмя", 
						Документ.Метаданные().ПолноеИмя());
	
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
		
		Возврат;
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	гф_ОтслеживаемыеОригиналыПервичныхДокументов.ВидДокумента КАК ВидДокумента,
	|	гф_ОтслеживаемыеОригиналыПервичныхДокументов.ПервичныйДокумент КАК ПервичныйДокумент
	|ИЗ
	|	РегистрСведений.гф_ОтслеживаемыеОригиналыПервичныхДокументов КАК гф_ОтслеживаемыеОригиналыПервичныхДокументов
	|ГДЕ
	|	гф_ОтслеживаемыеОригиналыПервичныхДокументов.ВидДокумента = &ВидДокумента
	|	И гф_ОтслеживаемыеОригиналыПервичныхДокументов.ПервичныйДокумент = &ПервичныйДокумент";
	
	Запрос.Параметры.Вставить("ВидДокумента", ВидДокумента);
	Запрос.Параметры.Вставить("ПервичныйДокумент", ПечатнаяФорма);
	
	Если Запрос.Выполнить().Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;	
	
	ПродолжитьВызов(Документ, ПечатнаяФорма, Представление, Состояние, Извне, Сотрудник);
	
КонецПроцедуры
