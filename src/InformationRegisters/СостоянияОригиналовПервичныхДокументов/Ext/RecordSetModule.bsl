#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий


// #wortmann {
// #
// Галфинд Окунев 2022/08/15
// Записывает историю изменения состояний
//
// Параметры:
//
// Отказ		- Булево
// Замещение	- Булево
//
&После("ПередЗаписью")
Процедура гф_ПередЗаписью(Отказ, Замещение)   
	
	ТаблицаИзменений	= ЭтотОбъект.Выгрузить(,"Владелец, ПервичныйДокумент, " +  
										"ПервичныйДокументПредставление, Состояние, Сотрудник");
	МоментВремени		= ТекущаяДатаСеанса();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТ_Состояния.Владелец КАК Владелец,
	               |	ВТ_Состояния.ПервичныйДокумент КАК ПервичныйДокумент,
	               |	ВТ_Состояния.Состояние КАК Состояние,
	               |	ВТ_Состояния.Сотрудник КАК Сотрудник,
	               |	ВТ_Состояния.ПервичныйДокументПредставление КАК ПервичныйДокументПредставление
	               |ПОМЕСТИТЬ ВТ_Состояния
	               |ИЗ
	               |	&ТаблицаИзменений КАК ВТ_Состояния
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Состояния.Владелец КАК Владелец,
	               |	ВТ_Состояния.Состояние КАК Состояние,
	               |	ВТ_Состояния.ПервичныйДокумент КАК ПервичныйДокумент,
	               |	ВТ_Состояния.Сотрудник КАК Сотрудник,
	               |	&МоментВремени КАК Период,
	               |	ВТ_Состояния.ПервичныйДокументПредставление КАК ПервичныйДокументПредставление
	               |ИЗ
	               |	ВТ_Состояния КАК ВТ_Состояния
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гф_ИсторияСостоянийОригиналовПервичныхДокументов.СрезПоследних(
	               |				&МоментВремени,
	               |				Владелец В
	               |					(ВЫБРАТЬ
	               |						Т.Владелец
	               |					ИЗ
	               |						ВТ_Состояния КАК Т)) КАК гф_ИсторияСостоянийОригиналовПервичныхДокументовСрезПоследних
	               |		ПО ВТ_Состояния.Владелец = гф_ИсторияСостоянийОригиналовПервичныхДокументовСрезПоследних.Владелец
	               |			И ВТ_Состояния.Состояние = гф_ИсторияСостоянийОригиналовПервичныхДокументовСрезПоследних.Состояние
	               |			И ВТ_Состояния.ПервичныйДокумент = гф_ИсторияСостоянийОригиналовПервичныхДокументовСрезПоследних.ПервичныйДокумент
	               |ГДЕ
	               |	гф_ИсторияСостоянийОригиналовПервичныхДокументовСрезПоследних.Владелец ЕСТЬ NULL
	               |	И ВТ_Состояния.ПервичныйДокумент <> """"";
	
	Запрос.Параметры.Вставить("ТаблицаИзменений", ТаблицаИзменений);
	Запрос.Параметры.Вставить("МоментВремени", МоментВремени);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Запись = РегистрыСведений.гф_ИсторияСостоянийОригиналовПервичныхДокументов.СоздатьМенеджерЗаписи();
			
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			Запись.Активность	= Истина;   
			
			Запись.Записать(Истина);
			
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли