#Область ОбработчикиСобытийФормы

// #wortmann {
// #
// Галфинд Окунев 2022/08/14
// Устанавливает список выбора документов
//
// Параметры:
//
// Отказ				- Булево
// СтандартнаяОбработка	- Булево
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьСписокВыбораВидовДокументов();
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #
// Галфинд Окунев 2022/08/14
// Устанавливает список выбора печатных форм
//
// Параметры:
//
// Отказ				- Булево
//
&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	УстановитьСписокВыбораПечатныхФорм();
	
КонецПроцедуры// } #wortmann

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// #wortmann {
// #
// Галфинд Окунев 2022/08/14
// Устанавливает список выбора печатных форм
//
// Параметры:
//
// Элемент	- ПолеФормы
//
&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	УстановитьСписокВыбораПечатныхФорм();
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #
// Галфинд Окунев 2022/08/14
// Реквизит представлене первичного документа для показа в списке
//
// Параметры:
//
// Элемент - ЭлементФормы
//
&НаКлиенте
Процедура ПервичныйДокументПриИзменении(Элемент)
	
	Запись.ПредставлениеПервичногоДокумента = Элемент.ТекстРедактирования;
	
КонецПроцедуры// } #wortmann

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// #wortmann {
// #
// Галфинд Окунев 2022/08/14
// Устанавливает список выбора документов
//
&НаСервере
Процедура УстановитьСписокВыбораВидовДокументов()
	
	ДоступныеТипы = Метаданные.ОпределяемыеТипы.ОбъектСУчетомОригиналовПервичныхДокументов.Тип.Типы();
	
	ИменаДокументов = Новый Массив;
	
	Для Каждого Тип Из ДоступныеТипы Цикл
		
		ТипДокумента = Метаданные.НайтиПоТипу(Тип);
		
		ИменаДокументов.Добавить(ТипДокумента.ПолноеИмя());
		
	КонецЦикла;
	
	Запрос = Новый Запрос;   
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИдентификаторыОбъектовМетаданных.ПолноеИмя КАК ПолноеИмя,
	               |	ИдентификаторыОбъектовМетаданных.Синоним КАК Синоним,
	               |	ИдентификаторыОбъектовМетаданных.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	               |ГДЕ
	               |	ИдентификаторыОбъектовМетаданных.ПолноеИмя В(&ИменаДокументов)";
	
	Запрос.УстановитьПараметр("ИменаДокументов",ИменаДокументов);
	
	Выборка = Запрос.Выполнить().Выбрать();    
	
	Элементы.ВидДокумента.СписокВыбора.Очистить();
	
	Пока Выборка.Следующий() Цикл         
		
		Элементы.ВидДокумента.СписокВыбора.Добавить(Выборка.Ссылка, Выборка.Синоним);
		
	КонецЦикла;   
	
	Элементы.ВидДокумента.СписокВыбора.СортироватьПоПредставлению();
	
	Элементы.ВидДокумента.РежимВыбораИзСписка = Истина;
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #
// Галфинд Окунев 2022/08/14
// Устанавливает список выбора печатных форм
//
&НаСервере
Процедура УстановитьСписокВыбораПечатныхФорм() 
	
	Элементы.ПервичныйДокумент.СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Запись.ВидДокумента) Тогда        
		
		МетаданныеДокумента = Метаданные.Документы[Запись.ВидДокумента.Имя];
		
		КомандыПечати = УправлениеПечатью.КомандыПечатиОбъектаДоступныеДляВложений(МетаданныеДокумента);
		
		Для Каждого КомандаПечати Из КомандыПечати Цикл 
			
			Если Не ЗначениеЗаполнено(КомандаПечати.Обработчик) Тогда
				
				Элементы.ПервичныйДокумент.СписокВыбора.Добавить(КомандаПечати.Идентификатор, КомандаПечати.Представление);
				
			КонецЕсли;	
			
		КонецЦикла;	 
		
		Если Не ЗначениеЗаполнено(Запись.ПервичныйДокумент) И Элементы.ПервичныйДокумент.СписокВыбора.Количество() Тогда
			
			Запись.ПервичныйДокумент				= Элементы.ПервичныйДокумент.СписокВыбора[0].Значение;  
			Запись.ПредставлениеПервичногоДокумента	= Элементы.ПервичныйДокумент.СписокВыбора[0].Представление;
			
		КонецЕсли;	
		
	КонецЕсли;	   
	
	Элементы.ПервичныйДокумент.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры// } #wortmann

#КонецОбласти


