
&После("ПриЗаписи")
Процедура гф_ПриЗаписи(Отказ, Замещение)
	Если ЭтотОбъект.Количество() = 1 Тогда
		Если ТипЗнч(ЭтотОбъект.Отбор.Документ.Значение) = Тип("ДокументСсылка.МаркировкаТоваровИСМП") Тогда
			ЗаписьРегистра = ЭтотОбъект[0];
			Если ЗаписьРегистра.Документ.Операция = Перечисления.ВидыОперацийИСМП.Агрегация
				И ЗаписьРегистра.Статус = Перечисления.СтатусыОбработкиМаркировкиТоваровИСМП.КодыМаркировкиАгрегированы
				Тогда
				// Галфинд СадомцевСА 04.07.2023
				// Реализовал рассылку письма при записи статуса "Коды маркировки агрегированы" для документа Маркировка товаров ИС МП
				//e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee166b434dc8ce
				ДокРТУ = НайтиДокРТУ(ЗаписьРегистра.Документ);
				Если ЗначениеЗаполнено(ДокРТУ) Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Документы по отгрузке ""%1"", ""%2"" готовы к выгрузке'"), ДокРТУ.Контрагент, ДокРТУ.Номер);
					ОтправитьПисьмо(ДокРТУ, ТекстСообщения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОтправитьПисьмо(ДокРТУ, ТекстПисьма)
	
	УчетнаяЗаписьПочты	= Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;
	ПрофильПочты		= РаботаСПочтовымиСообщениямиСлужебный.ИнтернетПочтовыйПрофиль(УчетнаяЗаписьПочты);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаКонтактнойИнформации.АдресЭП КАК АдресЭП
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ТаблицаКонтактнойИнформации
	|ГДЕ
	|	ТаблицаКонтактнойИнформации.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокРТУ.Автор);
	
	ВыборкаДанные = Запрос.Выполнить().Выгрузить();
	
	Если ВыборкаДанные.Количество() > 0 Тогда
		EmailПользователя = Строка(ВыборкаДанные[0].АдресЭП);
	Иначе
		ТекстСообщения = НСтр("ru = 'У пользователя не заполнен почтовый адрес, невозможно отправить сообщение об изменении статуса.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СообщениеПочты = Новый ИнтернетПочтовоеСообщение;
	СообщениеПочты.Кодировка = "utf-8";
	СообщениеПочты.Тема = "Проведена Отгрузка.";
	СообщениеПочты.Отправитель = Строка(УчетнаяЗаписьПочты.АдресЭлектроннойПочты);
	СообщениеПочты.ИмяОтправителя = "Рассылка 1С";
	СообщениеПочты.Получатели.Добавить(EmailПользователя);
	
	ТекстСообщения = 
	"<style>
	| body {font-family: Verdana, sans-serif;}
	|</style>
	|<h5 style=""padding: 0 0 15px 0; margin: 0 0 0 0;"">Добрый день!</h5>
	|<h5 style=""padding: 0 0 15px 0; margin: 0 0 0 0;"">%Содержимое%</h5>";
	
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Содержимое%", ТекстПисьма);
	
	ТекстСообщенияПочты = СообщениеПочты.Тексты.Добавить(ТекстСообщения, ТипТекстаПочтовогоСообщения.HTML);
	
	СообщениеПочты.Тема = "Создана «Планируемая отгрузка» клиенту " + ДокРТУ.Контрагент.Наименование + ".";
		
	ИнтернетПочта = Новый ИнтернетПочта;
	
	Попытка
		ИнтернетПочта.Подключиться(ПрофильПочты);
		ИнтернетПочта.Послать(СообщениеПочты);
		ИнтернетПочта.Отключиться();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

Функция НайтиДокРТУ(МаркировкаТоваровИСМП)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.гф_МаркировкаТоваровИСМП = &гф_МаркировкаТоваровИСМП
	               |	И РеализацияТоваровУслуг.Проведен";
	Запрос.УстановитьПараметр("гф_МаркировкаТоваровИСМП", МаркировкаТоваровИСМП);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции
