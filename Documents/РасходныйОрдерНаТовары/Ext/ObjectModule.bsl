#Если НЕ МобильныйАвтономныйСервер Тогда
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// #wortmann {
// #Монитор логиста
// Запись истории статусов ордеров
// Галфинд Окунев 2022/09/13
&После("ПриЗаписи")
Процедура гф_ПриЗаписи(Отказ)
	
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	гф_ИсторияСтатусовРасходныхОрдеровСрезПоследних.Статус КАК Статус
	               |ИЗ
	               |	РегистрСведений.гф_ИсторияСтатусовРасходныхОрдеров.СрезПоследних(, Объект = &Ссылка) КАК гф_ИсторияСтатусовРасходныхОрдеровСрезПоследних
	               |ГДЕ
	               |	гф_ИсторияСтатусовРасходныхОрдеровСрезПоследних.Статус = &Статус";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Запрос.Параметры.Вставить("Статус", Статус);
	
	// vvv Галфинд \ Sakovich 04.05.2023
	УстановитьПривилегированныйРежим(Истина);
	// ^^^ Галфинд \ Sakovich 04.05.2023
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		
		Запись = РегистрыСведений.гф_ИсторияСтатусовРасходныхОрдеров.СоздатьМенеджерЗаписи();
		
		Запись.Период			= ТекущаяДатаСеанса();
		Запись.Активность		= Истина;
		Запись.Объект			= Ссылка;
		Запись.Статус			= Статус;
		Запись.СтатусИзменил	= Пользователи.ТекущийПользователь();
		
		Запись.Записать();
		
	КонецЕсли;	
	
КонецПроцедуры // } #wortmann

// #wortmann {
// Заполнение тч ОтгружаемыеТовары из распоряжений на перемещение
// Галфинд Sakovich 2022/10/22
&После("ПередЗаписью")
Процедура гф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		гф_ЗаполнитьОтгружаемыеТовары();	
	КонецЕсли;
	
КонецПроцедуры // } #wortmann

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура гф_ЗаполнитьОтгружаемыеТовары()

	ЭтоКоробочныйСклад = УправлениеСвойствами.ЗначениеСвойства(Склад, "гф_СкладыТоварыВКоробах") = Истина;
	Если ЭтоКоробочныйСклад Тогда
		ЗаполнитьОтгружаемыеТоварыПоТоварамВКоробахПеремещения();
	Иначе
		ЗаполнитьОтгружаемыеТоварыПоТоварамПеремещения();
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьОтгружаемыеТоварыПоТоварамВКоробахПеремещения()
	
	Распоряжения = ТоварыПоРаспоряжениям.Выгрузить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Распоряжения.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ Распоряжения
	|ИЗ
	|	&Распоряжения КАК Распоряжения
	|ГДЕ 
	|	ВЫРАЗИТЬ(Распоряжения.Распоряжение КАК Документ.ПеремещениеТоваров) ЕСТЬ НЕ NULL
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеТоваровгф_ТоварыВКоробах.УпаковочныйЛист КАК УпаковочныйЛистРодитель,
	|	УпаковочныйЛистТовары.Номенклатура КАК Номенклатура,
	|	УпаковочныйЛистТовары.Характеристика КАК Характеристика,
	|	УпаковочныйЛистТовары.Упаковка КАК Упаковка,
	|	УпаковочныйЛистТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	УпаковочныйЛистТовары.Количество КАК Количество,
	|	УпаковочныйЛистТовары.ЭтоУпаковочныйЛист КАК ЭтоУпаковочныйЛист,
	|	УпаковочныйЛистТовары.Назначение КАК Назначение
	|ИЗ
	|	Распоряжения КАК Распоряжения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.гф_ТоварыВКоробах КАК ПеремещениеТоваровгф_ТоварыВКоробах
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.УпаковочныйЛист.Товары КАК УпаковочныйЛистТовары
	|			ПО (ПеремещениеТоваровгф_ТоварыВКоробах.УпаковочныйЛист = УпаковочныйЛистТовары.Ссылка)
	|		ПО (Распоряжения.Распоряжение = ПеремещениеТоваровгф_ТоварыВКоробах.Ссылка)
	|ГДЕ
	|	НЕ УпаковочныйЛистТовары.ЭтоУпаковочныйЛист
	|ИТОГИ ПО
	|	УпаковочныйЛистРодитель");
	
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	РезультатПоУпаковочнымЛистам = Запрос.Выполнить();
	
	ДействиеСоСтроками = ?(
	Статус = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке ИЛИ
	Статус = Перечисления.СтатусыРасходныхОрдеров.Отгружен, 
	Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить, 
	Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать);
	
	Если Не РезультатПоУпаковочнымЛистам.Пустой() Тогда
		ОтгружаемыеТовары.Очистить();
		ВыборкаИтогиПоУпЛист = РезультатПоУпаковочнымЛистам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИтогиПоУпЛист.Следующий() Цикл
			нс = ОтгружаемыеТовары.Добавить();
			нс["УпаковочныйЛист"] = ВыборкаИтогиПоУпЛист.УпаковочныйЛистРодитель;
			нс["ЭтоУпаковочныйЛист"] = Истина;
			нс["Количество"] = 1;
			нс["КоличествоУпаковок"] = 1;
			ВыборкаТовары = ВыборкаИтогиПоУпЛист.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл
				нс = ОтгружаемыеТовары.Добавить();
				ЗаполнитьЗначенияСвойств(нс, ВыборкаТовары);
				нс["Действие"] = ДействиеСоСтроками;
			КонецЦикла;
		КонецЦикла;
		Документы.РасходныйОрдерНаТовары.ИзменитьТоварыПоРаспоряжениямПоОтгружаемымТоварам(ЭтотОбъект, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОтгружаемыеТоварыПоТоварамПеремещения()
	
	Распоряжения = ТоварыПоРаспоряжениям.Выгрузить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Распоряжения.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ Распоряжения
	|ИЗ
	|	&Распоряжения КАК Распоряжения
	|ГДЕ 
	|	ВЫРАЗИТЬ(Распоряжения.Распоряжение КАК Документ.ПеремещениеТоваров) ЕСТЬ НЕ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.УпаковочныйЛист.ПустаяСсылка) КАК УпаковочныйЛистРодитель,
	|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПеремещениеТоваровТовары.Характеристика КАК Характеристика,
	|	ПеремещениеТоваровТовары.Упаковка КАК Упаковка,
	|	СУММА(ПеремещениеТоваровТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ПеремещениеТоваровТовары.Количество) КАК Количество,
	|	ПеремещениеТоваровТовары.Назначение КАК Назначение,
	|	ЛОЖЬ КАК ЭтоУпаковочныйЛист,
	|	Значение(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать) КАК Действие
	|ИЗ
	|	Распоряжения КАК Распоряжения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|		ПО Распоряжения.Распоряжение = ПеремещениеТоваровТовары.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеТоваровТовары.Номенклатура,
	|	ПеремещениеТоваровТовары.Характеристика,
	|	ПеремещениеТоваровТовары.Упаковка,
	|	ПеремещениеТоваровТовары.Назначение");
	
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ОтгружаемыеТовары.Очистить();
		ВыборкаТовары = Результат.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нс = ОтгружаемыеТовары.Добавить();
			ЗаполнитьЗначенияСвойств(нс, ВыборкаТовары);
		КонецЦикла;
		Документы.РасходныйОрдерНаТовары.ИзменитьТоварыПоРаспоряжениямПоОтгружаемымТоварам(ЭтотОбъект, Истина);
	КонецЕсли;
	
КонецПроцедуры

&После("ИнициализироватьДокумент")
Процедура гф_ИнициализироватьДокумент(ДанныеЗаполнения)
	
	// #wortmann {
	// #Автоматическое заполнение табличной части документа Расходный ордер 
	// Изменяем статус (КОтбору) у автоматически созданного обработкой по отгрузке документа Расходный ордер
	// Галфинд Volkov 2023/01/26
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыРасходныхОрдеров", Новый Структура("Склад", Склад)) Тогда
		ОбъектМетаданныхСтатус = Метаданные().Реквизиты.Статус; // ОбъектМетаданныхРеквизит
		Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
	Иначе
		Статус = Перечисления.СтатусыРасходныхОрдеров.Отгружен;
	КонецЕсли;
	// } #wortmann
	
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура гф_ОбработкаПроведения(Отказ, РежимПроведения)
	
	#Вставка
	//++Галфинд ЕсиповАВ 24.03.2023
	Если ЭтотОбъект.Статус <> Перечисления.СтатусыРасходныхОрдеров.КОтгрузке Тогда
	//Если Отказ Тогда
		Сообщить("Документы готовы к выгрузке по ЭДО");
	КонецЕсли;
	//--Галфинд ЕсиповАВ 24.03.2023
	#КонецВставки
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
#КонецЕсли