#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыИФункцииПечатиФормы

&Перед("ДобавитьКомандыПечати")
Процедура гф_ДобавитьКомандыПечати(КомандыПечати)
	// ++ Галфинд СадомцевСА 01.02.2024 Добавил печ. форму Счет на оплату с факсимиле (подписи)
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eec03fe158164d
	ЭтоПартнер = ПраваПользователяПовтИсп.ЭтоПартнер();
	Если НЕ ЭтоПартнер
		 И ПолучитьФункциональнуюОпцию("НеВыбиратьВариантВыводаСкидокПриПечатиДокументовПродажи") Тогда
		// Счет на оплату
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьСчетовНаОплату";
		КомандаПечати.Идентификатор = "СчетНаОплату";
		КомандаПечати.Представление = НСтр("ru = 'Счет на оплату с факсимиле (подписи)';
											|en = 'Commercial invoice with facsimile'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ОтображатьФаксимиле", Истина);
		
		КомандаПечати.ДополнительныеПараметры.Вставить("ОтображатьПодписи", Истина);
	КонецЕсли;
	// ++ Галфинд СадомцевСА 01.02.2024
КонецПроцедуры

&После("ДобавитьКомандыПечати")
Процедура гф_ДобавитьКомандыПечати1(КомандыПечати)
	// vvv Галфинд \ Sakovich 21.03.2024
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eee694baca1d51
	// удаление типовой команды "Счет на оплату с факсимиле"
	ЭтоПартнер = ПраваПользователяПовтИсп.ЭтоПартнер();
	Если НЕ ЭтоПартнер
		И ПолучитьФункциональнуюОпцию("НеВыбиратьВариантВыводаСкидокПриПечатиДокументовПродажи") Тогда
		мУдаляемых = Новый Массив;
		Для каждого Команда Из КомандыПечати Цикл
			Если Найти(Команда.Представление, "Счет на оплату с факсимиле") > 0 
				И Не Команда.ДополнительныеПараметры.Свойство("ОтображатьПодписи") Тогда
				мУдаляемых.Добавить(Команда);
			КонецЕсли;
		КонецЦикла;
		Для каждого Эл Из мУдаляемых Цикл
			КомандыПечати.Удалить(Эл);
		КонецЦикла;
	КонецЕсли;
	// ^^^ Галфинд \ Sakovich 21.03.2024
КонецПроцедуры

#КонецОбласти

#КонецЕсли