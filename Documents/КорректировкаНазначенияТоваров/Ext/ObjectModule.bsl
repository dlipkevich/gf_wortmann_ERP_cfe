
&Перед("ПередЗаписью")
Процедура гф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	// #wortmann {
	// #5.2.08
	// ID запрет ручного изменения документа, при наличии свойства "ДокументОснование"
	// Галфинд Volkov 2022/08/23
	Если Не ЭтотОбъект.ДополнительныеСвойства.Свойство("ДокументОснование") И ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование) Тогда
		Отказ = Истина;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Данный документ не доступен для ручного изменения. Введен на основании %1.'"), ЭтотОбъект.ДокументОснование);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект.ДокументОснование, , , Отказ);
	КонецЕсли;
	// } #wortmann
КонецПроцедуры

&Перед("ОбработкаПроверкиЗаполнения")
Процедура гф_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// #wortmann {
	// #5.2.08
	// отказ проведения, если Склад имеет дополнительный реквизит "гф_ТоварыВКоробах" со значением Истина
	// и не заполнено поле IDКороба
	// Галфинд Sakovich 2022/09/20
	Если (ВидОперации = Перечисления.ВидыОперацийКорректировкиНазначения.СнятьРезерв
		ИЛИ ВидОперации = Перечисления.ВидыОперацийКорректировкиНазначения.СнятьРезервПоМногимНазначениям
		ИЛИ ВидОперации =  Перечисления.ВидыОперацийКорректировкиНазначения.Резервировать
		ИЛИ ВидОперации = Перечисления.ВидыОперацийКорректировкиНазначения.РезервироватьИКорректировать) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Товары.НомерСтроки,
		|	Товары.Склад,
		|	Товары.гф_IDкороба
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СкладыДополнительныеРеквизиты.Ссылка КАК Склад
		|ПОМЕСТИТЬ СкладыСТоварамиВКоробах
		|ИЗ
		|	Справочник.Склады.ДополнительныеРеквизиты КАК СкладыДополнительныеРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|		ПО (СкладыДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка)
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Имя = ""гф_СкладыТоварыВКоробах"" 
		|	И  СкладыДополнительныеРеквизиты.Значение  = ИСТИНА
		|;
		|/////////////////////////
		|ВЫБРАТЬ 
		|	Товары.Склад,
		|	Товары.гф_IDкороба,
		|	Товары.НомерСтроки
		|ИЗ
		|	Товары КАК Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СкладыСТоварамиВКоробах КАК Склады
		|		ПО Товары.Склад = Склады.Склад
		|ГДЕ
		|	Товары.гф_IDкороба = Значение(Документ.УпаковочныйЛист.ПустаяСсылка)";
		Запрос.УстановитьПараметр("Товары", Товары.Выгрузить(,"НомерСтроки, Склад, гф_IDкороба"));
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			ШаблонСообщения = НСтр("ru = 'Не заполнено значение в поле ""ID короба"" в строке %НомерСтроки%.'");
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%НомерСтроки%", Выборка.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , "Объект", Отказ)
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	// } #wortmann

КонецПроцедуры
