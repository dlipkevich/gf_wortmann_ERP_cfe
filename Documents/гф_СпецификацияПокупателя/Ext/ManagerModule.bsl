#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс      

// #wortmann {
// #Отметка оригиналов документов
// Галфинд Окунев 2022/08/09
// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры// } #wortmann    

// #wortmann {
// #Отметка оригиналов документов
// Галфинд Окунев 2022/08/09
// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов - таблиц значений - данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда   
		
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
		
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции// } #wortmann    

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Заполняет список команд печати.
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	
	КомандаПечати.МенеджерПечати					= "Документ.гф_СпецификацияПокупателя";
	КомандаПечати.Идентификатор						= "СпецификацияПокупателя";
	КомандаПечати.Представление						= "Спецификация покупателя";
	КомандаПечати.ПроверкаПроведенияПередПечатью	= Ложь;
	
	КомандаПечати = КомандыПечати.Добавить();
	
	КомандаПечати.МенеджерПечати					= "Документ.гф_СпецификацияПокупателя";
	КомандаПечати.Идентификатор						= "СпецификацияПоНовойЦене";
	КомандаПечати.Представление						= "Спецификация покупателя на остаток по новой цене";
	КомандаПечати.ПроверкаПроведенияПередПечатью	= Ложь;   
	КомандаПечати.Обработчик						= "гф_МенеджерПечатиКлиент.ПараметрыСпецификацииПоНовойЦене";
	
КонецПроцедуры// } #wortmann

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Формирует печатные формы объектов
//
// Параметры:
//
//   ИменаМакетов    		- Строка
//   МассивОбъектов  		- Массив
//   ПараметрыПечати 		- Структура
//   КоллекцияПечатныхФорм	- Таблица значений
//   ОбъектыПечати			- Список значений
//   ПараметрыВывода       	- Структура
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СпецификацияПокупателя") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"СпецификацияПокупателя",
		"Спецификация покупателя",
		СформироватьСпецификация(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СпецификацияПоНовойЦене") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"СпецификацияПоНовойЦене",
		"Спецификация покупателя на остаток по новой цене",
		СформироватьСпецификация(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Создает табличный документ ПФ спецификации
//
// Параметры:
//
//   МассивОбъектов  		- Массив
//   ОбъектыПечати			- Список значений
//   ПараметрыПечати 		- Структура
//
Функция СформироватьСпецификация(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ПроблемыСПринтером = Ложь;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;  
	
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_СПЕЦИФИКАЦИЯПОКУПАТЕЛЯ";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Макет = Документы.гф_СпецификацияПокупателя.ПолучитьМакет("СпецификацияПокупателя");
	
	ПервыйДокумент = Истина;
	
	Для Каждого СсылкаНаДокумент Из МассивОбъектов Цикл
		
		Если ПервыйДокумент Тогда
			
			ПервыйДокумент = Ложь;
			
		Иначе	
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");  
		ОбластьПодвал		= Макет.ПолучитьОбласть("Подвал");  
		
		ПараметрыСпецификацииПоНовойЦене = Неопределено;
		
		ПараметрыПечати.Свойство("гф_Параметры", ПараметрыСпецификацииПоНовойЦене);
			
		ДанныеДляПечати = ДанныеДляПечатиСпецификации(СсылкаНаДокумент,
						ПараметрыСпецификацииПоНовойЦене);
		
		ФормированиеПечатныхФорм.ВывестиЛоготипВТабличныйДокумент(Макет, ОбластьЗаголовок, "ОбластьЗаголовок", ДанныеДляПечати.ДанныеШапки.Организация);
		
		СообщитьОНесколькихЦенах(ДанныеДляПечати.НесколькоЦен);
		
		ЗаполнитьЗначенияСвойств(ОбластьЗаголовок.Параметры, ДанныеДляПечати.ДанныеШапки);
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);  
		
		ПервичныеДокументыВКоробах = ДанныеДляПечати.ДанныеШапки.ПервичныеДокументыВКоробах;
		
		Подвал = Новый ТабличныйДокумент();
		
		Если ПервичныеДокументыВКоробах Тогда
			
			ПостфиксИмениМакета = "ВКоробах";
			
		Иначе
			
			ПостфиксИмениМакета = "";        
			
		КонецЕсли;	
			
		ОбластьИтоги = Макет.ПолучитьОбласть("Итого" + ПостфиксИмениМакета);
		
		ЗаполнитьЗначенияСвойств(ОбластьИтоги.Параметры, ДанныеДляПечати.ДанныеШапки);
		
		Подвал.Вывести(ОбластьИтоги);
		
		ЗаполнитьЗначенияСвойств(ОбластьПодвал.Параметры, ДанныеДляПечати.ДанныеШапки);
		
		Подвал.Вывести(ОбластьПодвал);
		
		ОбластьШапка		= Макет.ПолучитьОбласть("Шапка" + ПостфиксИмениМакета);  
		
		ТабличныйДокумент.Вывести(ОбластьШапка);   
		
		НомерПоследнейСтроки = ДанныеДляПечати.ДанныеТабличнойЧасти.Количество();
		НомерСтроки			 = 0;
		НомерСтрокиПП		 = 0;
		
		Для Каждого СтрокаДокумента ИЗ ДанныеДляПечати.ДанныеТабличнойЧасти Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаДокумента.ЗаказПредставление) Тогда
				
				Продолжить;
				
			ИначеЕсли Не ЗначениеЗаполнено(СтрокаДокумента.НоменклатураПредставление) Тогда
				
				ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаЗаказ" + ПостфиксИмениМакета);
				
			Иначе	
				
				ОбластьСтрока = Макет.ПолучитьОбласть("Строка" + ПостфиксИмениМакета);
				
				НомерСтрокиПП = НомерСтрокиПП + 1;
				
				ОбластьСтрока.Параметры.НомерСтрокиПП = НомерСтрокиПП;
				
			КонецЕсли;	
			
			НомерСтроки = НомерСтроки + 1;
			
			ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, СтрокаДокумента);  
			
			МассивПроверяемыхТаблиц = Новый Массив;         
			
			МассивПроверяемыхТаблиц.Добавить(ОбластьСтрока);
			
			Если НомерСтроки = НомерПоследнейСтроки Тогда
				
				МассивПроверяемыхТаблиц.Добавить(Подвал);
				
			КонецЕсли;                    
			
			Попытка
				
				Если Не ТабличныйДокумент.ПроверитьВывод(МассивПроверяемыхТаблиц) Тогда
					
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					
					ТабличныйДокумент.Вывести(ОбластьШапка);   
					
				КонецЕсли;
				
			Исключение   
				
				ПроблемыСПринтером = Истина;
				
			КонецПопытки;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;	
		
		ТабличныйДокумент.Вывести(Подвал);  
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
		ТабличныйДокумент,
		НомерСтрокиНачало,
		ОбъектыПечати,
		СсылкаНаДокумент);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);     
	
	Если ПроблемыСПринтером Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось определить высоту страницы. Возможно в системе не установлен принтер.");
		
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции// } #wortmann     

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Собщает пользователю о дублях цен
//
// Параметры:
//
//   НесколькоЦен	- Выборка из результата запроса
//
Процедура СообщитьОНесколькихЦенах(НесколькоЦен)
	
	Пока НесколькоЦен.Следующий() Цикл
		
		СтрокаСообщения = "На артикул " + НесколькоЦен.Артикул + " в документах";
		
		ВыборкаЗаказов = НесколькоЦен.Выбрать();
		
		Первый = Истина;
		
		Пока ВыборкаЗаказов.Следующий() Цикл
			
			СтрокаСообщения = СтрокаСообщения + ?(Первый, " ", ", ") + ВыборкаЗаказов.Заказ;
			
			Первый = Ложь;
			
		КонецЦикла;	
		
		СтрокаСообщения = СтрокаСообщения + " обнаружены разные цены.";
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;	
	
КонецПроцедуры// } #wortmann		

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Собирает данные для печати спецификации
//
// Параметры:
//
//   СсылкаНаДокумент					- ДокументСсылка.гф_СпецификацияПокупателя
//   ПараметрыСпецификацииПоНовойЦене	- Структура
//
Функция ДанныеДляПечатиСпецификации(СсылкаНаДокумент, ПараметрыСпецификацииПоНовойЦене = Неопределено)
	
	ПервичныеДокументыВКоробах = УправлениеСвойствами.ЗначениеСвойства(СсылкаНаДокумент.Договор, 
										"гф_ДоговорыКонтрагентовПервичныеДокументыВКоробах");
	Если ПервичныеДокументыВКоробах = Неопределено Тогда
		
		ПервичныеДокументыВКоробах = Ложь;
		
	КонецЕсли;	
	
	НаОстатокПоНовойЦене = ЗначениеЗаполнено(ПараметрыСпецификацииПоНовойЦене);
	
	РозничныеЦены = ПланыВидовХарактеристик.гф_ГлобальныеЗначения.НайтиПоРеквизиту("Ключ",
																			"гф_ГлобальныеЗначенияРозничнаяЦена");
	
	КодУпаковки = "778";
	
	ЕдиницаИзмеренияУпаковка = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(КодУпаковки, , ,
	ПредопределенноеЗначение("Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения"));
	
	Запрос = Новый Запрос();   
	
	Запрос.Текст = ТекстЗапросаДанныхПечатнойФормыСпецификации(ПервичныеДокументыВКоробах, НаОстатокПоНовойЦене);
	
	Запрос.Параметры.Вставить("НаОстатокПоНовойЦене", Ложь);   
	
	Запрос.Параметры.Вставить("СсылкаНаДокумент",			СсылкаНаДокумент);   
	Запрос.Параметры.Вставить("МоментВремени",				СсылкаНаДокумент.МоментВремени());   
	Запрос.Параметры.Вставить("ТекущаяДата",				ТекущаяДатаСеанса()); 
	Запрос.Параметры.Вставить("ПервичныеДокументыВКоробах",	ПервичныеДокументыВКоробах);     
	Запрос.Параметры.Вставить("Упаковка",					ЕдиницаИзмеренияУпаковка);   
	Запрос.Параметры.Вставить("РозничныеЦены",				РозничныеЦены.Значение);     
	Запрос.Параметры.Вставить("НаОстатокПоНовойЦене",		НаОстатокПоНовойЦене);   
	
	Если НаОстатокПоНовойЦене Тогда
		
		Запрос.Параметры.Вставить("ДатаОстатков",			ПараметрыСпецификацииПоНовойЦене.ДатаОстатков);   
		Запрос.Параметры.Вставить("ДатаЦен",				?(ПараметрыСпецификацииПоНовойЦене.ПредоплатныйКлиент,
		ПараметрыСпецификацииПоНовойЦене.ДатаЦен, 
		ТекущаяДатаСеанса()));   
		Запрос.Параметры.Вставить("ПрайсЛист",				ПараметрыСпецификацииПоНовойЦене.ПрайсЛист);   
		Запрос.Параметры.Вставить("СкидкаКлиента",			ПараметрыСпецификацииПоНовойЦене.СкидкаКлиента);   
		
	КонецЕсли;	
	
	Результаты = Запрос.ВыполнитьПакет();
	
	РазмерМассиваРезультатов = Результаты.Количество();
	
	СмещениеДублейЦен		= 3;
	СмещениеШапки			= 2;
	СмещениеТабличнойЧасти	= 1;
	
	ИндексДублейЦен			= РазмерМассиваРезультатов - СмещениеДублейЦен;
	ИндексШапки				= РазмерМассиваРезультатов - СмещениеШапки;
	ИндексТабличнойЧасти	= РазмерМассиваРезультатов - СмещениеТабличнойЧасти;
	
	ДанныеШапки = Новый Структура;
	
	ТаблицаШапки = Результаты[ИндексШапки].Выгрузить();
	
	СтрокаШапки = ТаблицаШапки[0];
	
	Для Каждого Колонка Из ТаблицаШапки.Колонки Цикл 
		
		ДанныеШапки.Вставить(Колонка.Имя,СтрокаШапки[Колонка.Имя]);
		
	КонецЦикла;	   
	
	ДанныеШапки.Вставить("ЗаголовокСпецификации", "Спецификация № " + ДанныеШапки.Номер + " от " + Формат(ДанныеШапки.Дата,"ДФ=dd.MM.yyyy"));
	ДанныеШапки.Вставить("СуммаПрописью", ЧислоПрописью(ДанныеШапки.СуммаСНДС,"Л=ru_RU; ДП=Ложь","рубль, рубля, рублей, м, копейка, копейки, копеек, ж"));
	
	СведенияОПоставщике = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеШапки.Организация, ДанныеШапки.Дата);
	
	ДанныеШапки.Вставить("ДатаДоговораСтрока", Формат(ДанныеШапки.ДатаДоговора, "ДЛФ=DD"));
	ДанныеШапки.Вставить("Банк");
	ДанныеШапки.Вставить("БИК");
	ДанныеШапки.Вставить("КоррСчет");
	ДанныеШапки.Вставить("НомерСчета");
	ДанныеШапки.Вставить("ИНН");
	ДанныеШапки.Вставить("КПП");
	ДанныеШапки.Вставить("НаименованиеДляПечатныхФорм");
	
	ЗаполнитьЗначенияСвойств(ДанныеШапки, СведенияОПоставщике);  
	
	
	ДанныеШапки.Вставить("ПредставлениеПоставщика", 
	ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеШапки.Организация, ДанныеШапки.Дата),
	"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
	
	ДанныеШапки.Вставить("ПредставлениеПолучателя", 
	ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеШапки.Контрагент, ДанныеШапки.Дата),
	"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
	
	ДанныеШапки.Вставить("ПредставлениеГрузоотправителя", 
	ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеШапки.Организация, ДанныеШапки.Дата),
	"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
	
	ДанныеШапки.Вставить("ПредставлениеГрузополучателя", 
	ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеШапки.Контрагент, ДанныеШапки.Дата),
	"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
	
	ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(ДанныеШапки.Организация);
	
	ДанныеШапки.Вставить("ФИОРуководителя", ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ОтветственныеЛица.Руководитель));
	ДанныеШапки.Вставить("РуководительДолжность", ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ОтветственныеЛица.РуководительДолжность));
	ДанныеШапки.Вставить("ФИОБухгалтера", ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ОтветственныеЛица.ГлавныйБухгалтер));
	
	
	НесколькоЦен			= Результаты[ИндексДублейЦен].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДанныеТабличнойЧасти	= Результаты[ИндексТабличнойЧасти].Выгрузить();
	
	Для Каждого СтрокаТовара Из ДанныеТабличнойЧасти Цикл   
		
		Если СтрокаТовара.Номенклатура = Null Тогда
			
			СтрокаТовара.ЗаказПредставление = СтрокаТовара.ЗаказНомер + " от " + Формат(СтрокаТовара.ЗаказДата, "ДФ=dd.MM.yyyy"); 
			
			Продолжить;
			
		КонецЕсли;	     
		
		ПредставлениеИнтервала = ПредставлениеИнтервала(СтрокаТовара.ДатаОтгрузки);
		
		СтрокаТовара.НоменклатураПредставление = СтрокаТовара.НоменклатураПредставление + ПредставлениеИнтервала;
		
	КонецЦикла;	
	
	Результат = Новый Структура("ДанныеШапки, ДанныеТабличнойЧасти, НесколькоЦен", ДанныеШапки, ДанныеТабличнойЧасти, НесколькоЦен);
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

// #Спецификация покупателя
// Галфинд Окунев 2022/09/07
// Представление интервала доствки для печати 
//
// Параметры:
//
// ДатаОтгрузки - Дата
//
&НаСервере
Функция ПредставлениеИнтервала(ДатаОтгрузки);
	
	ДнейВВисокосномГоду			= 366;    
	НомерДня28Февраля			= 59;
	НачалоВторойПоловиныМесяца	= 15;
	
	Если ДеньГода(КонецГода(ДатаОтгрузки)) = ДнейВВисокосномГоду И ДеньГода(ДатаОтгрузки) = НомерДня28Февраля Тогда
		
		ДатаОтгрузки = НачалоДня(КонецМесяца(ДатаОтгрузки));   
		
	Иначе	
		
		ДатаОтгрузки = НачалоДня(ДатаОтгрузки);   
		
	КонецЕсли;	
	
	Если ДатаОтгрузки = НачалоДня(КонецМесяца(ДатаОтгрузки)) Тогда
		
		ПредставлениеИнтервала = " (" + Формат(
		Дата(Год(ДатаОтгрузки), Месяц(ДатаОтгрузки), НачалоВторойПоловиныМесяца),
		"ДФ=dd.MM.yyyy")
		+ " - "	+ Формат(ДатаОтгрузки, "ДФ=dd.MM.yyyy")	+ ")";
		
	ИначеЕсли День(ДатаОтгрузки) < НачалоВторойПоловиныМесяца Тогда
		
		ПредставлениеИнтервала = " (" + Формат(
		НачалоМесяца(ДатаОтгрузки), 
		"ДФ=dd.MM.yyyy")
		+ " - "	+ Формат(ДатаОтгрузки, "ДФ=dd.MM.yyyy")	+ ")";  
		
	Иначе
		
		ПредставлениеИнтервала = "";
		
	КонецЕсли;	 
	
	Возврат ПредставлениеИнтервала;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса по шапке документа
//
Функция ТекстЗапросаДанныхПечатнойФормыСпецификации(ПервичныеДокументыВКоробах, НаОстатокПоНовойЦене)
	
	Результат = ТекстЗапроса_ВТШапка()
		+ ОбщегоНазначения.РазделительПакетаЗапросов() 
		+ ?(НаОстатокПоНовойЦене, ТекстЗапроса_ВТ_ПозицииНоменклатурыНаОстатокПоНовойЦене(), 
									ТекстЗапроса_ВТ_ПозицииНоменклатуры())
		+ ОбщегоНазначения.РазделительПакетаЗапросов() 
		+ ?(ПервичныеДокументыВКоробах, ТекстЗапросаВТ_НоменклатураВКоробах(), 
									ТекстЗапросаВТ_Номенклатура())
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		+ ТекстЗапросаДублиИШапка()
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		+ ?(ПервичныеДокументыВКоробах, ТекстЗапросаИтогиВКоробах(), 
									ТекстЗапросаИтоги());
	
	
	Возврат Результат;
	
КонецФункции// } #wortmann	


// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса по шапке документа
//
Функция ТекстЗапроса_ВТШапка()
	
	Результат = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	&ТекущаяДата КАК ТекущаяДата,
	|	гф_СпецификацияПокупателя.Номер КАК Номер,
	|	гф_СпецификацияПокупателя.Дата КАК Дата,
	|	гф_СпецификацияПокупателя.Организация КАК Организация,
	|	гф_СпецификацияПокупателя.Контрагент КАК Контрагент,
	|	гф_СпецификацияПокупателя.Договор КАК Договор,
	|	гф_СпецификацияПокупателя.Ответственный КАК Ответственный,
	|	ВЫРАЗИТЬ(гф_СпецификацияПокупателя.Комментарий КАК СТРОКА(100)) КАК Комментарий,
	|	Организации.Представление КАК ОрганизацияПредставление,
	|	Контрагенты.Представление КАК КонтрагентПредставление,
	|	ДоговорыКонтрагентов.Дата КАК ДатаДоговора,
	|	ДоговорыКонтрагентов.Номер КАК НомерДоговора,
	|	ДоговорыКонтрагентов.Представление КАК ДоговорПредставление,
	|	ВидыЦен.Ссылка КАК ВидЦен,
	|	гф_СпецификацияПокупателя.Ссылка КАК Спецификация,
	|	&ПервичныеДокументыВКоробах КАК ПервичныеДокументыВКоробах
	|ПОМЕСТИТЬ ВТ_Шапка
	|ИЗ
	|	Документ.гф_СпецификацияПокупателя КАК гф_СпецификацияПокупателя
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
	|			ПО ДоговорыКонтрагентов.ВидЦенПродажи = ВидыЦен.Ссылка
	|		ПО гф_СпецификацияПокупателя.Договор = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО гф_СпецификацияПокупателя.Контрагент = Контрагенты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО гф_СпецификацияПокупателя.Организация = Организации.Ссылка
	|ГДЕ
	|	гф_СпецификацияПокупателя.Ссылка = &СсылкаНаДокумент";
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса по шапке документа
//
Функция ТекстЗапроса_ВТ_ПозицииНоменклатуры()
	
	Результат = "ВЫБРАТЬ
	|	гф_СпецификацияПокупателяЗаказыКлиентов.ЗаказКлиента КАК ЗаказКлиента,
	|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
	|	СУММА(ЗаказКлиентаТовары.Количество) КАК Количество,
	|	ВЫРАЗИТЬ(ЗаказКлиентаТовары.Цена * (1 - ЗаказКлиентаТовары.ПроцентРучнойСкидки / 100) * (1 - ЗаказКлиентаТовары.ПроцентАвтоматическойСкидки / 100) КАК ЧИСЛО(15, 2)) КАК Цена,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК РекомендуемаяРозничнаяЦена,
	|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки,
	|	СтавкиНДС.Ссылка КАК СтавкаНДС,
	|	СтавкиНДС.Ставка КАК СтавкаНДСЧисло,
	|	0 КАК ЗаказаноОстаток
	|ПОМЕСТИТЬ ВТ_ПозицииНоменклатуры
	|ИЗ
	|	ВТ_Шапка КАК ВТ_Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гф_СпецификацияПокупателя.ЗаказыКлиентов КАК гф_СпецификацияПокупателяЗаказыКлиентов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
	|				ПО ЗаказКлиентаТовары.СтавкаНДС = СтавкиНДС.Ссылка
	|			ПО гф_СпецификацияПокупателяЗаказыКлиентов.ЗаказКлиента = ЗаказКлиентаТовары.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(&МоментВремени, ВидЦены = &РозничныеЦены) КАК ЦеныНоменклатурыСрезПоследних
	|			ПО (ЗаказКлиентаТовары.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура)
	|		ПО ВТ_Шапка.Спецификация = гф_СпецификацияПокупателяЗаказыКлиентов.Ссылка
	
	|СГРУППИРОВАТЬ ПО
	|	гф_СпецификацияПокупателяЗаказыКлиентов.ЗаказКлиента,
	|	ЗаказКлиентаТовары.Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика,
	|	ВЫРАЗИТЬ(ЗаказКлиентаТовары.Цена * (1 - ЗаказКлиентаТовары.ПроцентРучнойСкидки / 100) * (1 - ЗаказКлиентаТовары.ПроцентАвтоматическойСкидки / 100) КАК ЧИСЛО(15, 2)),
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ЗаказКлиентаТовары.ДатаОтгрузки,
	|	ЗаказКлиентаТовары.СтавкаНДС,
	|	СтавкиНДС.Ссылка,
	|	СтавкиНДС.Ставка";
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса по шапке документа
//
Функция ТекстЗапроса_ВТ_ПозицииНоменклатурыНаОстатокПоНовойЦене()
	
	Результат = "ВЫБРАТЬ
	|	гф_СпецификацияПокупателяЗаказыКлиентов.ЗаказКлиента КАК ЗаказКлиента,
	|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
	|	СУММА(ЗаказКлиентаТовары.Количество) КАК Количество,
	|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки,
	|	СтавкиНДС.Ссылка КАК СтавкаНДС,
	|	СтавкиНДС.Ставка КАК СтавкаНДСЧисло
	|ПОМЕСТИТЬ ВТ_ПозицииНоменклатурыПредварительно
	|ИЗ
	|	ВТ_Шапка КАК ВТ_Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гф_СпецификацияПокупателя.ЗаказыКлиентов КАК гф_СпецификацияПокупателяЗаказыКлиентов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
	|				ПО ЗаказКлиентаТовары.СтавкаНДС = СтавкиНДС.Ссылка
	|			ПО гф_СпецификацияПокупателяЗаказыКлиентов.ЗаказКлиента = ЗаказКлиентаТовары.Ссылка
	|		ПО ВТ_Шапка.Спецификация = гф_СпецификацияПокупателяЗаказыКлиентов.Ссылка
	
	|СГРУППИРОВАТЬ ПО
	|	гф_СпецификацияПокупателяЗаказыКлиентов.ЗаказКлиента,
	|	ЗаказКлиентаТовары.Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика,
	|	ЗаказКлиентаТовары.ДатаОтгрузки,
	|	ЗаказКлиентаТовары.СтавкаНДС,
	|	СтавкиНДС.Ссылка,
	|	СтавкиНДС.Ставка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПозицииНоменклатурыПредварительно.ЗаказКлиента КАК ЗаказКлиента,
	|	ВТ_ПозицииНоменклатурыПредварительно.Номенклатура КАК Номенклатура,
	|	ВТ_ПозицииНоменклатурыПредварительно.Характеристика КАК Характеристика,
	|	ВТ_ПозицииНоменклатурыПредварительно.Количество КАК Количество,
	|	ISNULL(ЦеныНоменклатурыСрезПоследних.Цена * (100 - &СкидкаКлиента),0) / 100 КАК Цена,
	|	ISNULL(ЦеныНоменклатурыРекомендуемыеСрезПоследних.Цена,0) КАК РекомендуемаяРозничнаяЦена,
	|	ВТ_ПозицииНоменклатурыПредварительно.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ВТ_ПозицииНоменклатурыПредварительно.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_ПозицииНоменклатурыПредварительно.СтавкаНДСЧисло КАК СтавкаНДСЧисло,
	|	ЗаказыКлиентовОстатки.ЗаказаноОстаток КАК ЗаказаноОстаток
	|ПОМЕСТИТЬ ВТ_ПозицииНоменклатуры
	|ИЗ
	|	ВТ_ПозицииНоменклатурыПредварительно КАК ВТ_ПозицииНоменклатурыПредварительно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыКлиентов.Остатки(
	|				&ДатаОстатков,
	|				(ЗаказКлиента, Номенклатура) В
	|					(ВЫБРАТЬ
	|						Т.ЗаказКлиента,
	|						Т.Номенклатура
	|					ИЗ
	|						ВТ_ПозицииНоменклатурыПредварительно КАК Т)) КАК ЗаказыКлиентовОстатки
	|		ПО ВТ_ПозицииНоменклатурыПредварительно.ЗаказКлиента = ЗаказыКлиентовОстатки.ЗаказКлиента
	|			И ВТ_ПозицииНоменклатурыПредварительно.Номенклатура = ЗаказыКлиентовОстатки.Номенклатура
	|			И ВТ_ПозицииНоменклатурыПредварительно.Характеристика = ЗаказыКлиентовОстатки.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(
	|				&ДатаЦен,
	|				ВидЦены = &ПрайсЛист
	|					И Номенклатура В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							Т.Номенклатура
	|						ИЗ
	|							ВТ_ПозицииНоменклатурыПредварительно КАК Т)) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВТ_ПозицииНоменклатурыПредварительно.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(
	|				&ДатаЦен,
	|				ВидЦены = &РозничныеЦены
	|					И Номенклатура В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							Т.Номенклатура
	|						ИЗ
	|							ВТ_ПозицииНоменклатурыПредварительно КАК Т)) КАК ЦеныНоменклатурыРекомендуемыеСрезПоследних
	|		ПО (ВТ_ПозицииНоменклатурыПредварительно.Номенклатура = ЦеныНоменклатурыРекомендуемыеСрезПоследних.Номенклатура)";

	Возврат Результат;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса создания временных таблиц с данными по номенклатуре
//
Функция ТекстЗапросаВТ_Номенклатура()
	
	Результат = "ВЫБРАТЬ
	|	ВТ_ПозицииНоменклатуры.ЗаказКлиента КАК Заказ,
	|	ВТ_ПозицииНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВТ_ПозицииНоменклатуры.Характеристика КАК Характеристика,
	|	ХарактеристикиНоменклатуры.НаименованиеПолное КАК Размер,
	|	НоменклатураСправочник.НаименованиеПолное КАК НоменклатураПредставление,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаШапка.НеОтгружатьЧастями
	|			ТОГДА ЗаказКлиентаШапка.ДатаОтгрузки
	|		ИНАЧЕ ВТ_ПозицииНоменклатуры.ДатаОтгрузки
	|	КОНЕЦ КАК ДатаОтгрузки,                    
	|	ВЫБОР
	|		КОГДА &НаОстатокПоНовойЦене
	|			ТОГДА СУММА(ВТ_ПозицииНоменклатуры.ЗаказаноОстаток)
	|		ИНАЧЕ СУММА(ВТ_ПозицииНоменклатуры.Количество)
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &НаОстатокПоНовойЦене
	|			ТОГДА СУММА(ВТ_ПозицииНоменклатуры.ЗаказаноОстаток)
	|		ИНАЧЕ СУММА(ВТ_ПозицииНоменклатуры.Количество)
	|	КОНЕЦ КАК КоличествоВКоробе,
	|	СУММА(1) КАК КоличествоКоробов,
	|	ВТ_ПозицииНоменклатуры.РекомендуемаяРозничнаяЦена КАК РекомендуемаяРозничнаяЦена,
	|	ВТ_ПозицииНоменклатуры.Цена КАК Цена,
	|	СУММА(0) КАК ЦенаЗаКороб,
	|	НоменклатураСправочник.Артикул КАК Артикул,
	|	НоменклатураСправочник.ЕдиницаДляОтчетов КАК ЕдиницаДляОтчетов,
	|	ВТ_ПозицииНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_ПозицииНоменклатуры.СтавкаНДСЧисло КАК СтавкаНДСЧисло
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	ВТ_ПозицииНоменклатуры КАК ВТ_ПозицииНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиентаШапка
	|		ПО ВТ_ПозицииНоменклатуры.ЗаказКлиента = ЗаказКлиентаШапка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО ВТ_ПозицииНоменклатуры.Номенклатура = НоменклатураСправочник.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ВТ_ПозицииНоменклатуры.Характеристика = ХарактеристикиНоменклатуры.Ссылка
	
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПозицииНоменклатуры.СтавкаНДС,
	|	НоменклатураСправочник.Артикул,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаШапка.НеОтгружатьЧастями
	|			ТОГДА ЗаказКлиентаШапка.ДатаОтгрузки
	|		ИНАЧЕ ВТ_ПозицииНоменклатуры.ДатаОтгрузки
	|	КОНЕЦ,
	|	ЗаказКлиентаШапка.ДатаОтгрузки,
	|	ВТ_ПозицииНоменклатуры.Характеристика,
	|	ВТ_ПозицииНоменклатуры.РекомендуемаяРозничнаяЦена,
	|	ХарактеристикиНоменклатуры.НаименованиеПолное,
	|	ВТ_ПозицииНоменклатуры.ЗаказКлиента,
	|	НоменклатураСправочник.ЕдиницаДляОтчетов,
	|	НоменклатураСправочник.НаименованиеПолное,
	|	ВТ_ПозицииНоменклатуры.Номенклатура,
	|	ВТ_ПозицииНоменклатуры.Цена,
	|	ВТ_ПозицииНоменклатуры.СтавкаНДСЧисло
	|;
	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Номенклатура.Заказ КАК Заказ,
	|	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
	|	ВТ_Номенклатура.Характеристика КАК Характеристика,
	|	ВТ_Номенклатура.Размер КАК Размер,
	|	ВТ_Номенклатура.НоменклатураПредставление КАК НоменклатураПредставление,
	|	ВТ_Номенклатура.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ВТ_Номенклатура.КоличествоВКоробе КАК КоличествоВКоробе,
	|	СУММА(ВТ_Номенклатура.Количество) КАК Количество,
	|	СУММА(ВТ_Номенклатура.КоличествоКоробов) КАК КоличествоКоробов,
	|	ВТ_Номенклатура.Цена КАК Цена,
	|	ВТ_Номенклатура.ЦенаЗаКороб КАК ЦенаЗаКороб,
	|	ВТ_Номенклатура.Артикул КАК Артикул,
	|	ВТ_Номенклатура.ЕдиницаДляОтчетов КАК ЕдиницаДляОтчетов,
	|	ВТ_Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_Номенклатура.СтавкаНДСЧисло КАК СтавкаНДСЧисло,
	|	ВТ_Номенклатура.РекомендуемаяРозничнаяЦена КАК РекомендуемаяРозничнаяЦена,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Номенклатура.Количество * ВТ_Номенклатура.Цена КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Номенклатура.Количество * ВТ_Номенклатура.Цена * ВТ_Номенклатура.СтавкаНДСЧисло / 100 КАК ЧИСЛО(15, 2))) КАК СуммаНДС,
	|	СУММА(ВЫРАЗИТЬ(ВТ_Номенклатура.Количество * ВТ_Номенклатура.Цена * (100 + ВТ_Номенклатура.СтавкаНДСЧисло) / 100 КАК ЧИСЛО(15, 2))) КАК СуммаСНДС
	|ПОМЕСТИТЬ ВТ_НоменклатураСуммы
	|ИЗ
	|	ВТ_Номенклатура КАК ВТ_Номенклатура
	
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Номенклатура.Заказ,
	|	ВТ_Номенклатура.Характеристика,
	|	ВТ_Номенклатура.НоменклатураПредставление,
	|	ВТ_Номенклатура.Артикул,
	|	ВТ_Номенклатура.СтавкаНДС,
	|	ВТ_Номенклатура.Размер,
	|	ВТ_Номенклатура.ДатаОтгрузки,
	|	ВТ_Номенклатура.Номенклатура,
	|	ВТ_Номенклатура.РекомендуемаяРозничнаяЦена,
	|	ВТ_Номенклатура.ЕдиницаДляОтчетов,
	|	ВТ_Номенклатура.Цена,
	|	ВТ_Номенклатура.СтавкаНДСЧисло,
	|	ВТ_Номенклатура.КоличествоВКоробе,
	|	ВТ_Номенклатура.ЦенаЗаКороб";
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса создания временных таблиц с данными по номенклатуре
//
Функция ТекстЗапросаВТ_НоменклатураВКоробах()
	
	Результат = "ВЫБРАТЬ
	|	ВТ_ПозицииНоменклатуры.ЗаказКлиента КАК ЗаказКлиента,
	|	ВТ_ПозицииНоменклатуры.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ВТ_ПозицииНоменклатуры.Цена) КАК Цена,
	|	МАКСИМУМ(ВТ_ПозицииНоменклатуры.СтавкаНДС) КАК СтавкаНДС,
	|	МАКСИМУМ(ВТ_ПозицииНоменклатуры.СтавкаНДСЧисло) КАК СтавкаНДСЧисло,
	|	МАКСИМУМ(ВТ_ПозицииНоменклатуры.РекомендуемаяРозничнаяЦена) КАК РекомендуемаяРозничнаяЦена
	|ПОМЕСТИТЬ ВТ_Цены
	|ИЗ
	|	ВТ_ПозицииНоменклатуры КАК ВТ_ПозицииНоменклатуры
	
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПозицииНоменклатуры.ЗаказКлиента,
	|	ВТ_ПозицииНоменклатуры.Номенклатура
	|;
	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыКлиентов.Ссылка КАК Заказ,
	|	ВариантыКомплектацииНоменклатуры.Наименование КАК Артикул,
	|	НоменклатураСправочник.Ссылка КАК Номенклатура,
	|	НоменклатураСправочник.НаименованиеПолное КАК НоменклатураПредставление,
	|	ЗаказыКлиентов.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ВЫБОР
	|		КОГДА &НаОстатокПоНовойЦене = ИСТИНА
	|			ТОГДА СУММА(ВТ_ПозицииНоменклатуры.Количество)
	|		ИНАЧЕ ВариантыКомплектацииНоменклатурыТовары.Количество
	|	КОНЕЦ КАК КоличествоВКоробе,
	|	ЗаказКлиентагф_ТоварыВКоробах.Количество КАК КоличествоКоробов,
	|	ВЫБОР
	|		КОГДА &НаОстатокПоНовойЦене = ИСТИНА
	|			ТОГДА СУММА(ВТ_ПозицииНоменклатуры.Количество * ЗаказКлиентагф_ТоварыВКоробах.Количество)
	|		ИНАЧЕ СУММА(ВариантыКомплектацииНоменклатурыТовары.Количество * ЗаказКлиентагф_ТоварыВКоробах.Количество)
	|	КОНЕЦ КАК Количество,
	|	ЗаказКлиентагф_ТоварыВКоробах.ЦенаКороба КАК ЦенаЗаКороб,
	|	&Упаковка КАК ЕдиницаДляОтчетов,
	|	СУММА(ВТ_ПозицииНоменклатуры.Количество) КАК КоличествоОстаток,
	|	ВЫБОР
	|		КОГДА &НаОстатокПоНовойЦене = ЛОЖЬ
	|			ТОГДА 1
	|		КОГДА СУММА(ВариантыКомплектацииНоменклатурыТовары.Количество * ЗаказКлиентагф_ТоварыВКоробах.Количество) = 0
	|			ТОГДА 1
	|		ИНАЧЕ СУММА(ВТ_ПозицииНоменклатуры.Количество) / СУММА(ВариантыКомплектацииНоменклатурыТовары.Количество * ЗаказКлиентагф_ТоварыВКоробах.Количество)
	|	КОНЕЦ КАК Коэффициент
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	ВТ_Шапка КАК ВТ_Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гф_СпецификацияПокупателя.ЗаказыКлиентов КАК СпецификацияТовары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказыКлиентов
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.гф_ТоварыВКоробах КАК ЗаказКлиентагф_ТоварыВКоробах
	|					ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
	|							ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПозицииНоменклатуры КАК ВТ_ПозицииНоменклатуры
	|							ПО ВариантыКомплектацииНоменклатурыТовары.Номенклатура = ВТ_ПозицииНоменклатуры.Номенклатура
	|								И ВариантыКомплектацииНоменклатурыТовары.Характеристика = ВТ_ПозицииНоменклатуры.Характеристика
	|						ПО ВариантыКомплектацииНоменклатуры.Ссылка = ВариантыКомплектацииНоменклатурыТовары.Ссылка
	|						ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|						ПО ВариантыКомплектацииНоменклатуры.Владелец = НоменклатураСправочник.Ссылка
	|					ПО ЗаказКлиентагф_ТоварыВКоробах.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка
	|				ПО ЗаказыКлиентов.Ссылка = ЗаказКлиентагф_ТоварыВКоробах.Ссылка
	|			ПО СпецификацияТовары.ЗаказКлиента = ЗаказыКлиентов.Ссылка
	|		ПО ВТ_Шапка.Спецификация = СпецификацияТовары.Ссылка
	
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыКлиентов.Ссылка,
	|	ВариантыКомплектацииНоменклатуры.Наименование,
	|	НоменклатураСправочник.Ссылка,
	|	НоменклатураСправочник.НаименованиеПолное,
	|	ЗаказыКлиентов.ДатаОтгрузки,
	|	ВариантыКомплектацииНоменклатурыТовары.Количество,
	|	ЗаказКлиентагф_ТоварыВКоробах.Количество,
	|	ЗаказКлиентагф_ТоварыВКоробах.ЦенаКороба,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика,
	|	ВТ_ПозицииНоменклатуры.Количество
	|;
	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Номенклатура.Заказ КАК Заказ,
	|	ВТ_Номенклатура.Артикул КАК Артикул,
	|	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
	|	ВТ_Номенклатура.НоменклатураПредставление КАК НоменклатураПредставление,
	|	"""" КАК Размер,
	|	ВТ_Номенклатура.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ВТ_Номенклатура.КоличествоВКоробе КАК КоличествоВКоробе,
	|	ВТ_Номенклатура.КоличествоКоробов КАК КоличествоКоробов,
	|	ВТ_Номенклатура.Количество КАК Количество,
	|	ВТ_Цены.Цена КАК Цена,
	|	ВТ_Номенклатура.ЦенаЗаКороб*ВТ_Номенклатура.Коэффициент КАК ЦенаЗаКороб,
	|	ВТ_Номенклатура.ЕдиницаДляОтчетов КАК ЕдиницаДляОтчетов,
	|	ВТ_Цены.РекомендуемаяРозничнаяЦена КАК РекомендуемаяРозничнаяЦена,
	|	ВТ_Цены.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_Цены.СтавкаНДСЧисло КАК СтавкаНДСЧисло,
	|	ВЫРАЗИТЬ(ВТ_Номенклатура.Коэффициент * ВТ_Номенклатура.КоличествоКоробов * ВТ_Номенклатура.ЦенаЗаКороб КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВТ_Номенклатура.Коэффициент * ВТ_Номенклатура.КоличествоКоробов * ВТ_Номенклатура.ЦенаЗаКороб * ВТ_Цены.СтавкаНДСЧисло / 100 КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВТ_Номенклатура.Коэффициент * ВТ_Номенклатура.КоличествоКоробов * ВТ_Номенклатура.ЦенаЗаКороб * (100 + ВТ_Цены.СтавкаНДСЧисло) / 100 КАК ЧИСЛО(15, 2)) КАК СуммаСНДС
	|ПОМЕСТИТЬ ВТ_НоменклатураСуммы
	|ИЗ
	|	ВТ_Номенклатура КАК ВТ_Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
	|		ПО ВТ_Номенклатура.Номенклатура = ВТ_Цены.Номенклатура
	|			И ВТ_Номенклатура.Заказ = ВТ_Цены.ЗаказКлиента";
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса строк дублей цен и шапки печатной формы
//
Функция ТекстЗапросаДублиИШапка()
	
	Результат = "ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВТ_НоменклатураСуммы.Количество), 0) КАК Количество,
	|	ЕСТЬNULL(СУММА(ВТ_НоменклатураСуммы.КоличествоКоробов), 0) КАК КоличествоКоробов,
	|	ЕСТЬNULL(СУММА(ВТ_НоменклатураСуммы.Сумма), 0) КАК Сумма,
	|	ЕСТЬNULL(СУММА(ВТ_НоменклатураСуммы.СуммаНДС), 0) КАК СуммаНДС,
	|	ЕСТЬNULL(СУММА(ВТ_НоменклатураСуммы.СуммаСНДС), 0) КАК СуммаСНДС
	|ПОМЕСТИТЬ ВТ_Итоги
	|ИЗ
	|	ВТ_НоменклатураСуммы КАК ВТ_НоменклатураСуммы
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Номенклатура.Артикул КАК Артикул,
	|	ВТ_Номенклатура.Заказ КАК Заказ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ПозицииНоменклатуры.Номенклатура КАК Номенклатура,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ПозицииНоменклатуры.Цена) КАК Цена
	|	ИЗ
	|		ВТ_ПозицииНоменклатуры КАК ВТ_ПозицииНоменклатуры
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_ПозицииНоменклатуры.Номенклатура) КАК Подзапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Номенклатура КАК ВТ_Номенклатура
	|		ПО Подзапрос.Номенклатура = ВТ_Номенклатура.Номенклатура
	|ГДЕ
	|	Подзапрос.Цена > 1
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Шапка.ТекущаяДата КАК ТекущаяДата,
	|	ВТ_Шапка.Номер КАК Номер,
	|	ВТ_Шапка.Дата КАК Дата,
	|	ВТ_Шапка.Организация КАК Организация,
	|	ВТ_Шапка.Контрагент КАК Контрагент,
	|	ВТ_Шапка.Договор КАК Договор,
	|	ВТ_Шапка.Ответственный КАК Ответственный,
	|	ВТ_Шапка.Комментарий КАК Комментарий,
	|	ВТ_Шапка.ОрганизацияПредставление КАК ОрганизацияПредставление,
	|	ВТ_Шапка.КонтрагентПредставление КАК КонтрагентПредставление,
	|	ВТ_Шапка.ДатаДоговора КАК ДатаДоговора,
	|	ВТ_Шапка.НомерДоговора КАК НомерДоговора,
	|	ВТ_Шапка.ДоговорПредставление КАК ДоговорПредставление,
	|	ВТ_Шапка.ВидЦен КАК ВидЦен,
	|	ВТ_Шапка.Спецификация КАК Спецификация,
	|	ВТ_Шапка.ПервичныеДокументыВКоробах КАК ПервичныеДокументыВКоробах,
	|	ВТ_Итоги.Количество КАК Количество,
	|	ВТ_Итоги.КоличествоКоробов КАК КоличествоКоробов,
	|	ВТ_Итоги.Сумма КАК Сумма,
	|	ВТ_Итоги.СуммаНДС КАК СуммаНДС,
	|	ВТ_Итоги.СуммаСНДС КАК СуммаСНДС
	|ИЗ
	|	ВТ_Шапка КАК ВТ_Шапка,
	|	ВТ_Итоги КАК ВТ_Итоги
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Шапка.Договор,
	|	ВТ_Шапка.Ответственный,
	|	ВТ_Шапка.НомерДоговора,
	|	ВТ_Шапка.Номер,
	|	ВТ_Шапка.Контрагент,
	|	ВТ_Шапка.ДатаДоговора,
	|	ВТ_Шапка.КонтрагентПредставление,
	|	ВТ_Шапка.ОрганизацияПредставление,
	|	ВТ_Шапка.ДоговорПредставление,
	|	ВТ_Шапка.ТекущаяДата,
	|	ВТ_Шапка.Комментарий,
	|	ВТ_Шапка.Организация,
	|	ВТ_Шапка.Дата,
	|	ВТ_Шапка.ПервичныеДокументыВКоробах,
	|	ВТ_Шапка.Спецификация,
	|	ВТ_Шапка.ВидЦен,
	|	ВТ_Итоги.Количество,
	|	ВТ_Итоги.КоличествоКоробов,
	|	ВТ_Итоги.Сумма,
	|	ВТ_Итоги.СуммаНДС,
	|	ВТ_Итоги.СуммаСНДС";
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса выбора строк печатной формы
//
Функция ТекстЗапросаИтоги()
	
	Результат = "ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК Заказ,
	|	ЗаказКлиента.Представление КАК ЗаказПредставление,
	|	ЗаказКлиента.Номер КАК ЗаказНомер,
	|	ЗаказКлиента.Дата КАК ЗаказДата,
	|	ЗаказКлиента.гф_ИмяЗаказа КАК ИмяЗаказа,
	|	ВТ_НоменклатураСуммы.Размер КАК Размер,
	|	ВТ_НоменклатураСуммы.Номенклатура КАК Номенклатура,
	|	ВТ_НоменклатураСуммы.НоменклатураПредставление КАК НоменклатураПредставление,
	|	ВТ_НоменклатураСуммы.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ВТ_НоменклатураСуммы.КоличествоВКоробе КАК КоличествоВКоробе,
	|	ВТ_НоменклатураСуммы.Количество КАК Количество,
	|	ВТ_НоменклатураСуммы.КоличествоКоробов КАК КоличествоКоробов,
	|	ВТ_НоменклатураСуммы.РекомендуемаяРозничнаяЦена КАК РекомендуемаяРозничнаяЦена,
	|	ВТ_НоменклатураСуммы.Цена КАК Цена,
	|	ВТ_НоменклатураСуммы.ЦенаЗаКороб КАК ЦенаЗаКороб,
	|	ВТ_НоменклатураСуммы.Артикул КАК Артикул,
	|	ВТ_НоменклатураСуммы.ЕдиницаДляОтчетов КАК ЕдиницаДляОтчетов,
	|	ВТ_НоменклатураСуммы.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_НоменклатураСуммы.СтавкаНДСЧисло КАК СтавкаНДСЧисло,
	|	ВТ_НоменклатураСуммы.Сумма КАК Сумма,
	|	ВТ_НоменклатураСуммы.СуммаНДС КАК СуммаНДС,
	|	ВТ_НоменклатураСуммы.СуммаСНДС КАК СуммаСНДС
	|ИЗ
	|	ВТ_НоменклатураСуммы КАК ВТ_НоменклатураСуммы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ПО ВТ_НоменклатураСуммы.Заказ = ЗаказКлиента.Ссылка
	
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказДата,
	|	ЗаказНомер,
	|	НоменклатураПредставление,
	|	Размер
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(КоличествоКоробов),
	|	СУММА(Сумма),
	|	СУММА(СуммаНДС),
	|	СУММА(СуммаСНДС)
	|ПО
	|	Заказ";
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

// #wortmann {
// #Спецификация покупателя
// Галфинд Окунев 2022/07/05
// Текст запроса выбора строк печатной формы
//
Функция ТекстЗапросаИтогиВКоробах()
	
	Результат = "ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК Заказ,
	|	ВТ_НоменклатураСуммы.Артикул КАК Артикул,
	|	ЗаказКлиента.Представление КАК ЗаказПредставление,
	|	ЗаказКлиента.Номер КАК ЗаказНомер,
	|	ЗаказКлиента.Дата КАК ЗаказДата,
	|	ЗаказКлиента.гф_ИмяЗаказа КАК ИмяЗаказа,
	|	МАКСИМУМ(ВТ_НоменклатураСуммы.НоменклатураПредставление) КАК НоменклатураПредставление,
	|	ВТ_НоменклатураСуммы.ДатаОтгрузки КАК ДатаОтгрузки,
	|	СУММА(ВТ_НоменклатураСуммы.КоличествоВКоробе) КАК КоличествоВКоробе,
	|	СУММА(ВТ_НоменклатураСуммы.Количество) КАК Количество,
	|	МАКСИМУМ(ВТ_НоменклатураСуммы.КоличествоКоробов) КАК КоличествоКоробов,
	|	ВТ_НоменклатураСуммы.РекомендуемаяРозничнаяЦена КАК РекомендуемаяРозничнаяЦена,
	|	ВТ_НоменклатураСуммы.Цена КАК Цена,
	|	ВТ_НоменклатураСуммы.ЦенаЗаКороб КАК ЦенаЗаКороб,
	|	ВТ_НоменклатураСуммы.ЕдиницаДляОтчетов КАК ЕдиницаДляОтчетов,
	|	ВТ_НоменклатураСуммы.СтавкаНДС КАК СтавкаНДС,
	|	МАКСИМУМ(ВТ_НоменклатураСуммы.Сумма) КАК Сумма,
	|	МАКСИМУМ(ВТ_НоменклатураСуммы.СуммаНДС) КАК СуммаНДС,
	|	МАКСИМУМ(ВТ_НоменклатураСуммы.СуммаСНДС) КАК СуммаСНДС,
	|	МАКСИМУМ(ВТ_НоменклатураСуммы.Номенклатура) КАК Номенклатура
	|ИЗ
	|	ВТ_НоменклатураСуммы КАК ВТ_НоменклатураСуммы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ПО ВТ_НоменклатураСуммы.Заказ = ЗаказКлиента.Ссылка
	
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиента.Ссылка,
	|	ВТ_НоменклатураСуммы.Артикул,
	|	ЗаказКлиента.Представление,
	|	ЗаказКлиента.Номер,
	|	ЗаказКлиента.Дата,
	|	ЗаказКлиента.гф_ИмяЗаказа,
	|	ВТ_НоменклатураСуммы.РекомендуемаяРозничнаяЦена,
	|	ВТ_НоменклатураСуммы.Цена,
	|	ВТ_НоменклатураСуммы.ЦенаЗаКороб,
	|	ВТ_НоменклатураСуммы.ЕдиницаДляОтчетов,
	|	ВТ_НоменклатураСуммы.СтавкаНДС,
	|	ВТ_НоменклатураСуммы.ДатаОтгрузки
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(КоличествоКоробов),
	|	СУММА(Сумма),
	|	СУММА(СуммаНДС),
	|	СУММА(СуммаСНДС)
	|ПО
	|	Заказ";
	
	Возврат Результат;
	
КонецФункции// } #wortmann	

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДанныеШапки.Дата КАК Период,
	               |	Контрагенты.Партнер КАК Партнер,
	               |	ДанныеШапки.Контрагент КАК Контрагент,
	               |	ДанныеШапки.Организация КАК Организация,
	               |	ДанныеШапки.Договор КАК Договор,
	               |	ДанныеШапки.Ответственный КАК Ответственный,
	               |	ДанныеШапки.Комментарий КАК Комментарий,
	               |	ДанныеШапки.Ссылка КАК Ссылка,
	               |	ДанныеШапки.Номер КАК Номер,
	               |	ДанныеШапки.Проведен КАК Проведен,
	               |	ДанныеШапки.ПометкаУдаления КАК ПометкаУдаления
	               |ИЗ
	               |	Документ.гф_СпецификацияПокупателя КАК ДанныеШапки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	               |		ПО ДанныеШапки.Контрагент = Контрагенты.Ссылка
	               |ГДЕ
	               |	ДанныеШапки.Ссылка = &Ссылка";       
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",						Реквизиты.Период);
	Запрос.УстановитьПараметр("Партнер",					Реквизиты.Партнер);
	Запрос.УстановитьПараметр("Организация",				Реквизиты.Организация);
	Запрос.УстановитьПараметр("Договор",					Реквизиты.Договор);
	Запрос.УстановитьПараметр("Контрагент",					Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("Номер",						Реквизиты.Номер);
	Запрос.УстановитьПараметр("Ответственный",				Реквизиты.Ответственный);
	Запрос.УстановитьПараметр("Комментарий",				Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("Проведен",					Реквизиты.Проведен);
	Запрос.УстановитьПараметр("ПометкаУдаления",			Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",	ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(ДокументСсылка)));
	
	ИнформацияПоДоговору = Неопределено;     
	
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		
		ИнформацияПоДоговору = НСтр("ru = 'По договору ""%Договор%""';
									|en = 'Under the ""%Договор%"" contract'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		
		ИнформацияПоДоговору = СтрЗаменить(ИнформацияПоДоговору, "%Договор%", Реквизиты.Договор);
		
	КонецЕсли;   
	
	Запрос.УстановитьПараметр("ИнформацияПоДоговору", ИнформацияПоДоговору);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка 					КАК Ссылка,
	|	&Период 					КАК ДатаДокументаИБ,
	|	&Номер 						КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных	КАК ТипСсылки,
	|	&Организация 				КАК Организация,
	|	НЕОПРЕДЕЛЕНО 				КАК ХозяйственнаяОперация,
	|	&Партнер 					КАК Партнер,
	|	&Контрагент 				КАК Контрагент,
	|	&Договор 					КАК Договор,
	|	НЕОПРЕДЕЛЕНО 				КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО 				КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО 				КАК Подразделение,
	|	&Ответственный 				КАК Ответственный,
	|	&Ответственный 				КАК Автор,
	|	&Комментарий 				КАК Комментарий,
	|	НЕОПРЕДЕЛЕНО 				КАК Валюта,
	|	НЕОПРЕДЕЛЕНО 				КАК Сумма,
	|	НЕОПРЕДЕЛЕНО 				КАК Статус,
	|	&Проведен 					КАК Проведен,
	|	&ПометкаУдаления			КАК ПометкаУдаления,
	|	ЛОЖЬ						КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору		КАК Дополнительно,
	|	НЕОПРЕДЕЛЕНО				КАК ДатаПервичногоДокумента,
	|	ЛОЖЬ						КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО				КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО				КАК ИсправляемыйДокумент,
	|	НЕОПРЕДЕЛЕНО				КАК НомерПервичногоДокумента,
	|	НЕОПРЕДЕЛЕНО				КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
	
#КонецЕсли
