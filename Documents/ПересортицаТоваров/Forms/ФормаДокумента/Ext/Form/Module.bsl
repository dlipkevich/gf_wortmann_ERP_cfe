#Область ОбработчикиСобытийФормы

&НаСервере
Процедура гф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)	

	// #движение кодов маркировки
	// Галфинд Sakovich 2022/12/23
	гф_ДобавитьНовыеЭлементы();
	// } #wortmann

КонецПроцедуры

&НаКлиенте
Процедура гф_ПередЗаписьюПеред(Отказ, ПараметрыЗаписи)
	
	// ++ Галфинд_ДомнышеваКР_10_04_2024 
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eef5b21da68c0c
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И _омОбщегоНазначенияВызовСервера.ЕстьПолныеПрава() Тогда
		Если Не ПараметрыЗаписи.Свойство("ВопросЗадан") Тогда
			ПараметрыОшибок = ПолучитьДанныеДляВозможногоПроведения();
			Если ПараметрыОшибок.ЕстьОшибки Тогда
				Отказ = Истина;
				ТекстВопроса = НСтр("ru='Для записи в РН Движение кодов маркировки организации не хватает данных. "
				+ ПараметрыОшибок.ТекстОшибки + " Продолжить без записи в этот регистр?'");
				Оповещение = Новый ОписаниеОповещения("гф_ВопросПроведенияПродолжение", ЭтотОбъект, ПараметрыЗаписи);
				ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			КонецЕсли;
		Иначе
			ЭтотОбъект.гф_БезДвиженийКМ = Истина;
		КонецЕсли;
	КонецЕсли;
    // -- Галфинд_ДомнышеваКР_10_04_2024

КонецПроцедуры

&НаСервере
Процедура гф_ПередЗаписьюНаСервереПосле(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ++ Галфинд_ДомнышеваКР_10_04_2024
	Если ЭтотОбъект.гф_БезДвиженийКМ Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("БезДвиженийКМ", Истина);
	КонецЕсли;
	// -- Галфинд_ДомнышеваКР_10_04_2024

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура гф_ДобавитьНовыеЭлементы()

	ТипГруппаФормы = Тип("ГруппаФормы");
	ТипПолеФормы = Тип("ПолеФормы");

	НовыйЭлемент = ЭтотОбъект.Элементы.Добавить("гф_ГруппаНовыхРеквизитов", ТипГруппаФормы, Элементы.ГруппаИнформация);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь; 
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.Ширина = 0;
	НовыйЭлемент.РастягиватьПоГоризонтали = Неопределено;

	НовыйЭлемент = ЭтотОбъект.Элементы.Вставить("гф_IDКороба", ТипПолеФормы,
	ЭтотОбъект.Элементы.гф_ГруппаНовыхРеквизитов);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Ширина = 30;
	НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
	НовыйЭлемент.ПутьКДанным = "Объект.гф_IDКороба";
	
	// ++ Галфинд_ДомнышеваКР_10_04_2024
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eef5b21da68c0c
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	ДобавляемыеРеквизиты = Новый Массив;
	НовыйРеквизит = Новый РеквизитФормы("гф_БезДвиженийКМ", ОписаниеТиповБулево);
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	// -- Галфинд_ДомнышеваКР_10_04_2024

КонецПроцедуры

&НаКлиенте
Процедура гф_ВопросПроведенияПродолжение(Результат, ПараметрыЗаписи) Экспорт
	
	// ++ Галфинд_ДомнышеваКР_10_04_2024
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаписи.Вставить("ВопросЗадан", Истина);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	// -- Галфинд_ДомнышеваКР_10_04_2024
	
КонецПроцедуры

// #wortmann {
// Галфинд_ДомнышеваКР_10_04_2024
// Процедура заимствованны из гф_ОбработкаПроведения.ДвиженияКодовМаркировкиПересортицаТоваров
// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eef5b21da68c0c
&НаСервере
Функция ПолучитьДанныеДляВозможногоПроведения() 
	
	СкладКоробной = УправлениеСвойствами.ЗначениеСвойства(Объект.Склад, "гф_СкладыТоварыВКоробах") = Истина;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	докПересортица.Ссылка КАК докПересортица,
	|	ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Ссылка КАК докЭмиссия
	|ПОМЕСТИТЬ ДокументыМаркировки
	|ИЗ
	|	Документ.ПересортицаТоваров КАК докПересортица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ КАК ЗаказНаЭмиссиюКодовМаркировкиСУЗ
	|		ПО (докПересортица.Ссылка = ЗаказНаЭмиссиюКодовМаркировкиСУЗ.ДокументОснование)
	|ГДЕ
	|	докПересортица.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	докПересортица.Ссылка КАК Регистратор,
	|	докПересортица.Дата КАК Период,
	|	докПересортица.Организация КАК Организация,
	|	докПересортица.Склад КАК Склад,
	|	гф_ПересортицаУдаляемыеШК.ШтрихкодУпаковки КАК КМ,
	|	NULL КАК Эмиссия,
	|	1 КАК Количество
	|ПОМЕСТИТЬ Движения
	|ИЗ
	|	Документ.ПересортицаТоваров КАК докПересортица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гф_ПересортицаУдаляемыеШК КАК гф_ПересортицаУдаляемыеШК
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ВложенныеШтрихкоды
	|			ПО гф_ПересортицаУдаляемыеШК.ШтрихкодУпаковки = ВложенныеШтрихкоды.Штрихкод
	|		ПО докПересортица.Ссылка = гф_ПересортицаУдаляемыеШК.Пересортица
	|ГДЕ
	|	гф_ПересортицаУдаляемыеШК.Пересортица.Ссылка = &Ссылка
	|	И гф_ПересортицаУдаляемыеШК.ШтрихкодУпаковки ЕСТЬ НЕ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ДокументыМаркировки.докПересортица,
	|	ДокументыМаркировки.докПересортица.Дата,
	|	ДокументыМаркировки.докПересортица.Организация,
	|	ДокументыМаркировки.докПересортица.Склад,
	|	ШтрихкодыУпаковокТоваров.Ссылка,
	|	ДокументыМаркировки.докЭмиссия,
	|	1
	|ИЗ
	|	ДокументыМаркировки КАК ДокументыМаркировки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК тчВложенныеШтрихкоды
	|				ПО ШтрихкодыУпаковокТоваров.Ссылка = тчВложенныеШтрихкоды.Штрихкод
	|			ПО ПулКодовМаркировкиСУЗ.КодМаркировки = ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
	|		ПО ДокументыМаркировки.докПересортица = ПулКодовМаркировкиСУЗ.ДокументОснование
	|			И ДокументыМаркировки.докЭмиссия = ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.Организация КАК Организация,
	|	Движения.Склад КАК Склад,
	|	Движения.КМ КАК КМ,
	|	Движения.Эмиссия КАК Эмиссия,
	|	Движения.Количество КАК Количество
	|ПОМЕСТИТЬ ДанныеДляКонтроляОстатков
	|ИЗ
	|	Движения КАК Движения
	|ГДЕ
	|	Движения.ВидДвижения = ЗНАЧЕНИЕ(видДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляКонтроляОстатков.Организация КАК Организация,
	|	ДанныеДляКонтроляОстатков.Склад КАК Склад,
	|	ДанныеДляКонтроляОстатков.КМ КАК КМ,
	|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) - ДанныеДляКонтроляОстатков.Количество КАК Нехватака
	|ИЗ
	|	ДанныеДляКонтроляОстатков КАК ДанныеДляКонтроляОстатков
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.гф_ДвижениеКодовМаркировкиОрганизации.Остатки(
	|				&Момент,
	|				Организация В
	|						(ВЫБРАТЬ
	|							т.Организация
	|						ИЗ
	|							ДанныеДляКонтроляОстатков КАК т)
	|					И Склад В
	|						(ВЫБРАТЬ
	|							т1.Склад
	|						ИЗ
	|							ДанныеДляКонтроляОстатков КАК т1)
	|					И КМ В
	|						(ВЫБРАТЬ
	|							т2.КМ
	|						ИЗ
	|							ДанныеДляКонтроляОстатков КАК т2)) КАК Остатки
	|		ПО ДанныеДляКонтроляОстатков.Организация = Остатки.Организация
	|			И ДанныеДляКонтроляОстатков.Склад = Остатки.Склад
	|			И ДанныеДляКонтроляОстатков.КМ = Остатки.КМ
	|ГДЕ
	|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) - ДанныеДляКонтроляОстатков.Количество < 0
	|	И ДанныеДляКонтроляОстатков.КМ ЕСТЬ НЕ NULL");
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("СкладКоробной", СкладКоробной);
	
	Запрос.УстановитьПараметр("Момент", Новый Граница(Объект.Ссылка.МоментВремени(), ВидГраницы.Включая));
	
	ПакетРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	ТекстОшибки = "";
	ЕстьОшибки = Ложь;

	ВыборкаЭмиссий = ПакетРезультатов[0].Выбрать();
	Если ВыборкаЭмиссий.НайтиСледующий(Новый Структура("докЭмиссия", Null)) Тогда
		ТекстОшибки = ТекстОшибки + "По документу Пересортицы не найден Заказ на эмиссию кодов маркировки" + Символы.ПС;
		ЕстьОшибки = Истина;
	Иначе
		ВыборкаДвижений = ПакетРезультатов[1].Выбрать();
		
		Если ВыборкаДвижений.НайтиСледующий(
			Новый Структура("ВидДвижения, КМ", ВидДвиженияНакопления.Расход, Null)) Тогда
			ТекстОшибки = ТекстОшибки + "В пуле кодов маркировки не найден Код маркировки по документу Пересортицы" + Символы.ПС;
			ЕстьОшибки = Истина;
		КонецЕсли;
		ВыборкаДвижений.Сбросить();
		Если ВыборкаДвижений.НайтиСледующий(
			Новый Структура("ВидДвижения, КМ", ВидДвиженияНакопления.Приход, Null)) Тогда
			ТекстОшибки = ТекстОшибки + "В регистре ""Пересортица удаляемые ШК"" не найден Код маркировки по документу Пересортицы" + Символы.ПС;
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		РезультатКонтроль = ПакетРезультатов[3];
		Если Не РезультатКонтроль.Пустой() Тогда
			Шаблон = "По организации %1, по складу %2 - нет остатков по списываемому коду маркировки: %3";
			ВыборкаКонтроль = РезультатКонтроль.Выбрать();
			Пока ВыборкаКонтроль.Следующий() Цикл
				ТекстОшибки = ТекстОшибки + СтрШаблон(Шаблон, ВыборкаКонтроль["Организация"],
				ВыборкаКонтроль["Склад"], ВыборкаКонтроль["КМ"]) + Символы.ПС;
			КонецЦикла;
			ЕстьОшибки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("ЕстьОшибки", ЕстьОшибки);
	СтруктураВозврата.Вставить("ТекстОшибки", ТекстОшибки);
	
	Возврат СтруктураВозврата; 
	
КонецФункции// } #wortmann

#КонецОбласти
