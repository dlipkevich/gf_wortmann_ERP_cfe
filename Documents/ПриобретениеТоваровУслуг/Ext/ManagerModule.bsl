#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

&После("ДобавитьКомандыПечати")
Процедура гф_ДобавитьКомандыПечати(КомандыПечати)
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_Импорт40";
	КомандаПечати.Представление = НСтр("ru = 'Импорт 40';
										|en = 'Import 40'");
	//КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.МенеджерПечати = "Документ.ПриобретениеТоваровУслуг";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_Импорт70";
	КомандаПечати.Представление = НСтр("ru = 'Импорт 70';
										|en = 'Import 70'");
	//КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.МенеджерПечати = "Документ.ПриобретениеТоваровУслуг";
	
КонецПроцедуры

&После("Печать")
Процедура гф_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_Импорт40") Тогда	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_Импорт40",
		НСтр("ru = 'Импорт 40'"),
		СформироватьПечатнуюФормуИмпорт40(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_Импорт70") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_Импорт70",
		НСтр("ru = 'Импорт 70'"),
		СформироватьПечатнуюФормуИмпорт70(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуИмпорт40(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из МассивОбъектов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Макет = ПолучитьМакет("МакетИмпорт40");
		
		ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьСтрока 				= Макет.ПолучитьОбласть("Строка");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		
		"ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатуры.Наименование КАК Наименование,
		|	ПриобретениеТоваровУслуг.КоличествоКоробов КАК КоличествоКоробов,
		|	ВариантыКомплектацииНоменклатуры.Владелец КАК Владелец,
		|	ВариантыКомплектацииНоменклатуры.Количество КАК Количество
		|ПОМЕСТИТЬ ТоварыВариантКомплектации
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.гф_ПродукцияВКоробах КАК ПриобретениеТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		|		ПО ПриобретениеТоваровУслуг.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка
		|ГДЕ
		|	ПриобретениеТоваровУслуг.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслуг.ВариантКомплектации,
		|	ПриобретениеТоваровУслуг.КоличествоКоробов,
		|	ПриобретениеТоваровУслуг.ЗаказПоставщику,
		|	ПриобретениеТоваровУслуг.IDКороба,
		|	ВариантыКомплектацииНоменклатуры.Наименование,
		|	ВариантыКомплектацииНоменклатуры.Характеристика,
		|	ВариантыКомплектацииНоменклатуры.Количество,
		|	ВариантыКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента,
		|	ВариантыКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента,
		|	ВариантыКомплектацииНоменклатуры.Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВариантКомплектации.Наименование КАК Наименование,
		|	ТоварыВариантКомплектации.КоличествоКоробов КАК КоличествоКоробов,
		|	ТоварыВариантКомплектации.Владелец КАК Владелец,
		|	ТоварыВариантКомплектации.Количество КАК Количество,
		|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
		|	Номенклатура.Марка КАК Марка,
		|	Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения
		|ПОМЕСТИТЬ НоменклатураРеквизиты
		|ИЗ
		|	ТоварыВариантКомплектации КАК ТоварыВариантКомплектации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ТоварыВариантКомплектации.Владелец = Номенклатура.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВариантыКомплектацииНоменклатурыТовары.Количество) КАК КоличествоВПарах,
		|	НоменклатураРеквизиты.Наименование КАК Наименование,
		|	НоменклатураРеквизиты.КоличествоКоробов КАК КоличествоКоробов,
		|	НоменклатураРеквизиты.Владелец КАК Владелец,
		|	НоменклатураРеквизиты.Количество КАК Количество,
		|	НоменклатураРеквизиты.КодТНВЭД КАК КодТНВЭД,
		|	НоменклатураРеквизиты.Марка КАК Марка,
		|	НоменклатураРеквизиты.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|		ПО НоменклатураРеквизиты.Владелец = ВариантыКомплектацииНоменклатурыТовары.Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураРеквизиты.Наименование,
		|	НоменклатураРеквизиты.КоличествоКоробов,
		|	НоменклатураРеквизиты.Владелец,
		|	НоменклатураРеквизиты.Количество,
		|	НоменклатураРеквизиты.КодТНВЭД,
		|	НоменклатураРеквизиты.Марка,
		|	НоменклатураРеквизиты.СтранаПроисхождения,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.КоличествоВПарах КАК КоличествоВПарах,
		|	ВременнаяТаблицаТовары.Наименование КАК Артикул,
		|	ВременнаяТаблицаТовары.КоличествоКоробов КАК КоличествоКоробов,
		|	ВременнаяТаблицаТовары.Владелец КАК Владелец,
		|	ВременнаяТаблицаТовары.Количество КАК КоличествоВКоробах,
		|	ВременнаяТаблицаТовары.КодТНВЭД КАК КодТНВЭД,
		|	ВременнаяТаблицаТовары.Марка КАК Марка,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	СтраныМира.КодАльфа2 КАК СтранаПроисхождения
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|		ПО ВременнаяТаблицаТовары.СтранаПроисхождения = СтраныМира.Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", СтруктураОбъектов.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		НомерСтроки 		= 0;
		ЕдиноеИзмерение 	= "Пара";	
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл	
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			
			ОбластьСтрока.Параметры.НомерПП            = НомерСтроки;
			ОбластьСтрока.Параметры.ЕдиноеИзмерение    = ЕдиноеИзмерение;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока); 
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуИмпорт70(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из МассивОбъектов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Макет = ПолучитьМакет("МакетИмпорт70");
		
		ОбластьЗаголовокТаблицы	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьСтрока 				= Макет.ПолучитьОбласть("Строка");
		
		ДокументСсылка = СтруктураОбъектов.Ссылка;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатуры.Наименование КАК Наименование,
		|	ВариантыКомплектацииНоменклатуры.Владелец КАК Владелец,
		|	ВариантыКомплектацииНоменклатуры.Количество КАК Количество,
		|	СУММА(ПриобретениеТоваровУслуг.КоличествоКоробов) КАК КоличествоКоробов,
		|	ПриобретениеТоваровУслуг.Ссылка КАК ВариантКомплектацииСсылка,
		|	ПриобретениеТоваровУслуг.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ ТоварыВариантКомплектации
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.гф_ПродукцияВКоробах КАК ПриобретениеТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		|		ПО ПриобретениеТоваровУслуг.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка
		|ГДЕ
		|	ПриобретениеТоваровУслуг.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВариантыКомплектацииНоменклатуры.Наименование,
		|	ВариантыКомплектацииНоменклатуры.Характеристика,
		|	ВариантыКомплектацииНоменклатуры.Количество,
		|	ВариантыКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента,
		|	ВариантыКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента,
		|	ВариантыКомплектацииНоменклатуры.Владелец,
		|	ПриобретениеТоваровУслуг.Ссылка,
		|	ПриобретениеТоваровУслуг.ВариантКомплектации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВариантКомплектации.Наименование КАК Наименование,
		|	ТоварыВариантКомплектации.КоличествоКоробов КАК КоличествоКоробов,
		|	ТоварыВариантКомплектации.Владелец КАК Владелец,
		|	ТоварыВариантКомплектации.Количество КАК Количество,
		|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
		|	Номенклатура.Марка КАК Марка,
		|	Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Номенклатура.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
		|	Номенклатура.Ссылка КАК Ссылка,
		|	ТоварыВариантКомплектации.ВариантКомплектацииСсылка КАК ВариантКомплектацииСсылка,
		|	ТоварыВариантКомплектации.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ НоменклатураРеквизиты
		|ИЗ
		|	ТоварыВариантКомплектации КАК ТоварыВариантКомплектации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ТоварыВариантКомплектации.Владелец = Номенклатура.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВариантыКомплектацииНоменклатурыТовары.Количество) КАК КоличествоВПарах,
		|	НоменклатураРеквизиты.Наименование КАК Наименование,
		|	НоменклатураРеквизиты.КоличествоКоробов КАК КоличествоКоробов,
		|	НоменклатураРеквизиты.Владелец КАК Владелец,
		|	НоменклатураРеквизиты.КодТНВЭД КАК КодТНВЭД,
		|	НоменклатураРеквизиты.Марка КАК Марка,
		|	НоменклатураРеквизиты.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|	НоменклатураРеквизиты.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	НоменклатураРеквизиты.НаименованиеПолное КАК НаименованиеПолное,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка,
		|	НоменклатураРеквизиты.ВариантКомплектацииСсылка КАК ВариантКомплектацииСсылка,
		|	НоменклатураРеквизиты.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ ТоварыВариантКомплектацииИтог
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|		ПО НоменклатураРеквизиты.Владелец = ВариантыКомплектацииНоменклатурыТовары.Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураРеквизиты.Наименование,
		|	НоменклатураРеквизиты.КоличествоКоробов,
		|	НоменклатураРеквизиты.Владелец,
		|	НоменклатураРеквизиты.Количество,
		|	НоменклатураРеквизиты.КодТНВЭД,
		|	НоменклатураРеквизиты.Марка,
		|	НоменклатураРеквизиты.СтранаПроисхождения,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура,
		|	НоменклатураРеквизиты.ПроизводительИмпортерКонтрагент,
		|	НоменклатураРеквизиты.НаименованиеПолное,
		|	НоменклатураРеквизиты.Ссылка,
		|	НоменклатураРеквизиты.ВариантКомплектацииСсылка,
		|	НоменклатураРеквизиты.ВариантКомплектации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение,
		|	ДополнительныеРеквизитыИСведения.Имя КАК Имя,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка,
		|	гф_ПереводЗначенийРеквизитовИСвойств.Значение КАК Значение1
		|ПОМЕСТИТЬ ДополнительныеСведенияМатериалыПодошвы
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|			ПО ДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ДополнительныеСведения.Значение.Ссылка = ЗначенияСвойствОбъектов.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гф_ПереводЗначенийРеквизитовИСвойств КАК гф_ПереводЗначенийРеквизитовИСвойств
		|			ПО ДополнительныеСведения.Значение = гф_ПереводЗначенийРеквизитовИСвойств.Объект
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведения.Объект
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Имя = &Свойство_Material_bottom
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение,
		|	ДополнительныеРеквизитыИСведения.Имя КАК Имя,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка,
		|	гф_ПереводЗначенийРеквизитовИСвойств.Значение КАК Значение1
		|ПОМЕСТИТЬ ДополнительныеСведенияМатериалыВерха
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|			ПО ДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ДополнительныеСведения.Значение.Ссылка = ЗначенияСвойствОбъектов.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гф_ПереводЗначенийРеквизитовИСвойств КАК гф_ПереводЗначенийРеквизитовИСвойств
		|			ПО ДополнительныеСведения.Значение = гф_ПереводЗначенийРеквизитовИСвойств.Объект
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведения.Объект
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Имя = &Свойство_Material_surface
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение,
		|	ДополнительныеРеквизитыИСведения.Имя КАК Имя,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДополнительныеСведенияТипыОбуви
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|			ПО ДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ДополнительныеСведения.Значение.Ссылка = ЗначенияСвойствОбъектов.Ссылка
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведения.Объект
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Имя = &СвойствоПол
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка,
		|	ДополнительныеСведенияМатериалыВерха.Значение1 КАК МатериалыВерха,
		|	ДополнительныеСведенияМатериалыПодошвы.Значение1 КАК МатериалыПодошвы,
		|	ДополнительныеСведенияТипыОбуви.Значение КАК ТипыОбуви
		|ПОМЕСТИТЬ ТоварыХарактеристика
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДополнительныеСведенияМатериалыПодошвы КАК ДополнительныеСведенияМатериалыПодошвы
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведенияМатериалыПодошвы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДополнительныеСведенияМатериалыВерха КАК ДополнительныеСведенияМатериалыВерха
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведенияМатериалыВерха.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДополнительныеСведенияТипыОбуви КАК ДополнительныеСведенияТипыОбуви
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведенияТипыОбуви.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВариантКомплектацииИтог.КоличествоВПарах КАК КоличествоВПарах,
		|	ТоварыВариантКомплектацииИтог.Наименование КАК Артикул,
		|	ТоварыВариантКомплектацииИтог.КоличествоКоробов КАК КоличествоКоробов,
		|	ТоварыВариантКомплектацииИтог.Владелец КАК Владелец,
		|	ТоварыВариантКомплектацииИтог.КодТНВЭД КАК КодТНВЭД,
		|	ТоварыВариантКомплектацииИтог.Марка КАК Марка,
		|	ТоварыВариантКомплектацииИтог.Номенклатура КАК Номенклатура,
		|	СтраныМира.КодАльфа2 КАК СтранаПроисхождения,
		|	ТоварыВариантКомплектацииИтог.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	ТоварыВариантКомплектацииИтог.НаименованиеПолное КАК НаименованиеПолное,
		|	ОбластиДействияСертификатовНоменклатуры.СертификатНоменклатуры КАК СертификатНоменклатуры,
		|	СертификатыНоменклатуры.Номер КАК Номер,
		|	СертификатыНоменклатуры.ДатаНачалаСрокаДействия КАК ДатаНачалаСрокаДействия,
		|	СертификатыНоменклатуры.ДатаОкончанияСрокаДействия КАК ДатаОкончанияСрокаДействия,
		|	ТоварыВариантКомплектацииИтог.Ссылка КАК Ссылка,
		|	ТоварыВариантКомплектацииИтог.ВариантКомплектацииСсылка КАК ВариантКомплектацииСсылка,
		|	ТоварыВариантКомплектацииИтог.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ ТоварыХарактеристикаИтог
		|ИЗ
		|	ТоварыВариантКомплектацииИтог КАК ТоварыВариантКомплектацииИтог
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|		ПО ТоварыВариантКомплектацииИтог.СтранаПроисхождения = СтраныМира.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбластиДействияСертификатовНоменклатуры КАК ОбластиДействияСертификатовНоменклатуры
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СертификатыНоменклатуры КАК СертификатыНоменклатуры
		|			ПО ОбластиДействияСертификатовНоменклатуры.СертификатНоменклатуры = СертификатыНоменклатуры.Ссылка
		|		ПО ТоварыВариантКомплектацииИтог.Номенклатура = ОбластиДействияСертификатовНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПриобретениеТоваровУслугТовары.Количество) КАК КоличествоВПарах,
		|	ПриобретениеТоваровУслугТовары.Цена КАК Цена,
		|	СУММА(ПриобретениеТоваровУслугТовары.Сумма) КАК СтоимостьТаможенная
		|ПОМЕСТИТЬ ТоварыСтоимостьТаможенная
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Цена
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыХарактеристикаИтог.Марка КАК Марка,
		|	ТоварыХарактеристикаИтог.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	ТоварыХарактеристикаИтог.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТоварыХарактеристикаИтог.НаименованиеПолное КАК НаименованиеПолное,
		|	ТоварыХарактеристикаИтог.КодТНВЭД КАК КодТНВЭД,
		|	ТоварыХарактеристикаИтог.Артикул КАК Артикул,
		|	ТоварыХарактеристикаИтог.КоличествоКоробов КАК КоличествоВКоробах,
		|	ТоварыСтоимостьТаможенная.КоличествоВПарах КАК КоличествоВПарах,
		|	"""" КАК Размер,
		|	0 КАК МассаБрутто,
		|	0 КАК МассаНетто,
		|	ТоварыСтоимостьТаможенная.СтоимостьТаможенная КАК СтоимостьТаможенная,
		|	ТоварыХарактеристика.МатериалыВерха КАК МатериалыВерха,
		|	ТоварыХарактеристика.МатериалыПодошвы КАК МатериалыПодошвы,
		|	""01402"" КАК Код,
		|	ТоварыХарактеристикаИтог.Номер КАК Номер,
		|	ТоварыХарактеристикаИтог.ДатаНачалаСрокаДействия КАК ДатаНачалаСрокаДействия,
		|	""Взрослые"" КАК Тип,
		|	ТоварыХарактеристикаИтог.ДатаОкончанияСрокаДействия КАК ДатаОкончанияСрокаДействия,
		|	ТоварыХарактеристикаИтог.СертификатНоменклатуры КАК СертификатНоменклатуры,
		|	ТоварыХарактеристика.ТипыОбуви КАК ТипыОбуви,
		|	ВариантыКомплектацииНоменклатуры.Товары.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Характеристика КАК Характеристика,
		|		Упаковка КАК Упаковка,
		|		КоличествоУпаковок КАК КоличествоУпаковок,
		|		ДоляСтоимости КАК ДоляСтоимости,
		|		Количество КАК Количество
		|	) КАК Товары,
		|	ТоварыХарактеристикаИтог.Владелец КАК Владелец,
		|	ТоварыХарактеристикаИтог.Номенклатура КАК Номенклатура,
		|	ТоварыХарактеристика.Ссылка КАК Ссылка,
		|	ТоварыХарактеристикаИтог.ВариантКомплектации КАК ВариантКомплектации
		|ИЗ
		|	ТоварыХарактеристикаИтог КАК ТоварыХарактеристикаИтог
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыХарактеристика КАК ТоварыХарактеристика
		|		ПО ТоварыХарактеристикаИтог.Номенклатура = ТоварыХарактеристика.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		|		ПО ТоварыХарактеристикаИтог.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыСтоимостьТаможенная КАК ТоварыСтоимостьТаможенная
		|		ПО ТоварыХарактеристикаИтог.Номенклатура = ТоварыСтоимостьТаможенная.Номенклатура";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Запрос.УстановитьПараметр("Свойство_Material_bottom", "гф_НоменклатураMaterialBottom");
		Запрос.УстановитьПараметр("Свойство_Material_surface", "гф_НоменклатураMaterialSurface");
		Запрос.УстановитьПараметр("СвойствоПол", "гф_НоменклатураПол");
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписиРазмер = РезультатЗапроса.Выгрузить();
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		РазмерПоВариантыКомплектацииНоменклатуры = гф_ПолучитьРазмерПоВариантыКомплектацииНоменклатуры(ДокументСсылка);
		
		ВыборкаДетальныеЗаписиРазмер.Свернуть("Марка, ПроизводительИмпортерКонтрагент, СтранаПроисхождения, НаименованиеПолное"
		+ ", КодТНВЭД, Артикул, СтоимостьТаможенная, МатериалыВерха, МатериалыПодошвы, Код, Номер, ДатаНачалаСрокаДействия, ДатаОкончанияСрокаДействия"
		+ ", ТипыОбуви, СертификатНоменклатуры, ВариантКомплектации", "КоличествоВКоробах, КоличествоВПарах");
		
		НомерСтроки 	= 0;
		Код  			= "01402";
		
		Для Каждого ВыборкаДетальныеЗаписи Из ВыборкаДетальныеЗаписиРазмер Цикл	
			
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			
			ОбластьСтрока.Параметры.НомерПП 	= НомерСтроки;
			ОбластьСтрока.Параметры.Код     	= Код;
			
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаНачалаСрокаДействия) И ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаОкончанияСрокаДействия) Тогда
				ОбластьСтрока.Параметры.Дата    = Строка(Формат(ВыборкаДетальныеЗаписи.ДатаНачалаСрокаДействия,"ДФ=dd.MM.yyyy"))
				+ " ДО " + Строка(Формат(ВыборкаДетальныеЗаписи.ДатаОкончанияСрокаДействия,"ДФ=dd.MM.yyyy"));
			ИначеЕсли ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаНачалаСрокаДействия) Тогда
				ОбластьСтрока.Параметры.Дата    = Строка(Формат(ВыборкаДетальныеЗаписи.ДатаНачалаСрокаДействия,"ДФ=dd.MM.yyyy"));
			ИначеЕсли ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаОкончанияСрокаДействия) Тогда
				ОбластьСтрока.Параметры.Дата    = " ДО " + Строка(Формат(ВыборкаДетальныеЗаписи.ДатаОкончанияСрокаДействия,"ДФ=dd.MM.yyyy"));
			КонецЕсли;
				
			НайдСтр = РазмерПоВариантыКомплектацииНоменклатуры.НайтиСтроки(Новый Структура("ВариантКомплектации", ВыборкаДетальныеЗаписи.ВариантКомплектации));
			
			ТЗ_Мин = Новый Таблицазначений;
			ТЗ_Мин.Колонки.Добавить("Характеристика");
			
			Для Каждого СтрокаРазмер Из НайдСтр Цикл
				Стр_М = ТЗ_Мин.Добавить();
				Стр_М.Характеристика = СтрокаРазмер.Характеристика;
			КонецЦикла;
			ТЗ_Мин.Сортировать("Характеристика Возр");
			РазмерМин = ТЗ_Мин[0].Характеристика;
			
			ТЗ_Макс = Новый Таблицазначений;
			ТЗ_Макс.Колонки.Добавить("Характеристика");
			
			Для Каждого СтрокаРазмер Из НайдСтр Цикл
				Стр_М = ТЗ_Макс.Добавить();
				Стр_М.Характеристика = СтрокаРазмер.Характеристика;
				
			КонецЦикла;
			ТЗ_Макс.Сортировать("Характеристика Убыв");
			РазмерМакс = ТЗ_Макс[0].Характеристика;

			ОбластьСтрока.Параметры.Размер = Строка(РазмерМин) + "-" + Строка(РазмерМакс);
			ОбластьСтрока.Параметры.СтоимостьТаможенная = СтрЗаменить(Формат(ВыборкаДетальныеЗаписи.СтоимостьТаможенная,"ЧЦ=15; ЧДЦ=2; ЧРГ=''"), Символы.НПП, "");
			
			ТабличныйДокумент.Вывести(ОбластьСтрока); 
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
		
КонецФункции

// Функция - Найти цифры в строке
//
// Параметры:
//  ВходнаяСтрока	 - Строка - Строка поиска числа
// 
// Возвращаемое значение:
//   Строка - Числовое представление
//
Функция НайтиЦифрыВСтроке(ВходнаяСтрока)

	НецифровыеСимволы = СтрСоединить(СтрРазделить(ВходнаяСтрока, "1234567890,."));
	Значение = СтрСоединить(СтрРазделить(ВходнаяСтрока, НецифровыеСимволы));
	Возврат Значение;
	
КонецФункции

Функция ПолучитьВариантыКомплектацииПродукцииВКоробах(Ссылка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		
		"ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатуры.Наименование КАК Наименование,
		|	ВариантыКомплектацииНоменклатуры.Владелец КАК Владелец,
		|	ВариантыКомплектацииНоменклатуры.Количество КАК Количество,
		|	СУММА(ПриобретениеТоваровУслуг.КоличествоКоробов) КАК КоличествоКоробов,
		|	ПриобретениеТоваровУслуг.Ссылка КАК ВариантКомплектацииСсылка,
		|	ПриобретениеТоваровУслуг.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ ТоварыВариантКомплектации
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.гф_ПродукцияВКоробах КАК ПриобретениеТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		|		ПО ПриобретениеТоваровУслуг.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка
		|ГДЕ
		|	ПриобретениеТоваровУслуг.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВариантыКомплектацииНоменклатуры.Наименование,
		|	ВариантыКомплектацииНоменклатуры.Характеристика,
		|	ВариантыКомплектацииНоменклатуры.Количество,
		|	ВариантыКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента,
		|	ВариантыКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента,
		|	ВариантыКомплектацииНоменклатуры.Владелец,
		|	ПриобретениеТоваровУслуг.Ссылка,
		|	ПриобретениеТоваровУслуг.ВариантКомплектации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВариантКомплектации.Наименование КАК Наименование,
		|	ТоварыВариантКомплектации.КоличествоКоробов КАК КоличествоКоробов,
		|	ТоварыВариантКомплектации.Владелец КАК Владелец,
		|	ТоварыВариантКомплектации.Количество КАК Количество,
		|	Номенклатура.КодТНВЭД КАК КодТНВЭД,
		|	Номенклатура.Марка КАК Марка,
		|	Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Номенклатура.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
		|	Номенклатура.Ссылка КАК Ссылка,
		|	ТоварыВариантКомплектации.ВариантКомплектацииСсылка КАК ВариантКомплектацииСсылка,
		|	ТоварыВариантКомплектации.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ НоменклатураРеквизиты
		|ИЗ
		|	ТоварыВариантКомплектации КАК ТоварыВариантКомплектации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ТоварыВариантКомплектации.Владелец = Номенклатура.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВариантыКомплектацииНоменклатурыТовары.Количество) КАК КоличествоВПарах,
		|	НоменклатураРеквизиты.Наименование КАК Наименование,
		|	НоменклатураРеквизиты.КоличествоКоробов КАК КоличествоКоробов,
		|	НоменклатураРеквизиты.Владелец КАК Владелец,
		|	НоменклатураРеквизиты.КодТНВЭД КАК КодТНВЭД,
		|	НоменклатураРеквизиты.Марка КАК Марка,
		|	НоменклатураРеквизиты.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|	НоменклатураРеквизиты.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	НоменклатураРеквизиты.НаименованиеПолное КАК НаименованиеПолное,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка,
		|	НоменклатураРеквизиты.ВариантКомплектацииСсылка КАК ВариантКомплектацииСсылка,
		|	НоменклатураРеквизиты.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ ТоварыВариантКомплектацииИтог
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|		ПО НоменклатураРеквизиты.Владелец = ВариантыКомплектацииНоменклатурыТовары.Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураРеквизиты.Наименование,
		|	НоменклатураРеквизиты.КоличествоКоробов,
		|	НоменклатураРеквизиты.Владелец,
		|	НоменклатураРеквизиты.Количество,
		|	НоменклатураРеквизиты.КодТНВЭД,
		|	НоменклатураРеквизиты.Марка,
		|	НоменклатураРеквизиты.СтранаПроисхождения,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура,
		|	НоменклатураРеквизиты.ПроизводительИмпортерКонтрагент,
		|	НоменклатураРеквизиты.НаименованиеПолное,
		|	НоменклатураРеквизиты.Ссылка,
		|	НоменклатураРеквизиты.ВариантКомплектацииСсылка,
		|	НоменклатураРеквизиты.ВариантКомплектации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение,
		|	ДополнительныеРеквизитыИСведения.Имя КАК Имя,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДополнительныеСведенияМатериалыПодошвы
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|			ПО ДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ДополнительныеСведения.Значение.Ссылка = ЗначенияСвойствОбъектов.Ссылка
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведения.Объект
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Имя = &Свойство_Material_bottom
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение,
		|	ДополнительныеРеквизитыИСведения.Имя КАК Имя,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДополнительныеСведенияМатериалыВерха
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|			ПО ДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ДополнительныеСведения.Значение.Ссылка = ЗначенияСвойствОбъектов.Ссылка
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведения.Объект
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Имя = &Свойство_Material_surface
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение,
		|	ДополнительныеРеквизитыИСведения.Имя КАК Имя,
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДополнительныеСведенияТипыОбуви
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|			ПО ДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ДополнительныеСведения.Значение.Ссылка = ЗначенияСвойствОбъектов.Ссылка
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведения.Объект
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Имя = &Свойство_Пол
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НоменклатураРеквизиты.Ссылка КАК Ссылка,
		|	ДополнительныеСведенияМатериалыВерха.Значение КАК МатериалыВерха,
		|	ДополнительныеСведенияМатериалыПодошвы.Значение КАК МатериалыПодошвы,
		|	ДополнительныеСведенияТипыОбуви.Значение КАК ТипыОбуви
		|ПОМЕСТИТЬ ТоварыХарактеристика
		|ИЗ
		|	НоменклатураРеквизиты КАК НоменклатураРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДополнительныеСведенияМатериалыПодошвы КАК ДополнительныеСведенияМатериалыПодошвы
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведенияМатериалыПодошвы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДополнительныеСведенияМатериалыВерха КАК ДополнительныеСведенияМатериалыВерха
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведенияМатериалыВерха.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДополнительныеСведенияТипыОбуви КАК ДополнительныеСведенияТипыОбуви
		|		ПО НоменклатураРеквизиты.Ссылка = ДополнительныеСведенияТипыОбуви.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВариантКомплектацииИтог.КоличествоВПарах КАК КоличествоВПарах,
		|	ТоварыВариантКомплектацииИтог.Наименование КАК Артикул,
		|	ТоварыВариантКомплектацииИтог.КоличествоКоробов КАК КоличествоКоробов,
		|	ТоварыВариантКомплектацииИтог.Владелец КАК Владелец,
		|	ТоварыВариантКомплектацииИтог.КодТНВЭД КАК КодТНВЭД,
		|	ТоварыВариантКомплектацииИтог.Марка КАК Марка,
		|	ТоварыВариантКомплектацииИтог.Номенклатура КАК Номенклатура,
		|	СтраныМира.КодАльфа2 КАК СтранаПроисхождения,
		|	ТоварыВариантКомплектацииИтог.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	ТоварыВариантКомплектацииИтог.НаименованиеПолное КАК НаименованиеПолное,
		|	ОбластиДействияСертификатовНоменклатуры.СертификатНоменклатуры КАК СертификатНоменклатуры,
		|	СертификатыНоменклатуры.ДатаНачалаСрокаДействия КАК ДатаНачалаСрокаДействия,
		|	СертификатыНоменклатуры.ДатаОкончанияСрокаДействия КАК ДатаОкончанияСрокаДействия,
		|	ТоварыВариантКомплектацииИтог.Ссылка КАК Ссылка,
		|	ТоварыВариантКомплектацииИтог.ВариантКомплектацииСсылка КАК ВариантКомплектацииСсылка,
		|	ТоварыВариантКомплектацииИтог.ВариантКомплектации КАК ВариантКомплектации
		|ПОМЕСТИТЬ ТоварыХарактеристикаИтог
		|ИЗ
		|	ТоварыВариантКомплектацииИтог КАК ТоварыВариантКомплектацииИтог
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|		ПО ТоварыВариантКомплектацииИтог.СтранаПроисхождения = СтраныМира.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбластиДействияСертификатовНоменклатуры КАК ОбластиДействияСертификатовНоменклатуры
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СертификатыНоменклатуры КАК СертификатыНоменклатуры
		|			ПО ОбластиДействияСертификатовНоменклатуры.СертификатНоменклатуры = СертификатыНоменклатуры.Ссылка
		|		ПО ТоварыВариантКомплектацииИтог.Номенклатура = ОбластиДействияСертификатовНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПриобретениеТоваровУслугТовары.Количество) КАК КоличествоВПарах,
		|	ПриобретениеТоваровУслугТовары.Цена КАК Цена,
		|	СУММА(ПриобретениеТоваровУслугТовары.Сумма) КАК СтоимостьТаможенная
		|ПОМЕСТИТЬ ТоварыСтоимостьТаможенная
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Цена
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыХарактеристикаИтог.Артикул КАК Артикул,
		|	ТоварыХарактеристикаИтог.КоличествоКоробов КАК КоличествоВКоробах,
		|	ТоварыХарактеристикаИтог.Владелец КАК Владелец,
		|	ТоварыХарактеристикаИтог.КодТНВЭД КАК КодТНВЭД,
		|	ТоварыХарактеристикаИтог.Марка КАК Марка,
		|	ТоварыХарактеристикаИтог.Номенклатура КАК Номенклатура,
		|	ТоварыХарактеристикаИтог.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТоварыХарактеристикаИтог.ПроизводительИмпортерКонтрагент КАК ПроизводительИмпортерКонтрагент,
		|	ТоварыХарактеристикаИтог.НаименованиеПолное КАК НаименованиеПолное,
		|	ТоварыХарактеристикаИтог.СертификатНоменклатуры КАК СертификатНоменклатуры,
		|	ТоварыХарактеристикаИтог.ДатаНачалаСрокаДействия КАК ДатаНачалаСрокаДействия,
		|	ТоварыХарактеристикаИтог.ДатаОкончанияСрокаДействия КАК ДатаОкончанияСрокаДействия,
		|	ТоварыХарактеристика.Ссылка КАК Ссылка,
		|	ТоварыХарактеристика.МатериалыВерха КАК МатериалыВерха,
		|	ТоварыХарактеристика.МатериалыПодошвы КАК МатериалыПодошвы,
		|	ТоварыХарактеристика.ТипыОбуви КАК ТипыОбуви,
		|	ВариантыКомплектацииНоменклатуры.Товары.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Характеристика КАК Характеристика,
		|		Упаковка КАК Упаковка,
		|		КоличествоУпаковок КАК КоличествоУпаковок,
		|		ДоляСтоимости КАК ДоляСтоимости,
		|		Количество КАК Количество
		|	) КАК Товары,
		|	ТоварыСтоимостьТаможенная.КоличествоВПарах КАК КоличествоВПарах,
		|	ТоварыСтоимостьТаможенная.СтоимостьТаможенная КАК СтоимостьТаможенная
		|ИЗ
		|	ТоварыХарактеристикаИтог КАК ТоварыХарактеристикаИтог
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыХарактеристика КАК ТоварыХарактеристика
		|		ПО ТоварыХарактеристикаИтог.Номенклатура = ТоварыХарактеристика.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
		|		ПО ТоварыХарактеристикаИтог.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыСтоимостьТаможенная КАК ТоварыСтоимостьТаможенная
		|		ПО ТоварыХарактеристикаИтог.Номенклатура = ТоварыСтоимостьТаможенная.Номенклатура";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Свойство_Material_bottom", "гф_НоменклатураMaterialBottom");
		Запрос.УстановитьПараметр("Свойство_Material_surface", "гф_НоменклатураMaterialSurface");
		Запрос.УстановитьПараметр("Свойство_Пол", "гф_НоменклатураПол");
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Возврат ВыборкаДетальныеЗаписи;
		
КонецФункции

#Область ПрограммныйИнтерфейс

// #wortmann {
// #5.2.04
// стандартные подсистемы. Загрузка из внешнего файла
// Галфинд Sakovich 2022/08/02
//
// Параметры:
//   Параметры - Структура - переопределяемые параметры (необходимо, 
//                  если поддерживается загрузка в несколько табличных частей).
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
	//Если Параметры["ИмяТабличнойЧасти"] = "Документ.ПроизводствоБезЗаказа.ТабличнаяЧасть.ВыходныеИзделия" Тогда
	//	Параметры["ИмяМакетаСШаблоном"] = "ЗагрузкаИзФайлаВыходныеИзделия";
	//КонецЕсли;
	Если Параметры["ИмяТабличнойЧасти"] = "Документ.ПроизводствоБезЗаказа.ТабличнаяЧасть.гф_ПродукцияВКоробах" Тогда
		Параметры["ИмяМакетаСШаблоном"] = "ЗагрузкаИзФайлагф_ПродукцияВКоробах";
	КонецЕсли;
	
КонецПроцедуры // } #wortmann

// #wortmann {
// #5.2.04
// прикладной алгоритм поиска данных в ИБ по данным, загруженным из файла
// Галфинд Sakovich 2022/08/02
// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
// СписокНеоднозначностей содержит список неоднозначных значений, для которых в ИБ имеется несколько
// подходящих вариантов.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                                        находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки.
//       Остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                                        являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений:
//     * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность.
//     * Идентификатор - Число - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - Произвольный - любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, 
	АдресТаблицыСопоставления, 
	СписокНеоднозначностей, 
	ПолноеИмяТабличнойЧасти, 
	ДополнительныеПараметры) Экспорт

	Если ПолноеИмяТабличнойЧасти = "Документ.ПриобретениеТоваровУслуг.ТабличнаяЧасть.Товары" Тогда
		
		Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
		ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(50)) КАК Артикул,
		|	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставления
		|ИЗ
		|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Ссылка,
		|	СпрНоменклатура.Артикул КАК Артикул,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (СпрНоменклатура.Артикул = ДанныеДляСопоставления.Артикул)
		|			И (ДанныеДляСопоставления.Артикул <> """")
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|		ПО ДанныеДляСопоставления.Идентификатор = СопоставленнаяНоменклатураПоАртикулу.Идентификатор
		|ГДЕ СопоставленнаяНоменклатураПоАртикулу.Идентификатор ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(СпрНоменклатура.Ссылка) КАК Ссылка,
		|	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор,
		|	КОЛИЧЕСТВО(ДанныеДляСопоставленияПоНаименованию.Идентификатор) КАК Количество
		|ИЗ
		|	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (СпрНоменклатура.Наименование = (ВЫРАЗИТЬ(ДанныеДляСопоставленияПоНаименованию.Номенклатура КАК СТРОКА(500))))
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДляСопоставленияПоНаименованию.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор";
		
		Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
		РезультатыЗапросов = Запрос.ВыполнитьПакет(); // Массив из РезультатЗапроса
		
		ТаблицаНоменклатура = РезультатыЗапросов[3].Выгрузить(); // ТаблицаЗначений
		Для каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл
			
			Товар = Товары.Добавить();
			Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
			Товар.КоличествоУпаковок = СтрокаТаблицы.КоличествоУпаковок;
			
			СтрокаНоменклатура = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
			Если СтрокаНоменклатура <> Неопределено Тогда 
				Если СтрокаНоменклатура.Количество = 1 Тогда 
					Товар.Номенклатура = СтрокаНоменклатура.Ссылка; 
				ИначеЕсли СтрокаНоменклатура.Количество > 1 Тогда
					ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
					ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
					ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ПолноеИмяТабличнойЧасти = "Документ.ПриобретениеТоваровУслуг.ТабличнаяЧасть.гф_ПродукцияВКоробах" Тогда
		
		ТоварыВКоробах = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
		ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.IDКороба КАК СТРОКА(200)) КАК IDКороба,
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.ВариантКомплектации КАК СТРОКА(100)) КАК ВариантКомплектации,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставления
		|ИЗ
		|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпрВариантыКомплектацииНоменклатуры.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставлениеВариантыКомплектацииПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК СпрВариантыКомплектацииНоменклатуры
		|		ПО (СпрВариантыКомплектацииНоменклатуры.Наименование = ДанныеДляСопоставления.ВариантКомплектации)
		|			И (ДанныеДляСопоставления.ВариантКомплектации <> """")
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокУпаковочныйЛист.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставлениеУпаковочныеЛистыПоКоду
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УпаковочныйЛист КАК ДокУпаковочныйЛист
		|		ПО (ДокУпаковочныйЛист.Код = ДанныеДляСопоставления.IDКороба)
		|			И (ДанныеДляСопоставления.IDКороба <> """")
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(СопоставлениеВариантыКомплектацииПоНаименованию.Ссылка) КАК Ссылка,
		|	СопоставлениеВариантыКомплектацииПоНаименованию.Идентификатор КАК Идентификатор,
		|	КОЛИЧЕСТВО(СопоставлениеВариантыКомплектацииПоНаименованию.Идентификатор) КАК Количество
		|ИЗ
		|	СопоставлениеВариантыКомплектацииПоНаименованию КАК СопоставлениеВариантыКомплектацииПоНаименованию
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставлениеВариантыКомплектацииПоНаименованию.Идентификатор
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(СопоставлениеУпаковочныеЛистыПоКоду.Ссылка) КАК Ссылка,
		|	СопоставлениеУпаковочныеЛистыПоКоду.Идентификатор КАК Идентификатор,
		|	КОЛИЧЕСТВО(СопоставлениеУпаковочныеЛистыПоКоду.Идентификатор) КАК Количество
		|ИЗ
		|	СопоставлениеУпаковочныеЛистыПоКоду КАК СопоставлениеУпаковочныеЛистыПоКоду
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставлениеУпаковочныеЛистыПоКоду.Идентификатор";
		
		Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
		РезультатыЗапросов = Запрос.ВыполнитьПакет(); // Массив из РезультатЗапроса
		
		ТаблицаВариантКомплектации = РезультатыЗапросов[3].Выгрузить(); // ТаблицаЗначений
		ТаблицаУпаковочныхЛистов = РезультатыЗапросов[4].Выгрузить(); // ТаблицаЗначений
		
		Для каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл
			
			Товар = ТоварыВКоробах.Добавить();
			Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
			Товар.КоличествоКоробов = СтрокаТаблицы.КоличествоКоробов;
			
			СтрокаВариантКомплектации = ТаблицаВариантКомплектации.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
			Если СтрокаВариантКомплектации <> Неопределено Тогда 
				Если СтрокаВариантКомплектации.Количество = 1 Тогда 
					Товар.ВариантКомплектации = СтрокаВариантКомплектации.Ссылка; 
				КонецЕсли;
				Если СтрокаВариантКомплектации.Количество > 1 Тогда
					ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
					ЗаписьОНеоднозначности.Идентификатор = СтрокаВариантКомплектации.Идентификатор;
					ЗаписьОНеоднозначности.Колонка = "ВариантКомплектации";
				КонецЕсли;
			КонецЕсли;
			
			СтрокаИДКороба = ТаблицаУпаковочныхЛистов.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
			Если СтрокаИДКороба <> Неопределено Тогда 
				Если СтрокаИДКороба.Количество = 1 Тогда 
					Товар.IDКороба = СтрокаИДКороба.Ссылка;
				КонецЕсли;	
				Если СтрокаИДКороба.Количество > 1 Тогда
					ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
					ЗаписьОНеоднозначности.Идентификатор = СтрокаИДКороба.Идентификатор;
					ЗаписьОНеоднозначности.Колонка = "IDКороба";
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // } #wortmann

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   СписокНеоднозначностей    - Массив из СправочникСсылка._ДемоНоменклатура - массив для заполнения
//   с неоднозначными данными.
//   ИмяКолонки                - Строка - имя колонки, в который возникла неоднозначность.
//   ЗагружаемыеЗначенияСтрока - Строка - загружаемые данные на основании которых возникла неоднозначность.
//   ДополнительныеПараметры   - Произвольный - любые дополнительные сведения.
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, 
	СписокНеоднозначностей, 
	ИмяКолонки, 
	ЗагружаемыеЗначенияСтрока, 
	ДополнительныеПараметры) Экспорт
	
	Если ПолноеИмяТабличнойЧасти = "Документ.ПроизводствоБезЗаказа.ТабличнаяЧасть.ВыходныеИзделия" Тогда
		Если ИмяКолонки = "Номенклатура" Тогда
			Запрос = Новый Запрос;
			
			ТекстГде = "";
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда
				ТекстГде = "ГДЕ Номенклатура.Наименование = &Наименование";
				Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда
				Если ЗначениеЗаполнено(ТекстГде) Тогда
					ТекстГде = ТекстГде + " ИЛИ _ДемоНоменклатура.Артикул = &Артикул";
				Иначе
					ТекстГде = "Номенклатура.Артикул = &Артикул";
				КонецЕсли;
				Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
			КонецЕсли;
			
			Запрос.Текст = "ВЫБРАТЬ
			|	Номенклатура.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура " + ТекстГде;
			
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СписокНеоднозначностей.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ПолноеИмяТабличнойЧасти = "Документ.ПроизводствоБезЗаказа.ТабличнаяЧасть.гф_ПродукцияВКоробах" Тогда
		Если ИмяКолонки = "ВариантКомплектации" Тогда
			Запрос = Новый Запрос;
			
			ТекстГде = "";
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.ВариантКомплектации) Тогда
				ТекстГде = "ГДЕ ВариантКомплектации.Наименование = &Наименование";
				Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.ВариантКомплектации);
			КонецЕсли;
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВариантыКомплектации.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектации " + ТекстГде;
			
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СписокНеоднозначностей.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
			КонецЦикла;
		КонецЕсли;
		
		Если ИмяКолонки = "IDКороба" Тогда
			Запрос = Новый Запрос;
			
			ТекстГде = "";
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.ВариантКомплектации) Тогда
				ТекстГде = "ГДЕ УпаковочныйЛист.Код = &Код";
				Запрос.УстановитьПараметр("Код", ЗагружаемыеЗначенияСтрока.IDКороба);
			КонецЕсли;
			
			Запрос.Текст = "ВЫБРАТЬ
			|	УпаковочныйЛист.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.УпаковочныйЛист КАК УпаковочныйЛист " + ТекстГде;
			
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СписокНеоднозначностей.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
			КонецЦикла;
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

// #wortmann {
// Галфинд Sakovich 2022/11/30
// Возвращает признак того, что в таблице есть маркируемая продукция (Обувная продукция)
// Параметры:
// 		Таблица - <ТаблицаЗначений> - Таблица значений с обязательной колонкой "Номенклатура"
// 			*Номенклатура - тип: Справочник.Номенклатура
Функция гф_ОпределитьНаличиеМаркируемойПродукции(Таблица) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	т.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&тзТовары КАК т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	|		ПО Товары.Номенклатура = спрНоменклатура.Ссылка
	|ГДЕ
	|	спрНоменклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция)");
	
	Запрос.УстановитьПараметр("тзТовары", Таблица);
	Результат = Запрос.Выполнить();
	ЕстьМаркируемаяПродукция = Не Результат.Пустой();
	Возврат ЕстьМаркируемаяПродукция;

КонецФункции // } #wortmann

// #wortmann {
// Галфинд Volkov 2022/12/02
// Возвращает таблицу с размеропи по комплектации
// Параметры:
// 		ДокументСсылка - Документ.ПриобретениеТоваровУслуг - Ссылка на документ.
Функция гф_ПолучитьРазмерПоВариантыКомплектацииНоменклатуры(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВариантыКомплектацииНоменклатуры.Наименование КАК Наименование,
	|	ВариантыКомплектацииНоменклатуры.Владелец КАК Владелец,
	|	ВариантыКомплектацииНоменклатуры.Количество КАК Количество,
	|	СУММА(ПриобретениеТоваровУслуг.КоличествоКоробов) КАК КоличествоКоробов,
	|	ПриобретениеТоваровУслуг.Ссылка КАК ВариантКомплектацииСсылка,
	|	ПриобретениеТоваровУслуг.ВариантКомплектации КАК ВариантКомплектации
	|ПОМЕСТИТЬ ТоварыВариантКомплектации
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.гф_ПродукцияВКоробах КАК ПриобретениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ПриобретениеТоваровУслуг.ВариантКомплектации = ВариантыКомплектацииНоменклатуры.Ссылка
	|ГДЕ
	|	ПриобретениеТоваровУслуг.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВариантыКомплектацииНоменклатуры.Наименование,
	|	ВариантыКомплектацииНоменклатуры.Характеристика,
	|	ВариантыКомплектацииНоменклатуры.Количество,
	|	ВариантыКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента,
	|	ВариантыКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента,
	|	ВариантыКомплектацииНоменклатуры.Владелец,
	|	ПриобретениеТоваровУслуг.Ссылка,
	|	ПриобретениеТоваровУслуг.ВариантКомплектации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыВариантКомплектации.ВариантКомплектации КАК ВариантКомплектации,
	|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика
	|ИЗ
	|	ТоварыВариантКомплектации КАК ТоварыВариантКомплектации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
	|		ПО ТоварыВариантКомплектации.ВариантКомплектации = ВариантыКомплектацииНоменклатурыТовары.Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаРезультат = РезультатЗапроса.Выгрузить();
	
	Возврат ВыборкаРезультат;

КонецФункции

#КонецОбласти

#КонецЕсли
