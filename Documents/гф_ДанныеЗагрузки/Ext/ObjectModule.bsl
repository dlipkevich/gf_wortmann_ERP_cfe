#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных
Перем НаборЗаписейТэгов; // РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
// Перем ЗагруженныеСтроки; // ТЗ с выгруженными данными из РегистрыСведений.гф_СтрокиДокументаДанныеЗагрузки
Перем ТекущийРежимЗаписи; // режим записи документа
Перем ПараметрыДокументыСоСтатусомИзменениеИзИ5; // ТЗ со значениями реквизитов из Документ.ЗаказКлиента
Перем ТЧДокументаДоСтатусаИзменениеИзИ5; // ТЗ со значениями ТЧ из Документ.ЗаказКлиента
Перем НомерОшибки; // число (для записи в РС гф_ОшибкиЗагрузки)
Перем ТекстыОшибок; // ТЗ для записи в РС гф_ОшибкиЗагрузки
Перем СобытиеЖурналаРегистрации; // событие журнала регистрации
Перем СтавкаНДС; // Значение для документа ЗаказКлиента
Перем СтавкаНДС20; // ставка НДС по умолчанию
Перем ФункцияДокумента; // Функция документа ORDRSP для ЗаказКлиента
Перем пар_НоменклатураАртикулПрошлый; // для проверки Артикула при Записи Цен
Перем ЗагруженоСОшибками; // для смены статуса у документа
#КонецОбласти

#Область ПрограммныйИнтерфейс

// #wortmann { 
// Экспортная процедура по созданию номенклатуры и установки цен 
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	Отказ - параметр системы 
//	СоздаватьНоменклатуру - Булево - признак создания новой номенклатурной позиции
//	СоздаватьУстановкуЦен - Булево - признак создания установки цен
//
Процедура СоздатьНоменклатуру(Отказ = Ложь, СоздаватьНоменклатуру = Истина, СоздаватьУстановкуЦен = Ложь) Экспорт
	
	СозатьТаблицуТекстыОшибок();
	
	Если НЕ Интерфейс = Перечисления.гф_Интерфейсы.PRICAT Тогда
		Возврат;	
	КонецЕсли;
	
	СобытиеЖурналаРегистрации = "ЗагрузкаИзI5." + Интерфейс;
	
	ПолучитьНаборЗаписейРегистраСтрок();
	
	Если СоздаватьНоменклатуру Тогда
		// BSLLS:Typo-off
		ПроведениеИнтерфейсаPricat(НаборЗаписейТэгов, Отказ, Истина);
		// BSLLS:Typo-on
		Если ЗагруженоСОшибками = Истина Тогда
			СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.ЗагруженоСОшибками; 
			ОбменДанными.Загрузка = Истина; // ++ Галфинд Домнышева 2023/08/29
		ИначеЕсли НЕ Отказ Тогда
			СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.НоменклатураЗагружена;
		Иначе
			СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.Загружено; // Для правильного синтаксиса
		КонецЕсли;
	КонецЕсли;
	
	СтруктураЦен = ПолучитьСтруктуруЦен();
	
	Если СоздаватьУстановкуЦен И СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.НоменклатураЗагружена 
		И СтруктураЦен <> Неопределено Тогда
		СозданиеПроведениеУстановкиЦен(НаборЗаписейТэгов, СтруктураЦен, Отказ);
		Если НЕ Отказ Тогда
			СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.СозданыЦеныНоменклатуры;
		КонецЕсли;
	КонецЕсли;
	
	//Если ТекстыОшибок.Количество() > 0 Тогда
	//	ЗаписатьОшибку(ТекстыОшибок);
	//КонецЕсли;
	Если ТекстыОшибок.Количество() > 0 Тогда
		
		ДополнительныеСвойства.Вставить("ТекстыОшибок", ТекстыОшибок);
		
		Если Не ДополнительныеСвойства.Свойство("ОбработкаЗагрузкаИзI5") Тогда
			Документы.гф_ДанныеЗагрузки.ЗаписатьОшибки(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

#КонецОбласти

#Область ОбработчикиСобытий

&Вместо("ОбработкаПроведения")
Процедура гф_ОбработкаПроведения(Отказ, РежимПроведения)
    // #wortmann { 
	// Внесение изменений в обработку проведения, для проведения документов с интерфейсом PRICAT согласно
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ecf78ee2ff45f8
	// Галфинд_Домнышева 2022/09/12
	Если Интерфейс = Перечисления.гф_Интерфейсы.PRICAT Тогда
		Если НЕ (СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.НоменклатураЗагружена
			ИЛИ СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.СозданыЦеныНоменклатуры) Тогда
			Отказ = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// } #wortmann

КонецПроцедуры

&Вместо("ПередЗаписью")
Процедура гф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	// #wortmann { 
	// Внесение изменений в процедуру перед записью согласно
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ecf78ee2ff45f8
	// Галфинд_Домнышева 2022/09/12
    
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;            
	
	ТекущийРежимЗаписи = РежимЗаписи;
	// } #wortmann

КонецПроцедуры

&Вместо("ПриЗаписи")
Процедура гф_ПриЗаписи(Отказ)
	
	// #wortmann { 
	// Внесение изменений в процедуру при записи согласно
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ecf78ee2ff45f8
	// Галфинд_Домнышева 2022/09/12
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	Если ТекущийРежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Интерфейс) Тогда
		СобытиеЖурналаРегистрации = "ЗагрузкаИзI5." + Интерфейс;
	Иначе
		СобытиеЖурналаРегистрации = "ЗагрузкаИзI5";
	КонецЕсли;
  	
	Если ЭтотОбъект.СтатусДокумента = Перечисления.гф_СтатусыДокументовЗагрузки.ЗагруженоСОшибками Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, 
		"Документ " + ЭтотОбъект + " имеет статус ""Загружено с ошибками""! Документ не проведен.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СозатьТаблицуТекстыОшибок();
	// } #wortmann
	
	ПолучитьНаборЗаписейРегистраСтрок();
	
	Если Интерфейс <> Перечисления.гф_Интерфейсы.PRICAT Тогда
		
		//НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		
		Попытка
			
			Если Интерфейс = Перечисления.гф_Интерфейсы.ORDRSP Тогда
				
				ОтказНоНеТот = Отказ;
				ПроведениеИнтерфейсаORDRSP(НаборЗаписейТэгов, ОтказНоНеТот);
				
				Если ОтказНоНеТот Тогда
					
					Если ДополнительныеСвойства.Свойство("ОбработкаЗагрузкаИзI5") Тогда
						ДополнительныеСвойства.Вставить("ОтказОтПроведения");
					Иначе
						Отказ = Истина;	
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли Интерфейс = Перечисления.гф_Интерфейсы.PRICAT_SORT Тогда
				СоздатьВариантКомплектации(НаборЗаписейТэгов, Отказ);
				
			Иначе
				Возврат;
			КонецЕсли;
			
			//ЗафиксироватьТранзакцию();
			
		Исключение
			
			//ОтменитьТранзакцию(); 
			
			Отказ = Истина;
			
			СообщениеОбОшибке = "Произошли ошибки при проведении документа " + ЭтотОбъект + ".|" + ОписаниеОшибки();
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
			
		КонецПопытки;
	КонецЕсли;
	
	// #wortmann { 
	// Загружаем в ДополнительныеСвойства ТекстыОшибок для дальнейшей загрузки текстов ошибки в РС гф_ОшибкиЗагрузки 
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ecf78ee2ff45f8
	// Галфинд_Домнышева 2022/10/14
	Если ТекстыОшибок.Количество() > 0 Тогда
		
		ДополнительныеСвойства.Вставить("ТекстыОшибок", ТекстыОшибок);
		
		Если Не ДополнительныеСвойства.Свойство("ОбработкаЗагрузкаИзI5") Тогда
			Документы.гф_ДанныеЗагрузки.ЗаписатьОшибки(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
// #wortmann { 
// Проведение документов с интерфейсом ORDRSP:
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//  ТабличнаяЧасть - ТаблицаЗначений - выгрузка РС гф_СтрокиДокументаДанныеЗагрузки по данному Объекту.
//  Отказ - параметр системы
// BSLLS:LatinAndCyrillicSymbolInWord-off
Процедура ПроведениеИнтерфейсаORDRSP(ТабличнаяЧасть, Отказ)
// BSLLS:LatinAndCyrillicSymbolInWord-on	

	НовыйДокумент = Неопределено;
	
	СтавкаНДСПоУмолчанию = СтавкаНДС20;
	СтавкаНДС = СтавкаНДСПоУмолчанию;
   	ЦенаВключаетНДС = Истина;
	ФлагЗаписиДокумента = Истина;
	
	ОписаниеСтроки = Новый ОписаниеТипов("Строка");
	ТаблицаРасхождений = Новый ТаблицаЗначений;
	ТаблицаРасхождений.Колонки.Добавить("Реквизит", ОписаниеСтроки);
	ТаблицаРасхождений.Колонки.Добавить("ЗначениеТекущее", ОписаниеСтроки);
	ТаблицаРасхождений.Колонки.Добавить("ЗначениеИзИ5", ОписаниеСтроки);
	
	ОписаниеЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	ОписаниеВариант = Новый ОписаниеТипов("СправочникСсылка.ВариантыКомплектацииНоменклатуры");
	ОписаниеДата = Новый ОписаниеТипов("Дата");

	ТаблицаЗначенийЦенИзИ5 = Новый ТаблицаЗначений;
	ТаблицаЗначенийЦенИзИ5.Колонки.Добавить("ЦенаКоробаИз5", ОписаниеЧисло);
	ТаблицаЗначенийЦенИзИ5.Колонки.Добавить("ВариантКомплектации", ОписаниеВариант);
	ТаблицаЗначенийЦенИзИ5.Колонки.Добавить("ДатаДоставки", ОписаниеДата);
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	ПроходимЦиклПоТЭГ(НовыйДокумент, ТабличнаяЧасть, ТекущаяДата, ТаблицаЗначенийЦенИзИ5, Отказ, ФлагЗаписиДокумента);
	
	Если ФлагЗаписиДокумента И НовыйДокумент <> Неопределено И НЕ Отказ Тогда
		
		РассчитатьСкидкуВКоробах(НовыйДокумент, ТаблицаЗначенийЦенИзИ5, ТаблицаРасхождений); 
		
		//ПересчетТЧТовары(НовыйДокумент, ТаблицаЗначенийЦенИзИ5);
		Если НовыйДокумент.ДополнительныеСвойства.Свойство("гф_ОбработкаОтгрузкаПоЗаказам") Тогда
			НовыйДокумент.ДополнительныеСвойства.Удалить("гф_ОбработкаОтгрузкаПоЗаказам");	
		КонецЕсли;
		
		НовыйДокумент.ЦенаВключаетНДС = Ложь;
		
		// Ставим флаг "СкидкиРасчитаны" для записи документа, без расчитанных скидок запись не проходит.			
		НовыйДокумент.СкидкиРассчитаны = Истина;
		
		НовыйДокумент.ОбменДанными.Загрузка = Ложь;
		
		Попытка
			
			Если Не НовыйДокумент.ПометкаУдаления И Не НовыйДокумент.ДополнительныеСвойства.Свойство("НеПроводить") Тогда
				НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			Иначе
				НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли; 
			
			ЗаполнитьИзмененияЗначений(ТаблицаРасхождений, НовыйДокумент, ТекущаяДата);
			
		Исключение
			СообщениеОбОшибке = "Произошли ошибки при проведении документа  " + НовыйДокумент
			+ " не проведен." + ОписаниеОшибки(); 
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		КонецПопытки;
		
	ИначеЕсли НовыйДокумент <> Неопределено И НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ИзменениеИзИ5 
			И НЕ Отказ Тогда
			
		ТаблицаСравненийДляЗаписиВРегистр = СравнитьДокументы(НовыйДокумент); 
		
		Для Каждого Строка Из ТаблицаСравненийДляЗаписиВРегистр Цикл 
			ЗаполнитьЗначенияСвойств(ТаблицаРасхождений.Добавить(), Строка); 
		КонецЦикла;
		
		РассчитатьСкидкуВКоробах(НовыйДокумент.Ссылка, ТаблицаЗначенийЦенИзИ5, ТаблицаРасхождений); // Галфинд_ДомнышеваКР_12_04_2024
		
		ЗаполнитьИзмененияЗначений(ТаблицаРасхождений, НовыйДокумент, ТекущаяДата);
		
	ИначеЕсли НЕ ФлагЗаписиДокумента И НовыйДокумент <> Неопределено И НЕ Отказ 
		И НЕ НовыйДокумент.ПометкаУдаления Тогда
		Сообщение = " В найденный документ " + НовыйДокумент.Ссылка + 
		" не внесены изменения, обновлен только комментарий.";
		ЭтотОбъект.Комментарий = ЭтотОбъект.Комментарий + Сообщение;
		ДобавитьСтрокуВТекстыОшибки(Сообщение, "Важная информация");
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Обход строк ТЧ РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки для Проведения документов с интерфейсом ORDRSP:
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ (в начале Неопределено)
//  ТабличнаяЧасть - ТаблицаЗначений - выгрузка РС гф_СтрокиДокументаДанныеЗагрузки по данному Объекту.
//	ТекущаяДата - Дата - единая дата для записей в РС
//	ТаблицаЗначенийЦенИзИ5 - ТаблицаЗначений - содержит колонки "ВариантКомплектации", "ЦенаКоробаИзИ5".  
//  Отказ - параметр системы 
//	ФлагЗаписиДокумента - Булево
Процедура ПроходимЦиклПоТЭГ(НовыйДокумент, ТабличнаяЧасть, ТекущаяДата, ТаблицаЗначенийЦенИзИ5,
							Отказ, ФлагЗаписиДокумента) 
							
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл // Цикл по тегам XML
		
		Если Отказ Тогда
			//Прервать;
			ФлагЗаписиДокумента = Ложь;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.ПолеЗагрузки) Тогда
			
			// Тэг создания нового документа
			Если СтрНайти(СтрокаТЧ.Тэг, "Message_Header") > 0 Тогда
				
				ОпределяемШапкуЗаказаКлиента(СтрокаТЧ, НовыйДокумент, ФлагЗаписиДокумента, ТекущаяДата, Отказ);
				
			// тэг добавления позиции в тч Товары	
			ИначеЕсли СтрНайти(СтрокаТЧ.Тэг, "Message_Position") > 0  Тогда
				НовыйЭлементТоварыВКоробах = НовыйДокумент.гф_ТоварыВКоробах.Добавить();
				
			// остальные тэги	
			Иначе 
				Если Не СтрокаТЧ.ПолеЗагрузки.ЭтоГруппа И СтрокаТЧ.ПолеЗагрузки.ТипОбъекта = "Заказ клиента"
					И НовыйДокумент <> Неопределено И НЕ НовыйДокумент.ПометкаУдаления Тогда
					ЗаполнитьРеквизитыДокумента(СтрокаТЧ, НовыйДокумент, НовыйЭлементТоварыВКоробах, ФлагЗаписиДокумента,
												ТаблицаЗначенийЦенИзИ5, Отказ);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;	 // Цикл по тегам XML
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Процедура вызывает процедуру по созданию документа ЗаказКлиента и 
// заполняеет его основные реквизиты.
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ (в начале Неопределено)
//	ФлагЗаписиДокумента - Булево
//	ТекущаяДата - Дата - единая дата для записей в РС
//  Отказ - параметр системы 
Процедура ОпределяемШапкуЗаказаКлиента(СтрокаТЧ, НовыйДокумент, ФлагЗаписиДокумента, ТекущаяДата, Отказ)
	
	НомерСтрокиРодителя = СтрокаТЧ.НомерСтрокиРодителя;
	
	ИмяЗаказа = НайтиИмяЗаказа("Customer_order_number", СтрокаТЧ);
	
	Если НЕ НовыйДокумент = Неопределено Тогда
		СменаСтатусовДокумента(НовыйДокумент, Отказ);
	КонецЕсли;
	
	НомерДокумента = НайтиСтрокуДереваПоТэгуОтПорядкового("Supplier_order_number", Отказ, СтрокаТЧ);
	ДатаДокумента = НайтиСтрокуДереваПоТэгуОтПорядкового("Supplier_order_date", Отказ, СтрокаТЧ);
	ФункцияДокумента = НайтиСтрокуДереваПоТэгуОтПорядкового("Document_function", Отказ, СтрокаТЧ);
	
	Если ЗначениеЗаполнено(НомерДокумента) И ЗначениеЗаполнено(ДатаДокумента) Тогда
		
		НовыйДокумент = ИщемИСоздаемНовыйДокумент(НомерДокумента, ДатаДокумента, 
			ТекущаяДата, СтрокаТЧ, ФлагЗаписиДокумента, Отказ);
		
		Если НовыйДокумент = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
	Иначе
		СообщениеОбОшибке = СформироватьСообщение(НомерДокумента, ДатаДокумента);
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ФлагЗаписиДокумента Тогда
		
		Если ЗначениеЗаполнено(ИмяЗаказа) Тогда
			НовыйДокумент.гф_ИмяЗаказа = ИмяЗаказа;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Функция ищет или создает документ ЗаказКлиента
// Если ТЭГ "Document_function" = 46, создается новый документ ЗаказКлиента
// Если ТЭГ "Document_function" = 4, вносятся изменения в ранее созданный документ
// Если ТЭГ "Document_function" = 27, ранее созданный документ помечается на удаление
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НомерДокумента - Строка
//	ДатаДокумента - Дата
//	ТекущаяДата - Дата - единая дата для записей в РС
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//	ФлагЗаписиДокумента - Булево
//  Отказ - параметр системы
//
// Возвращаемое значение:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ 
//	Неопределено - если документ есть в системе, но в файле XML указано не верное значение ТЭГ "Document_function" 
Функция ИщемИСоздаемНовыйДокумент(НомерДокумента, ДатаДокумента, ТекущаяДата, СтрокаТЧ, 
	ФлагЗаписиДокумента, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Документы.Ссылка КАК Документ
	|ИЗ
	|	Документ.ЗаказКлиента КАК Документы
	|ГДЕ
	|	Документы.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|	И Документы.Номер = &Номер
	|	И Документы.Организация = &Организация";
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Дата(ДатаДокумента)));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Дата(ДатаДокумента)));
	Запрос.УстановитьПараметр("Номер", НомерДокумента);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	// Найден ранее созданный документ ЗаказПокупателя...
	Если Результат.Следующий() Тогда
		
		НовыйДокумент = Результат.Документ.ПолучитьОбъект();
		НовыйДокумент.гф_ДатаОбновленияИзИ5 = Дата(НайтиСтрокуДереваПоТэгу("Document_date", Отказ)); 
		
		НовыйДокумент.ОбменДанными.Загрузка = Истина;
		
		ПроверкаОтгруженныхТоваров(НовыйДокумент, ФлагЗаписиДокумента);
		
		Если ФлагЗаписиДокумента Тогда
			
			Если ИзменяемСтатусНайденногоДокумента(НовыйДокумент, ФлагЗаписиДокумента) Тогда
				НовыйДокумент.ДополнительныеСвойства.Вставить("гф_ОбработкаОтгрузкаПоЗаказам", Истина);
				НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
			
			Если СокрЛП(ФункцияДокумента) = "27" Тогда 
				
				ПроверимФункциюДокументаДляУдаления(НовыйДокумент, СтрокаТЧ, ФлагЗаписиДокумента);
				
				
			ИначеЕсли СокрЛП(ФункцияДокумента) =  "46" ИЛИ СокрЛП(ФункцияДокумента) = "4" Тогда
				
				ПервоначальныеДействияСНайденнымДокументом(НовыйДокумент, ФлагЗаписиДокумента, Отказ);
				
			Иначе
				// BSLLS:Typo-off
				СообщениеОбОшибке = "Произошли ошибки при проведении документа " + НовыйДокумент 
				+ ".|Неверное значение тэга Document_function.";
				// BSLLS:Typo-on
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		// ЗаказПокупателя не найден - создаем новый...	
	Иначе
		
		// ++ Галфинд_ДомнышеваКР_22_02_2024
		Если СокрЛП(ФункцияДокумента) = "27" Тогда
			СообщениеОбОшибке = "Документ с номером " + НомерДокумента + " и датой " + ДатаДокумента +
			" по Организации " + Организация + " не найден. Так как файл имеет функцию ""27"""
			+ " - пометка удаления, документ не будет создан."; 
			
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
						
			ФлагЗаписиДокумента = Ложь;
			Возврат Неопределено;
		КонецЕсли;
		// -- Галфинд_ДомнышеваКР_22_02_2024
		
		НовыйДокумент = Документы.ЗаказКлиента.СоздатьДокумент();
		НовыйДокумент.Номер = НомерДокумента;
		НовыйДокумент.Дата = ДатаДокумента;
		// ++ Галфинд_ДомнышеваКР_24_03_2023
		// НовыйДокумент.гф_ДатаОбновленияИзИ5 = ТекущаяДатаСеанса();
		НовыйДокумент.гф_ДатаОбновленияИзИ5 = Дата(НайтиСтрокуДереваПоТэгу("Document_date", Отказ)); 
		// -- Галфинд_ДомнышеваКР_24_03_2023
		
		НовыйДокумент.Организация = Организация;
		
		//НовыйДокумент.гф_ВидЦены =
		//	_омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ГлобальныеЗначенияОптоваяЗакупочнаяЦена");
		
		НовыйДокумент.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
		НовыйДокумент.гф_СезонныйЗаказ = Истина;
		НовыйДокумент.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
		
		НовыйДокумент.Валюта = _омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ВалютаЗагрузкаИзИ5");
		
		ЗаполнитьСкладДляЗаказаКлиента(НовыйДокумент);
		
		НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ТребуетсяПодтверждение;
		НовыйДокумент.Статус = Перечисления.СтатусыЗаказовКлиентов.КОбеспечению; // изм. от _01_04_24_Галфинд_ДомнышеваКР
		
		НовыйДокумент.Приоритет = _омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ПриоритетЗаказКлиента");
		
		ПроверимФункциюДокументаДляУдаления(НовыйДокумент, СтрокаТЧ, ФлагЗаписиДокумента);
		
	КонецЕсли;
	
	Возврат НовыйДокумент; 
	
КонецФункции// } #wortmann 

// #wortmann { 
// Процедура проверяет наличие в найденном документе в ТЧ Товары отмененных товаров
// Галфинд_Домнышева 2022/03/03
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//  ФлагЗаписиДокумента - Булево
//  Отказ - параметр системы
Процедура ПервоначальныеДействияСНайденнымДокументом(НовыйДокумент, ФлагЗаписиДокумента, Отказ) 
	
	Если ЕстьПроведенныеПодчиненныеДокументы(НовыйДокумент.Ссылка)  Тогда
		
		СообщениеОбОшибке = "При проведении документа " + ЭтотОбъект + ", обнаружены проведенные подчиненные документы для "
			+ СокрЛП(НовыйДокумент) + ". Заказ не откорректирован!"; 
			
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		
		Отказ = Истина;
		ФлагЗаписиДокумента = Ложь;
		//НовыйДокумент = Неопределено;
		
		Возврат;
		
	КонецЕсли;
	
	// Галфинд_ДомнышеваКР_12_04_2024
	// Убрана проверка на ФлагЗаписиДокумента, чтобы формировалась ТЧ новыми данными из И5, для отправки уведомлений, 
	// документ все равно не будет записан.
	//Если ФлагЗаписиДокумента Тогда 
		
		НовыйДокумент.Товары.Очистить();
		НовыйДокумент.гф_ТоварыВКоробах.Очистить();
		НовыйДокумент.Статус = Перечисления.СтатусыЗаказовКлиентов.КОбеспечению; // изм. от _01_04_24_Галфинд_ДомнышеваКР
		НовыйДокумент.ПометкаУдаления = Ложь;
		
		//НовыйДокумент.гф_ДатаОбновленияИзИ5 = ТекущаяДатаСеанса(); // от _01_02_23_Галфинд_ДомнышеваКР
		НовыйДокумент.гф_ДатаОбновленияИзИ5 = Дата(НайтиСтрокуДереваПоТэгу("Document_date", Отказ)); // ДомнышеваК_24_03_2023 
		// ++ Галфинд_ДомнышеваКР_12_04_2024
		// Необходимо перезаполнять Склад, если он не заполнен ранее и очищать ВидЦены если заполнено(в старых документах)
		ЗаполнитьСкладДляЗаказаКлиента(НовыйДокумент); // Галфинд_ДомнышеваКР_12_04_2024
		Если ЗначениеЗаполнено(НовыйДокумент.гф_ВидЦены) Тогда
			 НовыйДокумент.гф_ВидЦены = Справочники.ВидыЦен.ПустаяСсылка();
		КонецЕсли;
		// --Галфинд_ДомнышеваКР_12_04_2024
		//НовыйДокумент.Комментарий = "";
		
		//НовыйДокумент.ДополнительныеСвойства.Вставить("гф_ОбработкаОтгрузкаПоЗаказам", Ложь);
		//НовыйДокумент.ОбменДанными.Загрузка = Истина;
		//НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		//НовыйДокумент.ОбменДанными.Загрузка = Ложь;
		//
		//СообщениеОбОшибке = "При проведении документа " + ЭтотОбъект + " Откорректирован документ " + СокрЛП(НовыйДокумент);
		//ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Информация, ЭтотОбъект, ЭтотОбъект, 
		//	СообщениеОбОшибке);
		
	//КонецЕсли; 
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Процедура проверяет наличие в найденном документе в ТЧ Товары отмененных товаров
// Галфинд_Домнышева 2022/02/28
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//  ФлагЗаписиДокумента - Булево
Процедура ПроверкаОтгруженныхТоваров(НовыйДокумент, ФлагЗаписиДокумента) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказКлиентаТовары.НомерСтроки КАК НомерСтроки,
	|	"", в табличной части Товары содержатся строки с Отменой."" КАК Комментарий
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка = &Ссылка
	|	И ЗаказКлиентаТовары.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказКлиентаТовары.НомерСтроки,
	|	"", в табличной части Товары есть строки с действием Отгрузить.""
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка = &Ссылка
	|	И ЗаказКлиентаТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказКлиентаТовары.НомерСтроки,
	|	"", в табличной части Товары есть строки с действием Добалено по причине.""
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка = &Ссылка
	|	И ЗаказКлиентаТовары.гф_ДобавленоПоПричине";
	
	Запрос.УстановитьПараметр("Ссылка", НовыйДокумент.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		НовыйДокумент.Комментарий = НовыйДокумент.Комментарий + Символы.ПС + "Изменения из I5 не внесены";
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НовыйДокумент.Комментарий = НовыйДокумент.Комментарий + Выборка.Комментарий;
		КонецЦикла;
		
		НовыйДокумент.ДополнительныеСвойства.Вставить("гф_ОбработкаОтгрузкаПоЗаказам", Истина);
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		
		ФлагЗаписиДокумента = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Находит соответствие организации по названию папки с файлами для загрузки 
// Галфинд_Домнышева 2022/11/28
// 
// Параметры:
//	ПолноеИмя - Строка - полное имя Файла или путь папки с файлами.
//
// Возвращаемое значение:
//	СправочникСсылка.Организации - Ссылка на Организацию если соответствие найдено,
//	Неопределено - если соответствие не найдено
Функция ОрганизацияПоПапке(ПолноеИмя)
	
	Если СтрНайти(ПолноеИмя, "WORTMANN") Тогда
		ИНН = "7705855199";
	ИначеЕсли СтрНайти(ПолноеИмя, "CAPRICE") Тогда
		ИНН = "7705856435";
	ИначеЕсли СтрНайти(ПолноеИмя, "VENDEL") Тогда
		ИНН = "7705993537";
	ИначеЕсли СтрНайти(ПолноеИмя, "SHOE") Тогда
		ИНН = "7705993544";
	ИначеЕсли СтрНайти(ПолноеИмя, "JANA") Тогда
		ИНН = "7705993551";
	Иначе                                               
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ИНН = &ИНН";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не РезультатЗапроса.Пустой() Тогда
	Выборка.Следующий(); 
    Возврат Выборка.Ссылка;
	КонецЕсли;

КонецФункции// } #wortmann

// #wortmann { 
// Процедура помечает документ на удаление в случае если ТЭГ "Document_function" = 27
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//
Процедура ПроверимФункциюДокументаДляУдаления(НовыйДокумент, СтрокаТЧ, ФлагЗаписиДокумента)
	
	Если СокрЛП(ФункцияДокумента) = "27" Тогда
		
		Попытка
			
			НовыйДокумент.Комментарий = НовыйДокумент.Комментарий + Символы.ПС +
				"Документ помечен на удаление согласно " + ЭтотОбъект;
			
			НовыйДокумент.ПометкаУдаления = Истина;
			НовыйДокумент.ДополнительныеСвойства.Вставить("гф_ОбработкаОтгрузкаПоЗаказам", Истина);
			НовыйДокумент.ОбменДанными.Загрузка = Ложь;
			НовыйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения); 
			//НовыйДокумент.УстановитьПометкуУдаления(Истина); 
			
			// Добавим запись об изменении значения ПометкиУдаления
			МенеджерЗаписи = РегистрыСведений.гф_ИзменениеЗначенийИзI5.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = ТекущаяДатаСеанса();
			МенеджерЗаписи.Документ = НовыйДокумент.Ссылка; 
			МенеджерЗаписи.Реквизит = "Пометка Удаления"; 
			МенеджерЗаписи.ЗначениеТекущее = "Ложь/Истина";
			МенеджерЗаписи.ЗначениеИзИ5 = "Истина";
			МенеджерЗаписи.Записать();
			
			ИзменитьКомментарийВРегистре(СтрокаТЧ.ПорядковыйНомерСтроки, СтрокаТЧ.Тэг, 
				"Документ помечен на удаление согласно " + ЭтотОбъект + ".");
			
		Исключение
			
			СообщениеОбОшибке = "Не удалось установить пометку удаления для документа " + НовыйДокумент + ": " +
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке); 
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка,, НовыйДокумент, СообщениеОбОшибке);  
			
		КонецПопытки; 
		
		ФлагЗаписиДокумента = Ложь;
			
	КонецЕсли;
	
КонецПроцедуры

// #wortmann { 
// Процедура изменяет реквизит гф_СтатусРаботыСЗаказомИ5
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//  ФлагЗаписиДокумента - Булево
Функция ИзменяемСтатусНайденногоДокумента(НовыйДокумент, ФлагЗаписиДокумента)
	
	ПоменялиСтатус = Ложь;
	
	Если Не ЗначениеЗаполнено(НовыйДокумент.гф_СтатусРаботыСЗаказомИ5) Тогда
		
		НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ТребуетсяПодтверждение;
		ПоменялиСтатус = Истина;
		
	ИначеЕсли НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ТребуетсяПодтверждение Тогда
		ПоменялиСтатус = Ложь;
		
	ИначеЕсли НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ТребуетсяИзменение Тогда
		
		НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.Изменен;
		ПоменялиСтатус = Истина;
		
	Иначе
		НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ИзменениеИзИ5;
		ФлагЗаписиДокумента = Ложь; 
		ПоменялиСтатус = Истина;
		ПолучитьДанныеДокумента(НовыйДокумент.Ссылка);
		//ОтправитьПисьмо(Неопределено, НовыйДокумент, Ложь); // Галфинд_ДомнышеваКР_12_04_2024
	КонецЕсли;
	
	Возврат ПоменялиСтатус; 
	
КонецФункции

// #wortmann { 
// Процедура получает значение Склада и заполняет соответствующий реквизит
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//
Процедура ЗаполнитьСкладДляЗаказаКлиента(НовыйДокумент)
	
	Если ЗначениеЗаполнено(НовыйДокумент.Склад) Тогда
		Возврат;
	КонецЕсли;
	
	Склады = _омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначениеМассив("гф_ГлобальныеЗначенияОсновнойСклад");
	
	Для каждого Склад Из Склады Цикл
		Если Склад.гф_Организация = НовыйДокумент.Организация Тогда
			НовыйДокумент.Склад = Склад;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры// } #wortmann 

// #wortmann { 
// Функция определяет имя табличной части документа ЗаказКлиента
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//
// Возвращаемое значение:
//	ИмяТЧ - Строка 
Функция ОпределитьИмяТЧ(СтрокаТЧ)
	ИмяТЧ = Сред(СтрокаТЧ.ПолеЗагрузки.Объект, СтрНайти(СтрокаТЧ.ПолеЗагрузки.Объект, "(") + 1);
	Если СтрНайти(ИмяТЧ, ")") > 0 Тогда	
		ПозицияСкобки = СтрНайти(ИмяТЧ, ")"); 
		ИмяТЧ = Лев(ИмяТЧ, ПозицияСкобки - 1); 
	КонецЕсли;
	Возврат ИмяТЧ;
КонецФункции// } #wortmann

// #wortmann { 
// Процедура заполняет реквизиты документа ЗаказКлиента 
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//	НовыйЭлементТоварыВКоробах - Строка ТЧ ТоварыВКоробах документа ЗаказКлиента
//	ФлагЗаписиДокумента - Булево
//	ТаблицаЗначенийЦенИзИ5 - ТаблицаЗначений - содержит колонки "ВариантКомплектации", "ЦенаКоробаИзИ5". 
//  Отказ - параметр системы 
Процедура ЗаполнитьРеквизитыДокумента(СтрокаТЧ, НовыйДокумент, НовыйЭлементТоварыВКоробах,
										ФлагЗаписиДокумента, ТаблицаЗначенийЦенИзИ5, Отказ)
	
	Если СтрНайти(СтрокаТЧ.ПолеЗагрузки.Объект, "(") > 0 Тогда
		
		ИмяТЧ = ОпределитьИмяТЧ(СтрокаТЧ);
		
		ИмяРеквизита = Лев(СтрокаТЧ.ПолеЗагрузки.Объект, СтрНайти(СтрокаТЧ.ПолеЗагрузки.Объект, "(") - 1);
		
		Если  ИмяТЧ = "гф_ТоварыВКоробах" И НовыйЭлементТоварыВКоробах <> Неопределено Тогда
			Объект = НовыйЭлементТоварыВКоробах[ИмяРеквизита];
			ТипОбъекта = Документы.ЗаказКлиента.ПустаяСсылка().Метаданные().ТабличныеЧасти[ИмяТЧ].Реквизиты[ИмяРеквизита].Тип;
			
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		
		ИмяТЧ = Неопределено;
		ИмяРеквизита = СтрокаТЧ.ПолеЗагрузки.Объект;
		Объект = НовыйДокумент[ИмяРеквизита];
		
		ТипОбъекта = ОпределимТипОбъекта(ИмяРеквизита, СтрокаТЧ);
		
	КонецЕсли;
	
	Если ИмяРеквизита = "Контрагент" Тогда   // Тэг - GLN_customer
		
		НайдемКонтрагента(СтрокаТЧ, НовыйДокумент, Отказ); 
		
	ИначеЕсли ИмяРеквизита = "ВариантКомплектации" Тогда
		
		ОбработкаВариантаКомплектацииИРасхождений(НовыйДокумент, НовыйЭлементТоварыВКоробах,
		СтрокаТЧ, ТаблицаЗначенийЦенИзИ5, Отказ);
		
	ИначеЕсли ИмяРеквизита = "АдресДоставки" Тогда
		
		ЗаполняемПеревозчикаИАдрес(НовыйДокумент, СтрокаТЧ, Отказ);
		
	ИначеЕсли Строка(ТипОбъекта) = "Строка" Тогда
		
		Объект = СтрокаТЧ.Значение; 
		
	ИначеЕсли Строка(ТипОбъекта) = "Число" Тогда
		
		Объект = Число(СтрокаТЧ.Значение);
		
	ИначеЕсли Строка(ТипОбъекта) = "Дата" Тогда 
		
		Объект = Дата(СтрокаТЧ.Значение);
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект) И СтрНайти(СтрокаТЧ.ПолеЗагрузки.Объект, "(") = 0 Тогда
		НовыйДокумент[ИмяРеквизита] = Объект;
	КонецЕсли; 
		
КонецПроцедуры// } #wortmann 

// #wortmann { 
// Процедура определяет тип объекта по данным из выгрузки РС гф_СтрокиДокументаДанныеЗагрузки 
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	ИмяРеквизита - Строка - табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//
// Возвращаемое значение:
//	ТипОбъекта - возможные типы реквизитов для документа ЗаказКлиента
Функция ОпределимТипОбъекта(ИмяРеквизита, СтрокаТЧ)
	
	Если ИмяРеквизита = "Номер" Тогда
		ТипОбъекта = Документы.ЗаказКлиента.ПустаяСсылка().Метаданные().ТипНомера;
	ИначеЕсли ИмяРеквизита = "Дата" Тогда
		ТипОбъекта = Тип("Дата");
	Иначе	
		ТипОбъекта = Документы.ЗаказКлиента.ПустаяСсылка().Метаданные().Реквизиты[СтрокаТЧ.ПолеЗагрузки.Объект].Тип;
	КонецЕсли;
	
	Возврат ТипОбъекта;
КонецФункции// } #wortmann

// #wortmann { 
// Процедура заполняет реквизиты "ПеревозчикПартнер" и "СпособДоставки" 
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
Процедура ЗаполняемПеревозчикаИАдрес(НовыйДокумент, СтрокаТЧ, Отказ)
	
	СпособДоставки = 
	_омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначение("гф_СпособДоставкиЗагрузкаXML");
	
	Если  ЗначениеЗаполнено(СпособДоставки) Тогда
		НовыйДокумент.СпособДоставки = СпособДоставки;
	Иначе
		НовыйДокумент.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТЧ.Значение) Тогда
		НайденныйАдресДоставки = НайтиЗначениеПоРеквизитуСправочника ("гф_АдресаДоставки", "GLNНомер", 
		СтрокаТЧ.Значение);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НайденныйАдресДоставки) Тогда
		// ++ Галфинд_Домнышева 2023/08/31
		//НовыйДокумент.АдресДоставки = НайденныйАдресДоставки;
		НовыйДокумент.АдресДоставки = НайденныйАдресДоставки.Адрес;
		НовыйДокумент.гф_АдресДоставки = НайденныйАдресДоставки;
		// ++ Галфинд_Домнышева 2023/08/31
		НовыйДокумент.ПеревозчикПартнер = НайденныйАдресДоставки.ТК.Партнер;
	Иначе
		
		Отказ = Истина;
		
		НайденныйПеревозчик = _омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначение("гф_ПеревозчикЗагрузкаXML");
		
		НовыйДокумент.ПеревозчикПартнер = НайденныйПеревозчик;
		
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №"
		+ СтрокаТЧ.ПорядковыйНомерСтроки 
		+ ". Описание ошибки: Не найден соответствующий адрес доставки ""GLN delivery party"" "
		+ СокрЛП(СтрокаТЧ.Значение);
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Процедура заполняет ТЧ ТоварыВКоробах документа ЗаказКлиента и заполняет ТЗ ТаблицаЗначенийЦенИзИ5
// значениями Цен Коробов из файла XML
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//	НовыйЭлементТоварыВКоробах - Строка ТЧ ТоварыВКоробах документа ЗаказКлиента
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//	ТаблицаЗначенийЦенИзИ5 - ТаблицаЗначений - содержит колонки "ВариантКомплектации", "ЦенаКоробаИзИ5". 
//  Отказ - параметр системы 
Процедура ОбработкаВариантаКомплектацииИРасхождений(НовыйДокумент, НовыйЭлементТоварыВКоробах,
	СтрокаТЧ, ТаблицаЗначенийЦенИзИ5, Отказ)
	
	НайденныйВариантКомплектации =  НайтиЗначениеПоРеквизитуСправочника ("ВариантыКомплектацииНоменклатуры", 
									"гф_Штрихкод", СтрокаТЧ.Значение);

	Если ЗначениеЗаполнено(НайденныйВариантКомплектации) Тогда
		
		НовыйЭлементТоварыВКоробах.ВариантКомплектации = НайденныйВариантКомплектации;
		НовыйЭлементТоварыВКоробах.Артикул = НайденныйВариантКомплектации.Владелец.Артикул;
		НовыйЭлементТоварыВКоробах.Количество = НайтиСтрокуДереваПоТэгу("Ordered_quantity", Отказ, СтрокаТЧ);
		ДатаДоставки = НайтиСтрокуДереваПоТэгу("Delivery_date", Отказ, СтрокаТЧ);
		НовыйЭлементТоварыВКоробах.ДатаДоставки = Дата(ДатаДоставки); // Галфинд_Домнышева_КР_08_09_2023
		
		ЦенаКоробаИз5 = НайтиСтрокуДереваПоТэгу("Effective_purchase_price", Отказ, СтрокаТЧ);
		
		Если ЗначениеЗаполнено(ЦенаКоробаИз5) Тогда
			
			НовыйЭлементТоварыВКоробах.ЦенаКороба = ЦенаКоробаИз5;
			
		Иначе 
			
			СообщениеОбОшибке = "В файле не заполнена цена Effective_purchase_price, документ " + ЭтотОбъект + " строка №" + СтрокаТЧ.ПорядковыйНомерСтроки 
								+ " вариант комплектации с ШК: " + СокрЛП(СтрокаТЧ.Значение);
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Предупреждение, , ЭтотОбъект, СообщениеОбОшибке);
			
			НовыйЭлементТоварыВКоробах.ВидЦены = 
				_омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ГлобальныеЗначенияОптоваяЗакупочнаяЦена");
				
			Отбор = Новый Структура;
			Отбор.Вставить("Номенклатура", НайденныйВариантКомплектации.Владелец);			
			Отбор.Вставить("ВидЦены", НовыйЭлементТоварыВКоробах.ВидЦены);
			// Так как документ может быть с функцией "4" - изменение, то дату нужно брать не как дату у НовыйДокумент
			// а как дату внесения изменения.
			ДатаДляЦен = НайтиСтрокуДереваПоТэгу("Document_date", Отказ);
			ДатаДляЦен = Дата(ДатаДляЦен);
			Цена = РегистрыСведений.ЦеныНоменклатуры25.ПолучитьПоследнее(ДатаДляЦен, Отбор).Цена;
			
			НовыйЭлементТоварыВКоробах.ЦенаКороба = Цена * НайденныйВариантКомплектации.КоличествоУпаковок; 
			
		КонецЕсли;

		НовыйЭлементТоварыВКоробах.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.КОбеспечению;
		
		НоваяСтрока = ТаблицаЗначенийЦенИзИ5.Добавить();
		НоваяСтрока.ЦенаКоробаИз5 = ЦенаКоробаИз5;
		НоваяСтрока.ВариантКомплектации = НайденныйВариантКомплектации;
		НоваяСтрока.ДатаДоставки = Дата(ДатаДоставки);                       
		
	Иначе
		
		Отказ = Истина;
		
		НовыйДокумент.гф_ТоварыВКоробах.Удалить(НовыйЭлементТоварыВКоробах);
		НовыйЭлементТоварыВКоробах = Неопределено;
		// в комментарий запись
		НовыйДокумент.Комментарий = НовыйДокумент.Комментарий + Символы.ПС +
								"Отсутствует в базе вариант комплектации со ШтрихКодом " + СтрокаТЧ.Значение;
		НовыйДокумент.ДополнительныеСвойства.Вставить("НеПроводить", Истина);
		
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
							+ ". Описание ошибки: Не найден соответствующий вариант комплектации с ШК: " + СокрЛП(СтрокаТЧ.Значение);
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Код частично заимствован из модуля формы док.ЗаказКлиента проц. РассчитатьСкидку() 
// расчитывает скидку и перезаполняет ТЧ ТоварыВКоробах документа ЗаказКлиента и ищет расхождение по суммам
// из РС ЦеныНоменклатуры25 и из файла XML
// Галфинд_Домнышева 2023/02/28
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента ИЛИ ДокументСсылка.ЗаказКлиента - Создаваемый документ
//	ТаблицаЗначенийЦенИзИ5 - ТаблицаЗначений - содержит колонки "ВариантКомплектации", "ЦенаКоробаИзИ5". 
//	ТаблицаРасхождений - ТаблицаЗначений - содержит колонки "Реквизит", "ЗначениеТекущее", "ЗначениеИзИ5".
Процедура РассчитатьСкидкуВКоробах(НовыйДокумент, ТаблицаЗначенийЦенИзИ5, ТаблицаРасхождений)
	
	ТаблицаДляПисьма = ТаблицаРасхождений.СкопироватьКолонки();// ++ Галфинд_ДомнышеваКР_11_04_2024
	
	УсловиеНеРасчетаСкидки = Не ЗначениеЗаполнено(НовыйДокумент.Организация) 
		ИЛИ Не ЗначениеЗаполнено(НовыйДокумент.Контрагент) ИЛИ Не ЗначениеЗаполнено(НовыйДокумент.Договор)
		ИЛИ Не ЗначениеЗаполнено(НовыйДокумент.гф_ДатаОбновленияИзИ5);
		
	Если УсловиеНеРасчетаСкидки Тогда
		// проверка НЕ пройдена: есть пустые параметры
		Скидка = 0;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	гф_ИсторияИзмененияСкидокСрезПоследних.Организация КАК Организация,
		|	гф_ИсторияИзмененияСкидокСрезПоследних.Сезон КАК Сезон,
		|	гф_ИсторияИзмененияСкидокСрезПоследних.Контрагент КАК Контрагент,
		|	гф_ИсторияИзмененияСкидокСрезПоследних.Договор КАК Договор,
		|	гф_ИсторияИзмененияСкидокСрезПоследних.ВидСкидки КАК ВидСкидки,
		|	гф_ИсторияИзмененияСкидокСрезПоследних.Скидка КАК Скидка
		|ИЗ
		|	РегистрСведений.гф_ИсторияИзмененияСкидок.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И Контрагент = &Контрагент
		|				И Договор = &Договор
		|				И ВидСкидки В (&ВидыСкидок)) КАК гф_ИсторияИзмененияСкидокСрезПоследних";
		Запрос.УстановитьПараметр("Период", НовыйДокумент.Дата);
		Запрос.УстановитьПараметр("Организация", НовыйДокумент.Организация);
		Запрос.УстановитьПараметр("Контрагент", НовыйДокумент.Контрагент);
		Запрос.УстановитьПараметр("Договор", НовыйДокумент.Договор);
		МассивВидыСкидок = _омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначениеМассив(
		"гф_ГлобальныеЗначенияСкидкиДляЗаказаКлиента");
		Запрос.УстановитьПараметр("ВидыСкидок", МассивВидыСкидок);
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Скидка = 0;
		КонецЕсли;
		Скидка = Результат.Выгрузить().Итог("Скидка");
	КонецЕсли; 
	
	ПараметрыДляСтавкиНДС = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(НовыйДокумент, Истина);
	
	// ++ Галфинд_ДомнышеваКР_12_04_2024
	Если ЗначениеЗаполнено(НовыйДокумент.ССылка) Тогда
		Для каждого Строка Из НовыйДокумент.Ссылка.гф_ТоварыВКоробах Цикл
			СтокаЦен = ТаблицаЗначенийЦенИзИ5.Найти(Строка.ВариантКомплектации, "ВариантКомплектации"); 
			ОпределениеРасхожденийЦен(ТаблицаРасхождений, Строка, СтокаЦен, ТаблицаДляПисьма);
		КонецЦикла;
	КонецЕсли;  
	// -- Галфинд_ДомнышеваКР_12_04_2024
	
	Если ТипЗнч(НовыйДокумент) = Тип("ДокументОбъект.ЗаказКлиента") Тогда // Галфинд_ДомнышеваКР_17_04_2024
	Для каждого Строка Из НовыйДокумент.гф_ТоварыВКоробах Цикл
		
		СтокаЦен = ТаблицаЗначенийЦенИзИ5.Найти(Строка.ВариантКомплектации, "ВариантКомплектации"); 
				
		Если Скидка = 0 И СтокаЦен <> Неопределено Тогда
			Строка.ЦенаКороба = СтокаЦен.ЦенаКоробаИз5;
		КонецЕсли;
		 
		СтоПроцентов = 100;
		Строка.Скидка = Скидка;
		Строка.ЦенаКоробаСоСкидкой = Строка.ЦенаКороба * (1 - Скидка / СтоПроцентов);
		Строка.Сумма = Строка.Количество * Строка.ЦенаКоробаСоСкидкой;
		
		//Строка.СтавкаНДС = СтавкаНДС;
		Строка.СтавкаНДС = СтавкаНДСПоВариантуКомплектации(Строка.ВариантКомплектации, ПараметрыДляСтавкиНДС);
		
		Строка.СуммаНДС =  Строка.Сумма * (Строка.СтавкаНДС.Ставка / СтоПроцентов);		
		Строка.СуммаСНДС = Строка.СуммаНДС + Строка.Сумма;	
		
		//ОпределениеРасхожденийЦен(ТаблицаРасхождений, Строка, СтокаЦен); // Галфинд_ДомнышеваКР_12_04_2024	
	КонецЦикла;
	КонецЕсли; // Галфинд_ДомнышеваКР_17_04_2024
	// ++ Галфинд_ДомнышеваКР_12_04_2024
	Если ТаблицаДляПисьма.Количество() > 0 Тогда
		 //ОтправитьПисьмо(ТаблицаДляПисьма, НовыйДокумент);
	КонецЕсли;
	// -- Галфинд_ДомнышеваКР_12_04_2024 
	
КонецПроцедуры// } #wortmann

Функция СтавкаНДСПоВариантуКомплектации(ВариантКомплектации, ПараметрыДляСтавкиНДС)
	
	Результат = Справочники.СтавкиНДС.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ВариантКомплектации) Тогда
		
		Номенклатура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантКомплектации, "Владелец");
		
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			
			Результат = УчетНДСУП.СтавкаНДСПоНоменклатуреИНалогообложению(Номенклатура, ПараметрыДляСтавкиНДС.НалогообложениеНДС,
				ПараметрыДляСтавкиНДС.Организация, ПараметрыДляСтавкиНДС.Дата);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// #wortmann { 
// Определяет расхождение цены записанной в документе с ценой из И5 
// Галфинд_Домнышева 2023/03/01
//
// Параметры:
//	ТаблицаРасхождений - ТаблицаЗначений - содержит колонки "Реквизит", "ЗначениеТекущее", "ЗначениеИзИ5".
// 	Строка - СтрокаТЧ - Строка ТЧ гф_ТоварыВКоробах
//	СтокаЦен - СтрокаТЧ - найденная строка из ТЧ ТаблицаЗначенийЦенИзИ5
//	ТаблицаДляПисьма - ТаблицаЗначений - содержит колонки "Реквизит", "ЗначениеТекущее", "ЗначениеИзИ5"
Процедура ОпределениеРасхожденийЦен(ТаблицаРасхождений, Строка, СтокаЦен, ТаблицаДляПисьма)
	
	Если СтокаЦен = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	АбсолютноеРасхождение = 
	_омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначение("гф_ГлобальныеЗначенияРазрешенноеРасхождениеИ5");
	// BSLLS:Typo-off
	Толеранс = _омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначение("гф_ГлобальныеЗначенияТолеранс");
	// BSLLS:Typo-on
	
	Расхождение = СтокаЦен.ЦенаКоробаИз5 - Строка.ЦенаКоробаСоСкидкой; 
	Расхождение = ?(Расхождение >= 0, Расхождение, -Расхождение);

	Если Расхождение > АбсолютноеРасхождение Тогда
		НоваяЗапись = ТаблицаРасхождений.Добавить();
		
		НоваяЗапись.Реквизит = "Цена короба со скидкой для " + Строка.ВариантКомплектации + " (Абсолютное расхождение)"; 
		НоваяЗапись.ЗначениеТекущее = Строка.ЦенаКоробаСоСкидкой;
		НоваяЗапись.ЗначениеИзИ5 = СтокаЦен.ЦенаКоробаИз5; 
		// ++ Галфинд_ДомнышеваКР_11_04_2024
		ДляПисьма = ТаблицаДляПисьма.Добавить();
		ЗаполнитьЗначенияСвойств(ДляПисьма, НоваяЗапись);
		ДляПисьма.Реквизит = "Цена короба со скидкой для Артикула: " + Строка.ВариантКомплектации;
		// -- Галфинд_ДомнышеваКР_11_04_2024
	КонецЕсли;
	Сто = 100;
	Если Строка.ЦенаКоробаСоСкидкой > 0 Тогда
		// BSLLS:Typo-off
		Если (Расхождение / Строка.ЦенаКоробаСоСкидкой ) * Сто > Толеранс Тогда
			// BSLLS:Typo-on	
			НоваяЗапись = ТаблицаРасхождений.Добавить();
			
			НоваяЗапись.Реквизит = "Цена короба со скидкой для " + Строка.ВариантКомплектации + " (Относительное расхождение)"; 
			НоваяЗапись.ЗначениеТекущее = Строка.ЦенаКоробаСоСкидкой;
			НоваяЗапись.ЗначениеИзИ5 = СтокаЦен.ЦенаКоробаИз5;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Код заимствован из модуля формы док.ЗаказКлиента проц. Функция ПодготовитьВременнуюТаблицуТЧТовары()
// Галфинд_Домнышева 2023/03/01
//
// Возвращаемое значение:
//	ВременнаяТаблица - ТаблицаЗначений - пустая таблица ТЧ Товары
Функция ПодготовитьВременнуюТаблицуТЧТовары()
	ВременнаяТаблица = Новый ТаблицаЗначений;
	МетаданныеТЧ = Метаданные.Документы.ЗаказКлиента.ТабличныеЧасти.Товары.Реквизиты;
	Для Каждого Реквизит Из МетаданныеТЧ Цикл
		ВременнаяТаблица.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	ВременнаяТаблица.Колонки.Добавить("ХарактеристикиИспользуются", Новый ОписаниеТипов(""));
	ВременнаяТаблица.Колонки.Добавить("ТипНоменклатуры", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры"));
	ВременнаяТаблица.Колонки.Добавить("СуммаНДСОтмененоБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаСНДСОтмененоБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаАвтоматическойСкидкиОтмененоБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаРучнойСкидкиОтмененоБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("ВариантОформленияПродажи",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыОформленияПродажи"));
	ВременнаяТаблица.Колонки.Добавить("СуммаОтмененоБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("ОтмененоБезВозвратнойТары", Новый ОписаниеТипов("Булево"));
	ВременнаяТаблица.Колонки.Добавить("СуммаНДСБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаСНДСБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаАвтоматическойСкидкиБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаРучнойСкидкиБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаБезВозвратнойТары", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("БезВозвратнойТары", Новый ОписаниеТипов("Булево"));
	ВременнаяТаблица.Колонки.Добавить("Артикул", Новый ОписаниеТипов("Строка"));
	ВременнаяТаблица.Колонки.Добавить("СуммаСНДСОтменено", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаОтменено", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаНДСОтменено", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаАвтоматическойСкидкиОтменено", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаРучнойСкидкиОтменено", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("ИндексНабора", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СкладОбязателен", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("ДатаОтгрузкиОбязательна", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("Доступно", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("ПерераспределятьЗапасы", Новый ОписаниеТипов("Булево"));
	ВременнаяТаблица.Колонки.Добавить("ОтгружатьЕслиДоступно", Новый ОписаниеТипов("Булево"));
	ВременнаяТаблица.Колонки.Добавить("СуммаБонусныхБалловКСписаниюВВалютеОтменено", Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаБонусныхБалловКСписаниюВВалютеОтмененоБезВозвратнойТары",
		Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("СуммаБонусныхБалловКСписаниюВВалютеБезВозвратнойТары",
		Новый ОписаниеТипов("Число"));
	ВременнаяТаблица.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число")); 
	Возврат ВременнаяТаблица;
КонецФункции// } #wortmann

// #wortmann { 
// Процедура ищет и записывает в документ значение Контрагента по  Тэг - GLN_customer.
// Если значение не найдено ФлагЗаписиДокумента = Ложь 
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки 
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//  Отказ - параметр системы
Процедура НайдемКонтрагента(СтрокаТЧ, НовыйДокумент, Отказ) 
	
	// #wortmann { 
	// Изменился порядок выбора контрагента и договора.  
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee470a5070d548
	// Галфинд_Домнышева 2023/08/30
	
	GLN_номер = СтрокаТЧ.Значение;
	ДоговорДляКонтрагента(НовыйДокумент, СтрокаТЧ, GLN_номер);
	
	гф_Год = НайтиСтрокуДереваПоТэгу("SaisonYear", Отказ, СтрокаТЧ);
	гф_Номер = НайтиСтрокуДереваПоТэгу("Saison", Отказ, СтрокаТЧ);
	
	Если ЗначениеЗаполнено(НовыйДокумент.Договор) Тогда 
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НовыйДокумент.Договор, "Контрагент, Партнер, СтавкаНДС, НалогообложениеНДС");
		
		Если ЗначениеЗаполнено(НовыйДокумент.Контрагент) И НовыйДокумент.Контрагент <> РеквизитыДоговора.Контрагент Тогда
			
			СообщениеОбОшибке = "Предупреждение при проведении документа " + ЭтотОбъект + " в строке № " + СтрокаТЧ.ПорядковыйНомерСтроки +
				": в документе " + НовыйДокумент + " уже был указан другой контрагент " + НовыйДокумент.Контрагент + ", а в файле " + РеквизитыДоговора.Контрагент;  
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Предупреждение, , ЭтотОбъект, СообщениеОбОшибке);
			
		Иначе
			НовыйДокумент.Контрагент = РеквизитыДоговора.Контрагент;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НовыйДокумент.Партнер) И НовыйДокумент.Партнер <> РеквизитыДоговора.Партнер Тогда 
			
			СообщениеОбОшибке = "Предупреждение при проведении документа " + ЭтотОбъект + " в строке № " + СтрокаТЧ.ПорядковыйНомерСтроки +
				": в документе " + НовыйДокумент + " уже был указан другой партнер " + НовыйДокумент.Партнер + ", а в файле " + РеквизитыДоговора.Партнер;  
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Предупреждение, , ЭтотОбъект, СообщениеОбОшибке); 
			
		Иначе
			НовыйДокумент.Партнер    = РеквизитыДоговора.Партнер;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитыДоговора.СтавкаНДС) Тогда
			СтавкаНДС = РеквизитыДоговора.СтавкаНДС;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитыДоговора.НалогообложениеНДС) Тогда
			НовыйДокумент.НалогообложениеНДС = РеквизитыДоговора.НалогообложениеНДС;
		КонецЕсли;
		//Если НовыйДокумент.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт Тогда
		//	НовыйДокумент.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров; 
		//КонецЕсли;
		
	
	//НайденныйКонтрагент = НайтиЗначениеПоРеквизитуСправочника ("Контрагенты", "гф_GLN_номер", СтрокаТЧ.Значение);
	//
	//Если ЗначениеЗаполнено(НайденныйКонтрагент) Тогда
	//	НовыйДокумент.Контрагент = НайденныйКонтрагент;
	//	НовыйДокумент.Партнер =  
	//	НайтиЗначениеПоРеквизитуСправочника("Партнеры", "Наименование", НайденныйКонтрагент.Наименование);
	//	ДоговорДляКонтрагента(НовыйДокумент, СтрокаТЧ, GLNНомер);
	//	
	//	Если НовыйДокумент.Договор <> Неопределено  Тогда
	//		
	//		Если НовыйДокумент.Партнер <> НовыйДокумент.Договор.Партнер Тогда 
	//			СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " . Партнер найденный для документа " +
	//			НовыйДокумент.Партнер + " не соответствует партнеру " + НовыйДокумент.Договор.Партнер + " из договора " +
	//			НовыйДокумент.Договор + "."; 								
	//			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
	//			НовыйДокумент.ДополнительныеСвойства.Вставить("НеПроводить", Истина);				
	//			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
	//			Отказ = Истина;
	//		Иначе
	//			
	//		КонецЕсли;
	//		
	//	КонецЕсли;
	//
	// } #wortmann
	Иначе
	
		Отказ = Истина;
		
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №"
		+ СтрокаТЧ.ПорядковыйНомерСтроки + ". Описание ошибки: Не найден договор по сезону с номером " + гф_Номер 
		+ " и годом " + гф_Год + " и организации " + ЭтотОбъект.Организация + " с наличием контрагента с GLN "  
		+ СокрЛП(СтрокаТЧ.Значение) + ".  Документ не может быть проведен."; 								
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		НовыйДокумент.Комментарий = НовыйДокумент.Комментарий + Символы.ПС + 
			"Отсутствует в базе контрагент с GLN " + СтрокаТЧ.Значение;
		НовыйДокумент.ДополнительныеСвойства.Вставить("НеПроводить", Истина);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Процедура меняет реквизит гф_СтатусРаботыСЗаказомИ5 и производит Запись документа
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//  Отказ - параметр системы 
Процедура СменаСтатусовДокумента(НовыйДокумент, Отказ)
	
	Попытка	
		НовыйДокумент.ЦенаВключаетНДС = Ложь;	
		
		Если НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ТребуетсяПодтверждение 
			ИЛИ НЕ ЗначениеЗаполнено(НовыйДокумент.гф_СтатусРаботыСЗаказомИ5) Тогда
			НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ТребуетсяПодтверждение;
			
		ИначеЕсли НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ТребуетсяИзменение Тогда
			НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.Изменен;
			
		Иначе
			НовыйДокумент.гф_СтатусРаботыСЗаказомИ5 = Справочники.гф_СтатусРаботыСЗаказомИ5.ИзменениеИзИ5;
			ФлагЗаписиДокумента = Ложь;
		КонецЕсли;
		
		//Если ФлагЗаписиДокумента Тогда
		//	
		//	НовыйДокумент.ОбменДанными.Загрузка = Истина; 
		//	
		//	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(НовыйДокумент, Отказ);
		//	
		//	РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(НовыйДокумент, Отказ, Истина);
		//	
		//	ДоставкаТоваров.ОтразитьСостояниеДоставки(НовыйДокумент.Ссылка, Отказ, Истина);
		//	
		//	РегистрыСведений.СтатусыСборкиИДоставки.ЗаписатьСтатусИзРаспоряжения(НовыйДокумент.Ссылка, Отказ, Истина);
		//	Если НовыйДокумент.ПометкаУдаления Тогда
		//		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		//	Иначе
		//		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		//	КонецЕсли;
		//	НовыйДокумент.ОбменДанными.Загрузка = Ложь;
		//	
		//КонецЕсли;
		
	Исключение
		СообщениеОбОшибке = "Произошли ошибки при проведении документа " + НовыйДокумент + ". Описание ошибки: " 
							+ ОписаниеОшибки();
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
	КонецПопытки;
	
КонецПроцедуры// } #wortmann
	
// #wortmann {
// Функция формирует сообщение об ошибке в зависимости от заполненных реквизитов
// Галфинд_Домнышева 2022/10/20
//
// Параметры:
//	НомерДокумента - Строка - номер документа ЗаказКлиента.
//  ДатаДокумента - Дата - дата документа ЗаказКлиента.
//
// Возвращаемое значение:
//  СообщениеОбОшибке - Строка
Функция СформироватьСообщение (НомерДокумента, ДатаДокумента) 
	// BSLLS:Typo-off
	Если (НЕ ЗначениеЗаполнено(НомерДокумента)) И (НЕ ЗначениеЗаполнено(ДатаДокумента)) Тогда
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект  
							+ ". Описание ошибки: Не заполнены тэги ""Document_number"" и ""Document_date"".";
	ИначеЕсли	НЕ ЗначениеЗаполнено(НомерДокумента) Тогда
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект  
							+ ". Описание ошибки: Не заполнены тэг ""Document_number"".";	
	ИначеЕсли	НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда	
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект  
							+ ". Описание ошибки: Не заполнены тэг  ""Document_date"".";	
	Иначе 				// Невероятный вариант, но все же ...	)))
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект ;		
	КонецЕсли;
	Возврат СообщениеОбОшибке;
	// BSLLS:Typo-on
КонецФункции// } #wortmann

// #wortmann {
// Функция получает подчиненные документы
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ЗаказКлиента - ссылка на найденный Заказ Клиента.
//
// Возвращаемое значение:
//  Массив - массив документов, если есть подчиненные документы
//  ТаблицаЗначений - пустая ТЗ, если текстзапроса пустой
Функция ПолучитьМассивПодчиненныхДокументов(ДокументСсылка)
	
	ТекущийДокумент = ДокументСсылка;
	Запрос = Новый Запрос;
	ТекстЗапроса = "";
	
	Для Каждого ЭлементСостава Из Метаданные.КритерииОтбора["гф_СвязанныеДокументы"].Состав Цикл
		
		Если Не ЭлементСостава.Тип.СодержитТип(ТипЗнч(ТекущийДокумент)) Тогда
			Продолжить;
		КонецЕсли;
		
		ПутьКДанным = ЭлементСостава.ПолноеИмя();
		СтруктураПутьКДанным = РазобратьПутьКОбъектуМетаданных(ПутьКДанным, ЭлементСостава.Родитель());
		
		ИмяОбъекта = СтруктураПутьКДанным.ТипОбъекта + "." + СтруктураПутьКДанным.ВидОбъекта;
		Псевдоним = СтруктураПутьКДанным.ТипОбъекта + "_" + СтруктураПутьКДанным.ВидОбъекта 
							+ "_" + СтруктураПутьКДанным.ИмяТаблЧасти;
		
		ТекущаяСтрокаГДЕ = "ГДЕ " + Псевдоним + "." + СтруктураПутьКДанным.ИмяРеквизита + " = &ЗначениеКритерияОтбора";
			
		ИмяТЧ = Лев(СтруктураПутьКДанным.ИмяРеквизита, СтрНайти(СтруктураПутьКДанным.ИмяРеквизита, ".") - 1);
		ИмяРеквизита = Лев(СтруктураПутьКДанным.ИмяРеквизита, СтрНайти(СтруктураПутьКДанным.ИмяРеквизита, ".") - 1);
		ТекстЗапроса = ТекстЗапроса + (?(ТекстЗапроса = "", "ВЫБРАТЬ", "ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ") + "
		|" + Псевдоним + ".Ссылка ИЗ " + ИмяОбъекта + "." + СтруктураПутьКДанным.ИмяТаблЧасти + " КАК " + Псевдоним + "
		|" + СтрЗаменить(ТекущаяСтрокаГДЕ, "..", ".") + "
		|");
		
	КонецЦикла;
	
	Если ТекстЗапроса = "" Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЗначениеКритерияОтбора", ТекущийДокумент);
	
	мМассивПочиненных = Запрос.Выполнить().Выгрузить();
    	
	Возврат мМассивПочиненных;
КонецФункции// } #wortmann

// #wortmann {
// Функция разбирает строку вида
// ТипОбъектаМетаданных.ИмяДокумента.ТабличнаяЧасть.ИмяТабличнойЧасти.Реквизит.ИмяРеквизита.
// ТипОбъектаМетаданных должен быть Справочник или Документ.
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//  ПутьКДанным - строка.
//  МетаданныеОбъекта - Объект метаданных
//
// Возвращаемое значение:
//  Структура - путь к объекту метаданных
Функция РазобратьПутьКОбъектуМетаданных(ПутьКДанным, МетаданныеОбъекта = Неопределено)
	
	Структура = Новый Структура;
	
	СоответствиеИмен = Новый Массив();
	СоответствиеИмен.Добавить("ТипОбъекта");
	СоответствиеИмен.Добавить("ВидОбъекта");
	СоответствиеИмен.Добавить("ПутьКДанным");
	СоответствиеИмен.Добавить("ИмяТаблЧасти");
	СоответствиеИмен.Добавить("ИмяРеквизита");
	
	Для индекс = 1 По 3 Цикл
		
		Точка = СтрНайти(ПутьКДанным, ".");
		ТекущееЗначение = Лев(ПутьКДанным, Точка - 1);
		Структура.Вставить(СоответствиеИмен[индекс - 1], ТекущееЗначение);
		ПутьКДанным = Сред(ПутьКДанным, Точка + 1);
		
	КонецЦикла;
	
	ПутьКДанным = СтрЗаменить(ПутьКДанным, "Реквизит.", "");
	
	Если Структура.ПутьКДанным = "ТабличнаяЧасть" Тогда
		
		Для индекс = 4 По 5  Цикл 
			
			Точка = СтрНайти(ПутьКДанным, ".");
			Если Точка = 0 Тогда
				ТекущееЗначение = ПутьКДанным;
			Иначе
				ТекущееЗначение = Лев(ПутьКДанным, Точка - 1);
			КонецЕсли;
			
			Структура.Вставить(СоответствиеИмен[индекс - 1], ТекущееЗначение);
			ПутьКДанным = Сред(ПутьКДанным,  Точка + 1);
			
		КонецЦикла;
		
	Иначе
		
		Структура.Вставить(СоответствиеИмен[3], "");
		Структура.Вставить(СоответствиеИмен[4], ПутьКДанным);
		
	КонецЕсли;
	
	Если МетаданныеОбъекта <> Неопределено Тогда
		Структура.Вставить("Метаданные", МетаданныеОбъекта);
	Иначе
		Если Структура.ТипОбъекта = "Документ" Тогда
			Структура.Вставить("Метаданные", Метаданные.Документы[Структура.ВидОбъекта]);
		Иначе
			Структура.Вставить("Метаданные", Метаданные.Справочники[Структура.ВидОбъекта]);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции// } #wortmann

// #wortmann { 
// Нахождение имени заказа
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ЗаказКлиента - ссылка на найденный Заказ Клиента
//
// Возвращаемое значение:
//	Булево - Истина если найдены подчиненные документы, Ложь - нет
Функция ЕстьПроведенныеПодчиненныеДокументы(ДокументСсылка)
	
	Нашли = Ложь;
	мМассивПодчиненных = ПолучитьМассивПодчиненныхДокументов(ДокументСсылка);
	Для каждого ЭлементМассива Из мМассивПодчиненных Цикл
		Если ЗначениеЗаполнено(ЭлементМассива.Ссылка) И ЭлементМассива.Ссылка.Проведен Тогда
			Нашли = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Нашли;

КонецФункции// } #wortmann

// #wortmann { 
// Нахождение имени заказа
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//  Тэг - Строка - имя ТЭГа из файла
//  СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//
// Возвращаемое значение:
//  - Строка - значение ТЭГа по порядковому номеру строки родителя для текущей строки
Функция НайтиИмяЗаказа(Тэг, СтрокаТЧ = Неопределено)
	
	СтруктураОтбора = Новый Структура;
	// BSLLS:Typo-off
	СтруктураОтбора.Вставить("Тэг", Тэг);
	// BSLLS:Typo-on
	Если СтрокаТЧ <> Неопределено Тогда
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.ПорядковыйНомерСтроки);
	КонецЕсли;
	НайденноеЗначение = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);
	Если НайденноеЗначение.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(НайденноеЗначение[0].Значение) Тогда
			Возврат НайденноеЗначение[0].Значение;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции// } #wortmann

// #wortmann { 
// Получение набора записей РС гф_СтрокиДокументаДанныеЗагрузки 
// Галфинд_Домнышева 2022/09/12
Процедура ПолучитьНаборЗаписейРегистраСтрок()

	Запрос = Новый Запрос; 
	// Галфинд dlip 2023/02/17 изменил текст запроса
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиДокумента.ПорядковыйНомерСтроки КАК ПорядковыйНомерСтроки,
	|	СтрокиДокумента.Тэг КАК Тэг,
	|	СтрокиДокумента.СтатусПоля КАК СтатусПоля,
	|	СтрокиДокумента.Значение КАК Значение,
	|	СтрокиДокумента.ПолеЗагрузки КАК ПолеЗагрузки,
	|	СтрокиДокумента.Комментарий КАК Комментарий,
	|	СтрокиДокумента.НомерСтрокиРодителя КАК НомерСтрокиРодителя
	|ИЗ
	|	РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.Документ = &Документ
	|
	|	И ( ВЫРАЗИТЬ(СтрокиДокумента.Значение КАК СТРОКА(16)) <> """"
	|		ИЛИ СтрокиДокумента.СтатусПоля = ЗНАЧЕНИЕ(Перечисление.гф_СтатусыПоля.Обязательное) 
	|		ИЛИ СтрокиДокумента.Тэг В (&МассивНужныхТЭГов))"; // ++ Галфинд_Домнышева 2023/02/21
	
	// ++ Галфинд_Домнышева 2023/02/22
	МассивНужныхТЭГов = Новый Массив;
	МассивНужныхТЭГов.Добавить("Extended_Attribute");
	МассивНужныхТЭГов.Добавить("Message_Position");
	МассивНужныхТЭГов.Добавить("Message_Sub_Position"); 
	МассивНужныхТЭГов.Добавить("Message_Header");
	// -- Галфинд_Домнышева 2023/02/22
	МассивНужныхТЭГов.Добавить("Country_of_origin"); // Галфинд_Домнышева 2023/10/18
	
	Запрос.УстановитьПараметр("Документ", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("МассивНужныхТЭГов", МассивНужныхТЭГов); // ++ Галфинд_Домнышева 2023/02/21
	
	НаборЗаписейТэгов = Запрос.Выполнить().Выгрузить();
	
	Если ЗначениеЗаполнено(НаборЗаписейТэгов) Тогда 
		
		// ++ Галфинд dlip 2023/02/17
		//НаборЗаписейТэгов.Индексы.Добавить("Тэг, СтатусПоля, НомерСтрокиРодителя"); 
		НаборЗаписейТэгов.Индексы.Добавить("Тэг, НомерСтрокиРодителя"); 
		НаборЗаписейТэгов.Индексы.Добавить("НомерСтрокиРодителя, СтатусПоля");
		// -- Галфинд dlip 2023/02/17
	
	КонецЕсли;
				
КонецПроцедуры// } #wortmann

// #wortmann { 
// Проведение документов с интерфейсом Pricat, создание номенклатуры 
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	ТабличнаяЧасть - ТаблицаЗначений - выгрузка РС гф_СтрокиДокументаДанныеЗагрузки по данному Объекту.
//	Отказ - параметр системы 
//	ВыводитьПрогрессор - Булево - выводит гф_ФормаПрогрессора при значении Истина
// BSLLS:LatinAndCyrillicSymbolInWord-off
Процедура ПроведениеИнтерфейсаPricat(ТабличнаяЧасть, Отказ, ВыводитьПрогрессор = Ложь)
// BSLLS:LatinAndCyrillicSymbolInWord-on		
	НовыйЭлемент 			= Неопределено;
	флЗаписиНоменклатуры	= Истина;
	НомерСтрокиРодителя 	= 1;
	
	ЗапросПоНаименованию = Новый Запрос(
	"ВЫБРАТЬ
	|	ArticlrDescriptions.NameRU,
	|	ArticlrDescriptions.Product_group КАК Product_group,
	|	ArticlrDescriptions.GLN_Supplier КАК GLN_Supplier,
	|	ArticlrDescriptions.ВидНоменклатуры,
	// #wortmann { 
	// Вытаскиваем значение Пола Номенклатуры
	// Галфинд_Домнышева 2022/10/10
	|	ArticlrDescriptions.ПолНоменклатуры КАК ПолНоменклатуры,
	// } #wortmann
	|	ArticlrDescriptions.Родитель,
	|	ArticlrDescriptions.ЕдиницаИзмеренияПоКлассификатору,
	|	ArticlrDescriptions.СтавкаНДС
	|ИЗ
	|	РегистрСведений.гф_ArticlrDescriptions КАК ArticlrDescriptions
	|
	|УПОРЯДОЧИТЬ ПО
	|	Product_group,
	|	GLN_Supplier
	|АВТОУПОРЯДОЧИВАНИЕ");							 
	// BSLLS:LatinAndCyrillicSymbolInWord-off
	ТаблицаРегистраСоответствияСвойств = ЗапросПоНаименованию.Выполнить().Выгрузить();
	// BSLLS:LatinAndCyrillicSymbolInWord-on
		
	НачатьТранзакцию();
	Попытка
		
		ОбработкаСтрокДокументаПоСозданиюНоменклатуры(ТабличнаяЧасть, ТаблицаРегистраСоответствияСвойств, Отказ);
		ЗафиксироватьТранзакцию();
		
	Исключение 
		
		ОтменитьТранзакцию();
		ЗагруженоСОшибками = Истина;
		
		СообщениеОбОшибке = "Ошибка загрузки номенклатуры: " + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , ЭтотОбъект, СообщениеОбОшибке);
		
	КонецПопытки;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Обход строк ТЧ РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки для создания номенклатуры 
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	ТабличнаяЧасть - ТаблицаЗначений - выгрузка РС гф_СтрокиДокументаДанныеЗагрузки по данному Объекту.
//	ТаблицаРегистраСоответствияСвойств - ТаблицаЗначений - выгрузка из РегистрСведений.гф_ArticlrDescriptions
//	Отказ - параметр системы 
//	ФормаПрогрессора - гф_ФормаПрогрессора, форма документа гф_ДанныеЗагрузки
Процедура ОбработкаСтрокДокументаПоСозданиюНоменклатуры(ТабличнаяЧасть, ТаблицаРегистраСоответствияСвойств,
														Отказ, ФормаПрогрессора = Неопределено)
	
	флЗаписиНоменклатуры = Истина;													
	НомерПредыдущегоРодителя = "0";
    ТЭГАртикулПрошлый = ""; 
	НоменклатураШ = Неопределено; // Галфинд_Домнышева_08_11_2023
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл 
				
		Если ЗначениеЗаполнено(СтрокаТЧ.ПолеЗагрузки) И СтрокаТЧ.НомерСтрокиРодителя <> НомерПредыдущегоРодителя 
			И СтрокаТЧ.ПолеЗагрузки.ТипОбъекта = "Номенклатура" Тогда
			
				НайденноеЗначение = ПоискАртикулаВЗагруженныхСтроках(СтрокаТЧ);
				НайденныйНомерЦвета = ПоискЦветаВЗагруженныхСтроках(СтрокаТЧ);
				
				Если НайденноеЗначение.Количество() > 0 Тогда
					Артикул = НайденноеЗначение[0].Значение + "-" + НайденныйНомерЦвета;
					Сезон = УстановитьСезон(СтрокаТЧ, Отказ); // ++ Галфинд_Домнышева 2023/02/22
					// ++ Галфинд_Домнышева 08_11_2023
					// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee717943747367
					НоваяНоменклатура = НайтиНоменклатуруВБазе(НайденноеЗначение[0].Значение, НайденныйНомерЦвета,
										Сезон, ТЭГАртикулПрошлый, НоменклатураШ);
					//НоваяНоменклатура = ПолучитьНоменклатуруПоАртикулу(Артикул, Сезон);
					// -- Галфинд_Домнышева 08_11_2023

					// #wortmann { 
					// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee4573acf7342c 
					// Галфинд_Домнышева 2023/08/28
					Если НоваяНоменклатура <> Неопределено И ДатыОбновленияИзИ5Старее(НоваяНоменклатура, СтрокаТЧ, Отказ) Тогда
						Продолжить;
					КонецЕсли;
					// } #wortmann

					НовыйЭлемент = ПолучитьНовуюНоменклатуруСАртикулом(НоваяНоменклатура, Артикул, СтрокаТЧ, Отказ, Сезон); 
					НоменклатураЗаписана = Ложь; // ++ Галфинд_Домнышева 2023/02/21
					
					ТЭГШтрихКодаТек = НайтиСтрокуДереваПоТэгу("EAN", Отказ, СтрокаТЧ);
					НомерПредыдущегоРодителя = СтрокаТЧ.НомерСтрокиРодителя;
					Если Артикул = ТЭГАртикулПрошлый Тогда
						СоздатьШтрихкод(СтрокаТч, НовыйЭлемент, ТЭГШтрихкодаТек, НовыйЭлемент.ЕдиницаИзмерения);
						НоменклатураЗаписана = Истина; // ++ Галфинд_Домнышева 2023/02/21
						Продолжить;
					КонецЕсли;
					
					ТЭГАртикулПрошлый = Артикул; 

				Иначе
					СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.НомерСтрокиРодителя
										+ ". Описание ошибки: " + ОписаниеОшибки();
					ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
					ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
					
					НовыйЭлемент = Справочники.Номенклатура.СоздатьЭлемент();
					ВременноеНаименование = Строка("Обувь без Артикула!" + СтрокаТЧ.НомерСтрокиРодителя);
					НовыйЭлемент.ВидНоменклатуры = 
									// #wortmann { 
									// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee4573acf7342c
									// Галфинд_Домнышева 2023/08/28
									//НайтиЗначениеПоРеквизитуСправочника("ВидыНоменклатуры", "Наименование", "Обувь импортная");
									_омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ВидНоменклатуры");
									// } #wortmann
					НовыйЭлемент.ЕдиницаИзмерения =	 НайтиЗначениеПоРеквизитуСправочника("УпаковкиЕдиницыИзмерения",
													"Наименование", "пар");
					НовыйЭлемент.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг;
					НовыйЭлемент.ВестиУчетПоГТД = Истина;
					НовыйЭлемент.ВестиУчетСертификатовНоменклатуры = Истина;
					НовыйЭлемент.ИспользованиеХарактеристик = 
															Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры;
					НовыйЭлемент.Качество = Перечисления.ГрадацииКачества.Новый;
					НовыйЭлемент.СтавкаНДС = СтавкаНДС20; 
					НовыйЭлемент.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
					НовыйЭлемент.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция;
					НовыйЭлемент.Наименование = ВременноеНаименование;
					НовыйЭлемент.КоэффициентЕдиницыДляОтчетов = 1; // Галфинд_Домнышева_17_02_2023
					// #wortmann { 
					// Заполняем сезон
					// Галфинд_Домнышева 2022/10/10
					НовыйЭлемент.КоллекцияНоменклатуры = УстановитьСезон(СтрокаТЧ, Отказ);
					// } #wortmann
					НовыйЭлемент.ОбменДанными.Загрузка = Истина; // Галфинд dlip 2023/02/17
					НовыйЭлемент.Записать();
					
					НомерПредыдущегоРодителя = СтрокаТЧ.НомерСтрокиРодителя;
				КонецЕсли;
				
				ОпределимПометкуУдаленияДляДокумента(СтрокаТЧ, НовыйЭлемент, Отказ);
				
				ДополнитьРеквизитыЭлементаНоменклатуры(СтрокаТЧ, Отказ, НовыйЭлемент, ТаблицаРегистраСоответствияСвойств);

		КонецЕсли;	
		
		ИтоговаяЗаписьЭлемента(СтрокаТЧ, НовыйЭлемент, ТабличнаяЧасть.Количество(), НоменклатураЗаписана, Отказ);				
	КонецЦикла; 
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee717943747367
// Функция ищет номенклатуру по Штрихкоду, затем по Артикулу.
// Галфинд_Домнышева 2023/11/10
//
// Параметры:
//	ЗначАртикул - строка - значение ТЭГа "Suppliers_article_number"
//	НомерЦвета - строка - значение ТЭГа "Colour_code_supplier"
//	Сезон - СправочникСсылка.КоллекцииНоменклатуры
//	ТЭГАртикулПрошлый - Строка - артикул номенклатуры из файла
//	НоменклатураШ - СправочникСсылка.Номенклатура или Неопределено
//
// Возвращаемое значение:
//	СправочникОбъект.Номенклатура - объект справочника Номенклатура
//	Неопределено - если по указанному артикулу нет номенклатуры
Функция НайтиНоменклатуруВБазе(ЗначАртикул, НомерЦвета, Сезон, ТЭГАртикулПрошлый, НоменклатураШ)
	
	Артикул = ЗначАртикул + "-" + НомерЦвета;
	
	Если Артикул = ТЭГАртикулПрошлый 
		И НоменклатураШ <> Неопределено Тогда
		Возврат НоменклатураШ;
	КонецЕсли;

	МассивШтрихкод = ПолучитьМассивШтрихкод(ЗначАртикул, НомерЦвета);
	
	НоваяНоменклатура = НоменклатураПоШтрихкоду(Артикул, МассивШтрихкод);
	НоменклатураШ = НоваяНоменклатура;
	
	Если НоваяНоменклатура = Неопределено Тогда		
		НоваяНоменклатура = ПолучитьНоменклатуруПоАртикулу(Артикул, Сезон);
	КонецЕсли;
	
	Возврат НоваяНоменклатура;
	
КонецФункции// } #wortmann

// #wortmann { 
// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee717943747367
// Функция определяет Номенклатуру по штрихкоду.
// Галфинд_Домнышева 2023/11/10
//
// Параметры:
//	Артикул - строка - значение артикула Номенклатуры в ИБ
//	МассивШтрихкод - содержит строчные значения ТЭГов EAN
//
// Возвращаемое значение:
//  СправочникОбъект.Номенклатура - объект справочника Номенклатура
//	Неопределено - если по указанному артикулу нет номенклатуры
Функция НоменклатураПоШтрихкоду(Артикул, МассивШтрихкод)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ Различные
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод В (&МассивШтрихкод)";
	
	Запрос.УстановитьПараметр("МассивШтрихкод", МассивШтрихкод);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Если Выборка.Количество() > 1 Тогда
			СообщениеОбОшибке = "Для ШтрихКодов " + СтрСоединить(МассивШтрихкод, ";") + " по указанному артикулу " + Артикул
								+ " из файла, в ИБ записано несколько номенклатур.";
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			Возврат Неопределено;
		Иначе
			Выборка.Следующий();
		Возврат Выборка.Номенклатура.ПолучитьОбъект(); 
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann

// #wortmann { 
// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee717943747367
// Функция выделяет все штрихкоды по артикулу.
// Галфинд_Домнышева 2023/11/10
//
// Параметры:
//	ЗначАртикул - строка - значение ТЭГа "Suppliers_article_number"
//	НомерЦвета - строка - значение ТЭГа "Colour_code_supplier"
//
// Возвращаемое значение:
//  Массив - содержит строчные значения ТЭГов EAN.
Функция ПолучитьМассивШтрихкод(ЗначАртикул, НомерЦвета) 
	
	Запрос = Новый Запрос; 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиДокумента.НомерСтрокиРодителя КАК НомерСтрокиРодителя,
	|	СтрокиДокумента.Тэг КАК ТЭГ
	|ПОМЕСТИТЬ НомераСтрокАртикул
	|ИЗ
	|	РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.Документ = &Документ
	|	И СтрокиДокумента.Тэг = ""Suppliers_article_number""
	|	И (ВЫРАЗИТЬ(СтрокиДокумента.Значение КАК СТРОКА(16))) = &Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.НомерСтрокиРодителя КАК НомерСтрокиРодителяЦ
	|ПОМЕСТИТЬ СтрокиРодителя
	|ИЗ
	|	РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.Документ = &Документ
	|	И СтрокиДокумента.Тэг = ""Colour_code_supplier""
	|	И (ВЫРАЗИТЬ(СтрокиДокумента.Значение КАК СТРОКА(16))) = &Цвет
	|	И СтрокиДокумента.НомерСтрокиРодителя В
	|			(ВЫБРАТЬ
	|				Т.НомерСтрокиРодителя
	|			ИЗ
	|				НомераСтрокАртикул КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.ПорядковыйНомерСтроки КАК ПорядковыйНомерСтроки,
	|	СтрокиДокумента.Тэг КАК Тэг,
	|	СтрокиДокумента.СтатусПоля КАК СтатусПоля,
	|	СтрокиДокумента.Значение КАК Значение,
	|	СтрокиДокумента.ПолеЗагрузки КАК ПолеЗагрузки,
	|	СтрокиДокумента.Комментарий КАК Комментарий,
	|	СтрокиДокумента.НомерСтрокиРодителя КАК НомерСтрокиРодителя
	|ИЗ
	|	РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.Документ = &Документ
	|	И СтрокиДокумента.Тэг = ""EAN""
	|	И СтрокиДокумента.НомерСтрокиРодителя В
	|			(ВЫБРАТЬ
	|				Т.НомерСтрокиРодителяЦ
	|			ИЗ
	|				СтрокиРодителя КАК Т)";

	Запрос.УстановитьПараметр("Документ", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Артикул", ЗначАртикул);
	Запрос.УстановитьПараметр("Цвет", НомерЦвета);
	
	МассивШ = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Значение");

	Возврат МассивШ;
	
КонецФункции// } #wortmann

// #wortmann { 
// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee4573acf7342c
// Функция определяет дату обновления из И5 Номенклатуры и устанавливает старше ли она новой даты.
// Галфинд_Домнышева 2023/08/28
//
// Параметры:
//	НоваяНоменклатура - СправочникОбъект.Номенклатура - если она найдено, Неопределено в ином случае.
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки 
//	Отказ - параметр системы
//
// Возвращаемое значение:
//  Булево - Истина, если Старше, Ложь если меньше.
Функция ДатыОбновленияИзИ5Старее(НоваяНоменклатура, СтрокаТЧ, Отказ)
	
	ДатаОбновленияНовая = Дата(НайтиСтрокуДереваПоТэгу("Document_date", Отказ));
	
	СвойствоДатаОбновления = НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураДатаОбновленияНоменклатурыИзI5");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Объект = &Объект
		|	И ДополнительныеСведения.Свойство = &Свойство";
	
	Запрос.УстановитьПараметр("Объект", НоваяНоменклатура.Ссылка);
	Запрос.УстановитьПараметр("Свойство", СвойствоДатаОбновления);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();

	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка.Следующий(); 
		// ++ Галфинд_ДомнышеваКР_10_11_2023
		// Из УПП вводом остатков могли загрузиться не верные в целом форматы дат(даже типом строка), 
		// (как вариант "8/22/2023 12:00:00 AM" ), по этому принимаем что дата однозначно Старая 
		Попытка 
			ДатаОбновленияСтарая = Дата(Выборка.Значение);
		Исключение
			ДатаОбновленияСтарая = Дата(1, 1, 1);
		КонецПопытки; 
		// -- Галфинд_ДомнышеваКР_10_11_2023
		Возврат ДатаОбновленияНовая < ДатаОбновленияСтарая;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции// } #wortmann

// #wortmann { 
// Процедура создает новую Номенклатуру иили присваивает значение НоваяНоменклатура  
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НоваяНоменклатура - СправочникОбъект.Номенклатура - если она найдено, Неопределено в ином случае.
//	Артикул - Строка - артикул номенклатуры из файла XML
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки 
//	Отказ - параметр системы
//	Сезон - СправочникСсылка.КоллекцииНоменклатуры - найденный выше Сезон
//
// Возвращаемое значение:
//  НовыйЭлемент - СправочникОбъект.Номенклатура
Функция ПолучитьНовуюНоменклатуруСАртикулом(НоваяНоменклатура, Артикул, СтрокаТЧ, Отказ, Сезон) 
	
	Если НоваяНоменклатура = Неопределено Тогда
		ВременноеНаименование = Строка("Обувь" + Артикул);
		НовыйЭлемент = Справочники.Номенклатура.СоздатьЭлемент();
		НовыйЭлемент.Артикул = Артикул;
		// #wortmann { 
		// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee4573acf7342c
		// Галфинд_Домнышева 2023/08/28
		//НовыйЭлемент.ВидНоменклатуры = НайтиЗначениеПоРеквизитуСправочника("ВидыНоменклатуры", "Наименование",
		//"Обувь");
		НовыйЭлемент.ВидНоменклатуры = _омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ВидНоменклатуры");
		// } #wortmann
		НовыйЭлемент.ЕдиницаИзмерения =	НайтиЗначениеПоРеквизитуСправочника("УпаковкиЕдиницыИзмерения",
		"Наименование", "пар");
		
		НовыйЭлемент.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг;
		НовыйЭлемент.ВестиУчетПоГТД = Истина;
		НовыйЭлемент.ВестиУчетСертификатовНоменклатуры = Истина;
		НовыйЭлемент.ИспользованиеХарактеристик = 
		Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры;
		НовыйЭлемент.Качество = Перечисления.ГрадацииКачества.Новый;
		НовыйЭлемент.СтавкаНДС = СтавкаНДС20;
		НовыйЭлемент.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
		НовыйЭлемент.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция;
		НовыйЭлемент.Наименование = ВременноеНаименование;
		НовыйЭлемент.КоэффициентЕдиницыДляОтчетов = 1; // Галфинд_Домнышева_17_02_2023
        НовыйЭлемент.УстановитьНовыйКод(); // Галфинд_Домнышева_16_03_2023
		// #wortmann { 
		// Заполняем сезон
		// Галфинд_Домнышева 2022/10/10
		НовыйЭлемент.КоллекцияНоменклатуры = Сезон;
		// } #wortmann 
		НовыйЭлемент.КодДляПоиска = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НовыйЭлемент.Код, Истина);// Галфинд_ДомнышеваКР_22_02_2024
		НовыйЭлемент.ОбменДанными.Загрузка = Истина;
		НовыйЭлемент.Записать();
		
	Иначе
		НовыйЭлемент = НоваяНоменклатура;
		// иногда не правильно стоит в уже загруженной номенклатуре сезон или он может поменяться у номенклатуры
		// #wortmann { 
		// Заполняем сезон
		// Галфинд_Домнышева 2023/11/08
		НовыйЭлемент.КоллекцияНоменклатуры = Сезон;
		// } #wortmann
		// ++ Галфинд_Домнышева_06_02_2024
		// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811eec0fe7668096d
		Если НЕ НовыйЭлемент.ИспользованиеХарактеристик = НовыйЭлемент.ВидНоменклатуры.ИспользованиеХарактеристик 
			И ЗначениеЗаполнено(НовыйЭлемент.ВидНоменклатуры) Тогда 
			НовыйЭлемент.ИспользованиеХарактеристик = НовыйЭлемент.ВидНоменклатуры.ИспользованиеХарактеристик
		Иначе
		// -- Галфинд_Домнышева_06_02_2024	
			НовыйЭлемент.ИспользованиеХарактеристик = 
		Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры;
		КонецЕсли; // Галфинд_Домнышева_06_02_2024

		НовыйЭлемент.ВестиУчетСертификатовНоменклатуры = Истина;
		// ++ Галфинд_ДомнышеваКР_22_02_2024
		Если Не ЗначениеЗаполнено(НовыйЭлемент.КодДляПоиска) Тогда
			НовыйЭлемент.КодДляПоиска = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НовыйЭлемент.Код, Истина);
		КонецЕсли;
		// -- Галфинд_ДомнышеваКР_22_02_2024
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции// } #wortmann 

// #wortmann { 
// Процедура записывает ранее созданную или полученную Номенклатуру  
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//	НовыйЭлемент - СправочникОбъект.Номенклатура - если она найдено, Неопределено в ином случае.
//	КоличествоСтрок - Число - кол-во строк в выгруженной ТЗ из РС гф_СтрокиДокументаДанныеЗагрузки
//	НоменклатураЗаписана - Булево - прзнак записанной номенклатуры (чтобы не записывать снова)
//	Отказ - параметр системы
Процедура ИтоговаяЗаписьЭлемента(СтрокаТЧ, НовыйЭлемент, КоличествоСтрок, НоменклатураЗаписана, Отказ)
	
	Если НовыйЭлемент <> Неопределено И НЕ НоменклатураЗаписана Тогда
		
		GLN_manufacturer	= НайтиСтрокуДереваПоТэгу("GLN_manufacturer", Отказ);
		ДописатьСвойствоGLN_manufacturer(НовыйЭлемент, GLN_manufacturer, СтрокаТЧ);
		
		// Перед записью предварительно проверим содержимое следующего по порядку тэга (следующей строки)
		// и убедимся что это тэг с новой "Message_Position"  или последняя строка
		МенеджерЗаписейСтрок		= РегистрыСведений.гф_СтрокиДокументаДанныеЗагрузки.СоздатьМенеджерЗаписи();
		МенеджерЗаписейСтрок.Документ 				= Ссылка;
		// BSLLS:Typo-off
		МенеджерЗаписейСтрок.Тэг 					= "Message_Position";
		// BSLLS:Typo-on
		МенеджерЗаписейСтрок.ПорядковыйНомерСтроки 	= СтрокаТЧ.НомерСтрокиРодителя; 
		МенеджерЗаписейСтрок.Прочитать();
		Если МенеджерЗаписейСтрок.Выбран() ИЛИ СтрокаТЧ.ПорядковыйНомерСтроки = КоличествоСтрок Тогда
			
			Попытка
				НовыйЭлемент.ОбменДанными.Загрузка = Истина; // Галфинд dlip 2023/02/17
				НовыйЭлемент.Записать();
				РегистрыСведений.ГруппыЗначенийДоступа.ОбновитьГруппыЗначенийДоступа(НовыйЭлемент.Ссылка); // Галфинд_ДомнышеваК_22_02_2024
				НовыйЭлемент 			= Неопределено;
				
			Исключение
				НовыйЭлемент 			= Неопределено;
				Отказ = Истина;
				СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.НомерСтрокиРодителя
				+ ". Описание ошибки: " + ОписаниеОшибки();
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
				флЗаписиНоменклатуры = Ложь;
			КонецПопытки;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Процедура находит ТЭГ "Position_action" и по необходимости ставит НовомуЭлементу пометку на удаление.  
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//	НовыйЭлемент - СправочникОбъект.Номенклатура - если она найдено, Неопределено в ином случае.
//	Отказ - параметр системы
Процедура ОпределимПометкуУдаленияДляДокумента(СтрокаТЧ, НовыйЭлемент, Отказ)
	
	Position_action = НайтиСтрокуДереваПоТэгу("Position_action", Отказ, СтрокаТЧ);
	Если ЗначениеЗаполнено(Position_action) Тогда
		Если СокрЛП(Position_action) = "2" Тогда
			НовыйЭлемент.ПометкаУдаления = Истина;
		Иначе
			НовыйЭлемент.ПометкаУдаления = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Ищет сезон из справочника КоллекцииНоменклатуры по наименованию ТЭГа "Supplier_season"
// Галфинд_Домнышева 2022/10/10
//
// Параметры:
//	СтрокаТЧ - Тип.СтрокаТЧ - текущая строка табличной части НаборЗаписейТэгов
//	Отказ - параметр системы
//	Сезон - Строка - значение ТЭГа "Supplier_season"
//
// Возвращаемое значение:
//	Сезон - СправочникСсылка.КоллекцииНоменклатуры 
Функция УстановитьСезон(СтрокаТЧ, Отказ, Сезон = Неопределено)
	
	Если Сезон = Неопределено Тогда
		Сезон = НайтиСтрокуДереваПоТэгу("Supplier_season", Отказ, СтрокаТЧ);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КоллекцииНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КоллекцииНоменклатуры КАК КоллекцииНоменклатуры
		|ГДЕ
		|	КоллекцииНоменклатуры.Код = &Сезон";
	
	Запрос.УстановитьПараметр("Сезон", Сезон);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка.Следующий();	
		Возврат Выборка.Ссылка;		
	Иначе
		СезонОбъект = Справочники.КоллекцииНоменклатуры.СоздатьЭлемент();
		СезонОбъект.Код = Сезон;
		СезонОбъект.Наименование = Сезон;
		СезонОбъект.Записать();
		Возврат СезонОбъект.Ссылка;
	КонецЕсли;

КонецФункции// } #wortmann

// #wortmann { 
// Возвращает строку родителя, либо саму строку-параметр, если родителя не нашлось.
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части по которой ищется ПорядковыйНомерСтроки родителя
//
// Возвращаемое значение:
//	Строка - если найден порядковый номер строки родителя 
//	СтрокаТЧ - строка табличной части по которой ищется ПорядковыйНомерСтроки родителя
Функция СтрокаРодитель(СтрокаТЧ)
	НайденноеЗначение = НаборЗаписейТэгов.Найти(СтрокаТЧ.НомерСтрокиРодителя, "ПорядковыйНомерСтроки");
	Если НайденноеЗначение = Неопределено Тогда
		Возврат СтрокаТЧ;
	Иначе
		Возврат НайденноеЗначение;
	КонецЕсли;
КонецФункции// } #wortmann

// #wortmann { 
// Создание и Проведение документа УстановкаЦенНоменклатуры 
// Не доработана, в связи с недостатком информации
// Галфинд_Домнышева 2022/10/12
//
// Параметры:
//	ТабличнаяЧасть - ТаблицаЗначений - выгрузка РС гф_СтрокиДокументаДанныеЗагрузки по данному Объекту.
//	СтруктураЦен - Структура - значения ТЭГов и глобальных значений
//	Отказ - параметр системы 
Процедура СозданиеПроведениеУстановкиЦен(ТабличнаяЧасть, СтруктураЦен, Отказ) 
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивЭлементовНоменклатуры = Новый Массив;
	ДокументУстановкаЦен = Документы.УстановкаЦенНоменклатуры.СоздатьДокумент();
	ДокументУстановкаЦен.Дата = Дата(НайтиСтрокуДереваПоТэгу("Document_date", Отказ));
	ДокументУстановкаЦен.Согласован = Ложь;
	ДокументУстановкаЦен.Статус = Перечисления.СтатусыУстановокЦенНоменклатуры.Согласован;

	ДокументУстановкаЦен.Ответственный = Пользователи.ТекущийПользователь();
	УстанавливатьЦены = Истина;
			
	НовыйЭлемент = Неопределено;
	
	СоответствиеЦен		= Новый Соответствие;
	СоответствиеЦен.Вставить("Suggested_retail_price",		СтруктураЦЕН.СПР_ТипЦен_СПР);
	СоответствиеЦен.Вставить("Effective_purchase_price",	СтруктураЦЕН.СПР_ТипЦен_ЕПП);
	
	УстанавливатьЦены = СтруктураЦЕН.УстанавливатьЦены;
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл 
		
		УсловиеСоздания = ЗначениеЗаполнено(СтрокаТЧ.ПолеЗагрузки) 
		И (СтрокаТЧ.ПолеЗагрузки.ТипОбъекта = "Установка цен номенклатуры") 
		И УстанавливатьЦены И (СтрокаТЧ.ПолеЗагрузки = СтруктураЦЕН.СПР_ТЭГ_СПР 
		ИЛИ СтрокаТЧ.ПолеЗагрузки = СтруктураЦЕН.СПР_ТЭГ_ЕПП);
		
		Если УсловиеСоздания  Тогда

			НайденноеЗначение = ПоискАртикулаВЗагруженныхСтроках(СтрокаТЧ);
			НайденныйНомерЦвета = ПоискЦветаВЗагруженныхСтроках(СтрокаТЧ);
			Сезон = УстановитьСезон(СтрокаТЧ, Отказ);
					
			ЭлементНоменклатуры = ПоискЭлементаНоменклатурыДляУстановкиЦен(НайденноеЗначение, НайденныйНомерЦвета, 
									СтрокаТЧ, Сезон);
			Если ЭлементНоменклатуры = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ВалютаДокумента = ОпределяемВалютуДокумента(Отказ);	
			Если ВалютаДокумента = Неопределено Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
			ВидЦеныСтроки = СоответствиеЦен.Получить(СтрокаТЧ.ПолеЗагрузки.Наименование); 
			СтрокаУникальна = СтрокаТовары2_5Уникальная(ДокументУстановкаЦен.Товары2_5, ЭлементНоменклатуры, ВидЦеныСтроки);
			
			Если СтрокаУникальна Тогда
				СделатьЗаписьУстановкиЦен(ЭлементНоменклатуры, ВидЦеныСтроки, ДокументУстановкаЦен, 
											СтрокаТЧ, ВалютаДокумента, Отказ);
			КонецЕсли;	
		КонецЕсли;		// устанавливать цены
		
	КонецЦикла;
	
	ЗапишемДокументУстановкиЦен(УстанавливатьЦены, ДокументУстановкаЦен);

КонецПроцедуры// } #wortmann 

// #wortmann { 
// Ищет номенклатуру по Артикулу
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НайденноеЗначение - Массив - Получееный массив по ТЭГ "Suppliers_article_number" из НаборЗаписейТэгов
//	НайденныйНомерЦвета - Строка - номер цвета
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки 
//	Сезон - СправочникСсылка.КоллекцииНоменклатуры
//
// Возвращаемое значение:
//	ЭлементНоменклатуры - СправочникСсылка.Номенклатура 
//	Неопределено - если не найден ТЭГ "Suggested_retail_price" или Номенклатура по артикулу
Функция ПоискЭлементаНоменклатурыДляУстановкиЦен(НайденноеЗначение, НайденныйНомерЦвета, СтрокаТЧ, Сезон)
	
	Если НайденноеЗначение.Количество() > 0 Тогда
		Артикул = НайденноеЗначение[0].Значение + "-" + НайденныйНомерЦвета;
		
		//НайденныйЭлемент = ПолучитьНоменклатуруПоАртикулу(Артикул, Сезон);
		НайденныйЭлемент = НайтиНоменклатуруВБазе(НайденноеЗначение[0].Значение, НайденныйНомерЦвета, Сезон, Неопределено, Неопределено);
		
		Если НайденныйЭлемент <> Неопределено Тогда
			ЭлементНоменклатуры = НайденныйЭлемент.Ссылка; 
			Возврат ЭлементНоменклатуры;
		Иначе
			СообщениеОбОшибке = "Установка цен номенклатуры. Не удалось найти номенклатуру для строки " 
			+ СтрокаТЧ.НомерСтрокиРодителя;
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
			Возврат Неопределено;
		КонецЕсли;
	Иначе	
		СообщениеОбОшибке = "Установка цен номенклатуры. Не удалось найти Suggested_retail_price для узла (строки) № "
		+ СтрокаТЧ.НомерСтрокиРодителя;
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann 

// #wortmann { 
// Определяет валюту документа
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	Отказ - параметр системы
//
// Возвращаемое значение:
//	ВалютаДокумента - СправочникСсылка.Валюты - если значение есть в справочнике 
//	Неопределено - если не найден ТЭГ "Suggested_retail_price" или Номенклатура по артикулу
Функция ОпределяемВалютуДокумента(Отказ) 
	
	СтрНазваниеВалюты = НайтиСтрокуДереваПоТэгуОтПорядкового("Currency", Отказ);	
	Если Отказ Или НЕ ЗначениеЗаполнено(СтрНазваниеВалюты) Тогда
		// BSLLS:Typo-off
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект 
		+ ". Описание ошибки: Не найден или не заполнен тэг Currency.";
		// BSLLS:Typo-on
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Возврат Неопределено;
	Иначе
		ВалютаДокумента = НайтиЗначениеПоРеквизитуСправочника ("Валюты", "Наименование", СокрЛП(СтрНазваниеВалюты));
		Возврат ВалютаДокумента;
	КонецЕсли;	
	
КонецФункции

// #wortmann { 
// Определяет артикул 
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//
// Возвращаемое значение:
//	НайденноеЗначение - Массив - массив со значением артикула 
Функция ПоискАртикулаВЗагруженныхСтроках(СтрокаТЧ)
	
	НомерСтрокиРодителя = СтрокаТЧ.НомерСтрокиРодителя;
	СтруктураОтбора = Новый Структура;
	// BSLLS:Typo-off
	СтруктураОтбора.Вставить("Тэг", "Suppliers_article_number");
	// BSLLS:Typo-on
	ТекущаяСтрокаРодителя   = СтрокаТЧ.ПорядковыйНомерСтроки;
	СтруктураОтбора.Вставить("НомерСтрокиРодителя", НомерСтрокиРодителя);
	
	НайденноеЗначение = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);
	
	Возврат НайденноеЗначение;
	
КонецФункции// } #wortmann 

// #wortmann { 
// Определяет номер цвета 
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	СтрокаТЧ - строка табличной части из РегистрСведений.гф_СтрокиДокументаДанныеЗагрузки
//
// Возвращаемое значение:
//	НайденныйНомерЦвета - Строка - номер цвета
Функция ПоискЦветаВЗагруженныхСтроках(СтрокаТЧ)
	
	НомерСтрокиРодителя = СтрокаТЧ.НомерСтрокиРодителя;
		
	СтруктураОтбораЦвета = Новый Структура;
	// BSLLS:Typo-off
	СтруктураОтбораЦвета.Вставить("Тэг", "Colour_code_supplier");
	// BSLLS:Typo-on
	СтруктураОтбораЦвета.Вставить("НомерСтрокиРодителя", НомерСтрокиРодителя);
	
	НомерЦвета = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбораЦвета);
	НайденныйНомерЦвета = НомерЦвета[0].Значение;
	
	Возврат НайденныйНомерЦвета;
	
КонецФункции// } #wortmann 

// #wortmann { 
// Запись и Проведение документа УстановкаЦенНоменклатуры 
// Не доработана, в связи с недостатком информации
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	УстанавливатьЦены - Булево 
//	ДокументУстановкаЦен - ДокументСсылка.УстановкаЦенНоменклатуры - создаваемый документ
Процедура ЗапишемДокументУстановкиЦен(УстанавливатьЦены, ДокументУстановкаЦен)
	
	Если УстанавливатьЦены И ДокументУстановкаЦен.Товары2_5.Количество() > 0 Тогда
		
		ВидЦены = ДокументУстановкаЦен.Товары2_5.Выгрузить(, "ВидЦены");
		ВидЦены.Свернуть("ВидЦены");
		ДокументУстановкаЦен.ВидыЦен.Загрузить(ВидЦены);
		ДокументУстановкаЦен.Записать(РежимЗаписиДокумента.Запись);
		
		НачатьТранзакцию();
		
		Попытка
			ДокументУстановкаЦен.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			СообщениеОбОшибке = "Произошли ошибки при проведении документа " + ДокументУстановкаЦен + ".|" + ОписаниеОшибки();
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		КонецПопытки;
	КонецЕсли; 
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Получает структуру цен необходимую для создания документа УстановкаЦенНоменклатуры
// Галфинд_Домнышева 2022/10/21
//
// Возвращаемое значение:
//	СтруктураЦен - Структура - содержащая элементы "СПР_ТЭГ_СПР", "СПР_ТЭГ_ЕПП", "СПР_ТипЦен_СПР",
//								"СПР_ТипЦен_ЕПП", "УстанавливатьЦены"
//	Неопределено - если при поиске хотя бы одного элемента был Отказ
Функция ПолучитьСтруктуруЦен()
	
	УстанавливатьЦены = Истина;
	Отказ = Ложь;
	// Проверяем наличие соответствующих тэгов по наименованию в настройках загружаемых данных
	СПР_ТЭГ_СПР			= НайтиЗначениеНастройкиЗагружаемыхДанных("Suggested_retail_price");
	Если СПР_ТЭГ_СПР	= Неопределено ИЛИ СПР_ТЭГ_СПР = Справочники.гф_НастройкаЗагружаемыхДанных.ПустаяСсылка() Тогда
		СообщениеОбОшибке = "Установка цен номенклатуры. Не удалось найти в настройках загружаемых данных 
							|тэг с наименованием Suggested_retail_price";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Отказ 				= Истина;
		УстанавливатьЦены = Ложь;	
	КонецЕсли;	
	СПР_ТЭГ_ЕПП			= НайтиЗначениеНастройкиЗагружаемыхДанных("Effective_purchase_price");
	Если СПР_ТЭГ_ЕПП	= Неопределено ИЛИ СПР_ТЭГ_ЕПП = Справочники.гф_НастройкаЗагружаемыхДанных.ПустаяСсылка() Тогда
		СообщениеОбОшибке = "Установка цен номенклатуры. Не удалось найти в настройках загружаемых данных 
							|тэг с наименованием Suggested_retail_price";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Отказ 				= Истина;
		УстанавливатьЦены = Ложь;	
	КонецЕсли;	
	//
	// Проверяем наличие типов цен по наименованию
	СПР_ТипЦен_СПР			=  _омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначение("гф_ГлобальныеЗначенияРозничнаяЦена");
	Если СПР_ТипЦен_СПР	= Неопределено Тогда
		СообщениеОбОшибке = "Установка цен номенклатуры. Не задано глобальное значение гф_ГлобальныеЗначенияРозничнаяЦена";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Отказ 				= Истина;
		УстанавливатьЦены = Ложь;	
	КонецЕсли;	
	
	СПР_ТипЦен_ЕПП			= 
							_омОбщегоНазначенияКлиентСервер.ПолучитьГлобальноеЗначение("гф_ГлобальныеЗначенияОптоваяЗакупочнаяЦена");
	Если СПР_ТипЦен_ЕПП	= Неопределено Тогда
		СообщениеОбОшибке = "Установка цен номенклатуры. Не задано глобальное значение 
							|гф_ГлобальныеЗначенияОптоваяЗакупочнаяЦена";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Отказ 				= Истина;
		УстанавливатьЦены = Ложь;	
	КонецЕсли;
	
	СтруктураЦен = Новый Структура;
	СтруктураЦен.Вставить("СПР_ТЭГ_СПР", СПР_ТЭГ_СПР);
	СтруктураЦен.Вставить("СПР_ТЭГ_ЕПП", СПР_ТЭГ_ЕПП);
	СтруктураЦен.Вставить("СПР_ТипЦен_СПР", СПР_ТипЦен_СПР);
	СтруктураЦен.Вставить("СПР_ТипЦен_ЕПП", СПР_ТипЦен_ЕПП);
	СтруктураЦен.Вставить("УстанавливатьЦены", УстанавливатьЦены);
	
	Если НЕ Отказ Тогда
		Возврат СтруктураЦен;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann

// #wortmann { 
// Проверяет уникальсть будущей строки в ТЧ Товары2_5
// Галфинд_Домнышева 2022/10/12
//
// Параметры:
//	Товары - ТЧ - табличная часть Товары2_5 документа УстановкаЦенНоменклатуры
//	Номенклатура - СправочникСсылка.Номенклатура - загружаемая номенклатура в ТЧ
//	ВидЦены - СправочникСсылка.ВидыЦен - загружаемый ВидЦен в ТЧ 
// 
// Возвращаемое значение:
//	Булево - Истина, если в ТЧ еще нет строки с такими параметрами, иначе Ложь 
Функция СтрокаТовары2_5Уникальная(Товары, Номенклатура, ВидЦены)
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);
	ПараметрыОтбора.Вставить("ВидЦены", ВидЦены);

	ТоварыНайдены = Товары.НайтиСтроки(ПараметрыОтбора);
	
	Если ТоварыНайдены.Количество() > 0 Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции// } #wortmann

// #wortmann { 
// Находит необходимый элемент спр гф_НастройкаЗагружаемыхДанных по Наименованию с интерфейсом ORDRSP
// Галфинд_Домнышева 2022/10/12
//
// Параметры:
//	Значение - Строка - Наименование элемента в спр гф_НастройкаЗагружаемыхДанных 
// 
// Возвращаемое значение:
//	СправочникСсылка.гф_НастройкаЗагружаемыхДанных - В случае нахождения элемента по заданному наименованию
//	Неопределено - В случае отсутствия элемента в спр гф_НастройкаЗагружаемыхДанных 
Функция НайтиЗначениеНастройкиЗагружаемыхДанных(Значение)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
		|	гф_НастройкаЗагружаемыхДанных.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.гф_НастройкаЗагружаемыхДанных КАК гф_НастройкаЗагружаемыхДанных
		|ГДЕ
		|	гф_НастройкаЗагружаемыхДанных.Интерфейс = &Интерфейс
		|	И гф_НастройкаЗагружаемыхДанных.Наименование = &Значение";
	
	Запрос.УстановитьПараметр("Значение", Значение);
	Запрос.УстановитьПараметр("Интерфейс", Перечисления.гф_Интерфейсы.PRICAT);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann 

// #wortmann { 
// Добавляет запись в табличную часть Товары2_5 документа УстановкаЦенНоменклатуры
// Галфинд_Домнышева 2022/10/12
//
// Параметры:
//	пар_Номенклатура - СправочникСсылка.Номенклатура - загружаемая номенклатура в ТЧ
//	пар_ТипЦен - СправочникСсылка.ВидыЦен - загружаемый ВидЦен в ТЧ 
//	пар_ДокУстановкиЦен - ДокументСсылка.УстановкаЦенаНоменклатуры - создаваемый документ
//	СтрокаТЧ - Тип.СтрокаТЧ - текущая строка табличной части НаборЗаписейТэгов 
//	ВалютаДокумента - СправочникСсылка.Валюты
//	Отказ - системный параметр
Процедура СделатьЗаписьУстановкиЦен(пар_Номенклатура, пар_ТипЦен, пар_ДокУстановкиЦен, СтрокаТЧ, ВалютаДокумента, Отказ)

	ИспользуетсяЦенообразование25 = ПолучитьФункциональнуюОпцию("ИспользуетсяЦенообразование25");
	
	СтруктураВходПар 		= Новый Структура;
	СтруктураВходПар.Вставить("Период", 				пар_ДокУстановкиЦен.Дата);						
	СтруктураВходПар.Вставить("Номенклатура", 		пар_Номенклатура.Ссылка);
	СтруктураВходПар.Вставить("ТипЦен", 				пар_ТипЦен);
	
	Если ЕстьЗаписьСТакимиПараметрамиВЦенах(СтруктураВходПар) Тогда
		// Галфинд_Домнышева_16_03_2023
		// Добавлено глоб.перем. пар_НоменклатураАртикулПрошлый, для не внесения повторных данных в ошибки
		Если пар_Номенклатура.Артикул <> пар_НоменклатураАртикулПрошлый Тогда
			СообщениеОбОшибке = "Для Артикула " + пар_Номенклатура.Артикул + " в периоде " 
			+ Формат(пар_ДокУстановкиЦен.Дата, "ДЛФ=Д") + " цена уже записана. Не обрабатывается";
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке, "Информация");
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Информация, ЭтотОбъект, ЭтотОбъект,
									СообщениеОбОшибке);
			пар_НоменклатураАртикулПрошлый = пар_Номенклатура.Артикул;
		// Галфинд_Домнышева_16_03_2023	
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	НоваяСтрока 			= пар_ДокУстановкиЦен.Товары2_5.Добавить();
	НоваяСтрока.Номенклатура 		= пар_Номенклатура.Ссылка;
	НоваяСтрока.ВидЦены 	= пар_ТипЦен;
	НоваяСтрока.Валюта 	= ВалютаДокумента;
	
	НоваяСтрока.Цена = Число(СтрокаТЧ.Значение);

КонецПроцедуры// } #wortmann

// #wortmann { 
// Проверка существования в регистре "Цены номенклатуры 25" записи
// с указанными ключевыми параметрами
// Галфинд_Домнышева 2022/10/12
//
// Параметры:
//	СтруктураВходПар - Структура - Структура состоит из данных записываемой строки в ТЧ Товары2_5 
// 
// Возвращаемое значение:
//	Булево - Истина, если в РС есть запись с такими параметрами, иначе Ложь
Функция ЕстьЗаписьСТакимиПараметрамиВЦенах(СтруктураВходПар)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныНоменклатуры25.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатуры25.ВидЦены КАК ВидЦены
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры25
		|ГДЕ
		|	ЦеныНоменклатуры25.Период = &Период
		|	И ЦеныНоменклатуры25.Номенклатура = &Номенклатура
		|	И ЦеныНоменклатуры25.ВидЦены = &ВидЦены";
	
	Запрос.УстановитьПараметр("ВидЦены", СтруктураВходПар.ТипЦен);
	Запрос.УстановитьПараметр("Номенклатура", СтруктураВходПар.Номенклатура);
	Запрос.УстановитьПараметр("Период", СтруктураВходПар.Период);

	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда
		Возврат Истина;
	КонецЕсли;	
	Возврат Ложь;
	
КонецФункции// } #wortmann	

// #wortmann { 
// Создание варианта комплектации
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	НаборЗаписейТэгов - ТаблицаЗначений - выгрузка РС гф_СтрокиДокументаДанныеЗагрузки по данному Объекту.
//	Отказ - параметр системы
Процедура СоздатьВариантКомплектации(НаборЗаписейТэгов, Отказ)
			
	Если Не ПроверкаПравильности_PricatSort() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;   
	
	ВариантКомплектации = Неопределено;
	НомерСтрокиРодителя = 0;
	
	Для Каждого СтрокаТЧ Из НаборЗаписейТэгов Цикл 				// Цикл по тегам XML
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		Если СтрНайти(СтрокаТЧ.Тэг, "Message_Position") > 0 Тогда

			Suppliers_article_number = НайтиСтрокуДереваПоТэгуОтПорядкового("Suppliers_article_number", Отказ, СтрокаТЧ);
			Colour_code_supplier = НайтиСтрокуДереваПоТэгуОтПорядкового("Colour_code_supplier", Отказ, СтрокаТЧ);
			
			// Разбираем строку из XML на необходимые значения
			ПозицияПервогоТире = СтрНайти(Suppliers_article_number, "-"); 
			НаименованиеБезСО = Сред(Suppliers_article_number, ПозицияПервогоТире + 1); 
			ПозицияВторогоТире = СтрНайти(НаименованиеБезСО, "-");
			Характеристика = Лев(НаименованиеБезСО, ПозицияВторогоТире - 1);
			Наименование = Сред(НаименованиеБезСО, ПозицияВторогоТире + 1); 
			НаименованиеВарианта = Наименование + "-" + Colour_code_supplier + "/" + Характеристика;
			Артикул = Наименование + "-" + Colour_code_supplier;
			Вариант = НайтиЗначениеПоРеквизитуСправочника("ВариантыКомплектацииНоменклатуры",
			"Наименование", НаименованиеВарианта);
			
			ВариантКомплектации = СозданиеВариантаКомплектации(Вариант, Артикул, НаименованиеВарианта, Характеристика,
									СтрокаТЧ, Отказ);
			Если ВариантКомплектации = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
		ИначеЕсли СтрНайти(СтрокаТЧ.Тэг, "Message_Sub_Position") > 0
			И ВариантКомплектации <> Неопределено Тогда
			НоваяСтрока = ВариантКомплектации.Товары.Добавить();
			EAN = НайтиСтрокуДереваПоТэгуОтПорядкового("EAN", Отказ, СтрокаТЧ);
			Отбор = Новый Структура("Штрихкод", EAN);
			СтруктураНоменклатуры = РегистрыСведений.ШтрихкодыНоменклатуры.Получить(Отбор);
			НоваяСтрока.Номенклатура = СтруктураНоменклатуры.Номенклатура;
			НоваяСтрока.Характеристика = СтруктураНоменклатуры.Характеристика;
			НоваяСтрока.Количество = НайтиСтрокуДереваПоТэгуОтПорядкового("Quantity", Отказ, СтрокаТЧ);
			НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество;
			НоваяСтрока.Упаковка = СтруктураНоменклатуры.Упаковка;
			ВариантКомплектации.Записать();	
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если Отказ Тогда
			СообщениеОбОшибке = "Произошли ошибки при записи Варианта комплектации " + ВариантКомплектации 
			+ ".|" + ОписаниеОшибки();
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
			
		КонецЕсли;
		
	КонецЦикла;				
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Создание или получение варианта комплектации
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	Вариант - СправочникСсылка.ВариантыКомплектацииНоменклатуры или Неопределено
//	Артикул - Строка - значение артикула составленное из данных ТЭГов
//	НаименованиеВарианта - Строка - значение наименования варианта составленное из данных ТЭГов
//	Характеристика - Строка - значение характеристики номенклатуры из ТЭГов 
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части которая обрабатывается в данный момент
//	Отказ - параметр системы
//	
// Возвращаемое значение:
//	ВариантКомплектации - СправочникСсылка.ВариантыКомплектацииНоменклатуры. 
Функция СозданиеВариантаКомплектации(Вариант, Артикул, НаименованиеВарианта, Характеристика, СтрокаТЧ, Отказ)
	
	Если Вариант <> Неопределено Тогда
		
		Если ВариантКомплектацииСОшибками(Вариант, СтрокаТЧ, Отказ) Тогда

		    ВариантКомплектации = Вариант.ПолучитьОбъект(); 
			ВариантКомплектации.Товары.Очистить();
		
		Иначе
			СообщениеОбОшибке = "Примечание при проведении документа " + ЭтотОбъект 
				+ ". Вариант комплектации уже существует - " + НаименованиеВарианта;
			//ТипИнформации = "Важная информация";
			//ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке, ТипИнформации);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Примечание, ЭтотОбъект, ЭтотОбъект,
									СообщениеОбОшибке);
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе
		ВариантКомплектации = Справочники.ВариантыКомплектацииНоменклатуры.СоздатьЭлемент();
		ВариантКомплектации.Наименование = НаименованиеВарианта;
		
	КонецЕсли;	
	
	// или перезапишем данные или заполним новый вариант
	// ++ Галфинд_ДомнышеваКР_от _27_02_23	
	СезонЗначение = НайтиСтрокуДереваПоТэгуОтПорядкового("Supplier_season", Отказ, СтрокаТЧ);
	Сезон = УстановитьСезон(СтрокаТЧ, Отказ, СезонЗначение);	
	// -- Галфинд_ДомнышеваКР_от _27_02_23
	ВариантКомплектации.Характеристика = НайтиЗначениеПоРеквизитуСправочника("ХарактеристикиНоменклатуры",
										"Наименование", Характеристика);	
	Номенклатура = ПолучитьНоменклатуруПоАртикулу(Артикул, Сезон); // ++ Галфинд_ДомнышеваКР_от _27_02_23
	
	Если Номенклатура <> Неопределено Тогда 
		Если НаличиеШтрихкодовВБазе(Артикул, СтрокаТЧ, Отказ, Номенклатура) Тогда
			ВариантКомплектации.Владелец = Номенклатура.Ссылка; 
		Иначе
			Возврат Неопределено;
		КонецЕсли;		
	Иначе
		Если НаличиеШтрихкодовВБазе(Артикул, СтрокаТЧ, Отказ, Номенклатура) Тогда
			ВариантКомплектации.Владелец = Номенклатура.Ссылка; 
		Иначе
			
			СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект 
			+ ". Описание ошибки: Не найдена номенклатура с артикулом - " + Артикул
			+ " Или у этой номенклатуры не заполнен Сезон. Вариант комплектации " + НаименованиеВарианта + " не создан.";
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ВариантКомплектации.Количество = НайтиСтрокуДереваПоТэгуОтПорядкового("Quantity_per_packing_unit", Отказ, СтрокаТЧ);
	ВариантКомплектации.КоличествоУпаковок = ВариантКомплектации.Количество; 
	ВариантКомплектации.гф_Штрихкод = НайтиСтрокуДереваПоТэгуОтПорядкового("EAN", Отказ, СтрокаТЧ);
	ВесКороба = НайтиСтрокуДереваПоТэгуОтПорядкового("Weight", Отказ, СтрокаТЧ);
	ЕдиницаИзмеренияВеса = НайтиСтрокуДереваПоТэгуОтПорядкового("WeightUnit", Отказ, СтрокаТЧ);
	//ВариантКомплектации.Упаковка = 
	К = ?(ЕдиницаИзмеренияВеса = "g", 1000, 1);
	
	// В спр ВариантКомплектации Вес указывается в граммах 
	Если ЗначениеЗаполнено(ВесКороба) Тогда
		ВариантКомплектации.гф_ВесКороба = ?(ВесКороба = 0, 0, ВесКороба / К); 
	КонецЕсли;
	ВариантКомплектации.гф_ДлинаКороба = НайтиСтрокуДереваПоТэгуОтПорядкового("Length", Отказ, СтрокаТЧ);
	ВариантКомплектации.гф_ШиринаКороба = НайтиСтрокуДереваПоТэгуОтПорядкового("Width", Отказ, СтрокаТЧ);
	ВариантКомплектации.гф_ВысотаКороба = НайтиСтрокуДереваПоТэгуОтПорядкового("Height", Отказ, СтрокаТЧ);
	ВариантКомплектации.Записать();
	
	ЗаписатьДатаОбновленияВариантаКомплектацииИзI5(ВариантКомплектации, СтрокаТЧ, Отказ); // Галфинд_Домнышева 2024/02/13
	
	Возврат ВариантКомплектации;		
		
КонецФункции// } #wortmann  

// #wortmann { 
// Дописывает дополнительное свойство гф_ДатаОбновленияВариантаКомплектацииИзI5 объекта в РС ДополнительныеСведения
// Галфинд_Домнышева 2024/02/13
//
// Параметры:
//	ВариантКомплектации - СправочникОбъект.ВариантыКомплектацииНоменклатуры - создоваемый объект 
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем 
//	Отказ - параметр системы
Процедура ЗаписатьДатаОбновленияВариантаКомплектацииИзI5(ВариантКомплектации, СтрокаТЧ, Отказ)
	
	ДатаОбновленияНовая = Дата(НайтиСтрокуДереваПоТэгу("Document_date", Отказ));

	ЗначенияСвойств = Новый Массив;
	Свойство_Дата = НайтиДополнительныйРеквизитПоИдентификатору("гф_ДатаОбновленияВариантаКомплектацииИзI5");
	Значение = Новый Структура("Свойство, Значение", Свойство_Дата, ДатаОбновленияНовая);
	ЗначенияСвойств.Добавить(Значение);
	ЗаписатьНаборСвойствВРегистр(ВариантКомплектации.Ссылка, ЗначенияСвойств, СтрокаТЧ);   
	
КонецПроцедуры// } #wortmann 

// #wortmann { 
// Создание или получение варианта комплектации
// Галфинд_Домнышева 2023/16/03
//
// Параметры:
//	Вариант - СправочникСсылка.ВариантыКомплектацииНоменклатуры или Неопределено
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части которая обрабатывается в данный момент
//	Отказ - параметр системы
//	
// Возвращаемое значение:
//	Булево - Истина, если есть ошибки в ВариантеКомплектации, иначе Ложь 
Функция ВариантКомплектацииСОшибками(Вариант, СтрокаТЧ, Отказ)	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатуры.Характеристика КАК Характеристика,
		|	ВариантыКомплектацииНоменклатуры.гф_Штрихкод КАК гф_Штрихкод,
		|	ВариантыКомплектацииНоменклатуры.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВариантыКомплектацииНоменклатуры.гф_ВысотаКороба КАК гф_ВысотаКороба,
		|	ВариантыКомплектацииНоменклатуры.гф_ШиринаКороба КАК гф_ШиринаКороба,
		|	ВариантыКомплектацииНоменклатуры.гф_ДлинаКороба КАК гф_ДлинаКороба,
		|	ВариантыКомплектацииНоменклатуры.гф_ВесКороба КАК гф_ВесКороба
		|ИЗ
		|	Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	    |ГДЕ
		|	ВариантыКомплектацииНоменклатуры.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Вариант);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();
	
	Если Выборка.гф_Штрихкод <> НайтиСтрокуДереваПоТэгуОтПорядкового("EAN", Отказ, СтрокаТЧ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Строка(Выборка.КоличествоУпаковок)
		<> НайтиСтрокуДереваПоТэгуОтПорядкового("Quantity_per_packing_unit", Отказ, СтрокаТЧ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	// ++ Галфинд_ДомнышеваКР_27_03_2024
	УсловиеНезаполненностиВГХ = НЕ ЗначениеЗаполнено(Выборка.гф_ВысотаКороба) ИЛИ Выборка.гф_ВысотаКороба = 0
	ИЛИ НЕ ЗначениеЗаполнено(Выборка.гф_ШиринаКороба) ИЛИ Выборка.гф_ШиринаКороба = 0
	ИЛИ НЕ ЗначениеЗаполнено(Выборка.гф_ДлинаКороба) ИЛИ Выборка.гф_ДлинаКороба = 0
	ИЛИ НЕ ЗначениеЗаполнено(Выборка.гф_ВесКороба) ИЛИ Выборка.гф_ВесКороба = 0;
	
	Если УсловиеНезаполненностиВГХ Тогда
		Возврат Истина;
	КонецЕсли;
    // -- Галфинд_ДомнышеваКР_27_03_2024

	Если Вариант.Владелец.ИспользованиеХарактеристик 
		= Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры 
		// #wortmann { 
		// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee4573acf7342c
		// Галфинд_Домнышева 2023/08/28
		// ИЛИ Вариант.Владелец.ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Обувь") Тогда
		ИЛИ Вариант.Владелец.ВидНоменклатуры = 
								_омОбщегоНазначенияВызовСервера.ПолучитьГлобальноеЗначение("гф_ВидНоменклатуры")  Тогда
		// } #wortmann
		
		Если Выборка.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() Тогда
			Возврат Истина;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика
		|ИЗ
		|	Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|ГДЕ
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Вариант);
		
		РезультатЗапроса = Запрос.Выполнить();
		// ++ Галфинд_ДомнышеваКР_13_02_2024
		// При загрузке январских ломаных файлов появились элементы с пустой ТЧ
		Если РезультатЗапроса.Пустой() Тогда
			Возврат Истина;
	    Иначе
		// -- Галфинд_ДомнышеваКР_13_02_2024
		Выборка2 = РезультатЗапроса.Выбрать();
		
		Пока Выборка2.Следующий() Цикл
			
			Если Выборка2.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() Тогда
				Возврат Истина;
			КонецЕсли;  
		КонецЦикла;
		КонецЕсли;// Галфинд_ДомнышеваКР_13_02_2024
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции// } #wortmann

// #wortmann { 
// Проверяет наличие всех значений тэгов EAN в РС Штрихкоды номенклатуры
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	ВыводитьПрогрессор - Булево - выводит форму Прогрессора при значении Истина
//	СтрокаНаименованиеТэга - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//
// Возвращаемое значение:
//	Результат - Булево - Возврат Истина, если в базе найдены все Штрихкоды номенклатуры, 
//                      - Ложь - если не найдено хоть одно значение штрихкода номенклатуры 
// BSLLS:LatinAndCyrillicSymbolInWord-off
Функция ПроверкаПравильности_PricatSort(ВыводитьПрогрессор = Ложь, СтрокаНаименованиеТэга = "Group_type")
// BSLLS:LatinAndCyrillicSymbolInWord-on
	Отказ = Ложь;
	Group_type = НайтиСтрокуДереваПоТэгу(СтрокаНаименованиеТэга, Отказ);

	Если Отказ Тогда
		// BSLLS:Typo-off
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + ". Описание ошибки: Не найден тэг Group_type.";
		// BSLLS:Typo-on
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Возврат Ложь;
	КонецЕсли;
	
	Если СокрЛП(Group_type) = "10E" Тогда								// Это Pricat Sort - производим проверку
		
		Возврат Истина;
		
	КонецЕсли; 
	
КонецФункции// } #wortmann 

// #wortmann { 
// Проверяет наличие Штрихкодов номенклатуры с заданным Артикулом
// И переопределяет Номенклатуру согласно значению из Штрихкода
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	Артикул - Строка - значение Артикула
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части которая обрабатывается в данный момент
//	Отказ - параметр системы
//	Номенклатура - СправочникСсылка.Номенклатура, Неопределено
//
// Возвращаемое значение:
//	Булево - Истина, если в базе есть все штрихкоды из файла, Ложь если не хватает хоть одного.
Функция НаличиеШтрихкодовВБазе(Артикул, СтрокаТЧ, Отказ, Номенклатура)
	
	МассивНеНайденныхШтрихкодов = Новый Массив;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Тэг", "Message_Sub_Position");
	СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.ПорядковыйНомерСтроки);
	СтрокиДляПоискаШтрихкодов = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);

	Для каждого Строка Из СтрокиДляПоискаШтрихкодов Цикл
		
		EAN	= НайтиСтрокуДереваПоТэгуОтПорядкового("EAN", Отказ, Строка);
		Отбор = Новый Структура("Штрихкод", EAN);
		НайденныйЭлемент = РегистрыСведений.ШтрихкодыНоменклатуры.Получить(Отбор).Номенклатура;
		
		Если НайденныйЭлемент = Неопределено ИЛИ НайденныйЭлемент.Пустая() Тогда
			МассивНеНайденныхШтрихкодов.Добавить(EAN);
		Иначе
			Номенклатура = НайденныйЭлемент;
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивНеНайденныхШтрихкодов.Количество() > 0 Тогда
		
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + ". Описание ошибки: При проверке номенклатуры "
		+ "с артикулом - " + Артикул + " для Pricat_Sort не обнаружены штрихкоды: ";	
		Для каждого Стр Из МассивНеНайденныхШтрихкодов Цикл
		СообщениеОбОшибке = СообщениеОбОшибке + Символы.ПС + Стр;
		КонецЦикла;
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
        Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

// #wortmann { 
// Заполнение дополнительных свойств номенклатуры
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	Отказ - параметр системы
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	ТаблицаРегистраСоответствияСвойств - Таблица Значений - выгрузка РегистрСведений_гф_ArticlrDescriptions
Процедура ДополнитьРеквизитыЭлементаНоменклатуры(СтрокаТЧ, Отказ, НовыйЭлемент, ТаблицаРегистраСоответствияСвойств)
	
	Product_group_supplier	= Неопределено;
	GLN_supplier			= Неопределено;
	
	СтруктураСвойств	= ВыбратьВсеТэгиСвойствПоСтрокеАртикулу(СтрокаТЧ);
	
	GLN_supplier = НайтиСтрокуДереваПоТэгу("GLN_supplier", Отказ);	
	Если Отказ Тогда
		// BSLLS:Typo-off
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект 
							+ ". Описание ошибки: Не найден тэг GLN_supplier.";
		// BSLLS:Typo-on
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	НайденноеЗначение	= Неопределено;
	СвойствоНайдено = СтруктураСвойств.Свойство("Product_group_supplier", НайденноеЗначение);
	Если СвойствоНайдено Тогда
		Product_group_supplier = НайденноеЗначение;
	КонецЕсли;
	
	// ----------------------------------------------------------------
	// 1. Формируем наименование
	// ----------------------------------------------------------------
	ФормированиеНаименования(НовыйЭлемент, СтрокаТЧ, Product_group_supplier, GLN_supplier,
							ТаблицаРегистраСоответствияСвойств);	
	// ----------------------------------------------------------------
	// 2. Формируем базовую единицу измерения
	// ----------------------------------------------------------------
	Если ЗначениеЗаполнено(Product_group_supplier) И ЗначениеЗаполнено(GLN_supplier) Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Product_group", Product_group_supplier);
		СтруктураОтбора.Вставить("GLN_Supplier", GLN_supplier);
		СтрокиНаименований	= ТаблицаРегистраСоответствияСвойств.НайтиСтроки(СтруктураОтбора);		
		Если СтрокиНаименований.Количество() > 0 Тогда
			ЕИКлассификатор	= СтрокиНаименований[0].ЕдиницаИзмеренияПоКлассификатору;
			// проверка на измененеие и если необходимо - запись
			Если НЕ НовыйЭлемент.ЕдиницаИзмерения = ЕИКлассификатор Тогда
				НовыйЭлемент.ЕдиницаИзмерения = ЕИКлассификатор;
				флЗаписиНоменклатуры	= Истина;					
			КонецЕсли;
		Иначе
			СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект 
								+ ". Описание ошибки: В регистре ""ArticlrDescriptions"" не удалось найти единицу измерения для "
								+ СокрЛП(Product_group_supplier);
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	Иначе
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект 
							+ ". Описание ошибки: Не удалось определить свойство ""Product_group_supplier"" для Articl:"
							+ СокрЛП(НовыйЭлемент.Артикул);
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		Отказ = Истина;
		Возврат;	
	КонецЕсли;
	// ----------------------------------------------------------------
	// 3. Добавляем значения производителя и марки 
	// ----------------------------------------------------------------
	
	ФормированиеПроизводителя(НовыйЭлемент, СтрокаТЧ, Отказ); 
	ФормированиеМарки(НовыйЭлемент, СтрокаТЧ, Отказ); 
	
	// ----------------------------------------------------------------
	// 4. Записываем дополнительные свойства объекта 
	// ----------------------------------------------------------------
	ФормированиеДополнительныхРеквизитов(НовыйЭлемент, СтрокаТЧ, GLN_supplier); 
	
	ДобавитьГруппуДоступаИГруппуСписка(НовыйЭлемент); // ++ Галфинд_Домнышева 2023/16/03
	
КонецПроцедуры// } #wortmann 

// #wortmann { 
// Заполнение группы доступа и родителя номенклатуры из даных РС
// Галфинд_Домнышева 2023/16/03
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
Процедура ДобавитьГруппуДоступаИГруппуСписка(НовыйЭлемент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гф_СоответствиеОрганизацииГруппеДступаИСписка.ГруппаДоступа КАК ГруппаДоступа,
		|	гф_СоответствиеОрганизацииГруппеДступаИСписка.ГруппаСписка КАК ГруппаСписка
		|ИЗ
		|	РегистрСведений.гф_СоответствиеОрганизацииГруппеДступаИСписка КАК гф_СоответствиеОрганизацииГруппеДступаИСписка
		|ГДЕ
		|	гф_СоответствиеОрганизацииГруппеДступаИСписка.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		НовыйЭлемент.Родитель = Выборка.ГруппаСписка;
		НовыйЭлемент.ГруппаДоступа = Выборка.ГруппаДоступа;
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Процедура формирует наименование для созданной или полученной Номенклатуры
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	Product_group_supplier - Строка - значение ТЭГ "Product_group_supplier"
//	GLN_supplier - Строка - значение ТЭГ "GLN_supplier"
//	ТаблицаРегистраСоответствияСвойств - Таблица Значений - выгрузка РегистрСведений_гф_ArticlrDescriptions
Процедура ФормированиеНаименования(НовыйЭлемент, СтрокаТЧ, Product_group_supplier, GLN_supplier,
									ТаблицаРегистраСоответствияСвойств)
		
	Отказ = Ложь;								
	СезонЗначение = НайтиСтрокуДереваПоТэгу("Supplier_season", Отказ, СтрокаТЧ);
								
	Если ЗначениеЗаполнено(Product_group_supplier) Тогда
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Product_group", Product_group_supplier);
		СтруктураОтбора.Вставить("GLN_Supplier", GLN_supplier);
		СтрокиНаименований	= ТаблицаРегистраСоответствияСвойств.НайтиСтроки(СтруктураОтбора);		
		Если СтрокиНаименований.Количество() > 0 Тогда
			
			СтрокаН = СтрокиНаименований[0];			
			
			РусскоеНаименование	 = СтрокаН.NameRU;
			// проверка наименования 
			ИскомоеНаименование = РусскоеНаименование + " " + СезонЗначение;
			Если СокрЛП(НовыйЭлемент.Наименование) <> ИскомоеНаименование Тогда
								
				НовыйЭлемент.Наименование = ИскомоеНаименование;
			КонецЕсли;
			
			Если СокрЛП(НовыйЭлемент.НаименованиеПолное) <> РусскоеНаименование Тогда
				НовыйЭлемент.НаименованиеПолное = РусскоеНаименование; // Галфинд_Домнышева 17.02.23
			КонецЕсли;
						
			// если указан родитель, помещаем в эту группу
			Родитель = СтрокаН.Родитель;
			Если НЕ НовыйЭлемент.Родитель = Родитель Тогда
				НовыйЭлемент.Родитель = Родитель;
			КонецЕсли;
			
			// проверка вида номенклатуры, по умолчанию присваиваем "Обувь в парах импортная"	
			ФормированиеВидаНоменклатурыПолСтавки(НовыйЭлемент, СтрокаН, СтрокаТЧ);
			НовыйЭлемент.ОбменДанными.Загрузка = Истина;
			НовыйЭлемент.Записать(); // Галфинд_Домнышева 30.01.23_запишем элемент чтобы обновилась ссылка на новое наименование
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// #wortmann { 
// Процедура формирует ВидНоменклатуры, Пол, СтавкиНДС
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	СтрокаН - ЭлементМассива - Элемент Массива отобранного из РегистрСведений_гф_ArticlrDescriptions 
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
Процедура ФормированиеВидаНоменклатурыПолСтавки(НовыйЭлемент, СтрокаН, СтрокаТЧ) 
	
	СтавкаНДСПоУмолчанию = СтавкаНДС20;
	ВидНоменклатуры = СтрокаН.ВидНоменклатуры;
	Если НовыйЭлемент.ВидНоменклатуры <> ВидНоменклатуры Тогда
		НовыйЭлемент.ВидНоменклатуры = ВидНоменклатуры;
	КонецЕсли;
	
	РеквизитыВида = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НовыйЭлемент.ВидНоменклатуры, "ТипНоменклатуры, ОсобенностьУчета,
		|ИспользованиеХарактеристик, ГруппаФинансовогоУчета, ВариантОформленияПродажи");

	НовыйЭлемент.ТипНоменклатуры  = РеквизитыВида.ТипНоменклатуры;
	НовыйЭлемент.ОсобенностьУчета = РеквизитыВида.ОсобенностьУчета;
	НовыйЭлемент.ВариантОформленияПродажи = РеквизитыВида.ВариантОформленияПродажи;
	
	// ++ Галфинд_Домнышева_06_02_2024
	Если НовыйЭлемент.ИспользованиеХарактеристик <> РеквизитыВида.ИспользованиеХарактеристик Тогда
		 НовыйЭлемент.ИспользованиеХарактеристик = РеквизитыВида.ИспользованиеХарактеристик;
	КонецЕсли;
	// -- Галфинд_Домнышева_06_02_2024

	// ++ Галфинд_Домнышева_16_03_2023
	Если НЕ ЗначениеЗаполнено(НовыйЭлемент.ГруппаФинансовогоУчета) Тогда
		НовыйЭлемент.ГруппаФинансовогоУчета = РеквизитыВида.ГруппаФинансовогоУчета;
	КонецЕсли;
	// -- Галфинд_Домнышева_16_03_2023
	Если ЗначениеЗаполнено(СтрокаН.СтавкаНДС) Тогда
		СтавкаНДС = СтрокаН.СтавкаНДС;
	Иначе
		СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	Если НЕ НовыйЭлемент.СтавкаНДС = СтавкаНДС Тогда
		НовыйЭлемент.СтавкаНДС = СтавкаНДС;
	КонецЕсли;

	Если ЗначениеЗаполнено (СтрокаН.ПолНоменклатуры) Тогда
		ЗаписатьПолНоменклатуры(СтрокаН.ПолНоменклатуры, НовыйЭлемент, СтрокаТЧ);
	КонецЕсли;

КонецПроцедуры

// #wortmann { 
// Процедура формирует Производителя
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	Отказ - параметр системы
Процедура ФормированиеПроизводителя(НовыйЭлемент, СтрокаТЧ, Отказ)
	
	СтранаПроисхождения = НайтиСтрокуДереваПоТэгу("Country_of_origin", Отказ, СтрокаТЧ);	
	Если ЗначениеЗаполнено(СтранаПроисхождения) Тогда
		НайденныйЭлемент = Справочники.СтраныМира.НайтиПоРеквизиту("КодАльфа2", СокрЛП(СтранаПроисхождения));
		
		Если ЗначениеЗаполнено(НайденныйЭлемент) Тогда
			
			Если НовыйЭлемент.СтранаПроисхождения <> НайденныйЭлемент Тогда
				НовыйЭлемент.СтранаПроисхождения = НайденныйЭлемент;
			КонецЕсли;
		Иначе
			СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
			+ ". Описание ошибки: В классификаторе стран мира не найдено указанное значение """ + СтранаПроисхождения + """";
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		КонецЕсли;
	Иначе
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки
		+ ". Описание ошибки: Не заполнено значение страны происхождения";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры 

// #wortmann { 
// Процедура формирует Марку
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	Отказ - параметр системы
Процедура ФормированиеМарки(НовыйЭлемент, СтрокаТЧ, Отказ) 
	
	// (н)Галфинд_ДомнышеваКР_02_02_31
	Марка = НайтиСтрокуДереваПоТэгу("Brand_name", Отказ, СтрокаТЧ);
	ЗначенияСвойств = Новый Массив;
	Свойство_Марка =  НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураBrand_Name");

	Если ЗначениеЗаполнено(Марка) Тогда
		НайденныйЭлемент = НайтиЗначениеПоРеквизитуСправочника("Марки", "Наименование", Марка);
		
		Если ЗначениеЗаполнено(НайденныйЭлемент) Тогда
			
			Если НовыйЭлемент.Марка <> НайденныйЭлемент Тогда
				НовыйЭлемент.Марка = НайденныйЭлемент;
			КонецЕсли;
			
			Значение = Новый Структура("Свойство, Значение", Свойство_Марка, НайденныйЭлемент.Наименование);
			ЗначенияСвойств.Добавить(Значение);
			ЗаписатьНаборСвойствВРегистр(НовыйЭлемент.Ссылка, ЗначенияСвойств, СтрокаТЧ);

		Иначе			
			ЗначениеИзРСПереводЗначений = ПолучитьЗначениеИзРСПереводЗначений(Марка);
			Если ЗначениеЗаполнено(ЗначениеИзРСПереводЗначений) Тогда
				СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
				+ ". Описание ошибки: В справочнике Марки не найдено указанное значение " +  Марка + " ."
				+ " в РС ""Перевод значений реквизитов и свойств"" есть этот объект со значением " + ЗначениеИзРСПереводЗначений
				+ Символы.НПП + " Требуется создать элемент справочника Марки.";
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
				
				Значение = Новый Структура("Свойство, Значение", Свойство_Марка, ЗначениеИзРСПереводЗначений);
				ЗначенияСвойств.Добавить(Значение);
				ЗаписатьНаборСвойствВРегистр(НовыйЭлемент.Ссылка, ЗначенияСвойств, СтрокаТЧ);
			Иначе
				ЗагруженоСОшибками = Истина;
				СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
				+ ". Описание ошибки: В справочнике Марки и в РС ""Перевод значений реквизитов и свойств"""
				+ "не найдено указанное значение " +  Марка;
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		СообщениеОбОшибке = "Ошибка при проведении документа " + ЭтотОбъект + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
		+ ". Не задано значение ТЭГа Brand_name";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
		
	КонецЕсли;
	// (к)Галфинд_ДомнышеваКР_02_02_31
	
КонецПроцедуры

// #wortmann { 
// Проверяет есть ли в РС гф_ПереводЗначенийРеквизитовИСвойств значение с соответствующим объектом
// и языком "Нац. каталог"
// Галфинд_Домнышева 2023/03/21
//
// Параметры:
//	Марка - Строка - имя искомого реквизита.
//
// Возвращаемое значение:
//	Строка - найденное значение, Неопределено - если значение не найдено.
Функция ПолучитьЗначениеИзРСПереводЗначений(Марка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гф_ПереводЗначенийРеквизитовИСвойств.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.гф_ПереводЗначенийРеквизитовИСвойств КАК гф_ПереводЗначенийРеквизитовИСвойств
		|ГДЕ
		|	гф_ПереводЗначенийРеквизитовИСвойств.Объект = &Марка
		|	И гф_ПереводЗначенийРеквизитовИСвойств.Язык = &Язык";
	
	Запрос.УстановитьПараметр("Марка", Марка); 
	Запрос.УстановитьПараметр("Язык", Справочники.гф_ВидыЯзыков.НацКаталог);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий(); 
		Возврат Выборка.Значение;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// #wortmann { 
// Процедура формирует дополнительные реквизиты хранящиеся в РегистрСведений.ДополнительныеСведения
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	GLN_supplier - Строка - значение ТЭГ "GLN_supplier"
Процедура ФормированиеДополнительныхРеквизитов(НовыйЭлемент, СтрокаТЧ, GLN_supplier)  
	
	Если НовыйЭлемент <> Неопределено Тогда
		
		СтруктураОтбора = Новый Структура;	
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.НомерСтрокиРодителя);	
		СтруктураОтбора.Вставить("СтатусПоля", Перечисления.гф_СтатусыПоля.Информационное);
		
		ДополнительныеРеквизиты = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора); 
		
		СтруктураОтбора = Новый Структура;	
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.НомерСтрокиРодителя);	
		СтруктураОтбора.Вставить("СтатусПоля", Перечисления.гф_СтатусыПоля.Обязательное);
		ОбязательныеРеквизиты = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора); 
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеРеквизиты, ОбязательныеРеквизиты);
		
		СтруктураОтбора = Новый Структура;	
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.НомерСтрокиРодителя);	
		СтруктураОтбора.Вставить("СтатусПоля", Перечисления.гф_СтатусыПоля.Критическое);
		КритическиеРеквизиты = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора); 
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеРеквизиты, КритическиеРеквизиты);		
		
		// #wortmann { 
		// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee4573acf7342c
		// Галфинд_Домнышева 2023/08/28
		СтруктураОтбора = Новый Структура;	
		СтруктураОтбора.Вставить("Тэг", "Document_date");	
		РеквизитДата = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);
		РеквизитДата[0].Значение = Дата(РеквизитДата[0].Значение);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеРеквизиты, РеквизитДата);		
		// } #wortmann
		
		ЗначенияСвойств = Новый Массив;
		
		ЗаполнитьЗначенияСвойствНовогоЭлемента(НовыйЭлемент, ДополнительныеРеквизиты, СтрокаТЧ, ЗначенияСвойств);		
		
		Если ЗначениеЗаполнено(GLN_supplier) Тогда
			Свойство_НомерОрганизации = НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураGLN_Supplier"); 
			Значение = Новый Структура("Свойство, Значение", Свойство_НомерОрганизации, GLN_supplier);
			ЗначенияСвойств.Добавить(Значение);
		КонецЕсли;
		
		ДополнитьСвойствами(ЗначенияСвойств, СтрокаТЧ);
		
		Если ЗначенияСвойств.Количество() > 0 Тогда
			ЗаписатьНаборСвойствВРегистр(НовыйЭлемент.Ссылка, ЗначенияСвойств, СтрокаТЧ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

// #wortmann { 
// Процедура заполняет заполняет массив ЗначенияСвойств доп.сведениями по группам ТЭГов "Extended_Attribute"
// Галфинд_Домнышева 2023/02/23
//
// Параметры:
//	ЗначенияСвойств - Массив
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	Отказ - параметр системы
Процедура ДополнитьСвойствами(ЗначенияСвойств, СтрокаТЧ, Отказ = Ложь)
	
	СтруктураОтбора = Новый Структура;	
	СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.НомерСтрокиРодителя);	
	СтруктураОтбора.Вставить("ТЭГ", "Extended_Attribute");
	
	Extended_Attribute = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора); 
	
	Для каждого Строка Из Extended_Attribute Цикл 
		
		СтруктураОтбора = Новый Структура;	
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", Строка.ПорядковыйНомерСтроки);	
		
		СтрокиДопРеквизитов = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);
		
		Для Каждого Стр Из  СтрокиДопРеквизитов Цикл
			
			Если Стр.ТЭГ = "QualifierName" Тогда
				
				QualifierName = НайтиСтрокуДереваПоТэгу("QualifierName", Отказ,  Стр);
				
				Если QualifierName = "LINING" Тогда
					ЗначениеСвойства = НайтиСтрокуДереваПоТэгу("Text", Отказ,  Стр);
					Свойство_Подклад = НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураMaterialLining"); 
					Значение = Новый Структура("Свойство, Значение", Свойство_Подклад, ЗначениеСвойства);
					ЗначенияСвойств.Добавить(Значение);
					
				ИначеЕсли QualifierName = "UPPER" Тогда
					ЗначениеСвойства = НайтиСтрокуДереваПоТэгу("Text", Отказ,  Стр);
					Свойство_Верх = НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураMaterialSurface"); 
					Значение = Новый Структура("Свойство, Значение", Свойство_Верх, ЗначениеСвойства);
					ЗначенияСвойств.Добавить(Значение);
					
				ИначеЕсли QualifierName = "OUTSOLE"	Тогда
					ЗначениеСвойства = НайтиСтрокуДереваПоТэгу("Text", Отказ,  Стр);
					Свойство_Подошва = НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураMaterialBottom"); 
					Значение = Новый Структура("Свойство, Значение", Свойство_Подошва, ЗначениеСвойства);
					ЗначенияСвойств.Добавить(Значение);
				// ++ Галфинд_ДомнышеваКР_08_02_2024	
				ИначеЕсли QualifierName = "DECLARATIO"	Тогда
					ЗначениеСвойства = НайтиСтрокуДереваПоТэгу("Text", Отказ,  Стр);
					Свойство_Декларация = НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураДекларация"); 
					Значение = Новый Структура("Свойство, Значение", Свойство_Декларация, ЗначениеСвойства);
					ЗначенияСвойств.Добавить(Значение);
				// -- Галфинд_ДомнышеваКР_08_02_2024	
				Иначе
					Продолжить;
				КонецЕсли;
				
			КонецЕсли; 
			
		КонецЦикла;
	КонецЦикла;	
	
КонецПроцедуры

// #wortmann { 
// Процедура заполняет оставшиеся реквизиты и заполняет массив ЗначенияСвойств
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	ДополнительныеРеквизиты - ТаблицаЗначений - возможные для заполнения дополнительные реквизиты Номенклатуры
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	ЗначенияСвойств - Массив
Процедура ЗаполнитьЗначенияСвойствНовогоЭлемента(НовыйЭлемент, ДополнительныеРеквизиты, СтрокаТЧ, ЗначенияСвойств)
	
	РеквизитыШапкиНоменклатуры = Новый Массив;
	Для Каждого МетаРеквизит Из Метаданные.Справочники.Номенклатура.Реквизиты Цикл
		РеквизитыШапкиНоменклатуры.Добавить(МетаРеквизит.Имя);	
	КонецЦикла;
	
	Для каждого Реквизит Из ДополнительныеРеквизиты Цикл
		
		ИндексЗначения = Реквизит.ПолеЗагрузки.Объект;
		
		Если Реквизит.ПолеЗагрузки.ТипОбъекта = "Номенклатура" 
			И Реквизит.ПолеЗагрузки.Объект <> "Артикул" Тогда 
			
			Если РеквизитыШапкиНоменклатуры.Найти(ИндексЗначения) <> Неопределено Тогда 
			
				ТипРеквизита = ТипЗнч(НовыйЭлемент[ИндексЗначения]);
				МетаданныеОбъекта = НовыйЭлемент[ИндексЗначения].Метаданные();
				
				ЗаписатьПростыеТипы(НовыйЭлемент, ТипРеквизита, Реквизит, ИндексЗначения, МетаданныеОбъекта);
				Если Реквизит.Тэг = "Customs_tarif_number" 
					// ИЛИ Реквизит.Тэг = "Brand_name" 
					Тогда												
					Свойство = НайтиДополнительныйРеквизитПоНаименованию(Реквизит.Тэг);
					Значение = Новый Структура("Свойство, Значение", Свойство, Реквизит.Значение);
					ЗначенияСвойств.Добавить(Значение);	
				КонецЕсли;
				
			Иначе
				
				СообщениеОбОшибке = "У номенклатуры нет реквизита  " + ИндексЗначения + " (настройка в справочнике ""Настройка загружаемых данных"")";
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка,, ЭтотОбъект, СообщениеОбОшибке);
			
			КонецЕсли;
		
		ИначеЕсли ТипЗнч(Реквизит.ПолеЗагрузки.ТипОбъекта)
			= Тип("СправочникСсылка.НаборыДополнительныхРеквизитовИСведений") Тогда												
			Если Реквизит.Тэг = "Document_date"  Тогда
				 Значение = Новый Структура("Свойство, Значение", Реквизит.ПолеЗагрузки.Объект, Дата(Реквизит.Значение));
			ИначеЕсли  Реквизит.Тэг = "Picture_name"  Тогда
				Значение = Новый Структура("Свойство, Значение", Реквизит.ПолеЗагрузки.Объект, Реквизит.Значение);
				ПоискИЗаписьКартинки(Реквизит.Значение, СтрокаТЧ, НовыйЭлемент);
			Иначе
				Значение = Новый Структура("Свойство, Значение", Реквизит.ПолеЗагрузки.Объект, Реквизит.Значение);
			КонецЕсли;
			
			ЗначенияСвойств.Добавить(Значение);
			
			// Если Реквизит.Тэг = "Picture_name"  Тогда
			//	ПоискИЗаписьКартинки(Реквизит.Значение, СтрокаТЧ, НовыйЭлемент);
			// КонецЕсли;
		ИначеЕсли Реквизит.Тэг = "EAN" Тогда
			СоздатьШтрихкод(СтрокаТч, НовыйЭлемент, Реквизит.Значение, НовыйЭлемент.ЕдиницаИзмерения );
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

// #wortmann { 
// Записывает в НовыйЭлемент значения простых реквизитов
// Галфинд_Домнышева 2022/10/21
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - созданный объект справочника Номенклатура
//	ТипРеквизита - тип значения по индексуЗначения 
//	Реквизит - ЭлементТаблицыЗначений - элемент ТЗ ДополнительныеРеквизиты 
//	ИндексЗначения - Строка - возможные типы значений из реквизита Объект спр гф_НастройкаЗагружаемыхДанных 
//	МетаданныеОбъекта - ОбъектМетаданных 
Процедура ЗаписатьПростыеТипы(НовыйЭлемент, ТипРеквизита, Реквизит, ИндексЗначения, МетаданныеОбъекта)
	
	Если ТипРеквизита = Тип("Строка") Тогда
		НовыйЭлемент[ИндексЗначения] = Строка(Реквизит.Значение);
	ИначеЕсли ТипРеквизита = Тип("Число") Тогда
		НовыйЭлемент[ИндексЗначения] = Число(Реквизит.Значение);
	ИначеЕсли ТипРеквизита = Тип("Дата") Тогда
		НовыйЭлемент[ИндексЗначения] = Дата(Реквизит.Значение);
	ИначеЕсли СтрНайти(МетаданныеОбъекта.ПолноеИмя(), "Справочник") > 0 Тогда	
		ЗначениеСправочника = Справочники[МетаданныеОбъекта.Имя].НайтиПоКоду(Реквизит.Значение);
		Если ЗначениеЗаполнено(ЗначениеСправочника) Тогда
			НовыйЭлемент[ИндексЗначения] = ЗначениеСправочника;
		Иначе ЗначениеСправочника = Справочники[МетаданныеОбъекта.Имя].НайтиПоНаименованию(Реквизит.Значение); 
			Если ЗначениеЗаполнено(ЗначениеСправочника) И МетаданныеОбъекта.ПолноеИмя() <> "Справочник.КлассификаторТНВЭД" Тогда
				НовыйЭлемент[ИндексЗначения] = ЗначениеСправочника;
			Иначе
				ИзменитьКомментарийВРегистре(Реквизит.ПорядковыйНомерСтроки, Реквизит.Тэг, "Значение в базе не найдено");
			КонецЕсли;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// #wortmann { 
// Находит значение в ПВХ ДополнительныеРеквизитыИСведения по наименованию 
// Галфинд_Домнышева 2022/10/19
//
// Параметры:
//	Наименование - Строка - имя искомого реквизита.
//
// Возвращаемое значение:
//	ПланВидовХарактеристикСсылка - если значение найдено, 
//	Неопределено - если значение реквизита не найдено.
//
Функция НайтиДополнительныйРеквизитПоНаименованию(Наименование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
		|ИЗ
		|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции 

// #wortmann { 
// Находит значение в ПВХ ДополнительныеРеквизитыИСведения по Идентификатору для формул 
// Галфинд_Домнышева 2022/10/19
//
// Параметры:
//	Наименование - Строка - имя искомого реквизита.
//
// Возвращаемое значение:
//	ПланВидовХарактеристикСсылка - если значение найдено, 
//	Неопределено - если значение реквизита не найдено.
//
Функция НайтиДополнительныйРеквизитПоИдентификатору(Наименование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
		|ИЗ
		|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.ИдентификаторДляФормул = &ИдентификаторДляФормул";
	
	Запрос.УстановитьПараметр("ИдентификаторДляФормул", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// #wortmann { 
// Дописывает дополнительное свойство GLN_manufacturer объекта в РС ДополнительныеСведения
// Галфинд_Домнышева 2022/10/10
//
// Параметры:
//	ПолНоменклатуры - ПеречислениеСсылка.гф_ПолНоменклатуры - Значение Пола Номенклатуры из ArticlrDescriptions
//	НовыйЭлемент - СправочникОбъект.Номенклатура - создоваемый объект справочника Номенклатура
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
Процедура ЗаписатьПолНоменклатуры(ПолНоменклатуры, НовыйЭлемент, СтрокаТЧ)
	
	ЗначенияСвойств = Новый Массив;
	Свойство_Пол = НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураПол");
	Значение = Новый Структура("Свойство, Значение", Свойство_Пол, ПолНоменклатуры);
	ЗначенияСвойств.Добавить(Значение);
	ЗаписатьНаборСвойствВРегистр(НовыйЭлемент.Ссылка, ЗначенияСвойств, СтрокаТЧ);
	
КонецПроцедуры

// #wortmann { 
// выбирает значение ТЭГа по порядковому номеру строки родителя для текущей строки
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	Тэг - Строка - имя ТЭГа из файла
//	Отказ - параметр системы
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//
// Возвращаемое значение:
//	Строка - значение ТЭГа по порядковому номеру строки родителя для текущей строки
//	Неопределено - если по указанному ТЭГу нет значений или этого ТЭГа нет в файле
Функция НайтиСтрокуДереваПоТэгу(Тэг, Отказ, СтрокаТЧ = Неопределено)
	
	СтруктураОтбора = Новый Структура;
	// BSLLS:Typo-off
	СтруктураОтбора.Вставить("Тэг", Тэг);
	// BSLLS:Typo-on
	Если Не СтрокаТЧ = Неопределено Тогда
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.НомерСтрокиРодителя);
	КонецЕсли;
	НайденноеЗначение = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);
	Если НайденноеЗначение.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(НайденноеЗначение[0].Значение) Тогда
			Возврат НайденноеЗначение[0].Значение;
		Иначе
			Если СтрокаТЧ = Неопределено Тогда
				// BSLLS:Typo-off
				СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + ". Описание ошибки: Не заполнен тэг " + Тэг
									+ " по строке родителя №" + СтрокаТЧ.НомерСтрокиРодителя;
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			Иначе
				СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + " по строке родителя №" 
									+ СтрокаТЧ.НомерСтрокиРодителя + ". Описание ошибки: Не заполнено значение строки " + Тэг;
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			КонецЕсли;
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, Ссылка, Ссылка, СообщениеОбОшибке);
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Если Тэг = "Country_of_origin" ИЛИ Тэг = "Text" Тогда
			 СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + ". Описание ошибки: Не заполнен тэг " + Тэг
								+ " по строке родителя №" + СтрокаТЧ.НомерСтрокиРодителя;

		Иначе
		СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + ". Описание ошибки: Не найден тэг " + Тэг
							+ " по строке родителя №" + СтрокаТЧ.НомерСтрокиРодителя;
		КонецЕсли;
		// BSLLS:Typo-on
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, Ссылка, Ссылка, СообщениеОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann 

// #wortmann { 
// выбирает значение ТЭГа по порядковому номеру текущей строки
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	Тэг - Строка - имя ТЭГа из файла
//	Отказ - параметр системы
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//
// Возвращаемое значение:
//	Строка - значение ТЭГа по порядковому номеру текущей строки
//	Неопределено - если по указанному ТЭГу нет значений или этого ТЭГа нет в файле
Функция НайтиСтрокуДереваПоТэгуОтПорядкового(Тэг, Отказ,  СтрокаТЧ = Неопределено)
	
	СтруктураОтбора = Новый Структура; 
	// BSLLS:Typo-off
	СтруктураОтбора.Вставить("Тэг", Тэг);
	Если Не СтрокаТЧ = Неопределено Тогда
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.ПорядковыйНомерСтроки);
	КонецЕсли;
	НайденноеЗначение = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);
	Если НайденноеЗначение.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(НайденноеЗначение[0].Значение) Тогда
			Возврат НайденноеЗначение[0].Значение;
		Иначе
			Если СтрокаТЧ = Неопределено Тогда
				СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + ". Описание ошибки: Не заполнен тэг " + Тэг;
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			Иначе
				СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
									+ ". Описание ошибки: Не заполнено значение строки " + Тэг;
				ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			КонецЕсли;
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, Ссылка, Ссылка, СообщениеОбОшибке);
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + ". Описание ошибки: Не найден тэг " + Тэг;
		// BSLLS:Typo-on
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, Ссылка, Ссылка, СообщениеОбОшибке);
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann

// #wortmann { 
// выбирает все тэги по номеру строки родителя
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//
// Возвращаемое значение:
//	Структура - структура тэгов по найденных по номеру строки родителя
Функция ВыбратьВсеТэгиСвойствПоСтрокеАртикулу(СтрокаТЧ)
	
	Результат = Новый Структура;
	
    СтруктураОтбора = Новый Структура;
	Если СтрокаТЧ <> Неопределено Тогда
		СтруктураОтбора.Вставить("НомерСтрокиРодителя", СтрокаТЧ.НомерСтрокиРодителя);
	Иначе
		Возврат Результат;
	КонецЕсли;
	
	МассивСвойств = НаборЗаписейТэгов.НайтиСтроки(СтруктураОтбора);
	Для каждого Свойство Из МассивСвойств Цикл
		Результат.Вставить(Свойство.Тэг, Свойство.Значение);
	КонецЦикла;
	
    Возврат Результат;

КонецФункции// } #wortmann

// #wortmann { 
// Записывает дополнительные свойства объекта в РС ДополнительныеСведения
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	Ссылка - СправочникСсылка.Номенклатура - ссылка на создоваемый объект справочника Номенклатура
//	ЗначенияСвойств - Структура - набор записываемых свойств объекта
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
Процедура ЗаписатьНаборСвойствВРегистр(Ссылка, ЗначенияСвойств, СтрокаТЧ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДополнительныеСведения");
		ЭлементБлокировки.УстановитьЗначение("Объект", Ссылка);
		Блокировка.Заблокировать();
																			   
		Набор = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
		Набор.Отбор.Объект.Установить(Ссылка);
		Набор.Прочитать();
		ТекущиеЗначения = Набор.Выгрузить();                             
		
		Для Каждого Строка Из ЗначенияСвойств Цикл
			Если НЕ ЗначениеЗаполнено(Строка.Значение) Тогда
				Продолжить;
			КонецЕсли;
			Запись = ТекущиеЗначения.Найти(Строка.Свойство, "Свойство");
			Если Запись = Неопределено Тогда
				Запись = ТекущиеЗначения.Добавить();
				Запись.Свойство = Строка.Свойство;
				Если Не ЗначениеЗаполнено(Запись.Свойство) Тогда
					Продолжить;
				КонецЕсли;
				Запись.Объект   = Ссылка;
			КонецЕсли;
			Запись.Значение = Строка.Значение;
			
		КонецЦикла;
		Набор.Загрузить(ТекущиеЗначения);
		Набор.Записать();
		
	Исключение
		СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
							+ ". Описание ошибки: Не удалось загрузить дополнительные сведения. " ;
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, Ссылка, Ссылка, СообщениеОбОшибке);		
	КонецПопытки;
	
КонецПроцедуры// } #wortmann 

// #wortmann { 
// Создает записи в РС ШтрихкодыНоменклатуры
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	НовыйЭлемент - СправочникОбъект.Номенклатура - создоваемый объект справочника Номенклатура
//	EAN - Строка - значение Тэга "EAN"
//	ЕИКлассификатор - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица измерения нового элемента
Процедура СоздатьШтрихкод(СтрокаТЧ, НовыйЭлемент, EAN, ЕИКлассификатор) 
	Характеристика = Неопределено;
	
	Размер =  НайтиСтрокуДереваПоТэгу("Size", , СтрокаТЧ);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|ГДЕ
	|   ХарактеристикиНоменклатуры.Владелец = &Владелец
	|	И ХарактеристикиНоменклатуры.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Владелец", НовыйЭлемент.ВидНоменклатуры);
	Запрос.УстановитьПараметр("Наименование", Размер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка.Следующий();
		Характеристика = Выборка.Ссылка;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура
		|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика";
	
	Запрос.УстановитьПараметр("Номенклатура", НовыйЭлемент.Ссылка);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Если НЕ РезультатЗапроса.Пустой() Тогда
        Выборка.Следующий();
		
		НайденныйШтрихкод = Выборка.Штрихкод;
		Если НайденныйШтрихкод <> EAN Тогда
			
        ТекЗапись = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
        ТекЗапись.Штрихкод = НайденныйШтрихкод;
        ТекЗапись.Прочитать();
        
        ТекЗапись.Штрихкод  = EAN;
        Попытка
            ТекЗапись.Записать();
		Исключение
			СообщениеОбОшибке = "Ошибка при проведении документа " + НовыйЭлемент.Ссылка + " в строке №" 
								+ СтрокаТЧ.ПорядковыйНомерСтроки + ". Описание ошибки: Не удалось загрузить ШтрихКоды " + ОписаниеОшибки();
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		КонецПопытки;
	    КонецЕсли;
       	
	Иначе
		Попытка	
			МенеджерЗаписиШтрихКода = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
			МенеджерЗаписиШтрихКода.Номенклатура   = НовыйЭлемент.Ссылка;
			МенеджерЗаписиШтрихКода.Характеристика = Характеристика;
			МенеджерЗаписиШтрихКода.Упаковка       = ЕИКлассификатор;
			МенеджерЗаписиШтрихКода.Штрихкод       = EAN;
			МенеджерЗаписиШтрихКода.Записать();
		Исключение
			СообщениеОбОшибке = "Ошибка при проведении документа " + Ссылка + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки 
								+ ". Описание ошибки: Не удалось загрузить ШтрихКоды " + ОписаниеОшибки();
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, Ссылка, Ссылка, СообщениеОбОшибке);		
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры// } #wortmann

// #wortmann { 
// Дописывает дополнительное свойство GLN_manufacturer объекта в РС ДополнительныеСведения
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	НовыйЭлемент - СправочникОбъект.Номенклатура - создоваемый объект справочника Номенклатура
//	GLN_manufacturer - Строка - значение ТЭГа GLN_manufacturer
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
// BSLLS:LatinAndCyrillicSymbolInWord-off
Процедура ДописатьСвойствоGLN_manufacturer(НовыйЭлемент, GLN_manufacturer, СтрокаТЧ) 
// BSLLS:LatinAndCyrillicSymbolInWord-on	
	Если НЕ ЗначениеЗаполнено(GLN_manufacturer) Тогда
		Возврат;
	КонецЕсли;
	ЗначенияСвойств = Новый Массив;
	Свойство_Производителя =  НайтиДополнительныйРеквизитПоИдентификатору("гф_НоменклатураGLN_Manufacturer");
	Значение = Новый Структура("Свойство, Значение", Свойство_Производителя, GLN_manufacturer);
	ЗначенияСвойств.Добавить(Значение);
	
	ЗаписатьНаборСвойствВРегистр(НовыйЭлемент.Ссылка, ЗначенияСвойств, СтрокаТЧ);
	
	// ++ Домнышева КР_20_04_2023
	//НовыйЭлемент.ПроизводительИмпортерКонтрагент = НайтиЗначениеПоРеквизитуСправочника("Контрагенты", 
	//																"гф_GLN_номер", GLN_manufacturer);
	
	НайденныйКонтрагент = НайтиЗначениеПоРеквизитуСправочника("Контрагенты", 
																	"гф_GLN_номер", GLN_manufacturer);
	СтруктураПроизводителей = ПолучитьИмпортера(НайденныйКонтрагент);
	Если ЗначениеЗаполнено(СтруктураПроизводителей) Тогда
		НовыйЭлемент.Производитель = СтруктураПроизводителей.Производитель;
		НовыйЭлемент.ПроизводительИмпортерКонтрагент = СтруктураПроизводителей.Импортер;
	КонецЕсли;
    // -- Домнышева КР_20_04_2023

КонецПроцедуры// } #wortmann

// #wortmann { 
// Подбирает имортера по организации из РС
// Галфинд_Домнышева 2023/04/20
//
// Параметры:                                                     
//	Контрагент - Справочникссылка.Контрагенты - значение найденного контрагента по GLN
//
// Возвращаемое значение:
//	СтруктураВозврата - Структура - содержит данные Импортера и Контрагента
//	Неопределено - если нет данных в ИБ
Функция ПолучитьИмпортера(Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гф_СоответствиеОрганизацииГруппеДступаИСписка.Импортер КАК Импортер,
		|	гф_СоответствиеОрганизацииГруппеДступаИСписка.Производитель КАК Производитель
		|ИЗ
		|	РегистрСведений.гф_СоответствиеОрганизацииГруппеДступаИСписка КАК гф_СоответствиеОрганизацииГруппеДступаИСписка
		|ГДЕ
		|	гф_СоответствиеОрганизацииГруппеДступаИСписка.Организация = &Организация
		|	И гф_СоответствиеОрганизацииГруппеДступаИСписка.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);

	СтруктураВозврата = Новый Структура;
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка.Следующий();
		СтруктураВозврата.Вставить("Импортер", Выборка.Импортер);
		СтруктураВозврата.Вставить("Производитель", Выборка.Производитель);
		Возврат СтруктураВозврата;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции// } #wortmann

// #wortmann { 
// заполняет ТЧ Товары элемента спр ВариантыКомплектации
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	ПорядковыйНомерСтроки - Строка
//	Тэг - Строка - имя ТЭГа из файла
//	Комментарий - Строка - строка табличной части НовогоДокумента
// BSLLS:Typo-off
Процедура ИзменитьКомментарийВРегистре(ПорядковыйНомерСтроки, Тэг, Комментарий)
	
	НаборЗаписейПоТэгу = РегистрыСведений.гф_СтрокиДокументаДанныеЗагрузки.СоздатьНаборЗаписей();
	НаборЗаписейПоТэгу.Отбор.Документ.Значение = ЭтотОбъект.Ссылка;
	НаборЗаписейПоТэгу.Отбор.Документ.Использование = Истина;
	НаборЗаписейПоТэгу.Отбор.ПорядковыйНомерСтроки.Значение = ПорядковыйНомерСтроки;
	НаборЗаписейПоТэгу.Отбор.ПорядковыйНомерСтроки.Использование = Истина;
	НаборЗаписейПоТэгу.Отбор.Тэг.Значение = Тэг;
	НаборЗаписейПоТэгу.Отбор.Тэг.Использование = Истина;
	
	НаборЗаписейПоТэгу.Прочитать();
	
	Для каждого Запись Из НаборЗаписейПоТэгу Цикл
		Запись.Комментарий = Комментарий;
	КонецЦикла;
	НаборЗаписейПоТэгу.Записать();	
// BSLLS:Typo-on	
КонецПроцедуры// } #wortmann

// #wortmann { 
// поиск картинки и запись в базу ее номера
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	РеквизитЗначение - Строка - Значение ТЭГа "Picture_name" 
//	СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части для которой ищется предок с заданным именем
//	НовыйЭлемент - СправочникОбъект.Номенклатура> - создоваемый объект справочника Номенклатура
Процедура ПоискИЗаписьКартинки(РеквизитЗначение, СтрокаТЧ, НовыйЭлемент)
	// ++ Галфинд_ДомнышеваКР_26_01_2024 
	// временно так, потом надо переопределить наименование картинки по шаблону от Сакович
	//  Артикул + пробел + имя_файла_картинки_в_скобках 2-24700-42-001 (0022470042001.JPG)
	Если ЗначениеЗаполнено(НовыйЭлемент.ФайлКартинки) Тогда
		 Возврат;
	КонецЕсли;
	// -- Галфинд_ДомнышеваКР_26_01_2024
	
	КартинкаОсновноеИзображение = Неопределено;
	ЗначениеКартинки = СокрЛП(РеквизитЗначение);
	
	УсловиеНеЗаписиКартинки = ЗначениеЗаполнено(НовыйЭлемент.ФайлКартинки) 
		И НовыйЭлемент.ФайлКартинки.Наименование = ЗначениеКартинки И НовыйЭлемент.ФайлКартинки.Размер > 0 
		И НовыйЭлемент.ФайлКартинки.ВладелецФайла = НовыйЭлемент.Ссылка; 
		
	Если УсловиеНеЗаписиКартинки Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РеквизитЗначение) Тогда
		СообщениеОбОшибке = "Ошибка при загрузке поля " + ЭтотОбъект + " в строке №" + СтрокаТЧ.ПорядковыйНомерСтроки
		+ ". Описание ошибки: Не заполнено значение Picture_Name";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, ЭтотОбъект, ЭтотОбъект, СообщениеОбОшибке);
	Иначе		
		//Картинка = НайтиЗначениеПоРеквизитуСправочника("НоменклатураПрисоединенныеФайлы",
		//											"Наименование", ЗначениеКартинки);// -- Галфинд_Домнышева_15_03_2023
		Картинка = ПоискКартинкиПоНаименованию(ЗначениеКартинки); // ++ Галфинд_Домнышева_15_03_2023
		Если ЗначениеЗаполнено(Картинка) Тогда
			КартинкаОсновноеИзображение = Картинка;
			
			Если КартинкаОсновноеИзображение.ВладелецФайла = НовыйЭлемент.Ссылка Тогда
				Если НЕ НовыйЭлемент.ФайлКартинки = КартинкаОсновноеИзображение Тогда	
					НовыйЭлемент.ФайлКартинки = КартинкаОсновноеИзображение;
				КонецЕсли;
				// ++ Галфинд_Домнышева_15_03_2023	
			Иначе
				ДубльКартинки = Картинка.Скопировать();
				ДубльКартинки.ВладелецФайла  = НовыйЭлемент.Ссылка;
				ДубльКартинки.Записать();
				НовыйЭлемент.ФайлКартинки = ДубльКартинки;
			КонецЕсли;
		Иначе	
			НовыйЭлемент.ФайлКартинки = Справочники.НоменклатураПрисоединенныеФайлы.ПустаяСсылка();
			СообщениеОбОшибке = "В ИБ картинки с именем " + ЗначениеКартинки + " не найдено";
			ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке, "Важная информация");
			// -- Галфинд_Домнышева_15_03_2023
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры// } #wortmann 

// #wortmann { 
// Ищет в ИБ картинку с нужным наименованием
// Галфинд_Домнышева 2023/16/03
//                               
// Параметры:
//	Наименование - Строка - Значение ТЭГа "Picture_name"
//
// Возвращаемое значение:
//	СправочникСсылка.НоменклатураПрисоединенныеФайлы - элемент справочника НоменклатураПрисоединенныеФайлы 
//	Неопределено - если по указанным данным нет картинки в ИБ
Функция ПоискКартинкиПоНаименованию(Наименование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
		|ГДЕ
		|	НоменклатураПрисоединенныеФайлы.Размер > 0
		|	И НоменклатураПрисоединенныеФайлы.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий(); 
		
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann

// #wortmann { 
// Подбирает и записывает в документ договор для заданного Контрагента
// Галфинд_Домнышева 2022/09/12
//                               
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//  СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части по которой ищется значение
//	GLNНомер - Строка - значение ТЭГ по Контрагенту
//
Процедура ДоговорДляКонтрагента(НовыйДокумент, СтрокаТЧ, GLNНомер)	
	
	Договор = ПолучитьДоговорПоСезону(НовыйДокумент, СтрокаТЧ, GLNНомер);
	
	// #wortmann { 
	// Изменился порядок выбора контрагента и договора. Без контрагента не можем пользоваться типовым поиском 
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee470a5070d548
	// Галфинд_Домнышева 2023/08/30
	//	Если Договор = Неопределено Тогда
	//	КомиссионныеПродажи25 = ?(ЗначениеЗаполнено(НовыйДокумент.Соглашение)
	//	И НовыйДокумент.Соглашение.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
	//	И НовыйДокумент.Соглашение.КомиссионныеПродажи25, Истина, Ложь);	
	//	
	//	Договор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(НовыйДокумент, 
	//	НовыйДокумент.ХозяйственнаяОперация, 
	//	НовыйДокумент.Валюта, , 
	//	КомиссионныеПродажи25);	
	//КонецЕсли;
	// } #wortmann
	
	Если ЗначениеЗаполнено(НовыйДокумент.Договор) И НовыйДокумент.Договор <> Договор Тогда
		
		СообщениеОбОшибке = "Предупреждение при проведении документа " + ЭтотОбъект + " в строке № " + СтрокаТЧ.ПорядковыйНомерСтроки +
			": в документе " + НовыйДокумент + " уже был указан другой договор " + НовыйДокумент.Договор + ", а в файле " + Договор;  
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Предупреждение, , ЭтотОбъект, СообщениеОбОшибке);
		
	Иначе
		НовыйДокумент.Договор = Договор;
		НовыйДокумент.ГруппаФинансовогоУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НовыйДокумент.Договор,
		"ГруппаФинансовогоУчета");
		ПродажиСервер.ЗаполнитьБанковскиеСчетаПоДоговору(НовыйДокумент.Договор, НовыйДокумент.БанковскийСчет, 
		НовыйДокумент.БанковскийСчетКонтрагента);
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Подбирает для документа договор по Сезону
// Галфинд_Домнышева 2022/09/12
//                               
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//  СтрокаТЧ - Тип.СтрокаТЧ - строка табличной части по которой ищется значение
//	GLNНомер - Строка - значение ТЭГ по Контрагенту
//
// Возвращаемое значение:
//	СправочникСсылка.ДоговорыКонтрагентов - элемент справочника ДоговорыКонтрагентов
//	Неопределено - если по указанным данным нет договора
Функция ПолучитьДоговорПоСезону(НовыйДокумент, СтрокаТЧ, GLNНомер)
	
	Отказ = Ложь;
	
	гф_Год = НайтиСтрокуДереваПоТэгу("SaisonYear", Отказ, СтрокаТЧ);
	гф_Номер = НайтиСтрокуДереваПоТэгу("Saison", Отказ, СтрокаТЧ);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДоговорыСезона
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Организация = &Организация
	//|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.гф_Сезон.гф_Номер = &гф_Номер
	|	И ДоговорыКонтрагентов.гф_Сезон.гф_Год = &гф_Год
	// #wortmann { 
	// Изменился порядок выбора контрагента и договора 
	// e1cib/data/Задача.ЗадачаИсполнителя?ref=8eabb083fed1320811ee470a5070d548
	// Галфинд_Домнышева 2023/08/30
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДоговорыКонтрагентовгф_GLN.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентовгф_GLN.Ссылка.Контрагент КАК Контрагент
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов.гф_GLN КАК ДоговорыКонтрагентовгф_GLN
	|ГДЕ
	|	ДоговорыКонтрагентовгф_GLN.GLNНомер = &GLNНомер
	|	И ДоговорыКонтрагентовгф_GLN.Ссылка В
	|			(ВЫБРАТЬ
	|				т.Ссылка
	|			ИЗ
	|				ДоговорыСезона КАК т)";
	
	Запрос.УстановитьПараметр("GLNНомер", GLNНомер);
    // } #wortmann
	
	Запрос.УстановитьПараметр("гф_Год", Число(гф_Год));
	Запрос.УстановитьПараметр("гф_Номер", Число(гф_Номер));
	//Запрос.УстановитьПараметр("Контрагент", НовыйДокумент.Контрагент);
	Запрос.УстановитьПараметр("Организация", НовыйДокумент.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции// } #wortmann	
	
// #wortmann { 
// Получает номенклатуру по артикулу и сезону
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	Артикул - Строка - артикул номенклатуры из файла
//	Сезон - СправочникСсылка.КоллекцииНоменклатуры
//
// Возвращаемое значение:
//	СправочникОбъект.Номенклатура - объект справочника Номенклатура
//	Неопределено - если по указанному артикулу нет номенклатуры
Функция ПолучитьНоменклатуруПоАртикулу(Артикул, Сезон = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Артикул ПОДОБНО &Артикул
		|	И Номенклатура.КоллекцияНоменклатуры = &Сезон
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура.КоллекцияНоменклатуры.гф_Год УБЫВ";
	
	Запрос.УстановитьПараметр("Артикул", Артикул);
	Запрос.УстановитьПараметр("Сезон", Сезон);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка.Следующий();
		Возврат Выборка.Ссылка.ПолучитьОбъект();
	КонецЕсли;

КонецФункции// } #wortmann

// #wortmann { 
// Заполняет переменные ПараметрыДокументыСоСтатусомИзменениеИзИ5 и ТЧДокументаСоСтатусомИзменениеИзИ5
// для сравнения значений из i5.
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	СсылкаДокумента - ДокументСсылка.ЗаказКлиента - Найденный документ со статусом гф_СтатусРаботыСЗаказомИ5 
//					не ТребуетсяПодтверждение и не ТребуетсяИзменение 
Процедура ПолучитьДанныеДокумента(СсылкаДокумента)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Партнер КАК Партнер,
		|	ЗаказКлиента.Контрагент КАК Контрагент,
		|	ЗаказКлиента.Валюта КАК Валюта,
		|	ЗаказКлиента.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ЗаказКлиента.АдресДоставки КАК АдресДоставки
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	ПараметрыДокументыСоСтатусомИзменениеИзИ5 = Новый ТаблицаЗначений;
	ПараметрыДокументыСоСтатусомИзменениеИзИ5 = РезультатЗапроса.Выгрузить(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентагф_ТоварыВКоробах.ВариантКомплектации КАК ВариантКомплектации,
		|	ЗаказКлиентагф_ТоварыВКоробах.НомерСтроки КАК НомерСтроки,
		|	ЗаказКлиентагф_ТоварыВКоробах.Количество КАК Количество,
		|	ЗаказКлиентагф_ТоварыВКоробах.ЦенаКороба КАК ЦенаКороба
		|ИЗ
		|	Документ.ЗаказКлиента.гф_ТоварыВКоробах КАК ЗаказКлиентагф_ТоварыВКоробах
		|ГДЕ
		|	ЗаказКлиентагф_ТоварыВКоробах.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТЧДокументаДоСтатусаИзменениеИзИ5 = Новый ТаблицаЗначений;
	ТЧДокументаДоСтатусаИзменениеИзИ5 = РезультатЗапроса.Выгрузить();
	
КонецПроцедуры// } #wortmann

// #wortmann { 
// Получает таблицу значений после сравнения полностью документов из базы и из i5 
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//
// Возвращаемое значение:
//	ТЗДляЗаписи - ТаблицаЗначений - каждая строка которой содержит наименование реквизита
// 				и отличающиеся значения.
Функция СравнитьДокументы(НовыйДокумент)
	
    ОписаниеСтроки = Новый ОписаниеТипов("Строка");
	ТЗДляЗаписи = Новый ТаблицаЗначений;
	ТЗДляЗаписи.Колонки.Добавить("Реквизит", ОписаниеСтроки);
	ТЗДляЗаписи.Колонки.Добавить("ЗначениеТекущее", ОписаниеСтроки);
	ТЗДляЗаписи.Колонки.Добавить("ЗначениеИзИ5", ОписаниеСтроки);
	
	Если ПараметрыДокументыСоСтатусомИзменениеИзИ5 <> Неопределено
		И ПараметрыДокументыСоСтатусомИзменениеИзИ5.Количество() > 0 Тогда
		
		Если ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].Партнер <> НовыйДокумент.Партнер Тогда
			СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
			СтрокаДляЗаписи.Реквизит = "Клиент";
			СтрокаДляЗаписи.ЗначениеТекущее = НовыйДокумент.Партнер;
			СтрокаДляЗаписи.ЗначениеИзИ5 = ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].Партнер;
		КонецЕсли;    
		
		Если ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].Валюта <> НовыйДокумент.Валюта Тогда
			СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
			СтрокаДляЗаписи.Реквизит = "Валюта";
			СтрокаДляЗаписи.ЗначениеТекущее = НовыйДокумент.Валюта;
			СтрокаДляЗаписи.ЗначениеИзИ5 = ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].Валюта;
		КонецЕсли;
		
		Если ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].ДатаОтгрузки <> НовыйДокумент.ДатаОтгрузки Тогда
			СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
			СтрокаДляЗаписи.Реквизит = "Дата Отгрузки";
			СтрокаДляЗаписи.ЗначениеТекущее = НовыйДокумент.ДатаОтгрузки;
			СтрокаДляЗаписи.ЗначениеИзИ5 = ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].ДатаОтгрузки;
		КонецЕсли;
		
		Если ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].АдресДоставки <> НовыйДокумент.АдресДоставки Тогда
			СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
			СтрокаДляЗаписи.Реквизит = "Адрес Доставки";
			СтрокаДляЗаписи.ЗначениеТекущее = НовыйДокумент.АдресДоставки;
			СтрокаДляЗаписи.ЗначениеИзИ5 = ПараметрыДокументыСоСтатусомИзменениеИзИ5[0].АдресДоставки;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТЧДокументаДоСтатусаИзменениеИзИ5 <> Неопределено И НовыйДокумент <> Неопределено
		И ТЧДокументаДоСтатусаИзменениеИзИ5.Количество() <> НовыйДокумент.гф_ТоварыВКоробах.Количество() Тогда 
		
		СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
		СтрокаДляЗаписи.Реквизит = "Кол-во строк в ТЧ Товары в коробах";
		СтрокаДляЗаписи.ЗначениеТекущее = ТЧДокументаДоСтатусаИзменениеИзИ5.Количество();
		                              
		СтрокаДляЗаписи.ЗначениеИзИ5 =  НовыйДокумент.гф_ТоварыВКоробах.Количество();
		
		ТаблицаСтрок = ИщемОдинаковыеСтроки(НовыйДокумент.гф_ТоварыВКоробах.Выгрузить(), ТЧДокументаДоСтатусаИзменениеИзИ5);
		
		Для Каждого Строка Из ТаблицаСтрок Цикл 
			ЗаполнитьЗначенияСвойств(ТЗДляЗаписи.Добавить(), Строка); 
		КонецЦикла;
		
	КонецЕсли;
	// ++ Галфинд_ДомнышеваКР_12_04_2024
	Если ТЗДляЗаписи.Количество() > 0 Тогда
		//ОтправитьПисьмо(ТЗДляЗаписи, НовыйДокумент, Ложь);
	КонецЕсли;
	// -- Галфинд_ДомнышеваКР_12_04_2024
	Возврат ТЗДляЗаписи;

КонецФункции 

// #wortmann { 
// Отправляет письмо автору документа Номеру, Дате, Организации
// Галфинд_Домнышева 2024/04/12
//
// Параметры:
//	ТЗДляЗаписи - ТаблицаЗначений
//	НовыйДокумент - ДокументСсылка.ЗаказКлиента
//	Цены - Булево - признак загрузки ценового уведомления
Процедура ОтправитьПисьмо(ТЗДляЗаписи, НовыйДокумент, Цены = Истина)
	
	УчетнаяЗаписьПочты	= Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;
	ПрофильПочты		= РаботаСПочтовымиСообщениямиСлужебный.ИнтернетПочтовыйПрофиль(УчетнаяЗаписьПочты);
	
	ОтветственныйМенеджер = ОпределитьОтветственногоМенеджера(НовыйДокумент);
	
	Если ОтветственныйМенеджер = Неопределено Тогда
		СообщениеОбОшибке = "Не найден Ответственный менеджер по Заказу: " + НовыйДокумент.Ссылка 
					+ " Сообщение не будет отправлено на почту";
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаКонтактнойИнформации.АдресЭП КАК АдресЭП
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ТаблицаКонтактнойИнформации
	|ГДЕ
	|	ТаблицаКонтактнойИнформации.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ОтветственныйМенеджер);
	
	ВыборкаДанные = Запрос.Выполнить().Выгрузить();
	
	Если ВыборкаДанные.Количество() > 0 Тогда
		EmailПользователя = Строка(ВыборкаДанные[0].АдресЭП);
	Иначе
		ТекстСообщения = НСтр("ru = 'У пользователя не заполнен почтовый адрес, невозможно отправить сообщение об изменениях.'");
		ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	СообщениеПочты = Новый ИнтернетПочтовоеСообщение;
	СообщениеПочты.Кодировка = "utf-8";
	СообщениеПочты.Тема = "Уведомление об изменениях в заказе " + НовыйДокумент.Номер;
	СообщениеПочты.Отправитель = Строка(УчетнаяЗаписьПочты.АдресЭлектроннойПочты);
	СообщениеПочты.ИмяОтправителя = "Рассылка 1С";
	СообщениеПочты.Получатели.Добавить(EmailПользователя);
	
	ТекстУведомления = "";
	
	Если ТЗДляЗаписи <> Неопределено Тогда
		Если Цены Тогда
			ТекстПисьма = "При загрузке заказа № " + НовыйДокумент.Номер + ", клиент " + НовыйДокумент.Контрагент 
			+ ", было обнаружено расхождение цен: "; 
		Иначе
			ТекстПисьма = "При загрузке заказа № " + НовыйДокумент.Номер + ", клиент " + НовыйДокумент.Контрагент 
			+ ", было обнаружено расхождение по следующим реквизитам: ";
		КонецЕсли; 
		
		Для каждого Строка Из ТЗДляЗаписи Цикл
			ТекстУведомления = ТекстУведомления + "<p>" + Строка.Реквизит + " старое значение "
			+ Строка.ЗначениеТекущее + ", новое значение " + Строка.ЗначениеИзИ5 + "</p>"; 
		КонецЦикла;
		
	Иначе 
		ТекстПисьма = "Обращаем внимание - при загрузке заказа № " + НовыйДокумент.Номер + ", клиент " + НовыйДокумент.Контрагент  
		+ " статус документа получил ""Изменение из i5""."; 
		ТекстУведомления = "Реквизиты и ТЧ Заказа меняться Не будут при дальнейшем обновлении."  
		+ " Для загрузки изменения из i5 статус документа требуется перевести в ""Требуется изменение"".";	
		
	КонецЕсли;
	ТекстСообщения = 
	"<style>
	| body {font-family: Verdana, sans-serif;}
	|</style>
	|<h5 style=""padding: 0 0 15px 0; margin: 0 0 0 0;"">Добрый день!</h5>
	|<h5 style=""padding: 0 0 15px 0; margin: 0 0 0 0;"">%Содержимое%</h5>
	|<p>%ТекстУведомления%</p>";
	
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Содержимое%", ТекстПисьма);

	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстУведомления%", ТекстУведомления);
	
	СообщениеПочты.Тексты.Добавить(ТекстСообщения, ТипТекстаПочтовогоСообщения.HTML);
	
	ИнтернетПочта = Новый ИнтернетПочта;
	
	Попытка
		ИнтернетПочта.Подключиться(ПрофильПочты);
		ИнтернетПочта.Послать(СообщениеПочты);
		ИнтернетПочта.Отключиться();
	Исключение
		ДобавитьСтрокуВТекстыОшибки(ОписаниеОшибки());
	КонецПопытки;
		
КонецПроцедуры// } #wortmann 

// #wortmann { 
// Находит ответственного менеджера по ТЧ Ответ.Мен. из Контрагента по Организации 
// Галфинд_Домнышева 2024/04/12
//
// Параметры:
//	НовыйДокумент -  - ДокументСсылка.ЗаказКлиента
//
// Возвращаемое значение:
//	Справочникссылка.Пользователи 
Функция ОпределитьОтветственногоМенеджера(НовыйДокумент)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтветственныйМенеджер.Пользователь КАК Пользователь
		|ИЗ
		|	Справочник.Контрагенты.гф_ОтветственныйМенеджер КАК ОтветственныйМенеджер
		|ГДЕ
		|	ОтветственныйМенеджер.Ссылка = &Ссылка
		|	И ОтветственныйМенеджер.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", НовыйДокумент.Организация);
	Запрос.УстановитьПараметр("Ссылка", НовыйДокумент.Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		 Выборка = РезультатЗапроса.Выбрать();
		 Выборка.Следующий();
		 Возврат Выборка.Пользователь;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции// } #wortmann

// #wortmann { 
// Получает таблицу значений после сравнения табличных частей документов из базы и из i5 
// Галфинд_Домнышева 2022/09/12
//
// Параметры:
//	Выгружаемая - ТаблицаЗначений - ТЧ гф_ТоварыВКоробах при внесении изменений из И5.
//	Текущая - ТаблицаЗначений - ТЧ гф_ТоварыВКоробах до внесения изменений из И5.
//
// Возвращаемое значение:
//	ТЗДляЗаписи - ТаблицаЗначений - каждая строка которой содержит наименование реквизита
// 				и отличающиеся значения.
//
Функция ИщемОдинаковыеСтроки(Выгружаемая, Текущая)
	
	ОписаниеСтроки = Новый ОписаниеТипов("Строка");
	ТЗДляЗаписи = Новый ТаблицаЗначений;
	ТЗДляЗаписи.Колонки.Добавить("Реквизит", ОписаниеСтроки);
	ТЗДляЗаписи.Колонки.Добавить("ЗначениеТекущее", ОписаниеСтроки);
	ТЗДляЗаписи.Колонки.Добавить("ЗначениеИзИ5", ОписаниеСтроки);
	
	Для каждого СтрВыгружаемая Из Выгружаемая Цикл
		СтруктураОтбора = Новый Структура("ВариантКомплектации");		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрВыгружаемая);
		МассивНайденныхВариантов = Текущая.НайтиСтроки(СтруктураОтбора);
		Если МассивНайденныхВариантов.Количество() > 0 Тогда
			СтруктураКоличества = Новый Структура("ВариантКомплектации, Количество");
			ЗаполнитьЗначенияСвойств(СтруктураКоличества, СтрВыгружаемая);
			МассивКоличества = Текущая.НайтиСтроки(СтруктураКоличества);
			Если МассивКоличества.Количество() = 0 Тогда
				СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
				СтрокаДляЗаписи.Реквизит = "Количество упаковок для " + СтрВыгружаемая.ВариантКомплектации;
				СтрокаДляЗаписи.ЗначениеТекущее = 0;
				СтрокаДляЗаписи.ЗначениеИзИ5 = СтрВыгружаемая.Количество;	
			КонецЕсли;	 
		Иначе
			СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
			СтрокаДляЗаписи.Реквизит = "ВариантКомплектации " + СтрВыгружаемая.ВариантКомплектации;
			СтрокаДляЗаписи.ЗначениеТекущее = "нет";
			СтрокаДляЗаписи.ЗначениеИзИ5 = "В количестве " + СтрВыгружаемая.Количество;					
		КонецЕсли;
	КонецЦикла;
	Для каждого СтрТекущая Из Текущая Цикл
		СтруктураОтбора = Новый Структура("ВариантКомплектации");		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрТекущая);
		МассивНайденныхВариантов = Выгружаемая.НайтиСтроки(СтруктураОтбора);
		Если МассивНайденныхВариантов.Количество() = 0 Тогда
			СтрокаДляЗаписи = ТЗДляЗаписи.Добавить();
			СтрокаДляЗаписи.Реквизит = "ВариантКомплектации " + СтрТекущая.ВариантКомплектации;
			СтрокаДляЗаписи.ЗначениеТекущее = "В количестве " + СтрТекущая.Количество;
			СтрокаДляЗаписи.ЗначениеИзИ5 = "нет";					
		КонецЕсли;
	КонецЦикла;	
	
	Возврат ТЗДляЗаписи;
	
КонецФункции

// #wortmann { 
// Находит значение в справочнике по указанному реквизиту 
// Галфинд_Домнышева 2022/10/14
//
// Параметры:
//	Справочник - Строка - имя справочника.
//	Реквизит - Строка - имя реквизита.
//	Значение - Строка - Значение искомого реквизита.
//
// Возвращаемое значение:
//	СправочникСсылка - если значение найдено, 
//	Неопределено - если значение реквизита в указанном справочнике не найдено.
//
Функция НайтиЗначениеПоРеквизитуСправочника(Справочник, Реквизит, Значение)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Спр.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник." + Справочник + " КАК Спр
		|ГДЕ
		|	Спр." + Реквизит + " = &Значение
		| И Спр.ПометкаУдаления = Ложь";
	
	Запрос.УстановитьПараметр("Значение", Значение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
    Возврат Неопределено;
КонецФункции

// #wortmann { 
// Заполняет ТЗ ТекстыОшибок 
// Галфинд_Домнышева 2022/10/14
//
// Параметры:
//	СообщениеОбОшибке - Строка - текущая ошибка
//	ТипИнформации - строка - по умолчанию "Ошибка"й
//
Процедура ДобавитьСтрокуВТекстыОшибки(СообщениеОбОшибке, ТипИнформации = "Ошибка")
	
	НомерОшибки = НомерОшибки + 1;
	
	СтрокаОшибки = ТекстыОшибок.Добавить();
	СтрокаОшибки.Дата = ЭтотОбъект.Дата;
	СтрокаОшибки.ПутьФайла = ЭтотОбъект.ПолноеИмяФайла;
	СтрокаОшибки.Номер = НомерОшибки;
	СтрокаОшибки.ТекстОшибки = СообщениеОбОшибке;
	СтрокаОшибки.Организация = ЭтотОбъект.Организация;
	СтрокаОшибки.Интерфейс = ЭтотОбъект.Интерфейс;
	СтрокаОшибки.ТипИнформации = ТипИнформации;
			
КонецПроцедуры

// #wortmann { 
// Заполняет ТЗ ТекстыОшибок 
// Галфинд_Домнышева 2022/10/14
//
// Параметры:
//	ТаблицаРасхождений - ТаблицаЗначений - ТЗ с колонками "Реквизит, ЗначениеТекущее, ЗначениеИзИ5"
//	НовыйДокумент - ДокументОбъект.ЗаказКлиента - Создаваемый документ
//	ТекущаяДата - Дата
//
Процедура ЗаполнитьИзмененияЗначений(ТаблицаРасхождений, НовыйДокумент, ТекущаяДата)
	
	Если ЗначениеЗаполнено(ТаблицаРасхождений) Тогда
		НаборЗаписей = РегистрыСведений.гф_ИзменениеЗначенийИзI5.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(НовыйДокумент.Ссылка); 
		НаборЗаписей.Отбор.Период.Установить(ТекущаяДата);
		Для каждого Запись Из ТаблицаРасхождений Цикл
			НоваяЗапись = НаборЗаписей.Добавить(); 
			НоваяЗапись.Период = ТекущаяДата;
			НоваяЗапись.Документ = НовыйДокумент.Ссылка; 
			НоваяЗапись.Реквизит = Запись.Реквизит; 
			НоваяЗапись.ЗначениеТекущее = Запись.ЗначениеТекущее;
			НоваяЗапись.ЗначениеИзИ5 = Запись.ЗначениеИзИ5;
		КонецЦикла;
		
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура СозатьТаблицуТекстыОшибок()
	
	Если ТекстыОшибок <> Неопределено И ТипЗнч(ТекстыОшибок) = Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	НомерОшибки = 0;
	
	ТекстыОшибок = Новый ТаблицаЗначений;
	ТекстыОшибок.Колонки.Добавить("Дата");
	ТекстыОшибок.Колонки.Добавить("ПутьФайла");
	ТекстыОшибок.Колонки.Добавить("Номер");
	ТекстыОшибок.Колонки.Добавить("ТекстОшибки");
	ТекстыОшибок.Колонки.Добавить("Организация");
	ТекстыОшибок.Колонки.Добавить("Интерфейс");
	ТекстыОшибок.Колонки.Добавить("ТипИнформации");
	
КонецПроцедуры

#КонецОбласти

СтавкаНДС20 = НайтиЗначениеПоРеквизитуСправочника("СтавкиНДС", "Наименование", "20%");

#КонецЕсли 