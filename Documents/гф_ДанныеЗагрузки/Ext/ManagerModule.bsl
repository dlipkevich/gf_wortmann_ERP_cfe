
// Записывает строки в РС гф_ОшибкиЗагрузки по значениям из ТЗ "ТекстыОшибок" 
// Галфинд_Домнышева 2022/10/13
//
// Параметры:
//	ДокументДанные - ДокументСсылка.гф_ДанныеЗагрузки.
//	ОчиститьПослеЗаписиВРегистр - Булево - признак очистки таблицы с ошибками после после записи в регистр 
//
Процедура ЗаписатьОшибки(ДокументДанные, ОчиститьПослеЗаписиВРегистр = Истина) Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса(); // Галфинд_Домнышева_20_03_2023
	Если ДокументДанные.ДополнительныеСвойства.Свойство("ТекстыОшибок") Тогда
		ТекстыОшибок = ДокументДанные.ДополнительныеСвойства.ТекстыОшибок;
		
		Попытка	
			НаборЗаписей = РегистрыСведений.гф_ОшибкиЗагрузки.СоздатьНаборЗаписей(); 
			//НаборЗаписей.Отбор.Дата.Установить(ДокументДанные.Дата); // Галфинд_Домнышева_20_03_2023 
			НаборЗаписей.Отбор.ПутьФайла.Установить(ДокументДанные.ПолноеИмяФайла);
			НаборЗаписей.Отбор.Организация.Установить(ДокументДанные.Организация);
			НаборЗаписей.Отбор.Интерфейс.Установить(ДокументДанные.Интерфейс);
			
			Для каждого Элемент Из ТекстыОшибок Цикл
				
				НоваяЗапись = НаборЗаписей.Добавить(); 
				//НоваяЗапись.Дата = Элемент.Дата; // Галфинд_Домнышева_20_03_2023
				НоваяЗапись.Дата = ТекущаяДата; 
				НоваяЗапись.ПутьФайла = Элемент.ПутьФайла; 
				НоваяЗапись.Номер = Элемент.Номер;
				НоваяЗапись.ТекстОшибки = Элемент.ТекстОшибки;
				НоваяЗапись.Организация = Элемент.Организация;
				НоваяЗапись.Интерфейс = Элемент.Интерфейс;
				НоваяЗапись.ТипИнформации = Элемент.ТипИнформации;
				
			КонецЦикла;
			НаборЗаписей.Записать();
			
			Если ОчиститьПослеЗаписиВРегистр Тогда
				ДокументДанные.ДополнительныеСвойства.ТекстыОшибок.Очистить();
			КонецЕсли;
			
		Исключение
			
			Если ЗначениеЗаполнено(ДокументДанные.Интерфейс) Тогда
				СобытиеЖурналаРегистрации = "ЗагрузкаИзI5." + ДокументДанные.Интерфейс;
			Иначе
				СобытиеЖурналаРегистрации = "ЗагрузкаИзI5";
			КонецЕсли;
			
			СообщениеОбОшибке = "Ошибка при проведении документа " + ДокументДанные.Ссылка  
				+ ". Не удалось записать ТекстОшибки в РС гф_ОшибкиЗагрузки: " + ОписаниеОшибки();
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка,, ДокументДанные.Ссылка, СообщениеОбОшибке);		
			
		КонецПопытки;
	КонецЕсли;

КонецПроцедуры
