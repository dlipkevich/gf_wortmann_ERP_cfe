
#Область ОбработчикиСобытийФормы
// #wortmann {
// устанавливает параметр в ТЧ Товары
// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ed0e55a7ad5d31
// Галфинд_Домнышева 2022/08/02  
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	СкладУпаковки = Параметры["СкладУпаковки"];
	ЗаполнитьСписокУЛ(СкладУпаковки);	
	Товары.Параметры.УстановитьЗначениеПараметра("УпаковочныйЛист", Истина);
	
КонецПроцедуры// } #wortmann 

#КонецОбласти 
#Область ОбработчикиСобытийЭлементовШапкиФормы

// #wortmann {
// устанавливает параметр в ТЧ Товары при переходе на новую строку
// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ed0e55a7ad5d31
// Галфинд_Домнышева 2022/08/02 
&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СписокУЛ.ТекущиеДанные;	
	Если ТекущиеДанные <> Неопределено Тогда
		Товары.Параметры.УстановитьЗначениеПараметра("УпаковочныйЛист", ТекущиеДанные.УпаковочныйЛист);
	Иначе
		Товары.Параметры.УстановитьЗначениеПараметра("УпаковочныйЛист", Истина);
	КонецЕсли;
	
КонецПроцедуры// } #wortmann

#КонецОбласти

#Область ОбработчикиКомандФормы

// #wortmann {
// формирование массива строк для передачи в док ПеремещениеТоваров
// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ed0e55a7ad5d31
// Галфинд_Домнышева 2022/08/02 
&НаКлиенте
Процедура ЗавершитьПодбор(Команда)
	
	МассивВыделенных = СписокУЛ.НайтиСтроки(Новый Структура("Пометка",Истина));
		
	МассивВыбранных = Новый Массив; 

	Для каждого ИденСтроки Из МассивВыделенных Цикл
		
		СтруСтроки = Новый Структура("УпаковочныйЛист, Артикул, IDКороба, КоличествоПар");
		
		ЗаполнитьЗначенияСвойств(СтруСтроки, ИденСтроки);
		
		МассивВыбранных.Добавить(СтруСтроки);
		
	КонецЦикла;

	Закрыть(МассивВыбранных);
	
КонецПроцедуры// } #wortmann 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

// #wortmann { 
// Заполнение ТЗ СписокУЛ
// e1cib/data/Задача.ЗадачаИсполнителя?ref=811dbcee7bda45d711ed0e55a7ad5d31
// Галфинд_Домнышева 2022/08/02
&НаСервере
Процедура ЗаполнитьСписокУЛ(СкладУпаковки)	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УпаковочныйЛистТовары.Ссылка КАК Ссылка,
		|	УпаковочныйЛистТовары.Номенклатура КАК Номенклатура,
		|	УпаковочныйЛистТовары.Характеристика КАК Характеристика,
		|	УпаковочныйЛистТовары.Количество КАК Количество
		|ПОМЕСТИТЬ ВТ_ТЧ
		|ИЗ
		|	Документ.УпаковочныйЛист.Товары КАК УпаковочныйЛистТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТЧ.Ссылка КАК Ссылка,
		|	ВТ_ТЧ.Номенклатура КАК Номенклатура,
		|	ВТ_ТЧ.Характеристика КАК Характеристика,
		|	ЕСТЬNULL(ТоварыНаСкладахОстатки.ВНаличииОстаток, 0) КАК КоличествоНаСкладе
		|ПОМЕСТИТЬ ВТ_Отбор
		|ИЗ
		|	ВТ_ТЧ КАК ВТ_ТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
		|				,
		|				Склад = &СкладУпаковки
		|					И (Номенклатура, Характеристика) В
		|						(ВЫБРАТЬ
		|							ВТ_ТЧ.Номенклатура КАК Номенклатура,
		|							ВТ_ТЧ.Характеристика КАК Характеристика
		|						ИЗ
		|							ВТ_ТЧ КАК ВТ_ТЧ)) КАК ТоварыНаСкладахОстатки
		|		ПО ВТ_ТЧ.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
		|			И ВТ_ТЧ.Характеристика = ТоварыНаСкладахОстатки.Характеристика
		|ГДЕ
		|	ТоварыНаСкладахОстатки.ВНаличииОстаток >= ВТ_ТЧ.Количество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УпаковочныйЛист.Ссылка КАК УпаковочныйЛист,
		|	УпаковочныйЛист.Код КАК IDКороба,
		|	УпаковочныйЛист.ВсегоМест КАК КоличествоПар,
		|	ЛОЖЬ КАК Пометка,
		|	ВТ_Отбор.Номенклатура.Артикул КАК Артикул
		|ИЗ
		|	Документ.УпаковочныйЛист КАК УпаковочныйЛист
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Отбор КАК ВТ_Отбор
		|		ПО (ИСТИНА)
		|ГДЕ
		|	УпаковочныйЛист.Ссылка = ВТ_Отбор.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	УпаковочныйЛист.Ссылка,
		|	УпаковочныйЛист.Код,
		|	УпаковочныйЛист.ВсегоМест,
		|	ВТ_Отбор.Номенклатура.Артикул";
	
	Запрос.УстановитьПараметр("СкладУпаковки", СкладУпаковки);
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НоваяСтрока = СписокУЛ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Результат);
	КонецЦикла;
   
КонецПроцедуры// } #wortmann

#КонецОбласти
